export enemyWayX, enemyWayY;
Every 3$0 do
var rnd;
begin
     enemyWayX = [];
     enemyWayY = [];
     rnd = rand(1, 3);

     if rnd = 1 then
     begin
          enemyWayX = [73, 46, 29];
          enemyWayY = [71, 43, 29];
     end;

     if rnd = 2 then
     begin
          enemyWayX = [64];
          enemyWayY = [43];
     end;

     if rnd = 3 then
     begin
          enemyWayX = [80];
          enemyWayY = [28];
     end;

     enable;

end;

Every 0$1 do
begin
    enable;
    if escapeAvalaible = 0 then EnterBunkerOrBarrack(3, RussianBase);
    if escapeAvalaible = 0 then DefenseBase(1, RussianBaseTerritory);

    if russianAI = 1 then
    begin;
        HealUnitInBase(3, RussianHealArea, RussianBase, ruLab);
        RepairAndRebuildBase(3, ruDepot, RussianBase, ruDestroyedBuildings);
        AttackVehicles(3, RussianTanksSpot, enemyWayX, enemyWayY);
        AttackNearBuildings(3, 1);
    end;

    if SikorskyArrive = 1 then RepairVehicles(3, SikorskyRepairArea, 53, 59);
end;

Every 0$1 trigger russianAI = 1 do
var i;
begin
    for i in FilterAllUnits([[f_side, 3], [f_type, unit_building], [f_btype, b_bunker], [f_not, [f_or, [f_bweapon, 43], [f_bweapon, 44], [f_bweapon, 46]]]]) do PlaceWeaponTurret(i, [43, 44, 46][Rand(1,3)]);
    for i in FilterAllUnits([[f_side, 3], [f_type, unit_building], [f_btype, b_turret], [f_not, [f_or, [f_bweapon, 43], [f_bweapon, 44], [f_bweapon, 46]]]]) do PlaceWeaponTurret(i, [43, 44, 46][Rand(1,3)]);
end;

Every 0$1 trigger SikorskyAI = 1 do 
begin
    enable;
    RepairVehicles(4, SikorskyRepairArea, 93, 93);
    SikorskyHealUnits(4, SikorskyHealArea, 3);

end;

Every 0$1 trigger SikorskyAI = 1 do SikorskyVehiclesAttack(4);

Every 5$0 trigger russianAI = 1 and IsOk(ruFab) and GetSide(ruFab) = 3 and FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_control, control_computer]]) = 0 do
begin
    enable; 
    if Difficulty = 1 then BuildingVehicle(3, ruFab, rand(5, 8), [43, 43, 43, 43, 44, 44, 44, 46], 1, 3, 24);
    if Difficulty = 2 then BuildingVehicle(3, ruFab, rand(6, 8), [43, 43, 44, 44, 44, 44, 46, 46], 1, 3, 24);
    if Difficulty = 3 then BuildingVehicle(3, ruFab, rand(7, 8), [43, 43, 44, 44, 44, 46, 46, 46], 1, 3, 24);
end;

Every 0$1 trigger startCutscene = 1 do
var i;
begin
     for i in FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_not, [f_lives, 650]]]) do ComMoveXY(i, 51, 12);
end;
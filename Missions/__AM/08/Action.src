Export function Action;
var i, tmp, un, inUnit;
begin
Video(true);

RevealMapOnXY(50, 12, 1, -56);

CenterOnXY(50, 12);

wait(0$3);

PrepareStartRussianAttack;

CenterOnXY(66, 47);

wait(0$5);

case gammaCommander of
     1: Say(Donaldson, 'D1-Don-1');
     2: Say(Brown, 'D1-Brown-1');
     3: Say(Houten, 'D1-VanH-1');
     end;

tmp := FilterAllUnits([[f_side, 1], [f_control, control_remote], [f_linked]]);

if tmp then
   ComAgressiveMove(tmp, 74, 44);

tmp := FilterAllUnits([[f_side, 1], [f_class, class_soldier]]) diff [Donaldson, Brown, Houten];

if tmp then
   for i in tmp do
       begin
       inUnit := IsInUnit(i);

       if inUnit and GetBType(inUnit) in [b_bunker, b_breastwork] then
          tmp := tmp diff i;
       end;

if tmp then
   begin
   ComExitAll(tmp);
   AddComAgressiveMove(tmp, 74, 44);

   tmp := FilterAllUnits([[f_side, 1], [f_class, class_scientistic]]);

   if tmp then
      begin
      ComExitAll(tmp);
      AddComMoveXY(tmp, 66, 28);
      end;
   end;

game_speed := 4;

wait(0$2);

SayRadio(Harisson, 'D1-Har-1');

case gammaCommander of
     1: Say(Donaldson, 'D1-Don-2');
     2: Say(Brown, 'D1-Brown-2');
     3: Say(Houten, 'D1-VanH-2');
     end;

SayRadio(Harisson, 'D1-Har-2');

Video(false);

ChangeMissionObjectives('M1');
SaveForQuickRestart;

wait(2$30);

PrepareRussianAttack(1);

wait(4$30);

if Difficulty > 2 then
   PrepareRussianAttack(2);

if Difficulty > 3 then
   begin
   for i := 1 to 4 do
       begin
       uc_side := 6;
       uc_nation := 3;

       PrepareSoldier(false, 6);
       un := CreateHuman;
       PlaceUnitXYR(un, 122, 89, 3, false);

       ComAgressiveMove(un, 88, 42);
       AddComHold(un);
       end;

   PrepareScientist(false, 6);
   un := CreateHuman;
   PlaceUnitXYR(un, 122, 89, 3, false);

   ComMoveXY(un, 91, 48);
   end;
End;

// support from delta
Every 0$45 trigger FilterAllUnits([[f_side, 1], [f_see, 3]]) = 0 and tick > 5$00 do
var dialog, p;
begin
PrepareSupportFromDelta;

deltaSupport := true;
dialog := false;

if IsOk(Gladstone) then
   dialog := Say(Gladstone, 'D2-Glad-1')
else
   dialog := SayRandom(FilterAllUnits([[f_side, 1], [f_tag, 107], [f_type, unit_human], [f_nation, 1]]) diff [Joan, Denis], 'D2-Sol1-1', 'D2-FSol1-1');

if dialog then
   begin
   if IsOK(Donaldson) and gammaCommander = 1 then
      Say(Donaldson, 'D2-Don-1');

   if IsOK(Brown) and gammaCommander = 2 then
      Say(Brown, 'D2-Brown-1');

   if IsOK(Houten) and gammaCommander = 3 then
      Say(Houten, 'D2-VanH-1');
   end;

PrepareRussianAttack(3);

wait(LoadVariable('07_time', 9$00) div 3);

if GetTech(tech_advmet, 1) = state_researched then
   SetAchievement('ACH_HEAVYGUN');

PrepareMacmillanSquad;

CenterNowOnUnits(JMM);

wait(0$10);

RaiseSailEvent(1);

p := 0;

repeat
 wait(0$1);
 p := Inc(p);

 if p > 100 then
    exit;

until IsInArea(JMM, gammaBaseArea) or IsInArea(IsInUnit(JMM), gammaBaseArea) or GetDistUnits(JMM, Joan) < 10 or FilterAllUnits([[f_side, 1], [f_or, [f_tag, 106], [f_tag, 107]], [f_dist, JMM, 12]]);

DialogueOn;

Say(JMM, 'D3-JMM-1');

if IsOK(Donaldson) and gammaCommander = 1 then
   Say(Donaldson, 'D3-Don-1')
else
   begin
   if gammaCommander = 1 then
      begin
      if SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3-Sol1-1', '') then
         Say(JMM, 'D3-JMM-2');
      end;
   end;

if IsOK(Brown) and gammaCommander = 2 then
   Say(Brown, 'D3-Brown-1')
else
   begin
   if gammaCommander = 2 then
      begin
      if SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3-Sol1-1a', '') then
         Say(JMM, 'D3-JMM-2');
      end;
   end;

if IsOK(Houten) and gammaCommander = 3 then
   Say(Houten, 'D3-VanH-1')
else
   begin
   if gammaCommander = 3 then
      begin
      if SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3-Sol1-1b', '') then
          Say(JMM, 'D3-JMM-2');
      end;
   end;

if IsOK(Joan) then
   begin
   Say(Joan, 'D3a-Joan-1');
   Say(JMM, 'D3a-JMM-1');
   end
else
   begin
   if LoadVariable('06_JoanStatus_1', 0) = 1 then
      begin
      Say(JMM, 'D3b-JMM-1');

      dialog := false;

      if gammaCommander = 1 then
         if IsOK(Donaldson) then
            dialog := Say(Donaldson, 'D3b-Don-1')
         else
            dialog := SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3b-Sol1-1', '');

       if gammaCommander = 2 then
          if IsOk(Brown) then
             dialog := Say(Brown, 'D3b-Brown-1')
          else
             dialog := SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3b-Sol1-1', '');

       if gammaCommander = 3 then
          if IsOk(Houten) then
             dialog := Say(Houten, 'D3b-VanH-1')
          else
             dialog := SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_tag, 106], [f_sex, sex_male]]), 'D3b-Sol1-1', '');

       if dialog then
          begin
          Say(JMM, 'D3b-JMM-2');

          if IsOK(Lisa) then
             Say(Lisa, 'D3b-Lisa-2');
          end;
       end;
   end;      

if not seenComputer then
   begin
   DialogueOff;
   exit;
   end;

if SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_or, [f_tag, 106], [f_tag, 107]], [f_sex, sex_male]]), 'DCa-Sol1-1', '') then
   Say(JMM, 'DCb-JMM-1');

if SayRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1], [f_or, [f_tag, 106], [f_tag, 107]], [f_sex, sex_male]]), 'DCb-Sol1-1', '') then
   Say(JMM, 'DCb-JMM-2');

DialogueOff;
End;

// see computer
Export Function DialogSeenComputer(un);
begin
if jmmArrival then
   exit;

CenterOnUnits(un); 
SayRandom(FilterAllUnits([[f_side, 1], [f_or, [f_tag, 106], [f_tag, 107]]]), 'DC-Sol1-1', 'DC-FSol1-1');
End;

// dialog with Powell about Harrison
Every 0$30 trigger jmmArrival and tick >= 23$00 and IsOk(JMM) do
begin
wait(2$00);

DialogueOn;
SayRadio(Powell, 'D4-Pow-1');
Say(JMM, 'D4-JMM-1');
SayRadio(Powell, 'D4-Pow-2');
Say(JMM, 'D4-JMM-2');
SayRadio(Powell, 'D4-Pow-3');
DialogueOff;

wait(1$00);

RaiseSailEvent(1);

wait(7$30);

DialogueOn;
SayRadio(Powell, 'D4a-Pow-1');
DialogueOff;

SikorskiCounter;

PrepareSikorsky;

CenterNowOnUnits(Sikorski);

DialogueOn;

Say(Sikorski, 'D5-Sik-1');
Say(JMM, 'D5-JMM-1a');
Say(Sikorski, 'D5-Sik-2');

case Query('QSikorski') of
     1: begin
        Say(Sikorski, 'D5a-Sik-1');
        sikorskiWait := 1$00;
        end;

     2: begin
        Say(Sikorski, 'D5b-Sik-1');
        sikorskiWait := 5$00;
        end;

     3: begin
        Say(Sikorski, 'D5c-Sik-1');
        Say(JMM, 'D5c-JMM-1');
        Say(Sikorski, 'D5c-Sik-2');
        sikorskiWait := 5$00;
        end;
     end;

DialogueOff;

SikorskiWaitCounter;

DialogueOn;
Say(Sikorski, 'D6-Sik-1');
DialogueOff;

ChangeMissionObjectives('M2');

RaiseSailEvent(1);
SikorskiAttack;
End;


// Sikorsky dialog about attack with JMM
Every 0$1 trigger FilterUnitsInArea(ruDefendArea, [f_side, 4]) > 2 do
begin
Say(Sikorski, 'D7-Sik-1');

wait(0$10);

if IsInArea(JMM, ruDefendArea) or IsInArea(IsInUnit(JMM), ruDefendArea) then
   begin
   Say(Sikorski, 'D7a-Sik-1');
   sikorskiStatus := 1;
   end
else
   begin
   Say(Sikorski, 'D7b-Sik-1');
   sikorskiStatus := 0;
   end;

wait(0$45);

if IsInArea(JMM, ruDefendArea) or IsInArea(IsInUnit(JMM), ruDefendArea) or not IsOk(JMM) then
   begin
   Say(Sikorski, 'D7a-Sik-1');
   sikorskiStatus := 1;
   end
else
   begin
   Say(Sikorski, 'D7b-Sik-1');
   sikorskiStatus := 0;
   end;
End;

Export Function SikorskiKilledInAction;
begin
wait(0$2);

DialogueOn;

dwait(0$1);

SayRadio(Powell, 'D8b-Pow-1');
YouLost('Dismissed');
DialogueOff;
End;

// end mission
Every 0$2 trigger jmmArrival and IsDead(ruDep) and IsDead(ruCommander) and FilterAllUnits([[f_side, 3], [f_btype, b_barracks]]) = 0 do
var i, tmp, prefix;
begin
Video(true);

PlaceSeeing(104, 108, 1, -36);
CenterOnXY(104, 108);

tmp := FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_not, [f_lives, 249]]]);

if tmp then
   for i in tmp do
       SetLives(i, 249);

for i in FilterAllUnits([[f_side, 3], [f_type, unit_human]]) do
    begin
    ComExit(i);
    AddComMoveXY(i, 131, 127);
    AddComMoveXY(i, 141, 147);
    end;

AddComAgressiveMove(FilterAllUnits([[f_enemy, 3], [f_not, [f_type, unit_building]]]), 131, 127);

wait(0$08);

DialogueOn;

if sikorskiStatus = 1 and IsOk(Sikorski) then    
   Say(Sikorski, 'D9-Sik-1');        

if FilterAllUnits([f_side, 4]) = 0 and sikorskiArriveCounter then
   SetAchievement('ACH_BLITZKRIEG');

if tick <= 25$08 then
   begin
   wait(3);
   SetAchievement('ACH_ASPEED_8');
   end;

AddMedal('med1', 1);
AddMedal('med2', 1);

if sikorskiStatus = 1 then
   AddMedal('perfect', 1)
else
   AddMedal('perfect', -1);

if Difficulty >= 3 then
   SetAchievementEX('ACH_AMER', 8);

GiveMedals('Main');

RewardPeople(FilterAllUnits([[f_side, 1], [f_nation, 1], [f_type, unit_human]]));

prefix := '08_';

SaveCharacters(JMM, prefix & 'JMM');

if IsLive(Brown) then
   SaveCharacters(Brown, prefix & 'Brown');

if IsLive(Donaldson) then
   SaveCharacters(Donaldson, prefix & 'Donaldson');

if IsLive(Bobby) then
   SaveCharacters(Bobby, prefix & 'Bobby');

if IsLive(Cyrus) then
   SaveCharacters(Cyrus, prefix & 'Cyrus');

if IsLive(Lisa) then
   SaveCharacters(Lisa, prefix & 'Lisa');

if IsLive(Frank) then
   SaveCharacters(Frank, prefix & 'Frank');

if IsLive(Gladstone) then
   SaveCharacters(Gladstone, prefix & 'Gladstone');

if IsLive(Kikuchi) then
   SaveCharacters(Kikuchi, prefix & 'Kikuchi');

if IsLive(Denis) then
   SaveCharacters(Denis, prefix & 'Denis');

if IsLive(Joan) then
   SaveCharacters(Joan, prefix & 'Joan');

if IsLive(Khatam) then
   SaveCharacters(Khatam, prefix & 'Khatam');

if IsLive(Houten) then
   SaveCharacters(Houten, prefix & 'Houten');

SaveCharacters(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_nation, 1]]) diff [JMM, Brown, Donaldson, Bobby, Cyrus, Lisa, Frank, Gladstone, Kikuchi, Denis, Joan, Houten, Khatam], '08_other_survivors');
SaveBase(gammaBaseArea);
SaveVariable(sikorskiStatus, '08_SikorskiStatus');

YouWin;
End;


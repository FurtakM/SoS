On VehicleConstructionStarted(building, chasis, engine, control, weapon) Do
begin
if weapon = us_siberium_rocket and eventVar1 = 0 then
   begin
   eventVar1 := 1;

   DialogueOn;
   Say(JMM, 'D6a-JMM-1');
   SayRadio(Powell, 'D6a-Pow-1');
   DialogueOff;

   wait(3$0);
   PowellSupport;
   end;
End;

On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

if un = JMM then
   YouLost('JMM');

if GetWeapon(un) = ar_control_tower then
   achivCounter := achivCounter + 1;

if GetSide(un) <> 1 and GetType(un) = unit_vehicle then
   destroyCounter := destroyCounter + 1;
End;

On VehicleCaptured(newId, oldId, oldSide, capturingUnit) do
begin
if oldId = Masha then
   SetAchievement('ACH_MASHA');
End;

On VehicleConstructed(vehicle, building) Do
begin
SOS_VehicleConstructed(vehicle, building);

if GetWeapon(vehicle) = us_siberium_rocket and eventVar2 = 0 then
   begin
   eventVar2 := 1;

   DialogueOn;
   Say(JMM, 'D6b-JMM-1');
   SayRadio(Powell, 'D6b-Pow-1');
   DialogueOff;

   SetAreaMapShow(TestArea, 1);
   SetAreaMapShow(RadarArea, 1);

   ChangeMissionObjectives('M3');
   end;
End;

On SiberiteRocketExploded(nn, x, y) Do
begin
if InArea(x, y, TestArea) and eventVar3 = 0 then
   begin
   eventVar3 := 1;

   wait(0$5);

   if FilterUnitsInArea(RadarArea, [[f_side, 1], [f_type, unit_vehicle], [f_weapon, us_radar]]) then
      begin
      radarOnSpot := 1;
      ChangeMissionObjectives('M3a');
      end
   else
      ChangeMissionObjectives('M3b');

   DialogueOn;    

   Say(Stevens, 'D8-Huck-1');
   Say(JMM, 'D8-JMM-1');

   if IsOk(Stevens) then
      begin
      Say(Stevens, 'D8-Huck-2');
      Say(JMM, 'D8-JMM-2');
      Say(Stevens, 'D8-Huck-3');
      Say(JMM, 'D8-JMM-3');
      Say(Stevens, 'D8-Huck-4');
      end;

   DialogueOff;

   SetAreaMapShow(TestArea, 0);
   SetAreaMapShow(RadarArea, 0);
   SetAreaMapShow(SibRadiationArea, 1);

   ChangeMissionObjectives('M5');
   end;
End;

On EnterBuilding(building, human) do
begin
if IsOk(AmSciSib) and human = AmSciSib and eventVar4 = 0 and eventVar3 = 1 and
   (GetLabKind(building, 1) = b_lab_siberium or GetLabKind(building, 2) = b_lab_siberium) then
   eventVar4 := 1;
End;

On SibDepositContaminated(unit, x, y) do //[ [91, 111],[23, 37] ]
var i, tmp;
begin
if GetNation(unit) = nation_arabian then
   begin
   sibDestroyedCounter := sibDestroyedCounter + 1;

   for i := sibDeposits downto 1 do
       if sibDeposits[i][1] = x and sibDeposits[i][2] = y then
          begin
          sibDeposits := Delete(sibDeposits, i);
          break;
          end;

   if eventVar5 = 0 and HexInfo(x, y) in FilterAllUnits([f_side, 1]) then
      begin
      eventVar5 := 1;

      DialogueOn;
      Say(Stevens, 'D4-Sci1-1');
      Say(JMM, 'D4-JMM-1');
      Say(Stevens, 'D4-Sci1-2');
      Say(JMM, 'D4-JMM-2');
      Say(Stevens, 'D4-Sci1-3');
      Say(JMM, 'D4-JMM-3');
      Say(JMM, 'D4-JMM-3a');
      DialogueOff;
      end;
   end;
End;

On BuildingStarted(building, unit) do
begin
if GetSide(building) = 1 and GetBType(building) = b_depot and FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]) = 1 then
   SetBName(building, 'omega');
End;

On Command(cmd) do
begin
SOS_Command(cmd);
End;

On SailEvent(event) do
begin
if event = eventArabianAttack then
   ArabianAttack;

if event = eventRussianAttack then
   RussianAttack;
End;

On SailEventCustom(event, p1, p2, p3, p4, p5, p6) do
begin
if event = eventContaminate then
   begin
   if IsOk(p1) and ValidHex(p2, p3) then
      begin
      repeat
       wait(0$1);

       if not HasTask(p1) then
          begin
          ComContaminate(p1, p2, p3);
          AddComMoveXY(p1, 42, 3);
          end;
      until not IsOk(p1);
      end;
   end;
End;

On EvacuateVehicle(driver, vehicle, p1, p2) do
begin
if GetSide(driver) in [3, 8] then
   begin
   ComMoveXY(driver, 41, 2);

   repeat
    wait(0$1);

    if IsDead(driver) then
       exit;

   until IsAt(driver, 41, 2);

   RemoveUnit(driver);
   end;
End;
Export ruAttackers, ruAttackersInForest;
Export function PrepareRussian;
var i;
begin
ruAttackers := [];
ruAttackersInForest := [];

if Difficulty < 3 then
   RemoveUnit(HexInfo(130, 81));

RespawnInside(3, 3, rudepo, rand(RuSolMinLevel, RuSolMaxLevel), 2, 4);
RespawnInside(3, 3, ruarm1, rand(RuSolMinLevel, RuSolMaxLevel), 1, 4);
RespawnInside(3, 3, ruarm2, rand(RuSolMinLevel, RuSolMaxLevel), 1, 2);
RespawnInside(3, 3, rufab, rand(RuSolMinLevel, RuSolMaxLevel), 3, 6);
RespawnInside(3, 3, rufabsupp, rand(RuSolMinLevel, RuSolMaxLevel), 3, 6);
RespawnInside(3, 3, rulab, rand(RuSolMinLevel, RuSolMaxLevel), 4, 2);

for i in FilterAllUnits([[f_side, 3], [f_type, unit_building], [f_btype, b_bunker]]) do
    RespawnInside(3, 3, i, rand(RuSolMinLevel, RuSolMaxLevel), 1, 1);

for i in FilterAllUnits([[f_side, 3], [f_type, unit_building], [f_btype, b_breastwork]]) do
    PrepareSolBun(rand(RuSolMinLevel, RuSolMaxLevel), i);

ResAllTechs(3, rulab, 3);
End;

Every 0$2 trigger FilterAllUnits([[f_side, 3], [f_engine, engine_combustion]]) do
var i;
begin
enable;

for i in FilterAllUnits([[f_side, 3], [f_engine, engine_combustion]]) do
    if GetFuel(i) < 3 then
       SetFuel(i, 3);
End;

// send troops to forest
Every 10$00 trigger Difficulty > 1 do
var i, un, points, tag;
begin
uc_side := 3;
uc_nation := 3;

for i := 1 to 2 do
    begin
    InitHC_All();
    PrepareSoldier(false, [4, 5, 6, 6][Difficulty]);
    un := CreateHuman;
    PlaceUnitXYR(un, 91, 87, 3, false);
    ruAttackersInForest := Replace(ruAttackersInForest, ruAttackersInForest + 1, un);

    ComAgressiveMove(un, 46, 78);
    end;

if Difficulty > 2 then
   begin
   for i := 1 to 3 do
       begin
       InitHC_All();
       PrepareSoldier(false, [4, 5, 6, 6][Difficulty]);
       un := CreateHuman;
       PlaceUnitXYR(un, 91, 87, 3, false);
       ruAttackersInForest := Replace(ruAttackersInForest, ruAttackersInForest + 1, un);

       ComAgressiveMove(un, 89, 148);
       end;
   end;

points := [ [33,61], [49,85], [76,99], [81,135], [111,155], [147,158] ];

repeat
 wait(0$30);

 for i := 1 to ruAttackersInForest do
     begin
     un := ruAttackersInForest[i];
     tag := GetTag(un);

     if IsOk(un) and not HasTask(un) then
        begin
        if tag <> i then
           begin
           SetTag(un, i);
           tag := i;
           end
        else
           begin
           SetTag(un, i + 1);
           tag := i + 1;
           end;

        ComAgressiveMove(un, points[tag][1], points[tag][2]);
        end;
     end;

until not ruAttackersInForest;
End;

Every 0$2 trigger FilterUnitsInArea(RussianBase, [f_side, 1]) do
var i, tmp, bar, e;
begin
enable;

tmp := FilterAllUnits([[f_side, 3], [f_class, class_soldier], [f_tag, 111]]);
bar := FilterAllUnits([[f_side, 3], [f_ok], [f_or, [f_btype, b_armoury], [f_btype, b_barracks]]]);

if not tmp then
   begin    
   if not bar then
      begin
      disable;
      exit;
      end;

   tmp := [];

   for i in bar do
       tmp := tmp union UnitsInside(i);

   if not tmp then
      begin
      disable;
      exit;
      end;

   for i in tmp do
       SetTag(i, 111);
   end
else
   begin
   e := FilterUnitsInArea(RussianBase, [f_side, 1]);

   for i in tmp do
       if IsOk(i) then
          begin
          if IsInUnit(i) then
             ComExitBuilding(i);

          wait(3);

          ComAttackUnit(i, NearestUnitToUnit(e, i));
          AddComEnterUnit(i, bar[rand(1, bar)]);
          end;
   end;
End;

Every 3$00 trigger tick > 2$00 and Prob(50) do
var un;
begin
uc_side := 3;
uc_nation := nation_russian;

PrepareEngineer(false, 2);
un := CreateHuman;
PlaceUnitXYR(un, 51, 7, 12, true);

wait(0$1);

ComMoveXY(un, 106, 83);
End;

Every 3$30 trigger tick > 10$00 and Prob(45) do
var un;
begin
uc_side := 3;
uc_nation := nation_russian;

PrepareScientist(false, 2);
un := CreateHuman;
PlaceUnitXYR(un, 91, 18, 15, true);

wait(0$1);

ComMoveXY(un, 106, 83);
AddComEnterUnit(un, ruarm2);
AddComChangeProfession(un, class_soldier);
End;
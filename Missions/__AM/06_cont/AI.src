Every 0$1 do ResAllTechs(3, rulab, 3);

export enemyWayX, enemyWayY;
Every 3$0 do
var rnd;
begin
     enemyWayX = [];
     enemyWayY = [];

     rnd = rand(1, 2);

     if rnd = 1 then
     begin
          enemyWayX = [93, 84, 104];
          enemyWayY = [51, 35, 28];
     end;

     if rnd = 2 then
     begin
          enemyWayX = [93, 52, 34, 28, 30];
          enemyWayY = [51, 45, 35, 26, 20];
     end;

     enable;

end;

Every 0$1 do
begin
     enable;
     RepairAndRebuildBase(3, rudepo, RussianBase, ruDestroyedBuildings);
     CraneRepairBuild(3, RussianRepairArea, ruDestroyedBuildings);
     if disableHealAI = 0 then HealUnitInBase(3, RussianHealArea, RussianBase, [rulab]);
     CollectCrates(3, 51, CratesSpawn1);
     RepairVehicles(3, RussianRepairArea, 121, 100);
     AttackVehicles(3, RussianTanksSpot, enemyWayX, enemyWayY);
     EnterBunkerOrBarrack(3, RussianBase);
     AttackNearBuildings(3, 1);
     if IsDying(rudepo) or GetSide(rudepo) = 1 then DefendDepot(3, rudepo);

end;

Every 10$0 trigger FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_driving], [f_not, [f_weapon, 51]]]) = 0 and GetSide(rufab) = 3 and IsOk(rufab) and GetSide(rudepo) = 3 and IsOk(rudepo)  do
begin
    enable;
    SetResourceType(GetBase(rudepo), 1, 1000);
    BuildingVehicleAndAttackEnemy(3, rufab, [5, 6, 6][Difficulty], [ [43, 43, 44, 44, 46], [43, 43, 43, 44, 44, 46], [43, 43, 43, 44, 44, 46] ][Difficulty], 1, 1, [22, [22, 24][Rand(1,2)], 24][Difficulty]);
end;

Every 5$0 trigger GetSide(rufabsupp) = 3 and IsOk(rufabsupp) and GetSide(rudepo) = 3 and IsOk(rudepo) do
begin
   enable;
   AddComConstruct(rufabsupp, 22, 1, 1, [42, 43, 44][Rand(1,3)]);
   AddComConstruct(rufabsupp, 22, 1, 1, ru_cargo_bay);
end;

Every 1$0 trigger IsOK(rufabsupp) and GetSide(rufabsupp) = 3 and IsOk(rudepo) and GetSide(rudepo) = 3 and NOT UnitsInside(rufabsupp) do
var i;
begin

for i = 1 to 6 do
begin
uc_side = 3;
uc_nation = 3;

hc_gallery := '';
hc_name := '';

PrepareMechanic(false, rand(RuSolMinLevel, RuSolMaxLevel));
PlaceHumanInUnit(CreateHuman, rufabsupp);
end;

end;

// Tame apemans
Every 2$0 do 
var i, j;
begin
    wait([7$0, 16$0][Rand(1,2)]);
    disableHealAI = 1;

    for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do ComExitBuilding(i);
    wait(0$1);

    repeat
        wait(0$1);
        for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do
        begin
            for j in FilterUnitsInArea(ApemansArea, [[f_side, 0], [f_class, 12]]) do 
            begin
                AddComTameXY(i, GetX(j), GetY(j));
            end;
        end;

    until FilterAllUnits([[f_side, 3], [f_class, 4]]) = 0 or tamedApemans >= [6, 7, 8][Difficulty] or FilterUnitsInArea(ApemansArea, [[f_side, 0], [f_class, 12]]) = 0;

    disableHealAI = 0;

    for i in FilterAllUnits([[f_side, 3], [f_class, 12]]) do SetClass(i, 16);
    for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do ComMoveXY(i, 113, 94);

end;

// Build new cargo

Every 0$1 trigger GetSide(rufab) = 3 and IsOK(rufab) and FilterAllUnits([[f_side, 3], [f_weapon, ru_cargo_bay]]) = 0 do
begin
    AddComConstruct(rufab, 23, 1, 1, ru_cargo_bay);
    wait(5$0);
    enable;
end;

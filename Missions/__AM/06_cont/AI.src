Every 0$2 do
begin
enable;

RepairAndRebuildBase(3, rudepo, RussianBase, ruDestroyedBuildings);
CraneRepairBuild(3, RussianRepairArea, ruDestroyedBuildings);

if disableHealAI = 0 then
   HealUnitInBase(3, RussianHealArea, RussianBase, [rulab]);

CollectCrates(3, 51, CratesSpawn1);

if IsDying(rudepo) or GetSide(rudepo) = 1 then
   DefendDepot(3, rudepo)
else
   EnterBunkerOrBarrack(3, RussianBase);
End;

Every 10$0 trigger FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_driving], [f_not, [f_weapon, 51]]]) = 0 and GetSide(rufab) = 3 and IsOk(rufab) and GetSide(rudepo) = 3 and IsOk(rudepo) and UnitsInside(rufab) and ruAttackers < [4, 5, 6, 6][Difficulty] do
begin
enable;
SetResourceType(GetBase(rudepo), 1, 2000);
BuildingVehicle(3, rufab, ([4, 5, 6, 6][Difficulty]) - ruAttackers, [ [43, 43, 44, 44], [43, 43, 43, 44, 44, 46], [43, 43, 43, 44, 44, 46], [43, 43, 44, 46, 46, 46] ][Difficulty], 1, 1, [22, [22, 24][Rand(1,2)], 24, 24][Difficulty]);
End;

Every 2$00 + 10$0 trigger GetSide(rufabsupp) = 3 and IsOk(rufabsupp) and GetSide(rudepo) = 3 and IsOk(rudepo) and ruAttackers < [4, 5, 6, 7][Difficulty] do
begin
enable;
AddComConstruct(rufabsupp, 22, 1, 1, [42, 43, 44][Rand(1,3)]);
End;

// Tame apemans
Every 2$0 do 
var i, j;
begin
wait([7$0, 16$0][Rand(1,2)]);
disableHealAI = 1;

for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do
    ComExitBuilding(i);

wait(0$1);

repeat
 wait(0$1);

 for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do
     for j in FilterUnitsInArea(ApemansArea, [[f_side, 0], [f_class, 12]]) do
         AddComTameXY(i, GetX(j), GetY(j));
until FilterAllUnits([[f_side, 3], [f_class, 4]]) = 0 or tamedApemans >= [6, 7, 8, 8][Difficulty] or FilterUnitsInArea(ApemansArea, [[f_side, 0], [f_class, 12]]) = 0;

disableHealAI = 0;

for i in FilterAllUnits([[f_side, 3], [f_class, 12]]) do
    SetClass(i, 16);

for i in FilterAllUnits([[f_side, 3], [f_class, 4]]) do
    ComEnterUnit(i, rulab);
End;

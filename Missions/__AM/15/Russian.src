Export Platonov, Kovalyuk, Bystrov, Yakotich, Gleb, Bierezov;
Export Vsevolod, sewiVeh;
Export behemoths, behemothBuilders;
Export Function PrepareRussian;
var i, j, base, tmp, side, xy, un, b, teleport;
begin
side := 3;

uc_side := side;
uc_nation := 3;

for i in FilterAllUnits([[f_side, side], [f_type, unit_building]]) do
    SetBLevel(i, 10);

teleport := FilterAllUnits([[f_side, side], [f_btype, b_teleport]]);

if teleport then
   TeleportExit(teleport[1], 123, 122);

Platonov := NewCharacter('Platonov');
Kovalyuk := NewCharacter('Kovalyuk');
Yakotich := PrepareUnit('Yakotich', (not debug), '09_');
Bystrov := NewCharacter('Bystrov');
Gleb := NewCharacter('Gleb');

if TestCharacters('03_Cornel') then
   begin
   Bierezov := NewCharacter('Mikhail');
   PlaceUnitXYR(Bierezov, 197, 111, 9, false);
   SetClass(Bierezov, 3);
   end;

PlaceHumanInUnit(Platonov, ru_depot);
PlaceHumanInUnit(Kovalyuk, ru_fac1);

PlaceUnitXYR(Yakotich, 197, 111, 9, false);
PlaceUnitXYR(Bystrov, 197, 111, 9, false);
PlaceUnitXYR(Gleb, 197, 111, 9, false);

// PrepareBase(base_dep, area, name, skill, sources, personel);
tmp := PrepareBase(ru_depot, russianBaseArea, 'zhukov', [9, 10, 10, 10][Difficulty], [90000, 1000, 300], [18, 8, 12, 6]);

mc_bases := Replace(mc_bases, 2, tmp union [Yakotich, Bystrov, Gleb, Bierezov]);

// second base
base := GetBase(ru_depot2);

SetResourceType(base, mat_cans, 5000);
SetResourceType(base, mat_oil, 200);
SetResourceType(base, mat_siberit, 200);

for i := 1 to 6 do
    begin
    PrepareSoldier(false, [8, 9, 10, 10][Difficulty]);
    un := CreateHuman;

    if i mod 2 = 0 then
       SetClass(un, class_bazooker);

    PlaceHumanInUnit(un, ru_specBar);
    end;

PrepareVehicle(ru_medium_wheeled, engine_siberite, control_computer, ru_crane, 100);
PlaceUnitXYD(CreateVehicle, 259, 145, 3, false);
PlaceUnitXYD(CreateVehicle, 245, 139, 3, false);

// behemoths
behemoths := [];
behemothBuilders := [];

if Kovalyuk then
   behemothBuilders := Join(behemothBuilders, Kovalyuk);
End;

Export Function BuildBehemoths;
var i, j, xy, tmp;
begin
xy := [ [221,154], [223,149], [228,147], [232,155] ];

if not behemothBuilders then
   exit;

j := 1;

for i in behemothBuilders do
    begin
    if GetClass(i) <> class_mechanic then
       SetClass(i, class_mechanic);

    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComBuildBehemoth(i, b_behemoth, xy[j][1], xy[j][2], 0);
    j := j + 1;
    end;
End;

Every 0$1 trigger FilterUnitsInArea(behemothParking, [f_btype, b_behemoth]) do
behemothUnderConstruct := true;

// Send behemoth attack
Every 0$3 trigger GetBehemoths(3) and not FilterAllUnits([[f_side, 3], [f_btype, b_behemoth]]) do
var i, tmp;
begin
enable;

tmp := GetBehemoths(3);

for i in tmp do
    begin
    if IsInArea(i, powellBase) and not GetTag(i) = 2 then
       SetTag(i, 2);

    if not FilterAllUnits([[f_enemy, 3], [f_dist, i, 12]]) and not GetTag(i) = 2 then
       ComAgressiveMove(i, 64, 93)
    else
       ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 3]), i));
    end;
End;

Export Function PrepareRussianAttack(strength);
var i, j;
begin
result := [];

uc_side := 6;
uc_nation := 3;

case strength of
1: begin
   for i = 1 to [4, 5, 6, 7][Difficulty] do
       begin
       PrepareVehicle([ru_medium_tracked, ru_heavy_tracked][i mod 2 + 1],
                      [engine_combustion, engine_siberite][rand(1,2)],
                      control_computer,
                      [ru_gatling_gun, ru_gun, ru_rocket_launcher][rand(1,3)],
                      80
                     );
       result := result union CreateVehicle;
       end;
   end;
2: begin
   for i = 1 to [5, 6, 7, 8][Difficulty] do
       begin
       PrepareVehicle([ru_medium_tracked, ru_heavy_tracked, ru_heavy_tracked][i mod 3 + 1],
                      [engine_combustion, engine_siberite][rand(1,2)],
                      control_computer,
                      [ru_gatling_gun, ru_gun, ru_rocket_launcher,
                       ru_gun, ru_heavy_gun, ru_heavy_gun, ru_time_lapser, ru_time_lapser][i mod 8 + 1],
                      80
                     );
       result := result union CreateVehicle;
       end;
   end;
3: begin
   for i = 1 to [6, 7, 8, 9][Difficulty] do
       begin
       PrepareVehicle([ru_medium_tracked, ru_heavy_tracked, ru_heavy_tracked][i mod 3 + 1],
                      [engine_combustion, engine_siberite][rand(1,2)],
                      control_computer,
                      [ru_gatling_gun, ru_rocket, ru_rocket_launcher,
                       ru_rocket_launcher, ru_heavy_gun, ru_heavy_gun, ru_time_lapser, ru_time_lapser, ru_time_lapser][i mod 9 + 1],
                      80
                     );
       result := result union CreateVehicle;
       end;
   end;
4: begin
   uc_nation := 3;

   for i = 1 to [6, 8, 9, 10][Difficulty] do
       begin
       PrepareVehicle([ru_medium_tracked, ru_heavy_tracked, ru_heavy_tracked][i mod 3 + 1],
                      [engine_combustion, engine_siberite][rand(1,2)],
                      control_computer,
                      [ru_rocket_launcher, ru_rocket, ru_rocket,
                       ru_rocket_launcher, ru_heavy_gun, ru_heavy_gun, ru_time_lapser, ru_time_lapser, ru_time_lapser][i mod 9 + 1],
                      80
                     );
       result := result union CreateVehicle;
       end;

   if not KappaStatus then
      begin
      uc_nation := 1;

      for i = 1 to 3 do
          begin
          j := rand(0,1);
          PrepareVehicle([us_medium_tracked, us_morphling, us_morphling, us_heavy_tracked][j + rand(1,3)],
                         [engine_combustion, engine_siberite][rand(1,2)],
                         control_computer,
                         [us_laser, us_rocket_launcher, us_heavy_gun][j + rand(1,2)],
                         85
                        );
          result := result union CreateVehicle;
          end;

      if vsevolodFirstAttack then
         begin
         vsevolodFirstAttack := false;

         PrepareVehicle(us_morphling, engine_siberite, control_manual, us_heavy_gun, 100);
         sewiVeh := CreateVehicle;
         SetMark(sewiVeh, 1);

         result := result union sewiVeh;
         end;
      end
   else
      if vsevolodFirstAttack then
         begin
         vsevolodFirstAttack := false;

         uc_nation := 3;

         for i = 1 to 2 + Difficulty do
             begin
             PrepareVehicle([ru_medium_tracked, ru_heavy_tracked, ru_heavy_tracked][i mod 3 + 1],
                            [engine_combustion, engine_siberite][rand(1,2)],
                            control_manual,
                            [ru_rocket_launcher, ru_rocket, ru_rocket][i mod 3 + 1],
                            80
                           );
             result := result union CreateVehicle;
             end;

         PrepareVehicle(ru_heavy_tracked, engine_siberite, control_manual, ru_rocket, 100);
         sewiVeh := CreateVehicle;
         SetMark(sewiVeh, -6);

         result := result union sewiVeh;
         end;
   end;
end;

End;

// Purple attacks
Every 0$30 trigger activeAttacks do
var i, tmp, time, areas, strength, xy;
begin
areas := [southSpawn, southWestSpawn];
time := [5$30, 4$50, 4$40, 4$30][Difficulty];
xy := [ [70, 118], [78, 31] ];

repeat
 if missionStage = 2 then
    wait(1$30)
 else
    wait(time);

 if missionStage = 6 or Difficulty > 2 then
    begin
    SetTech(tech_weap1, 6, state_researched);
    SetTech(tech_comp1, 6, state_researched);
    end;

 if missionStage = 8 then
    begin
    SetTech(tech_weap2, 6, state_researched);
    SetTech(tech_comp2, 6, state_researched);
    end;

 if Difficulty = 4 or missionStage = 10 then
    begin
    SetTech(tech_weap3, 6, state_researched);
    SetTech(tech_comp3, 6, state_researched);
    end;

 if activeAttacks then
    begin

    if missionStage = 2 then
       strength := 1;

    if missionStage > 2 then
       strength := 2;

    if missionStage > 6 then
       strength := 3;

    if missionStage > 10 then
       strength := 4;

    tmp := PrepareRussianAttack(strength);

    for i in tmp do
        begin
        PlaceUnitArea(i, areas[rand(1,2)], false);

        if i = sewiVeh then
           begin
           Vsevolod = PrepareUnit('Vsevolod', false, '');
           PlaceHumanInUnit(Vsevolod, i);
           end
        else
           if GetControl(i) = control_manual then
              begin
              uc_side := 6;
              uc_nation := 3;

              hc_gallery := '';
              hc_name := '';

              PrepareHuman(false, 3, 10);
              PlaceHumanInUnit(CreateHuman, i);
              wait(0$0.3);
              end;

        case rand(1,3) of
             1: ComMoveXY(i, 111, 197);
             2: ComMoveXY(i, 91, 165);
             3: ComMoveXY(i, 137, 157);
             end;

        wait(0$2);
        end;

    repeat
     wait(0$1);

     for i in UnitFilter(tmp, [f_ok]) do
         AgressiveMove(i, [ [108, 153], [105, 149], [85, 131], [64, 105] ]);

    until not UnitFilter(tmp, [[f_not, [f_weapon, ru_time_lapser]], [f_ok]]);
    end;
until russianDestroyed;
End;

// Platonov attacks
Every 7$30 trigger not russianDestroyed and missionStage >= 6 do
var i, tmp, target, teleport, p;
begin
enable;

if not FilterAllUnits([[f_side, 3], [f_btype, b_factory]]) then
   exit;

teleport := FilterAllUnits([[f_side, 3], [f_btype, b_teleport]]);

if Prob(40) then
   begin
   MC_InsertProduceList(2, [
      [ru_medium_tracked, engine_siberite, control_computer, ru_time_lapser],
      [ru_medium_tracked, engine_siberite, control_computer, ru_time_lapser],
      [ru_medium_tracked, engine_siberite, control_computer, ru_time_lapser],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun]
   ]);
   end
else
   begin
   MC_InsertProduceList(2, [
      [ru_heavy_tracked, engine_siberite, control_computer, ru_rocket],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_rocket],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_rocket],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun],
      [ru_heavy_tracked, engine_siberite, control_computer, ru_heavy_gun]
   ]);
   end;

wait(2$00);

//tmp := MC_GetVehicles(2, true);
tmp := FilterAllUnits([[f_side, 3], [f_nation, 3], [f_type, unit_vehicle],
                      [f_not, [f_weapon, ru_siberium_rocket]],
                      [f_not, [f_weapon, ru_cargo_bay]],
                      [f_not, [f_weapon, ru_crane]]]) diff mc_defender[2];
mc_vehicles := Replace(mc_vehicles, 2, mc_vehicles[2] diff tmp);

target := rand(0, 1);
p := 0;

if target then
   begin
   repeat
    tmp := UnitFilter(tmp, [f_ok]);

    for i in tmp do
        if GetDistUnitXY(i, 139, 89) > 9 then
           ComMoveXY(i, 139, 89);

    wait(0$1);
    p := Inc(p);
   until UnitFilter(tmp, [f_distxy, 139, 89, 9]) >= (tmp-1) or p > 60;
   end
else
   begin
   ComEnterUnit(tmp, teleport[1]);

   repeat
    tmp := UnitFilter(tmp, [f_ok]);

    for i in tmp do
        if GetDistUnitXY(i, 124, 139) > 9 then
           ComMoveXY(i, 124, 139);
           
    wait(0$1);
    p := Inc(p);
   until UnitFilter(tmp, [f_distxy, 124, 139, 9]) >= (tmp-1) or p > 60;
   end;

repeat
 wait(0$1);

 tmp := UnitFilter(tmp, [f_ok]);

 for i in tmp do
     begin
     if GetWeapon(i) = ru_time_lapser then
        ComMoveUnit(i, NearestUnitToUnit(FilterAllUnits([f_or, [f_side, 1], [f_side, 8]]), i))
     else
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_or, [f_side, 1], [f_side, 8]]), i));
     end;
until not tmp;
End;

Export ruMobile;
Every 0$01 trigger missionStage >= 7 and Difficulty > 1 do
var i, veh, un;
begin
ruMobile := [];

for i := 1 to [4, 5, 6][Difficulty - 1] do
    begin
    uc_side := 3;
    uc_nation := 1;

    PrepareVehicle(us_morphling, engine_siberite, control_manual, [us_laser, us_rocket_launcher][i mod 2 + 1], 100);
    veh := CreateVehicle;
    SetDir(veh, 3);
    PlaceUnitArea(veh, arabianAttackArea2, false);

    uc_nation := 3;
    PrepareMechanic(false, 10);
    un := CreateHuman;
    SetTag(un, 105);

    if GetSkill(un, skill_mechanical) < [8, 9, 10][Difficulty] then
       SetSkill(un, skill_mechanical, [8, 9, 10][Difficulty]);

    PlaceHumanInUnit(un, veh);

    ruMobile := Join(ruMobile, veh);
    end;
End;

Every 0$1 trigger ruMobile do
var i, mech, enemy;
begin
enable;

if not ruMobile then
   begin
   disable;
   exit;
   end;

// tags:
// 0 - idle,
// 1 - go to parking, repair if needed
// 2 - attack

for i in ruMobile do
    begin
    if not IsOk(i) or GetSide(i) <> 3 then
       begin
       ruMobile := ruMobile diff i;
       continue;
       end;

    if GetTag(i) = 300 then
       begin
       ComMoveXY(i, 160, 81);

       if GetDistUnitXY(i, 160, 81) < 8 then
          SetTag(i, 301);
       end;

    if GetTag(i) = 301 then
       begin
       if not IsInArea(i, ruMobileParkingArea) then
          ComMoveToArea(i, ruMobileParkingArea)
       else
          SetTag(i, 302);
       end;

    if GetTag(i) = 302 then
       begin
       if GetLives(i) < 1000 then
          begin
          if not IsDrivenBy(i) then
             continue;

          mech := IsDrivenBy(i);

          ComExitVehicle(mech);
          AddComRepairVehicle(mech, i);

          repeat
           wait(0$1);
          until GetLives(i) = 1000;

          ComEnterUnit(mech, i);
          end
       else if IsDrivenBy(i) then
          SetTag(i, 0);
       end;

    if GetTag(i) < 300 then
       begin
       enemy := FilterUnitsInArea(russianBaseArea, [f_enemy, 3]);

       if GetLives(i) < 650 then
          begin
          ComStop(i);
          SetTag(i, 300);
          continue;
          end;

       if enemy then
          begin
          if not HasTask(i) then
             ComAttackUnit(i, NearestUnitToUnit(enemy, i));
          end
       else 
          AgressiveMove(i, [ [158, 61], [98, 100], [78, 93] ]);
       end;
    end;
End;

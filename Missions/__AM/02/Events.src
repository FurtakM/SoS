On UnitDestroyed(unit) Do
begin
SOS_UnitDestroyed(unit);

if unit = JMM then
   YouLost('JMM');

if GetBType(unit) = b_depot then
   YouLost('DepotDestroyed');

if GetSide(unit) = 1 and GetType(unit) = unit_human then
   deadsCounter = deadsCounter + 1;
End;

On UnitDestroyed_Extra(un, killerSide, killerUnit) do
begin
if un in FilterAllUnits([[f_side, 1], [f_nation, 1], [f_type, unit_human]]) and killerSide = 1 then
   JMMTraitor;
End;

On EvacuateVehicle(driver, veh, p1, p2) do
begin
if GetSide(driver) = 3 then
   begin
   ComMoveXY(driver, 6, 10);

   repeat
    wait(0$1);
    ComMoveXY(driver, 6, 10);

    if IsDead(driver) then
       exit;    
   until IsAt(driver, 6, 10);

   RemoveUnit(driver);
   end;
End;

On Command(cmd) do
var i, j, tmp;
begin
SOS_Command(cmd);

for i in FilterAllUnits([[f_side, 1], [f_type, unit_vehicle]]) do
    begin                 
    if GetTaskList(i) then
       begin
       for j = 1 to GetTaskList(i) do
           begin
           tmp = GetTaskList(i)[j][4];

           if (GetSide(tmp) = 3 and GetType(tmp) = unit_vehicle and GetWeapon(tmp) = ru_cargo_bay) then
              ComStop(i);
           end;
       end;
    end;
End;

On LeaveBuilding(b, un) do
begin
if un = Mikhail and mikhailStatus <> 4 then
   begin
   if IsOk(Mikhail) then
      AskMikhail
   else if IsOk(llab1) then
        begin
        InGameOn;

        SetSide(Mikhail, 6);
        SetSide(llab1, 6);
        ComEnterUnit(Mikhail, llab1);

        repeat
         wait(0$1);
        until IsInUnit(Mikhail) = llab1 or IsDead(Mikhail);

        SetSide(llab1, 1);
        InGameOff;
        end
      else
        begin
        SetAttitude(3, 6, att_enemy, true);
        SetSide(Mikhail, 6);
        ComHold(Mikhail);
        end;
   end;
End;

On VehicleConstructed(veh, fac) do
begin
SOS_VehicleConstructed(veh, fac);
End;

Every 3 trigger GetClass(Mikhail) <> 3 do
begin
enable;
SetClass(Mikhail, 3);
End;
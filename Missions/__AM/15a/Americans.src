Export Powell;
Export Function PrepareAmerican;
var i, b, veh, cl, p, tmp, depot, lab, armoury, workshop;
begin
if Difficulty = 1 then
   for i in FilterAllUnits([f_inarea, delBuildingsArea]) do
       RemoveUnit(i);

InitHc;

SetBName(am_depot, 'sigma');

uc_side := 1;
uc_nation := 1;

for i in FilterAllUnits([[f_side, 1], [f_type, unit_building]]) do
    SetBLevel(i, 10);

SetResourceType(GetBase(am_depot), mat_cans, 7500);
SetResourceType(GetBase(am_depot), mat_oil, 4000);
SetResourceType(GetBase(am_depot), mat_siberit, 50);

SetResourceType(GetBase(am_depot2), mat_cans, 5500);
SetResourceType(GetBase(am_depot2), mat_oil, 4000);
SetResourceType(GetBase(am_depot2), mat_siberit, 10);

if LoadVariable('12a_PowellKilled', false) then
   begin
   PrepareHuman(sex_male, class_soldier, 10);
   Powell := CreateHuman;
   end
else
   Powell := NewCharacter('Powell');

PlaceHumanInUnit(Powell, am_depot);

tmp := [];

// Lisa, Donaldson, Bobby, Cyrus, Brown,
// Gladstone, Houten, Cornel, Gary, Frank, Kikuchi

Lisa := PrepareUnit('Lisa', (not debug), '12p_');

if Lisa then
   tmp := tmp ^ Lisa;

Donaldson := PrepareUnit('Donaldson', (not debug), '12p_');

if Donaldson then
   tmp := tmp ^ Donaldson;

Bobby := PrepareUnit('Bobby', (not debug), '12p_');

if Bobby then
   tmp := tmp ^ Bobby;

Cyrus := PrepareUnit('Cyrus', (not debug), '12p_');

if Cyrus then
   tmp := tmp ^ Cyrus;

Brown := PrepareUnit('Brown', (not debug), '12p_');

if Brown then
   tmp := tmp ^ Brown;

Gladstone := PrepareUnit('Gladstone', (not debug), '12p_');

if Gladstone then
   tmp := tmp ^ Gladstone;

Houten := PrepareUnit('Houten', (not debug), '12p_');

if Houten then
   tmp := tmp ^ Houten;

Cornel := PrepareUnit('Cornel', (not debug), '12p_');

if Cornel then
   tmp := tmp ^ Cornel;

Gary := PrepareUnit('Gary', (not debug), '12p_');

if Gary then
   tmp := tmp ^ Gary;

Kikuchi := PrepareUnit('Kikuchi', (not debug), '12p_');

if Kikuchi then
   tmp := tmp ^ Kikuchi;

tmp := tmp union CreateCharacterSet('12p_others');

InitHc_All();

if tmp < 36 then
   for i = 1 to 36 - tmp do
       begin
       PrepareHuman(sex_male, i mod 4 + 1, 10);
       tmp := tmp ^ CreateHuman;
       end;

p := 0;

for i in tmp do
    begin
    p := Inc(p);
    PlaceUnitXYR(i, 62, 93, 9, false);

    if p > 36 then
       break;
    end;

mc_bases := Replace(mc_bases, 4, FilterAllUnits([f_side, 1]) diff Powell);

uc_side := 0;
uc_nation := 0;

for i = 1 to 4 do
    begin
    InitHc;
    hc_class := class_apeman;
    PlaceUnitArea(CreateHuman, powellApe, false);
    end;
End;

// American attacks
Every 9$30 + 7$00 trigger not americanDestroyed do
var i, tmp, target, p;
begin
enable;

if not FilterAllUnits([[f_side, 1], [f_nation, 1], [f_btype, b_factory]]) then
   exit;

if Prob(40) then
   begin
   MC_InsertProduceList(4, [
      [us_morphling, engine_combustion, control_remote, us_rocket_launcher],
      [us_morphling, engine_combustion, control_remote, us_rocket_launcher],
      [us_morphling, engine_combustion, control_remote, us_rocket_launcher],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
   ]);
   end
else
   begin
   MC_InsertProduceList(4, [
      [us_morphling, engine_combustion, control_remote, us_rocket_launcher],
      [us_morphling, engine_combustion, control_remote, us_laser],
      [us_morphling, engine_combustion, control_remote, us_laser],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
      [us_morphling, engine_combustion, control_remote, us_heavy_gun],
   ]);
   end;

p := 0;

repeat
 wait(0$1);
 p := Inc(p);
until MC_GetVehicles(4, true) >= 6 or p > 100;

wait(0$30);

tmp := FilterAllUnits([[f_side, 1], [f_type, unit_vehicle],
                      [f_not, [f_weapon, us_siberium_rocket]],
                      [f_not, [f_weapon, us_cargo_bay]],
                      [f_not, [f_weapon, us_crane]]]) diff mc_defender[4];
mc_vehicles := Replace(mc_vehicles, 4, mc_vehicles[4] diff tmp);

target := rand(0, 2);
p := 0;

if target then
   begin
   repeat
    tmp := UnitFilter(tmp, [f_lives, 250]);

    for i in tmp do
        if GetDistUnitXY(i, 114, 108) > 9 then
           ComMoveXY(i, 114, 108);

    wait(0$1);
    p := Inc(p);
   until UnitFilter(tmp, [f_distxy, 114, 108, 9]) >= (tmp-1) or p > 30;
   end
else
   begin
   repeat
    tmp := UnitFilter(tmp, [f_lives, 250]);

    for i in tmp do
        if GetDistUnitXY(i, 129, 139) > 9 then
           ComMoveXY(i, 129, 139);

    wait(0$1);
    p := Inc(p);
   until UnitFilter(tmp, [f_distxy, 129, 139, 9]) >= (tmp-1) or p > 30;
   end;

repeat
 wait(0$1);
 tmp := UnitFilter(tmp, [f_ok]);

 for i in tmp do
     ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 1]), i));
until not tmp;
End;

Every 30$00 trigger not americanDestroyed do
begin
wait([30$00, 20$00, 15$00, 10$00][Difficulty]);

if americanDestroyed then
   exit;

MC_InsertProduceList(4, [[us_morphling, engine_siberite, control_manual, us_siberium_rocket]]);
End;
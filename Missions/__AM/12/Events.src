On UnitDestroyed(unit) Do
var i;
begin

    if GetSide(unit) = 3 and GetType(unit) = unit_human then
    begin
        for i in RuScientists do 
            if i = unit then
            begin
                RuScientists = RuScientists diff [unit];
                sciCounter = sciCounter + 1;
            end; 
    end;
end;

On UnitTeleported(building, human) Do
begin
    if building = ruteleprt and GetSide(human) = 1 and eventVar1 = 0 then
    begin
        eventVar1 = 1;
        teleportExplored = 1;
        ChangeMissionObjectives('M2a');
        wait(0$2);
        DialogueOn;
        DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human]]), 'DT3a-Sol1-1', 'DT3a-FSol1-1');
        Say(JMM, 'DT3a-JMM-1');
        CenterNowOnUnits(human);
        Say(JMM, 'DT3a-JMM-1a');
        DialogueOff;
    end;

    if building = ruteleprt and GetType(human) = unit_vehicle and GetSide(human) = 3 and vehicleTeleported = 0 then begin wait(0$3); vehicleTeleported = 1; end;
    if building = ruteleprt and GetType(human) = unit_vehicle and GetSide(human) = 3 and vehicleTeleported = 2 then begin wait(0$3); vehicleTeleported = 3; end;
    if building = ruteleprt and GetType(human) = unit_vehicle and GetSide(human) = 3 then tpCounter = tpCounter + 1;
end;

On VehicleConstructed(vehicle, factory) Do
begin
    if factory = ru_fact or factory = rufab2 then
        if GetSide(vehicle) = 3 then
        begin
            if GetWeapon(vehicle) = 46 or GetWeapon(vehicle) = 45 or GetWeapon(vehicle) = 44 then
            begin
                russianTanks = russianTanks ^ vehicle;
                if attackTactic = 1 or attackTactic = 0 then ComEnterUnit(vehicle, ruteleprt);
                if attackTactic = 2 then ComMoveXY(vehicle, 88, 69);
            end;

            if GetWeapon(vehicle) = 49 then
            begin
                russianTimeLapsers = russianTimeLapsers ^ vehicle;
                ComEnterUnit(vehicle, rutp);
            end;

        end;
end;

On UnitGoesToRed(unit) Do
var i;
begin
    if GetSide(unit) = 3 and GetType(unit) = unit_vehicle then
    begin
        if russianTanks > 0 then
            for i in russianTanks do
                if i = unit then russianTanks = russianTanks diff [unit];

        if russianTimeLapsers > 0 then
            for i in russianTimeLapsers do
                if i = unit then russianTimeLapsers = russianTimeLapsers diff [unit];
    end;
end;

On LeaveVehicle(vehicle, human) Do
var i;
begin
    if GetSide(human) = 3 and GetClass(human) = 3 and GetSide(ru_fact) = 3 and IsOk(ru_fact) then ComEnterUnit(human, ru_fact);


    if GetClass(human) = 4 and GetSide(human) = 3 and ruAI = 0 then
       for i in RuScientists do
           if i = human then
              ComMoveToArea(i, [EscapeArea1, EscapeArea2][Rand(1,2)]);
end;

On EnterVehicle(vehicle, human) Do
var i, rnd;
begin
     if GetSide(human) = 3 and GetClass(human) = 4 then
        for i in RuScientists do
            if i = human then
               if IsOk(rutp) then
               begin
                  rnd = rand(1, 2);
                  if rnd = 1 then ComEnterUnit(vehicle, rutp);
                  AddComMoveToArea(vehicle, [EscapeArea1, EscapeArea2][Rand(1,2)]);
               end else
               begin
                  ComMoveToArea(vehicle, [EscapeArea1, EscapeArea2][Rand(1,2)]);
               end;
end;
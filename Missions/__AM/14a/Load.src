Export Function LoadBuildings;
var i, tmp, b, j;
begin
tmp := LoadVariable('13a_buildings', [ [7, 1, b_warehouse, 70, 46, 3, 10, 0, 0, 0] ]);

bc_kind1 := -1;
bc_kind2 := -1;

// GetSide(i), GetNation(i), GetBType(i), GetX(i), GetY(i), GetDir(i), GetBLevel(i), GetLabKind(i, 1), GetLabKind(i, 2), GetBWeapon(i)
for i in tmp do
    begin
    uc_side := 7;
    uc_nation := i[2];

    if i[2] = 2 then
       uc_side := 4;

    bc_type := i[3];

    if i[3] in [b_oil_mine, b_siberite_mine] then
       SetResourceVisibility(i[4], i[5], i[1]);

    if i[8] then
       bc_kind1 := i[8];

    if i[9] then
       bc_kind2 := i[9];

    b := CreateAndPlaceBuildingXYD(i[4], i[5], i[6]);
    SetBLevel(b, i[7]);

    if i[10] then
       PlaceWeaponTurret(b, i[10]);
    end;

if HexInfo(144, 65) = 0 then
   begin
   uc_side := 4;
   bc_type := b_warehouse;
   b := CreateAndPlaceBuildingXYD(144, 65, 1);
   end;

for i in FilterAllUnits([[f_side, 7], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]) do
    for j = 1 to 3 do
        SetResourceType(GetBase(i), j, 3000 / j);

for i in FilterUnitsInArea(spot_area, [f_side, 7]) do
    RemoveUnit(i);
End;

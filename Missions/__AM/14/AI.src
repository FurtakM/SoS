Every 0$1 do begin
    ResAllTechs(3, ru_lab1, 3);
    ResAllTechs(3, ru_lab2, 3);
end;

Every 0$1 trigger russianAI = 1 do 
begin
    enable;
    RepairAndRebuildBase(3, ru_depot, RussianBase, ruDestroyedBuildings);
    HealUnitInBase(3, RussianHealArea, RussianBase, [ru_lab1, ru_lab2][Rand(1, 2)]);
    IntBazooka(3);
    CollectCrates(3, ru_cargo_bay, russianCrates);
    CraneRepairBuild(3, RussianBase);
end;

// attacks
Every 0$30 trigger russianTanks >= [5, 6, 8, 8][Difficulty] and russianTimeLapsers >= [1, 2, 3, 3][Difficulty] do
var j, tmp, target1, target2;
begin
enable;

tmp := russianTanks union russianTimeLapsers;

russianTanks := russianTanks diff tmp;
russianTimeLapsers := russianTimeLapsers diff tmp;

if IsOk(ruTeleport) then
   begin
   for j in tmp do
       if Prob(40) then
          ComEnterUnit(j, ruTeleport)
       else
          ComAgressiveMove(j, 117, 92);

   wait(0$14);
   end;

repeat
 wait(0$1);

 for j in tmp do
     begin
     if IsDead(j) then
        tmp := tmp diff j;

     target1 := NearestUnitToUnit(FilterAllUnits([f_enemy, 3]), j);
     target2 := NearestUnitToUnit(FilterAllUnits([[[f_enemy, 3], [f_type, unit_human]]]), j);

     if GetWeapon(j) = ru_time_lapser then
        begin
        if UnitFilter(tmp, [f_not, [f_weapon, ru_time_lapser]]) > 1 and UnitFilter(tmp, [f_not, [f_weapon, ru_time_lapser]]) then
           ComMoveUnit(j, NearestUnitToUnit(UnitFilter(tmp, [f_not, [f_weapon, ru_time_lapser]]), j))
        else
           ComMoveUnit(j, NearestUnitToUnit(target1, j));
        end
     else
        begin
        if GetDistUnits(j, target2) < GetDistUnits(j, target1) + 2 then
           ComAttackUnit(j, target2)
        else
           ComAttackUnit(j, target1);
        end;
     end;
until not tmp;

End;

Every 0$1 trigger IsDying(ru_depot) or GetSide(ru_depot) = 1 do DefendDepot(3, ru_depot);

Every 6$0 + 7$0 trigger russianAI = 1 and FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_ok], [f_control, 3], [f_or, [f_weapon, 46], [f_weapon, 45], [f_weapon, 44], [f_weapon, 49], [f_weapon, 47]]]) < 3 do
begin
    enable;

    if russianRocketsDisabled then
       BuildingVehicle(3, ru_fact1, [5, 6, 8, 8][Difficulty], [46, 45, 44], 3, 3, 24)
    else
       BuildingVehicle(3, ru_fact1, [5, 6, 8, 8][Difficulty], [46, 45, 44, 47], 3, 3, 24);

    if russianTimeLapsers = 0 then
       BuildingVehicle(3, ru_fact2, [1, 2, 3, 3][Difficulty], [49, 49], 3, 3, 24);
end;


// steal JMM car
Every 0$10+0$30 trigger baseGuard and FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_empty], [f_control, control_manual], [f_dist, baseGuard, 45]]) do
begin
wait(0$5);
ComEnterUnit(baseGuard, FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_empty], [f_control, control_manual], [f_dist, baseGuard, 45]])[1]);
AddComMoveXY(baseGuard, 127, 63);
End;
// Execute's AI functions for Red and Magenta base

export enemyWayX, enemyWayY;
Every 3$0 do
var rnd;
begin
     enemyWayX = [];
     enemyWayY = [];
     rnd = rand(1, 3);

     if rnd = 1 then
     begin
          enemyWayX = [73, 46, 29];
          enemyWayY = [71, 43, 29];
     end;

     if rnd = 2 then
     begin
          enemyWayX = [64];
          enemyWayY = [43];
     end;

     if rnd = 3 then
     begin
          enemyWayX = [80];
          enemyWayY = [28];
     end;

     enable;

end;



Every 0$1 trigger AIEnable = 1 do
begin
    enable;

    RepairBuilding(3, rubware0, RussianBase);
    HealUnitInBase(3, RussianHealArea, RussianBase, rubbrid0);
    RepairVehicles(3, RussianAttackRepair, 108, 95);
    AttackVehicles(3, RussianTanksSpot, enemyWayX, enemyWayY);

    if baseReadyToBuild = 2 then
    begin
        RepairBuilding(6, ruPDepot, SecondRussianBase);
        HealUnitInBase(6, SecondRussianHealArea, SecondRussianBase, ruPLab);
    end;
end;

Every 1 trigger not IsInUnit(Stolypin) and IsOk(rubware0) and GetSide(rubware0) = 3 do
begin
 enable;
 ComEnterUnit(Stolypin, rubware0);
end;

Every 5$1 + 10$0 trigger FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_driving]]) = 0 and GetSide(rubbrid0) = 3 and IsOk(rubware0) and GetSide(rubware0) = 3 and IsOk(rubware0)  do
begin
    enable;
                                                        
    if Difficulty = 1 then 
       BuildVehicle(3, rubfact0, Difficulty + 2, [42, 44, 43], 1, 1, 21);

    if Difficulty = 2 then
       BuildVehicle(3, rubfact0, Difficulty + 2, [44, 44, 43, 43], 1, 1, [21, 22][Rand(1,2)]);

    if Difficulty = 3 then
       BuildVehicle(3, rubfact0, Difficulty + rand(2, 3), [44, 44, 43], 1, 1, [ru_medium_tracked, ru_heavy_wheeled][rand(1,2)]);

end;

Every 0$1 do ResAllTechs(3, rubbrid0, 3);

// Building base

Every 0$1 trigger baseReadyToBuild = 1 do
var i, cargo;
begin
    cargo = FilterAllUnits([[f_side, 6], [f_type, unit_vehicle], [f_weapon, 51]]);

    ComMoveXY(cargo[1], GetX(ruPDepot) - 5, GetY(ruPDepot) - 5);
    wait(0$5);
    AddComCollect(cargo[1], 41, 52);
    wait(0$40);
    ComMoveXY(cargo[1], 41, 44);
end; 

Every 0$1 trigger GetResourceType(GetBase(ruPDepot), 1) >= 100  do
var eng;
begin
    eng = FilterAllUnits([[f_side, 6], [f_type, unit_human], [f_class, 2]]);

    ComBuild(eng[1], 4, 39, 37, 3);
    AddComBuild(eng[1], 29, 42, 54, 3);
    AddComBuild(eng[1], 6, 38, 48,0);
    AddComBuild(eng[1], 2, 32, 38, 2);
    AddComBuild(eng[1], 26, 39, 55, 3);
    AddComBuild(eng[1], 32, 31, 31, 3);
    AddComBuild(eng[1], b_breastwork, 50, 51, 4);

end;

Every 0$1 trigger ruMobile do
var i, mech, enemy;
begin
enable;

if not ruMobile then
   exit;

// tags:
// 0 - idle,
// 1 - go to parking, repair if needed
// 2 - attack

for i in ruMobile do
    begin
    if not IsOk(i) or GetSide(i) <> 3 then
       begin
       ruMobile := ruMobile diff i;
       continue;
       end;

    if not GetTag(i) or GetTag(i) = 0 then
       begin
       if GetFuel(i) < 50 then
          begin
          ComMoveXY(i, 106, 88);

          repeat
           wait(0$1);
          until GetDistUnitXY(i, 106, 88) < 4;

          SetFuel(i, 100);
          end
       else if not IsInArea(i, ruParking) then
          ComMoveToArea(i, ruParking)
       else
          SetTag(i, 1);
       end;

    if GetTag(i) = 1 then
       begin
       if GetLives(i) < 1000 then
          begin
          if not IsDrivenBy(i) then
             continue;

          mech := IsDrivenBy(i);

          ComExitVehicle(mech);
          AddComRepairVehicle(mech, i);

          repeat
           wait(0$1);
          until GetLives(i) = 1000;

          ComEnterUnit(mech, i);
          end
       else if IsDrivenBy(i) then
          SetTag(i, 2);
       end;

    if GetTag(i) = 2 then
       begin
       enemy := FilterUnitsInArea(ruDefArea, [f_enemy, 3]);

       if GetLives(i) < 650 then
          begin
          ComStop(i);
          SetTag(i, 0);
          continue;
          end;

       if enemy then
          begin
          if not HasTask(i) then
             ComAttackUnit(i, NearestUnitToUnit(enemy, i));
          end
       else if ruMobile > 2 then
          begin
          enemy := FilterAllUnits([f_enemy, 3]);

          if enemy then
             if not HasTask(i) then
                ComAttackUnit(i, NearestUnitToUnit(enemy, i));
          end;
       end;
    end;
End;
Every 10$0 do 
RespawnAttackInfantry(5, 3, 2 + [2, 3, 4, 4][Difficulty], 0, rand(ruSoldMinLevel, ruSoldMaxLevel), RussianAttack, RussianAttackHeal, 1, 2);

Every 20$0 do
RespawnAttackInfantry(5, 3, 4 + [2, 3, 4, 5][Difficulty], 2, rand(ruSoldMinLevel, ruSoldMaxLevel), RussianAttack, RussianAttackHeal, 1, 1);

Every 25$0 do
var i, un, veh, hex, tmp;
begin
hex := [123, 95];
tmp := [];

if Difficulty > 1 then
   specAttack := true;

for i := 1 to [4, 5, 6, 7][Difficulty] do
    begin
    uc_side := 5;
    uc_nation := 3;

    vc_chassis := [ru_medium_wheeled, ru_medium_tracked, ru_heavy_wheeled, ru_heavy_tracked][i mod Difficulty + 1];
    vc_engine := engine_combustion;
    vc_control := control_manual;
    vc_weapon := [ru_heavy_machine_gun, ru_gatling_gun, ru_gun, ru_gun][i mod Difficulty + 1];

    veh := CreateVehicle;
    SetDir(veh, 5);

    repeat
     wait(0$1);
    until HexInfo(hex[1], hex[2]) = 0;

    PlaceUnitXY(veh, hex[1], hex[2], false);

    uc_side := 5;
    uc_nation := 3;

    InitHC_All();
    PrepareMechanic(false, 2 + Difficulty);

    PlaceHumanInUnit(CreateHuman, veh);

    ComMoveXY(veh, 101, 77);

    tmp := Replace(tmp, tmp + 1, veh);

    wait(0$3);
    end;

repeat
 wait(0$1);

 for i in tmp do
     if not IsOk(i) then
        tmp := tmp diff i
     else
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 1]), i));
until not tmp;
End;

Every 0$1 trigger specAttack do
var un, tmp, i, eng;
begin
wait(0$20);

uc_side := 5;
uc_nation := 3;

InitHc;
hc_importance := 0;

tmp := [];

InitHC_All();
PrepareHuman(false, 2, 6);

eng := CreateHuman;
PlaceUnitXYR(eng, 78, 2, 4, false);

for i = 1 to 2 + Difficulty do
    begin
    InitHC_All();
    PrepareHuman(false, 1, [4, 5, 6, 6][Difficulty]);
    un := CreateHuman;
    PlaceUnitXYR(un, 78, 2, 4, false);

    tmp := tmp ^ un;
    end;

ComMoveXY(tmp, 49, 3);

wait(0$2);

ComMoveXY(eng, 49, 3);
AddComDismantle(eng, ambdept0);

wait(0$10);

repeat
 wait(0$1);

 for i in tmp do
     if not IsOk(i) then
        tmp := tmp diff i
     else
       if not HasTask(i) then
          ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 1]), i));
until not tmp; 
End;
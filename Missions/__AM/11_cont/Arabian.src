Export ar_force_west, ar_force_south, ar_force_north, ar_force_north_2, ar_force_east;
Export ar_force_tmp, ar_vehicles_tmp, ar_kamikadze, ar_defenders_tmp;
Export Saliba, Gensher;

Export Function PrepareArabian;
var i, d, un, skill, tmp;
begin
skill := [6, 7, 7][Difficulty];

uc_side := 2;
uc_nation := 2;

// control_towers 156 15
vc_chassis := ar_half_tracked;
vc_engine := engine_siberite;
vc_control := control_manual;
vc_weapon := ar_control_tower;

for i = 1 to 3 do
    begin
    PrepareHuman(false, class_mechanic, skill);

    un := CreateVehicle;
    SetDir(un, rand(0, 5));
    PlaceUnitXYR(un, 156, 15, 6, false);
    PlaceHumanInUnit(CreateHuman, un);
    end;

ar_force_north := PrepareBase(ar_dep_n, base_north, 'dammam', skill, [10000, 1000, 300], [-1, 5, -1, 4]);
ar_force_north := ar_force_north union PrepareBase(ar_dep_n2, base_north_2, '', skill, [500, 60, 0], [-1, 4, 2, 0]);
ar_force_west := PrepareBase(ar_dep_w, base_west, 'jeddah', skill, [700, 300, 10], [-1, 5, 3, 2]);
ar_force_south := PrepareBase(ar_dep_s, base_south, 'riyadh', skill, [500, 60, 0], [4, 2, 3, 1]);
ar_force_east := PrepareBase(ar_dep_e, base_east, '', skill, [500, 50, 0], [-1, 3, 3, 2]);

mc_bases := [ar_force_north, ar_force_west, ar_force_east];

for i in FilterAllUnits([[f_side, 2], [f_btype, b_breastwork], [f_empty]]) do
    begin
    if GetBase(i) then
       continue;

    d := GetDir(i);

    if d < 3 then
       d := d + 3
      else
       d := d - 3;

    PrepareHuman(false, class_mortar, skill);
    un := CreateHuman;

    SetDir(un, d);
    PlaceHumanInUnit(un, i);
    end;

if Difficulty > 1 then
   begin
   ar_kamikadze := [];

   PrepareHuman(false, 1, skill);
   un := CreateHuman;
   SetDir(un, 3);
   PlaceUnitXY(un, 23, 44, false);
   ComCrawl(un);

   un := CreateHuman;
   SetDir(un, 3);
   PlaceUnitXY(un, 30, 39, false);
   ComCrawl(un);

   PrepareHuman(false, 17, skill);
   un := CreateHuman;
   SetDir(un, 3);
   PlaceUnitXY(un, 45, 86, false);
   ComHold(un);

   ar_kamikadze := Replace(ar_kamikadze, ar_kamikadze+1, un);

   un := CreateHuman;
   SetDir(un, 3);
   PlaceUnitXY(un, 60, 85, false);
   ComHold(un); 
   
   ar_kamikadze := Replace(ar_kamikadze, ar_kamikadze+1, un);

   un := CreateHuman;
   SetDir(un, 3);
   PlaceUnitXY(un, 222, 166, false);
   ComHold(un);

   ar_kamikadze := Replace(ar_kamikadze, ar_kamikadze+1, un);
   end;



ar_force_tmp := [];

PrepareHuman(sex_male, 1, skill);
hc_name := 'Pavel Grigorovic';
hc_gallery := 'SecondCharsGal';
hc_face_number := 4;
ar_force_tmp := Replace(ar_force_tmp, 1, CreateHuman);

PrepareHuman(sex_female, 4, skill);
hc_name := 'Lucy Sebel';
hc_gallery := 'SecondCharsGal';
hc_face_number := 15;
ar_force_tmp := Replace(ar_force_tmp, 2, CreateHuman);


PrepareHuman(sex_female, 4, skill);
hc_gallery := '';
hc_name := '';
ar_force_tmp := Replace(ar_force_tmp, 3, CreateHuman);

hc_sex := sex_male;
hc_class = 11;
hc_gallery = 'sandar';
hc_face_number = 33;
hc_name = 'Thabit Muhair Saliba';
hc_skills = [0,0,0,0];

Saliba = CreateHuman;
PlaceHumanInUnit(Saliba, ar_dep_s);

if gensher_active then
   begin
   Gensher = NewCharacter('Dietrich');
   PlaceHumanInUnit(Gensher, ar_dep_n);
   end;

InitHc;

// prepare vehicles
ar_vehicles_tmp := [];

for i = 1 to 5 do
    begin
    PrepareVehicle([ar_medium_trike, ar_half_tracked][rand(1,2)],
                   [engine_combustion, engine_solar][rand(1,2)],
                   control_manual,
                   [ar_gatling_gun, ar_gun, ar_flame_thrower][rand(1,3)],
                   rand(60, 100));

    un := CreateVehicle;
    ar_vehicles_tmp := Replace(ar_vehicles_tmp, ar_vehicles_tmp+1, un);
    SetDir(un, rand(0,5));
    PlaceUnitXYR(un, 124, 141, 8, false);
    end;


ar_defenders_tmp := [ [], [], [] ];

for d = 1 to [3, 3, 4][Difficulty] do
for i = 1 to 3 do
    begin
    PrepareVehicle(ar_half_tracked,
                   [engine_siberite, engine_solar][rand(1,2)],
                   [control_manual, control_apeman][rand(1,2)],
                   [ar_gatling_gun, ar_gun, ar_flame_thrower, ar_rocket_launcher][rand(1,4)],
                   100);
    un := CreateVehicle;
    ar_defenders_tmp := ReplaceIn(ar_defenders_tmp, [i, ar_defenders_tmp[i]+1], un);
    SetDir(un, rand(0,5));
    PlaceUnitArea(un, [parking_north, parking_west, parking_east][i], false);
    end;

InitHc;

// artifact
CreateResourcesXY(mat_artifact, 5, 10, 5, false);
End;

Every 0$1 trigger ar_kamikadze do
var i;
begin
enable;

for i in ar_kamikadze do
    if See(1, i) then
       ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 2]), i));
End;


// Try to stole artifact
Every 5$20 trigger artifact_get do
var i, cargo, tmp, pos;
begin
repeat
 wait(0$1);
until MC_GetProduceList(1) = 0;

MC_SetProduceList(1, [ [ar_half_tracked, engine_siberite, control_remote, ar_cargo_bay] ]);

repeat
 wait(0$1);
until UnitFilter(mc_vehicles[1], [[f_control, control_remote], [f_weapon, ar_cargo_bay]]);

cargo := UnitFilter(mc_vehicles[1], [[f_control, control_remote], [f_weapon, ar_cargo_bay]])[1];

// scan artifact pos
pos := FindArtifact(5);

repeat
 wait(0$1);
 pos := FindArtifact(5);
 tmp := 100;

 if pos then
    tmp := DangerAtRangeXY(2, pos[1], pos[2], 20)[4];
until pos and not artifact_oncargo and tmp < 10;

ComGet(cargo, pos[1], pos[2]);
AddComMoveXY(cargo,  198, 113);
AddComMoveXY(cargo, 124, 7);
End;


// ATTACKS
// First attempt
// Attacks from 3 side's
Every 5$00 trigger base_captured do
var i, un, tmp, skill, coords, flags, w, list;
begin
skill := [5, 6, 7][Difficulty];
coords := [];
flags := [0, 0, 0, 0, 1, 0, 0, 0, 1, 0];

MC_SetProduceList(1, [ [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
                       [ar_half_tracked, engine_combustion, control_remote, ar_gatling_gun],
                       [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
                       [ar_half_tracked, engine_combustion, control_remote, ar_selfpropelled_bomb]
                     ]);
                           
// wait until jakes get rage
wait([10$00, 9$30, 9$00][Difficulty]);


InitHc;
InitUc;

uc_side := 2;
uc_nation := 2;

tmp := [ [], [], [], [], [] ];

// attack from North
tmp := Replace(tmp, 1, mc_vehicles[1] diff FilterAllUnits([f_weapon, ar_cargo_bay]));

for i = 1 to Difficulty do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareVehicle(ar_medium_trike, engine_siberite, control_apeman, ar_selfpropelled_bomb, 100);
    un := CreateVehicle;
    tmp := ReplaceIn(tmp, [1, tmp[1]+1], un);
    SetDir(un, 3);
    PlaceUnitArea(un, north_attack_spawn, false);
    ComMoveXY(un, 51, 10);
    wait(0$2);
    end;

coords := [ [51,24], [75,90] ];
MC_PrepareAttack(1, tmp[1], coords, flags);


// attack from West
// kamikaze
for i = 1 to [2,3,3][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareHuman(false, 17, skill);
    un := CreateHuman;
    tmp := ReplaceIn(tmp, [2, tmp[2]+1], un);
    PlaceUnitArea(un, west_attack_spawn, false);
    end;

// mortar
for i = 1 to [2,3,3][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareHuman(false, [1,8][rand(1,2)], skill);
    un := CreateHuman;
    tmp := ReplaceIn(tmp, [2, tmp[2]+1], un);
    PlaceUnitArea(un, west_attack_spawn, false);
    end;

coords := [ [67,112], [85,130] ];
MC_PrepareAttack(2, tmp[2], coords, flags);


// attack from South

// kamikadze
for i = 1 to [1,2,3][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareHuman(false, 17, skill);
    un := CreateHuman;
    tmp := ReplaceIn(tmp, [3, tmp[3]+1], un);
    PlaceUnitArea(un, south_attack_spawn, false);
    end;

coords := [ [148,158], [148,158] ];
MC_PrepareAttack(3, tmp[3], coords, flags);

// attack from East

for i = 1 to [3, 4, 4][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareVehicle(ar_half_tracked, engine_siberite, [control_manual, control_apeman][rand(1,2)], [ar_gun, ar_flame_thrower, ar_rocket_launcher][rand(1,3)], 100);
    un := CreateVehicle;
    tmp := ReplaceIn(tmp, [4, tmp[4]+1], un);
    SetDir(un, 5);
    PlaceUnitArea(un, east_attack_spawn, false);

    if GetControl(un) = control_manual then
       begin
       PrepareHuman(false, 3, skill);
       PlaceHumanInUnit(CreateHuman, un);
       end;

    ComMoveXY(un, 179, 135);
    wait(0$2);
    end;

vc_chassis := 15;
tmp := ReplaceIn(tmp, [4, tmp[4]+1], CreateVehicle);
PlaceUnitArea(tmp[4][tmp[4]], east_attack_spawn, false);

PrepareHuman(false, class_desert_warior, skill);
tmp := ReplaceIn(tmp, [4, tmp[4]+1], CreateHuman);
PlaceHumanInUnit(tmp[4][tmp[4]], tmp[4][tmp[4]-1]);

coords := [ [148, 140] ];
MC_PrepareAttack(1, tmp[4], coords, flags);

if gensher_active then
   begin
   SayRadio(Gensher, 'D10-Diet-1');

   for i = 1 to 2 do
       begin
       uc_side := 2;
       uc_nation := 2;

       PrepareVehicle(ar_medium_trike, engine_siberite, control_apeman, ar_selfpropelled_bomb, 100);
       un := CreateVehicle;
       tmp := ReplaceIn(tmp, [5, tmp[5]+1], un);
       SetDir(un, 0);
       PlaceUnitArea(un, gensher_attack, false);
       ComMoveXY(un, 85, 152);
       wait(0$2);
       end;

   for i = 1 to 3 do
       begin
       uc_side := 2;
       uc_nation := 2;

       PrepareVehicle(ar_half_tracked, engine_siberite, control_apeman, [ar_gun, ar_rocket_launcher][rand(1,2)], 100);
       un := CreateVehicle;
       tmp := ReplaceIn(tmp, [5, tmp[5]+1], un);
       SetDir(un, 0);
       PlaceUnitArea(un, gensher_attack, false);
       ComMoveXY(un, 85, 152);
       wait(0$2);
       end;

   coords := [ [97,143] ];

   MC_PrepareAttack(1, tmp[5], coords, flags);
   end;


Wait(13$00);

tmp := [];
w := 1;

repeat
  tmp := [];

  if w mod 4 = 0 then
     list := [ [ar_hovercraft, engine_combustion, control_remote, ar_double_machine_gun],
               [ar_hovercraft, engine_combustion, control_remote, ar_double_machine_gun],
               [ar_hovercraft, engine_combustion, control_remote, ar_double_machine_gun],
               [ar_hovercraft, engine_combustion, control_remote, ar_double_machine_gun]
             ]
  else
     list := [ [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
               [ar_half_tracked, engine_combustion, control_remote, ar_gatling_gun],
               [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
               [ar_half_tracked, engine_combustion, control_remote, ar_selfpropelled_bomb],
               [ar_hovercraft, engine_combustion, control_remote, ar_double_machine_gun]
             ];

  if w mod 3 = 0 then
     list := ReplaceIn(list, [1, list[i]+1], [ar_half_tracked, engine_combustion, control_remote, [ar_gatling_gun, ar_rocket_launcher][rand(1,2)]]);

  MC_SetProduceList(1, list);

  if GetSide(ar_dep_w) = 2 then
     begin
     list := [ [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
               [ar_half_tracked, engine_combustion, control_remote, ar_gun],
               [ar_half_tracked, engine_combustion, control_remote, ar_gun]
                ];

     MC_SetProduceList(2, list);

     wait(0$50);
     end;

  repeat
   wait(0$1);
  until UnitFilter(mc_vehicles[1], [f_not, [f_weapon, ar_cargo_bay]]) >= 4;

  wait(0$10);

  tmp := UnitFilter(mc_vehicles[1], [f_not, [f_weapon, ar_cargo_bay]]);

  if Prob(100) < 50 then
     coords := [ [55,7], [75,90] ]
  else
     coords := [ [128,94], [180,135] ];

  if w mod 4 = 0 then
     coords := [ [91,58], [117,107] ];

  MC_PrepareAttack(1, tmp, coords, flags);

  if mc_vehicles[2] then
     MC_PrepareAttack(2, mc_vehicles[2], [ [73,115] ], flags);

  wait(rand(10$30, 13$30));
  w := w + 1;

until IsDead(ar_dep_n) or not UnitFilter(mc_bases[1], [f_btype, b_factory]);

End;

// Run
Every 28$00 trigger ar_dep_e do
var i, tmp, un, x;
begin
enable;

wait(rand(0$1, 0$34));

tmp := [];

uc_side := 8;
uc_nation := 2;

InitHc;

// first spawn on east
for i = 1 to 3 do
    begin
    PrepareVehicle([ar_medium_trike, ar_half_tracked][rand(1,2)], engine_siberite, control_apeman, [ar_gun, ar_rocket_launcher][rand(1,2)], 100);
    un := CreateVehicle;
    SetDir(un, 4);
    PlaceUnitArea(un, east_attack_spawn, false);
    tmp := Replace(tmp, tmp+1, un);
    end;

for i = 1 to 4 do
    begin
    PrepareHuman(false, rand(1,4), 6);
    un := CreateHuman;
    PlaceUnitArea(un, east_attack_spawn, false);
    tmp := Replace(tmp, tmp+1, un);
    end;

wait(0$3);

for i in tmp do
    if GetClass(i) = 1 or GetType(i) = unit_vehicle then
       ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 8]), i));

repeat
 AddComAgressiveMove(tmp, 210, 178);

 x := FilterUnitsInArea(escape_area, [f_side, 8]);

 if x then
    for i in x do
        RemoveUnit(i);

 wait(0$1);
until not FilterAllUnits([f_side, 8]);
End;


Every 0$2 trigger FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower], [f_not, [f_lives, 1000]]]) do
begin
ComMoveXY(FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]), 106, 14);
End;
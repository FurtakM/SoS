Export russian_force, russian_guard, ru_attackers;
Export Function PrepareRussian;
var i, veh, tmp, b;
begin
uc_side := 3;
uc_nation := 3;

russian_force := PrepareBase(ru_depot, area_base_russian, '', [7, 8, 9, 10][Difficulty], [10000, 3000, 300], [9, 5, 6, 6]);
mc_bases := Replace(mc_bases, 2, russian_force);

tmp := [];

for i = 1 to 4 do
    begin
    PrepareVehicle(ru_medium_tracked, engine_siberite, control_computer, [ru_gatling_gun, ru_rocket_launcher, ru_rocket_launcher, ru_gun][i mod 4 + 1], 100);

    veh := CreateVehicle;
    tmp := tmp ^ veh;

    PlaceUnitArea(veh, parking_russian, false);
    end;

russian_guard := tmp;

if Difficulty >= 3 then
   begin
   bc_type := b_breastwork;
   bc_level := 10;
   b := CreateAndPlaceBuildingXYD(96, 105, 3);

   PrepareHuman(false, class_bazooker, 10);
   PlaceHumanInUnit(CreateHuman, b);
   end;
End;


Every 5$00+2$00 trigger IsOk(ru_depot) and player_spotted do
var i, p, base, tmp, _target, attackers, amount;
begin
enable;

base := 2;
tmp := [
         [ru_medium_tracked, engine_combustion, control_computer, ru_rocket_launcher],
         [ru_medium_wheeled, engine_combustion, control_computer, ru_rocket_launcher],
         [ru_medium_tracked, engine_combustion, control_computer, ru_rocket_launcher],
         [ru_heavy_wheeled, engine_combustion, control_computer, ru_heavy_gun]
       ];

amount := Difficulty;

if tick > 30$00 then
   amount := amount + [2, 3, 4, 4][Difficulty];

for i = 1 to amount do
    tmp := Insert(tmp, tmp+1, [[ru_heavy_wheeled, ru_heavy_tracked][rand(1,2)], engine_combustion, control_computer, [ru_heavy_gun, ru_rocket, ru_rocket_launcher][rand(1,3)]]);

p := 1;

MC_InsertProduceList(base, tmp);

repeat
 wait(0$1);
 p := Inc(p);
until MC_GetVehicles(base, true) >= amount or p >= 60;

wait(0$30);

_target := [[71,19], [91,67], [52,44], [68,48]];
attackers := FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_not, [f_or, [f_weapon, ru_crane], [f_weapon, ru_cargo_bay]]]]) diff mc_defender[base];

if not attackers then
   exit;

ru_attackers := attackers;
mc_vehicles := Replace(mc_vehicles, base, mc_vehicles[base] diff attackers);

for i = 1 to attackers do
    begin
         case i mod 3 of
         0:;
         1: ComAgressiveMove(i, 32, 49);
         2: ComAgressiveMove(i, 117, 107);
         end;
    end;

p := 0;

repeat
 wait(0$1);
 p := Inc(p);
until not UnitFilter(attackers, [f_hastask]) or p > 30;  

if not first_attack then
   first_attack := true;

repeat
 wait(0$1);
 attackers := UnitFilter(attackers, [f_ok]);
 AgressiveMove(attackers, _target);
until not attackers;  
End;

Export function PrepareRussianAttack;
var i, tmp, veh, chassis, weapon;
begin
uc_side := 6;
uc_nation := 3;

InitHc;
InitVc;

tmp := [];

for i := 1 to [4, 5, 6, 7][Difficulty] do
    begin
    chassis := [ru_medium_wheeled, ru_medium_tracked, ru_heavy_tracked][rand(1,3)];

    case chassis of
         ru_medium_wheeled, ru_medium_tracked: weapon := [ru_rocket_launcher, ru_gun, ru_gatling_gun][rand(1, 3)];
         ru_heavy_tracked: weapon := [ru_heavy_gun, ru_rocket, ru_rocket_launcher, ru_heavy_gun][rand(1, 4)];
    end;

    uc_side := 6;
    uc_nation := 3;

    PrepareVehicle(chassis, [engine_combustion, engine_siberite][rand(1,2)], control_computer, weapon, rand(70, 90));
    veh := CreateVehicle;
    SetDir(veh, 5);
    PlaceUnitArea(veh, south_spawn, false);

    tmp := tmp ^ veh;
    end;

if not tmp then
   exit;

repeat
 wait(0$2);

 for i in tmp do
     if IsOk(i) then
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 7]), i))
     else
        tmp := tmp diff i;
until not tmp;
End;

Every 10$00 + 3$00 trigger first_attack and IsOk(ru_depot) and GetSide(ru_depot) = 3 and ru_active_attack do
begin
enable;
PrepareRussianAttack;
End;
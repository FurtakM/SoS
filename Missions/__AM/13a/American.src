Export Function PrepareAmericanAttack;
var i, tmp, veh, chassis, weapon;
begin
uc_side := 1;
uc_nation := 1;

InitHc;
InitVc;

tmp := [];

for i = 1 to [6, 8, 10, 10][Difficulty] do
    begin
    chassis := [us_medium_wheeled, us_heavy_tracked, us_morphling][rand(1,3)];

    case chassis of
         us_medium_wheeled: weapon := [us_laser, us_double_gun, us_rocket_launcher][rand(1, 3)];
         us_heavy_tracked: weapon := [us_laser, us_heavy_gun, us_heavy_gun, us_rocket_launcher][rand(1, 4)];
         us_morphling: weapon := [us_laser, us_heavy_gun, us_rocket_launcher][rand(1, 3)];
    end;

    uc_side := 1;
    uc_nation := 1;

    PrepareVehicle(chassis, [engine_combustion, engine_siberite][rand(1,2)], control_computer, weapon, rand(70, 90));
    veh := CreateVehicle;
    SetDir(veh, 2);
    PlaceUnitArea(veh, north_spawn, false);

    tmp := tmp ^ veh;
    end;

if not tmp then
   exit;

if not first_powell_attack then
   first_powell_attack := true;

repeat
 wait(0$2);

 for i in tmp do
     if IsOk(i) then
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 1]), i))
     else
        tmp := tmp diff i; 
until not tmp;  
End;

Export function TryToStoleArtifact(artifact);
var veh;
begin
uc_side := 1;
uc_nation := 1;

InitHc;
InitVc;

PrepareVehicle(us_morphling, engine_siberite, control_computer, us_cargo_bay, 100);
veh := CreateVehicle;
SetDir(veh, 2);
PlaceUnitArea(veh, north_spawn, false);

ComGet(veh, artifact[1], artifact[2]);
AddComMoveXY(veh, 59, 2);

repeat
 wait(0$1);

 if not IsOk(veh) then
    exit;

until IsAt(veh, 59, 2);

RemoveUnit(veh);

if GetCargo(veh, mat_artifact) then
   begin
   wait(0$2);
   YouLost('Artifact');
   end;
End;
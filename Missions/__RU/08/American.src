Export amAttackTeam;

Export function PrepareAmericans;
begin
    amAttackTeam = [];

    SpawnAmericansSnipers;

    case Difficulty of
        2: begin
            SetTech(tech_comp1, 1, state_researched);
            SetTech(tech_opto1, 1, state_researched);
        end;
        3..4: begin
            SetTech(tech_comp1, 1, state_researched);
            SetTech(tech_comp2, 1, state_researched);
            SetTech(tech_weap3, 1, state_researched);
            SetTech(tech_opto1, 1, state_researched);
            SetTech(tech_opto2, 1, state_researched);
        end;
    end;
end;

Export function SpawnAmericansSnipers;
var i, un;
begin
    uc_side = 1;
    uc_nation = 1;

    for i := 1 to 8 do
    begin
        PrepareHuman(false, class_sniper, enemySkillLevel);    
        un = CreateHuman;

        PlaceUnitArea(un, AmericansSnipersSpawnArea, false);
        ComHold(un);
    end;

end;

Export function SpawnAmAttackTeam;
var i, unit, veh, randVehType, weapon, chassis, amSolds, amSnipers, amVehs;
begin

    uc_side = 1;
    uc_nation = 1;

    amSolds = Rand(enemyAttackUnitsData[1][2], enemyAttackUnitsData[1][3]);
    amSnipers = Rand(enemyAttackUnitsData[1][4], enemyAttackUnitsData[1][5]);
    amVehs = Rand(enemyAttackUnitsData[1][6], enemyAttackUnitsData[1][7]);

    if paidLegionOffert then
    begin
        amSolds = amSolds - legionReduceAmericansUnits;
        amVehs = amVehs - legionReduceAmericansUnits;
    end;

    for i := 1 to amSolds do
    begin
        PrepareSoldier(false, enemySkillLevel);    

        unit = CreateHuman;
        PlaceUnitArea(unit, AmericanSpawnArea, false);

        amAttackTeam = amAttackTeam ^ unit;
    end;

    for i := 1 to amSnipers do
    begin
        PrepareHuman(false, class_sniper, enemySkillLevel);    

        unit = CreateHuman;
        PlaceUnitArea(unit, AmericanSpawnArea, false);

        amAttackTeam = amAttackTeam ^ unit;
    end;

    for i := 1 to amVehs do
    begin
        randVehType = Rand(1, 4);
        weapon = [us_gatling_gun, us_double_gun, us_light_gun, us_rocket_launcher, us_heavy_gun][Rand(1, 5)];

        if weapon = us_heavy_gun then chassis = us_heavy_tracked
        else chassis = [us_medium_tracked, us_heavy_tracked][Rand(1, 2)];

        if randVehType = 1 then PrepareVehicle(chassis, engine_combustion, control_manual, weapon, Rand(70, 90))
        else PrepareVehicle(chassis, engine_combustion, control_computer, weapon, Rand(70, 90));

        veh = CreateVehicle;
        PlaceUnitArea(veh, AmericanSpawnArea, false);

        amAttackTeam = amAttackTeam ^ veh;
      
        if randVehType = 1 then
        begin
            PrepareMechanic(false, enemySkillLevel);  
            unit = CreateHuman;
            PlaceHumanInUnit(unit, veh);

            amAttackTeam = amAttackTeam ^ unit;
        end;  
    end;
end;

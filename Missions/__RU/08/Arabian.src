Export Heike, Kurt, Dietrich;
Export KurtEng;
Export GensherTeam;
Export arAttackTeam;

Export Function PrepareArabians;
begin
    arAttackTeam = [];

    PrepareHeroes;    

    case Difficulty of
        2: begin
            SetTech(tech_stimDrugs, 8, state_researched);
        end;
        3: begin
            SetTech(tech_stimDrugs, 8, state_researched);
            SetTech(tech_bio1, 8, state_researched);
            SetTech(tech_weap3, 8, state_researched);
        end;
    end;

end;

Export function SpawnGensherTeam;
var i, driver, veh;
begin
    GensherTeam = [];

    uc_side = 2;
    uc_nation = 2;

    for i := 1 to 3 do
    begin
        PrepareSoldier(sex_male, rand(3, 5));

        driver = CreateHuman;

        PrepareVehicle(ar_medium_trike, engine_combustion, control_manual, ar_cargo_bay, rand(50, 70));

        veh = CreateVehicle;

        SetDir(veh, 4);
        PlaceUnitArea(veh, GensherSpawnArea, false);
        PlaceHumanInUnit(driver, veh);
        AddCargo(veh, i, 100);

        GensherTeam = GensherTeam ^ driver;
        GensherTeam = GensherTeam ^ veh;
    end;

    if IsOK(KurtEng) AND GetSide(KurtEng) = 3 then
    begin
        GensherTargetX = GetX(Kurt);
        GensherTargetY = GetY(Kurt);
    end else
    begin
        GensherTargetX = GetX(Burlak);
        GensherTargetY = GetY(Burlak);
    end;

    ComMoveXY(GensherTeam, GensherTargetX, GensherTargetY);
end;

Function PrepareHeroes;
begin
    uc_side = 5;
    uc_nation = 2;

    Heike = PrepareUnitExt('Heike', true, class_engineer);

    uc_side = 8;

    Kurt = PrepareUnitExt('Kurt', false, 0);

    uc_side = 2;

    Dietrich = PrepareUnitExt('Dietrich', false, 0);
end;


Export function SpawnArAttackTeam;
var i, unit, veh, weapon, chassis;
begin

    uc_side = 8;
    uc_nation = 2;

    for i := 1 to Rand(enemyAttackUnitsData[1][2], enemyAttackUnitsData[1][3]) do
    begin
        PrepareSoldier(false, enemySkillLevel);    

        unit = CreateHuman;
        PlaceUnitArea(unit, KurtUnitsSpawnArea, false);

        arAttackTeam = arAttackTeam ^ unit;
    end;

    for i := 1 to Rand(enemyAttackUnitsData[1][4], enemyAttackUnitsData[1][5]) do
    begin
        PrepareHuman(false, class_mortar, enemySkillLevel);    

        unit = CreateHuman;
        PlaceUnitArea(unit, KurtUnitsSpawnArea, false);

        arAttackTeam = arAttackTeam ^ unit;
    end;

    for i := 1 to Rand(enemyAttackUnitsData[1][6], enemyAttackUnitsData[1][7]) do
    begin
        weapon = [ar_gun, ar_flame_thrower, ar_rocket_launcher, ar_light_gun, ar_gatling_gun][Rand(1, 5)];
        chassis = [ar_medium_trike, ar_half_tracked][Rand(1, 2)];

        PrepareVehicle(chassis, engine_combustion, control_manual, weapon, Rand(70, 90));

        veh = CreateVehicle;
        PlaceUnitArea(veh, KurtUnitsSpawnArea, false);

        arAttackTeam = arAttackTeam ^ veh;
      

        PrepareMechanic(false, enemySkillLevel);  
        unit = CreateHuman;
        PlaceHumanInUnit(unit, veh);

        arAttackTeam = arAttackTeam ^ unit;
 
    end;
end;

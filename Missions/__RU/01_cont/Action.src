Export Function MissionIntro;
begin
    InGameOn;

    CenterNowOnXY(burlakSpawnPosition[1], burlakSpawnPosition[2]);

    wait(0$3);
    CenterNowOnUnits(Burlak);

    Say(Burlak, 'D1-Bur-1');

    ComMoveXY(Burlak, 94, 89);

    CenterNowOnUnits(Burlak);

    Say(Burlak, 'D1-Bur-1a');

    ComMoveXY(Burlak, 102, 88);

    CenterNowOnUnits(Burlak);

    Say(Burlak, 'D1-Bur-1b');

    CenterNowOnUnits(Burlak);

    InGameOff;

    ChangeMissionObjectives('M1');

    SaveForQuickRestart;

    introEnd = true;
end;

// Burlak comment waterfall
every 0$1 trigger GetDistUnitXY(Burlak, 74, 88) < 5 AND introEnd do
begin
    spotWaterfall = true;

    Say(Burlak, 'DWaterfall-Bur-1');
end;

// Burlak comment mastodont's corpse
every 0$1 trigger GetDistUnitXY(Burlak, 130, 96) < 5 AND introEnd do
begin
    Say(Burlak, 'DCorpse-Bur-1');
end;

// Burlak contact with apemans
every 0$1 trigger not apemansContact do
var apeman;
begin
    enable;

    for apeman in FilterAllUnits([[f_side, 0], [f_class, apeman]]) do
        if GetDistUnits(apeman, Burlak) < 7 then
        begin
            apemansContact = true;
            
            Say(Burlak, 'D2-Bur-1');

            exit;
        end;
end;

// Burlak comment attack by apeman
every 0$1 trigger not attackedByApeman do
var un, apeman;
begin
    enable;

    for apeman in FilterAllUnits([[f_side, 0], [f_class, apeman]]) do
        if Attacks(apeman) then
            if GetSide(Attacks(apeman)) = 3 then
            begin
                attackedByApeman = true;
                apemanAttackedBurlak = apeman;

                Say(Burlak,'D2-Bur-1a');

                exit;
            end;
end;

// Save radio position if apeman with radio is dying
every 1 trigger IsDying(apemanWithRadio) do
    radioDropPosition = [GetX(apemanWithRadio), GetY(apemanWithRadio)];

// Find radio
every 0$1 trigger IsDead(apemanWithRadio) and SeeXY(3, radioDropPosition[1], radioDropPosition[2]) do
var nearUnit;
begin
    Say(Burlak,'D2a-Bur-1');

    // Burlak is alone
    if FilterAllUnits([[f_side, 3]]) = 1 then
    begin
        Say(Burlak,'D2a-Bur-2');
    end
    else
    begin
        nearUnit = NearestUnitToXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), radioDropPosition[1], radioDropPosition[2]);
        
        ComMoveXY(nearUnit, radioDropPosition[1], radioDropPosition[2]);

        DialogueOn;
            case nearUnit of
                Burlak: Say(Burlak, 'D3-Bur-2a');
                Belkov: Say(Belkov,'D2a-Bel-1');
                Kirilenkova: Say(Kirilenkova,'D2a-Kir-1');
                Gnyevko: Say(Gnyevko,'D2a-Gny-1');
            else
                DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]), 'D2a-RSol1-1', 'D2a-RFSol1-1');
            end;

            if (apemanWithRadio = apemanAttackedBurlak) then
                Say(Burlak, 'D3-Bur-2');

        DialogueOff;
    end;
end;

// Find Gnyevko
every 0$1 trigger See(3, Gnyevko) do
var nearUnit, randomUnit;
begin
    CenterNowOnUnits(Gnyevko);

    DialogueOn;

    nearUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), Gnyevko);

    if nearUnit = Burlak then
    begin
        Say(Burlak, 'D3-Bur-1');
        Say(Gnyevko, 'D3-Gny-1');
        Say(Burlak, 'D3-Bur-2b');

        if IsLive(Pokryshkin) OR FilterAllUnits([[f_side, 3], [f_type, unit_human]]) = 1 then
        begin
            Say(Burlak, 'D3-Bur-2c');
            exit;
        end;

        if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) > 1 AND NOT evacuationAvailable then
        begin
            Say(Burlak, 'D3-Bur-2c');
            exit;
        end;   

        if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) > 1 AND evacuationAvailable then
        begin
            Say(Burlak, 'D3-Bur-2e');
            exit;
        end; 

        Say(Gnyevko, 'D3-Gny-2');  
    end
    else
    begin
        randomUnit = DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]), 'D3-RSol1-1', 'D3-RFSol1-1');
        DialogRandom(randomUnit, 'D3-RSol1-2', 'D3-RFSol1-2');
        Say(Gnyevko, 'D3-Gny-2a');  
    end;

    DialogueOff;

    SetSide(Gnyevko, 3);

    gnyevkoSaved = true;
end;

// Meet Pokryshkin
every 0$1 trigger See(3, Pokryshkin) AND NOT meetPokryshkin do
begin
    enable;

    InGameOn;
    CenterNowOnUnits(Pokryshkin);
    ComHold([Burlak, Gnyevko]);

    DialogueOn;

    if gnyevkoWaitWithPokryshkin then
    begin
        meetPokryshkin = true;

        ChangeSideFog(6, 3);

        Say(Gnyevko, 'D4-Gny-3');
        Say(Burlak, 'D4-Bur-3');
        Say(Pokryshkin, 'D4-Pok-4');

        PlaceUnitArea(Charles, CharlesSpawnArea, true);
        wait(0$2);

        Say(Burlak, 'D4-Bur-4');
        Say(Pokryshkin, 'D4-Pok-5');
        SayRadio(Charles, 'D4-Sol1-5');
        Say(Pokryshkin, 'D4-Pok-6');
        SayRadio(Pokryshkin, 'D4-Pok-6a');
        Say(Burlak, 'D4-Bur-6');
        Say(Pokryshkin, 'D4-Pok-7');

        DialogueOff;
        InGameOff;

        ChangeMissionObjectives('M2');

        goToHill = true;

        MoveToHill;

       exit;
    end;

    Say(Pokryshkin, 'D4-Pok-1');

    if GetSide(Gnyevko = 3) AND IsOK(Gnyevko) then
    begin
        Say(Gnyevko, 'D4-Gny-1');

        if GetDistUnits(Burlak, Pokryshkin) < 10 then
        begin
            Say(Pokryshkin, 'D4-Pok-2');
            Say(Gnyevko, 'D4-Gny-2');
            Say(Pokryshkin, 'D4-Pok-3');

            gnyevkoWaitWithPokryshkin = true;

            SetSide(Gnyevko, 6);

            ComMoveXY(Gnyevko, GetX(Pokryshkin), GetY(Pokryshkin));
        end
        else
        begin
            meetPokryshkin = true;

            ChangeSideFog(6, 3);

            Say(Burlak, 'D4-Bur-3');
            Say(Pokryshkin, 'D4-Pok-4');

            PlaceUnitArea(Charles, CharlesSpawnArea, true);
            wait(0$2);

            Say(Burlak, 'D4-Bur-4');
            Say(Pokryshkin, 'D4-Pok-5');
            SayRadio(Charles, 'D4-Sol1-5');
            Say(Pokryshkin, 'D4-Pok-6');
            SayRadio(Pokryshkin, 'D4-Pok-6a');
            Say(Burlak, 'D4-Bur-6');
            Say(Pokryshkin, 'D4-Pok-7');

            ChangeMissionObjectives('M2');
            
            goToHill = true;

            MoveToHill;

        end;
    end;

    DialogueOff;
    InGameOff;
end;

// Gorky is too far
every 0$1 trigger GetDistUnits(Burlak, Pokryshkin) > 10 and goToHill AND NOT traitor do
var ruSolds;
begin
    enable;

    ruSolds = [Pokryshkin, Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    ComStop(ruSolds);

    CenterNowOnUnits(Burlak);

    DialogueOn;
    Say(Pokryshkin, 'DSplit-Pok-1');
    Say(Burlak, 'DSplit-Bur-1');
    DialogueOff;

    InGameOn;

    repeat
        ComMoveXY(Burlak, GetX(Pokryshkin), GetY(Pokryshkin));
        wait(0$1);
    until GetDistUnits(Burlak, Pokryshkin) < 6;

    InGameOff;

    MoveToHill;
end;

// Gorky attack comrades - execute in event On Contact
Export function Dialogue_AttackComrade;
var ruSolds;
begin
    traitor = true;

    ruSolds = [Pokryshkin, Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    DialogueOn;
    Say(Pokryshkin, 'DAttack-Pok-1');
    DialogueOff;

    SetAttitude(3, 6, att_enemy, true);
    ComAttackUnit(ruSolds, Burlak);
end;

// On hill
every 0$1 trigger GetDistUnitXY(Pokryshkin, 65, 59) < 4 AND NOT traitor do
begin

    InGameOn;
    CenterOnXY(59, 55);
    ComMoveXY(Burlak, 66, 57);
    AddComHold(Burlak);
    ComMoveXY(Charles, 59, 55);

    repeat
        wait(0$1);
    until See(1, Pokryshkin);

    CenterNowOnUnits([Charles, Pokryshkin]);

    ComStop(Charles);
    AddComHold(Charles);
    AddComTurnUnit(Charles, Pokryshkin);

    Say(Charles, 'D5-Sol1-1');
    Say(Pokryshkin, 'D5-Pok-1');

    SetAttitude(1, 6, att_enemy, true);

    Say(Charles, 'D5-Sol1-2');
end;

// Charles died
every 0$1 trigger IsDead(Charles) AND goToHill do
var ruSolds;
begin
    goToHill = false;

    ruSolds = [Pokryshkin, Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    ComWalk(ruSolds);

    CenterOnXY(63, 76);
    AddComMoveXY([Burlak, ruSolds], 63, 76);
    Say(Burlak, 'D5-Bur-2');
    Say(Pokryshkin, 'D5-Pok-2');
    Say(Pokryshkin, 'D5-Bel-2');
    Say(Pokryshkin, 'D5-Pok-3');

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        Say(Gnyevko, 'D5-Gny-3');

    Say(Burlak, 'D5-Bur-3');

    wait(0$2);

    ComMoveXY([Burlak, ruSolds], 97, 83);

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-1');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Bur-1');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-2');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Bel-2');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Bur-2');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-3');

    CenterNowOnUnits(Burlak);
    SayRadio(Kurin, 'D6-Kur-3');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-4');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Bel-4');

    InGameOff;

    ComAttackUnit(ruSolds, am_bun);

    DoNotAttack(1, Gnyevko);
end;

// Focus fire on Pokryshkin
every 0$1 trigger See(1, Pokryshkin) and IsDead(Charles) do
    ComAttackUnit(AmSol, Pokryshkin);

// Pokryshkin attack bunker
every 0$1 trigger See(3, am_bun) do
begin
    Say(Pokryshkin, 'D7-Pok-1');
    Say(AmSol, 'D7-FSol1-1');
end;

// Pokryshkin dying
every 0$1 trigger IsDying(Pokryshkin) do
var i, unit, ruSolds;
begin
    ruSolds = [Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    DialogueOn;
    Say(Belkov, 'D7a-Bel-1');
    DialogueOff;

    for i := 1 to ruSolds do
    begin
        ComMoveXY(ruSolds[i], 87 + i, 63 + i);
        AddComTurnXY(ruSolds[i], 89, 67);
    end;
        
    ComMoveXY(Belkov, 89, 67);
    AddComTurnXY(Belkov, 90, 66);

    Say(Saseda, 'D7a-RSol1-1');
    Say(AmSol, 'D7a-FSol1-1');

    wait(0$12);

    Say(Belkov, 'D7b-Bel-1');
    Say(Jaworska, 'D7b-FSol1-1');
    Say(Belkov, 'D7b-Bel-2');
    Say(Saseda, 'D7a-RSol1-2');

    for unit in ruSolds do
        if (GetDistUnits(Burlak, unit)) < 7 then
        begin
            InGameOn;

            CenterNowOnUnits([Belkov, Saseda]);
            ComMoveXY(Burlak, 90, 68);
            ComTurnUnit(Belkov, Burlak);
            AddComTurnXY(Burlak, 90, 66);

            Say(Burlak, 'D7b-Bur-3');
            Say(Belkov, 'D7b-Bel-4');
            Say(Burlak, 'D7b-Bur-4');
            Say(Belkov, 'D7b-Bel-5');
            Say(Jaworska, 'D7b-FSol1-5');
            Say(Belkov, 'D7b-Bel-6');
            Say(Burlak, 'D7b-Bur-6');
            Say(Saseda, 'D7a-RSol1-6');

            InGameOff;
            SetSide(ruSolds, 3);
            ChangeMissionObjectives('M3');

            secondAttack = true;
            
            exit;
        end;

    Say(Belkov, 'D7b-Bel-3');
    InGameOff;

    belkovWaitForGorky = true;
end;

every 0$1 trigger belkovWaitForGorky do
var unit, ruSolds;
begin
    enable;

    ruSolds = [Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    for unit in ruSolds do
        if (GetDistUnits(Burlak, unit)) < 7 then
        begin
            belkovWaitForGorky = false;
            InGameOn;

            CenterNowOnUnits([Belkov, Saseda]);
            ComMoveXY(Burlak, 90, 68);
            ComTurnUnit(Belkov, Burlak);
            AddComTurnXY(Burlak, 90, 66);

            Say(Belkov, 'D7b-Bel-3a');
            Say(Burlak, 'D7b-Bur-3');
            Say(Belkov, 'D7b-Bel-4');
            Say(Burlak, 'D7b-Bur-4');
            Say(Belkov, 'D7b-Bel-5');
            Say(Jaworska, 'D7b-FSol1-5');
            Say(Belkov, 'D7b-Bel-6');
            Say(Burlak, 'D7b-Bur-6');
            Say(Saseda, 'D7a-RSol1-6');

            InGameOff;

            SetSide(ruSolds, 3);
            ChangeMissionObjectives('M3');

            secondAttack = true;
            
            exit;
        end;
end;

// Second attack  - execute in event On Contact
Export Function Dial_SecondAttack;
begin
    Say(AmSol, 'D8-FSol1-1');
    Say(Gladstone, 'D8-Glad-1');
    Say(Burlak, 'D8-Bur-1');
end;

// Dialog about camp in trees
every 0$1 trigger GetLives(am_bun) < 350 and FilterUnitsInArea(TreeArea, [[f_side, 3], [f_type, unit_human]]) > 0 do
begin
    Say(AmSol, 'D8-FSol1-2');
end;

// Destroy bunker
every 0$1 trigger IsDying(am_bun) do
begin
    Say(AmSol, 'D8-FSol1-2a');
    Say(Gladstone, 'D8-Glad-2');

    ComMoveToArea([Gladstone, AmSol], AmericanExitArea);

    Say(Burlak, 'D8-Bur-2');
end;

// Kill Gladstone and Holland
every 0$1 trigger IsDead(Gladstone) AND IsDead(AmSol) AND NOT gladstoneEscape AND NOT amSolEscape do
    Say(Burlak, 'D9-Bur-1');

// Kill Holland but Gladstone escape
every 0$1 trigger IsDead(Gladstone) AND IsDead(AmSol) AND gladstoneEscape AND NOT amSolEscape do
    Say(Burlak, 'D9-Bur-1a');

// Kill Gladstone but Holland escape
every 0$1 trigger IsDead(Gladstone) AND IsDead(AmSol) AND NOT gladstoneEscape AND amSolEscape do
    Say(Burlak, 'D9-Bur-1b');

// Americans escape
every 0$1 trigger IsDead(Gladstone) AND IsDead(AmSol) AND gladstoneEscape AND amSolEscape do
    Say(Burlak, 'D9-Bur-1c');

// Destroy all americans buildings
every 0$1 trigger FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_ok]]) = 0 do
var ruKirovSolds, ruKirovSoldsMaleRandom;
begin
    ruKirovSolds = FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]) diff [Burlak, Gnyevko];
    ruKirovSoldsMaleRandom = UnitFilter(ruKirovSolds, [f_sex, sex_male]);

    DialogueOn;

    if IsOK(Belkov) then
        Say(Belkov, 'D10-Bel-1');

    if ruKirovSolds > 0 AND (ruKirovSoldsMaleRandom > 0 OR IsOk(Belkov)) then
    begin
        Say(Burlak, 'D10-Bur-1');

        if IsOK(Belkov) then
            Say(Belkov, 'D10-Bel-1a')
        else
            Say(ruKirovSoldsMaleRandom[1], 'D10-RSol1-1');

        Say(Burlak, 'D10-Bur-2');
    end;

    DialogueOff;

    ChangeMissionObjectives('M4a');

    evacuationAvailable = true;

    SetAreaMapShow(ExitArea, 1);
end;

// Spawn Jelena
every 0$15 trigger evacuationAvailable do
begin
    PlaceUnitArea(Kirilenkova, JelenaSpawnArea, true);
    wait(0$3);

    DialogueOn;
    SayRadio(Kirilenkova, 'D11b-Kir-1');

    if IsOK(Belkov) then
    begin
        Say(Belkov, 'D11b-Bel-1');
        SayRadio(Belkov, 'D11b-Bel-1a');
    end
    else
    begin
        DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]), 'D11b-RSol1-1', 'D11b-RFSol1-1');

    if FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) > 0 then
        SayRadio(Kirilenkova, 'D11b1-Kir-1')
    else
        SayRadio(Kirilenkova, 'D11b2-Kir-1');

    if attackedByApeman AND FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) > 0 then
    begin
        Say(Burlak, 'D11b1-Bur-1');

        if IsOk(Belkov) then
            Say(Belkov, 'D11b2-Bel-1')
        else
            DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]), 'D11b1-RSol1-1', 'D11b1-RFSol1-1');

        DialogueOff;
        exit;
    end;

    if spotWaterfall then
    begin
        Say(Burlak, 'D11b1-Bur-1a');

        if IsOk(Belkov) then
            Say(Belkov, 'D11b2-Bel-1')
        else
            DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]), 'D11b1-RSol1-1', 'D11b1-RFSol1-1');

        DialogueOff;
        exit;    
    end;

        if IsOk(Belkov) then
            Say(Belkov, 'D11b1-Bel-1')
        else
            DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]), 'D11b1-RSol1-1', 'D11b1-RFSol1-1');

        DialogueOff;
        exit;  
end;
DialogueOff;
end;

// Save Jelena
every 0$1 trigger See(3, Kirilenkova) do
var nearUnit;
begin
    CenterNowOnUnits(Kirilenkova);

    nearUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]), Kirilenkova);

    DialogueOn;

    Say(Kirilenkova, 'D11a-Kir-1');

    if nearUnit = Belkov then
    begin   
        Say(Belkov, 'D11a-Bel-1');
        Say(Kirilenkova, 'D11a-Kir-2');
        Say(Belkov, 'D11a-Bel-2');
    end
    else
    begin
        if GetSex(nearUnit) = sex_male then
            Say(nearUnit, 'D11a-RSol1-2')
        else
            Say(nearUnit, 'D11a-RFSol1-2');
    end;

    DialogueOff;

    SetSide(Kirilenkova, 3);

    wait(0$3);

    Say(Kirilenkova, 'D11c-Kir-1');
    Say(Burlak, 'D11c-Bur-1');
end;
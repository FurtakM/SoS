// Mission intro
Export Function MissionIntro;
begin
    InGameOn;

    CenterOnXY(burlakSpawnPosition[1] - 8, burlakSpawnPosition[2] - 8);

    wait(0$3);

    CenterNowOnUnits(Burlak);

    Say(Burlak, 'D1-Bur-1');

    ComMoveXY(Burlak, 94, 89);

    CenterOnXY(94, 89);

    Say(Burlak, 'D1-Bur-1a');

    ComMoveXY(Burlak, 102, 88);

    CenterOnXY(102, 88);

    Say(Burlak, 'D1-Bur-1b');

    InGameOff;

    ChangeMissionObjectives('M1');

    SaveForQuickRestart;

    introEnd = true;
end;

// Burlak comment waterfall
every 0$1 trigger GetDistUnitXY(Burlak, 74, 88) < 5 AND introEnd do
begin
    spotWaterfall = true;

    Say(Burlak, 'DWaterfall-Bur-1');
end;

// Burlak comment mastodont's corpse
every 0$1 trigger GetDistUnitXY(Burlak, 130, 96) < 5 AND introEnd do
    Say(Burlak, 'DCorpse-Bur-1');

// Burlak contact with apemans
every 0$1 trigger not apemansContact do
var apeman;
begin
    enable;

    for apeman in FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) do
        if GetDistUnits(apeman, Burlak) < 7 then
        begin
            apemansContact = true;
            
            Say(Burlak, 'D2-Bur-1');

            exit;
        end;
end;

// Burlak comment attack by apeman
every 0$1 trigger not attackedByApeman do
var un, apeman;
begin
    enable;

    for apeman in FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) do
        if Attacks(apeman) then
            if GetSide(Attacks(apeman)) = 3 then
            begin
                attackedByApeman = true;
                apemanAttackedBurlak = apeman;

                Say(Burlak,'D2-Bur-1a');

                exit;
            end;
end;

// Burlak attack near lake where will spawn Jelena
every 0$1 trigger NOT attackedByApemanNearLake do
var un, apeman;
begin
    enable;

    for apeman in FilterUnitsInArea(ApeLakeArea, [[f_side, 0], [f_class, class_apeman]]) do
        if Attacks(apeman) then
            if GetSide(Attacks(apeman)) = 3 then
            begin
                attackedByApemanNearLake = true;
                exit;
            end;
end;

// Save radio position if apeman with radio is dying
every 1 trigger IsDying(apemanWithRadio) do
    radioDropPosition = [GetX(apemanWithRadio), GetY(apemanWithRadio)];

// Find radio
every 0$1 trigger IsDying(apemanWithRadio) and SeeXY(3, radioDropPosition[1], radioDropPosition[2]) do
var nearUnit;
begin
    wait(0$2);

    Say(Burlak,'D2a-Bur-1');

    // Burlak is alone
    if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) = 1 then
        Say(Burlak,'D2a-Bur-2')

    // Burlak is with others
    else
    begin
        nearUnit = NearestUnitToXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]) diff Gnyevko, radioDropPosition[1], radioDropPosition[2]);
        
        ComMoveXY(nearUnit, radioDropPosition[1], radioDropPosition[2]);

        case nearUnit of
            Burlak: Say(Burlak, 'D3-Bur-2a');
            Belkov: Say(Belkov,'D2a-Bel-1');
            Kirilenkova: Say(Kirilenkova,'D2a-Kir-1');
        else
            if GetSex(nearUnit) = sex_male then
                Say(nearUnit, 'D2a-RSol1-1')
            else
                Say(nearUnit, 'D2a-RFSol1-1');
        end;

        if (GetSide(Gnyevko) = 3 and IsOK(Gnyevko)) then
            Say(Gnyevko,'D2a-Gny-1');

        if (apemanWithRadio = apemanAttackedBurlak) then
            Say(Burlak, 'D3-Bur-2');

        if (GetSide(Gnyevko) = 8) then
        begin
            Say(Burlak, 'D12-Bur-1');

            if GetSide(Belkov) = 3 AND IsOK(Belkov) then
                Say(Belkov, 'D12-Bel-2');
        end;

    end;
end;

// Gnyevko control lives before save or passing time
every 1 trigger NOT gnyevkoSaved AND apeAttackGnyevkoTime > 0$02 do
begin
  enable;
  SetLives(Gnyevko, gnyevkoStartHP);
end;

// Find Gnyevko
every 0$1 trigger See(3, Gnyevko) do
var nearUnit, randomUnit;
begin
    CenterNowOnUnits(Gnyevko);

    DialogueOn;

    nearUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), Gnyevko);

    if nearUnit = Burlak then
    begin
        Say(Burlak, 'D3-Bur-1');
        Say(Gnyevko, 'D3-Gny-1');
        Say(Burlak, 'D3-Bur-2b');

        if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) = 1 AND NOT meetPokryshkin then
        begin
            Say(Burlak, 'D3-Bur-2c');
            Say(Gnyevko, 'D3-Gny-2');  
            DialogueOff;

            SetSide(Gnyevko, 3);

            gnyevkoSaved = true;
            exit;
        end;

        if (FilterAllUnits([[f_side, 3], [f_type, unit_human]]) > 1 OR meetPokryshkin) AND NOT evacuationAvailable then
        begin
            Say(Burlak, 'D3-Bur-2d');
            Say(Gnyevko, 'D3-Gny-2');  
            DialogueOff;
            
            SetSide(Gnyevko, 3);

            gnyevkoSaved = true;
            exit;
        end;   

        if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) > 1 AND evacuationAvailable then
        begin
            Say(Burlak, 'D3-Bur-2e');
            Say(Gnyevko, 'D3-Gny-2');  
            DialogueOff;

            SetSide(Gnyevko, 3);

            gnyevkoSaved = true;
            exit;
        end; 
    end
    else
    begin
        if GetSex(nearUnit) = sex_male then
            Say(nearUnit, 'D3-RSol1-1')
        else
            Say(nearUnit, 'D3-RFSol1-1');

        Say(Gnyevko, 'D3-Gny-1');

        if GetSex(nearUnit) = sex_male then
            Say(nearUnit, 'D3-RSol1-2')
        else
            Say(nearUnit, 'D3-RFSol1-2');

        Say(Gnyevko, 'D3-Gny-2a');  

        DialogueOff;

        SetSide(Gnyevko, 3);

        gnyevkoSaved = true;
    end;
end;

// Block attack Gnyevko and Jelena by ape before time end
every 1 trigger NOT gnyevkoSaved OR NOT jelenaSaved do
var ape;
begin
    enable;

    if NOT gnyevkoSaved AND apeAttackGnyevkoTime > 0$02 then
        for ape in FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) do
            GetAwayFromXY(ape, GetX(Gnyevko), GetY(Gnyevko), 10, false);

    if NOT jelenaSaved AND apeAttackJelenaTime > 0$02 then
        for ape in FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) do
            GetAwayFromXY(ape, GetX(Kirilenkova), GetY(Kirilenkova), 10, false);

end;

// Count time
every 0$1 trigger NOT gnyevkoSaved OR NOT jelenaSaved do
begin

    enable;

    if NOT gnyevkoSaved AND apeAttackGnyevkoTime > 0$00 then
        apeAttackGnyevkoTime = apeAttackGnyevkoTime - 0$01;

    if IsPlaced(Kirilenkova) AND NOT jelenaSaved AND apeAttackJelenaTime > 0$00 then
        apeAttackJelenaTime = apeAttackJelenaTime - 0$01;

    if IsPlaced(Kirilenkova) AND NOT jelenaSaved then
       achievement_JelenaTimer = achievement_JelenaTimer + 0$01;
end;

// Meet Pokryshkin
every 0$1 trigger (See(3, Pokryshkin) OR See(6, Burlak) OR See(6, Gnyevko)) AND NOT meetPokryshkin do
begin
    enable;

    InGameOn;
    CenterNowOnUnits(Pokryshkin);
    ComWalk(Gnyevko);
    AddComHold([Burlak, Gnyevko]);

    DialogueOn;

    if gnyevkoWaitWithPokryshkin then
    begin
        meetPokryshkin = true;

        ChangeSideFog(6, 3);

        DialogueOff;

        ComMoveXY(Burlak, GetX(Pokryshkin), GetY(Pokryshkin));
        AddComTurnUnit(Burlak, Pokryshkin);
        ComTurnUnit(Pokryshkin, Burlak);

        Say(Gnyevko, 'D4-Gny-3');
        Say(Burlak, 'D4-Bur-3');
        Say(Pokryshkin, 'D4-Pok-4');

        PlaceUnitArea(Charles, CharlesSpawnArea, true);
        wait(0$2);

        Say(Burlak, 'D4-Bur-4');
        Say(Pokryshkin, 'D4-Pok-5');
        SayRadio(Charles, 'D4-Sol1-5');
        Say(Pokryshkin, 'D4-Pok-6');
        SayRadio(Pokryshkin, 'D4-Pok-6a');
        Say(Burlak, 'D4-Bur-6');
        Say(Pokryshkin, 'D4-Pok-7');

        DialogueOff;
        InGameOff;

        ChangeMissionObjectives('M2');

        goToHill = true;

        MoveToHill;

       exit;
    end;

    ComStop([Burlak, Gnyevko]);

    Say(Pokryshkin, 'D4-Pok-1');

    if GetSide(Gnyevko) = 3 AND IsOK(Gnyevko) then
        Say(Gnyevko, 'D4-Gny-1');


    if GetDistUnits(Burlak, NearestUnitToUnit(FilterAllUnits([[f_side, 6], [f_type, unit_human]]), Burlak)) > 8 AND GetDistUnits(Burlak, Gnyevko) > 8 AND GetSide(Gnyevko) = 3 AND IsOK(Gnyevko) then
    begin
        ComWalk(Gnyevko);

        Say(Pokryshkin, 'D4-Pok-2');
        Say(Gnyevko, 'D4-Gny-2');
        Say(Pokryshkin, 'D4-Pok-3');

        gnyevkoWaitWithPokryshkin = true;

        SetSide(Gnyevko, 6);

        AddComMoveXY(Gnyevko, GetX(Pokryshkin), GetY(Pokryshkin));
    end
    else
    begin
        meetPokryshkin = true;

        ChangeSideFog(6, 3);
        DialogueOff;

        if GetSide(Gnyevko) = 3 and IsOK(Gnyevko) then
        begin
            ComMoveXY([Burlak, Gnyevko], GetX(Pokryshkin), GetY(Pokryshkin));
            AddComTurnUnit([Burlak, Gnyevko], Pokryshkin); 
            
            SetSide(Gnyevko, 6);   
        end
        else
        begin
            ComMoveXY(Burlak, GetX(Pokryshkin), GetY(Pokryshkin));
            AddComTurnUnit(Burlak, Pokryshkin);        
        end;

        ComTurnUnit(Pokryshkin, Burlak);

        Say(Burlak, 'D4-Bur-3');
        Say(Pokryshkin, 'D4-Pok-4');

        PlaceUnitArea(Charles, CharlesSpawnArea, true);
        wait(0$2);

        Say(Burlak, 'D4-Bur-4');
        Say(Pokryshkin, 'D4-Pok-5');
        SayRadio(Charles, 'D4-Sol1-5');
        Say(Pokryshkin, 'D4-Pok-6');
        SayRadio(Pokryshkin, 'D4-Pok-6a');
        Say(Burlak, 'D4-Bur-6');
        Say(Pokryshkin, 'D4-Pok-7');

        ChangeMissionObjectives('M2');
        
        goToHill = true;

        MoveToHill;

    end;

    DialogueOff;
    InGameOff;
end;

// Gorky is too far
every 0$1 trigger GetDistUnits(Burlak, Pokryshkin) > 10 and goToHill AND NOT traitor do
var ruSolds;
begin
    enable;

    ruSolds = [Pokryshkin, Saseda, Jaworska, Balei, Belkov];

    if (GetSide(Gnyevko) = 6 AND gnyevkoSaved) then
        ruSolds = ruSolds ^ Gnyevko;

    ComStop(ruSolds);

    CenterNowOnUnits(Burlak);

    DialogueOn;
    Say(Pokryshkin, 'DSplit-Pok-1');
    Say(Burlak, 'DSplit-Bur-1');
    DialogueOff;

    InGameOn;

    repeat
        ComMoveXY(Burlak, GetX(Pokryshkin), GetY(Pokryshkin));
        wait(0$1);
    until GetDistUnits(Burlak, Pokryshkin) < 6;

    InGameOff;

    MoveToHill;
end;

// Gorky attack comrades - execute in event On Contact
Export function Dialogue_AttackComrade;
var ruSolds;
begin
    traitor = true;

    DialogueOn;
    Say(Pokryshkin, 'DAttack-Pok-1');
    DialogueOff;

    SetAttitude(3, 6, att_enemy, true);

    ComAttackUnit(FilterAllUnits([[f_side, 6], [f_type, unit_human]]), Burlak);
end;

// On hill
every 0$1 trigger GetDistUnitXY(Pokryshkin, 65, 59) < 4 AND NOT traitor do
begin

    InGameOn;

    CenterOnXY(55, 50);
    ComMoveXY(Burlak, 66, 57);
    AddComHold(Burlak);
    ComMoveXY(Charles, 59, 55);

    repeat
        wait(0$1);
    until See(1, Pokryshkin);

    CenterNowOnUnits([Charles, Pokryshkin]);

    ComStop(Charles);
    AddComHold(Charles);
    AddComTurnUnit(Charles, Pokryshkin);

    Say(Charles, 'D5-Sol1-1');
    Say(Pokryshkin, 'D5-Pok-1');

    SetAttitude(1, 6, att_enemy, true);
    SetAttitude(1, 3, att_enemy, true);

    Say(Charles, 'D5-Sol1-2');
end;

// Charles died
every 0$1 trigger IsDead(Charles) AND goToHill do
var ruSolds;
begin
    goToHill = false;

    ComStop(FilterAllUnits([[f_side, 6], [f_type, unit_human]]));
    ComWalk(FilterAllUnits([[f_side, 6], [f_type, unit_human]]));
    wait(0$1);

    CenterOnXY(55, 50);

    ComMoveXY(Burlak, 56, 56);
    ComMoveXY(FilterAllUnits([[f_side, 6], [f_type, unit_human]]), 56, 56);

    Say(Burlak, 'D5-Bur-2');
    Say(Pokryshkin, 'D5-Pok-2');
    Say(Belkov, 'D5-Bel-2');
    Say(Pokryshkin, 'D5-Pok-3');

    if (GetSide(Gnyevko) = 6) then
        Say(Gnyevko, 'D5-Gny-3');

    Say(Burlak, 'D5-Bur-3');

    interface_hidden = true;
    IngameOff;

    wait(0$2);

    ComMoveXY(Burlak, 85, 64);
    ComMoveXY(FilterAllUnits([[f_side, 6], [f_type, unit_human]]), 85, 64);

    interface_hidden = false;
    InGameOn;

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-1');

    CenterNowOnUnits(Burlak);
    Say(Burlak, 'D6-Bur-1');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-2');

    CenterNowOnUnits(Burlak);
    Say(Belkov, 'D6-Bel-2');

    CenterNowOnUnits(Burlak);
    Say(Burlak, 'D6-Bur-2');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-3');

    CenterNowOnUnits(Burlak);
    SayRadio(Kurin, 'D6-Kur-3');

    CenterNowOnUnits(Burlak);
    Say(Pokryshkin, 'D6-Pok-4');

    CenterNowOnUnits(Burlak);
    Say(Belkov, 'D6-Bel-4');

    InGameOff;

    interface_hidden = false;

    ComAttackUnit(Pokryshkin, am_bun);

    wait(0$5);

    ComAttackUnit(FilterAllUnits([[f_side, 6], [f_type, unit_human]]), am_bun);

    DoNotAttack(1, Gnyevko);
end;

// Focus fire on Pokryshkin
every 0$1 trigger See(1, Pokryshkin) and IsDead(Charles) do
    ComAttackUnit(Johanna, Pokryshkin);

// Pokryshkin attack bunker
every 0$1 trigger See(3, am_bun) do
begin
    Say(Pokryshkin, 'D7-Pok-1');
    Say(Johanna, 'D7-FSol1-1');
end;

// Pokryshkin dying
every 0$1 trigger IsDying(Pokryshkin) do
var i, unit, ruSolds;
begin
    ruSolds = FilterAllUnits([[f_side, 6], [f_type, unit_human], [f_ok]]) diff Belkov;

    DialogueOn;
    Say(Belkov, 'D7a-Bel-1');
    DialogueOff;

    for i := 1 to ruSolds do
    begin
        ComMoveXY(ruSolds[i], 87 + i, 63 + i);
        AddComTurnXY(ruSolds[i], 89, 67);
    end;
        
    ComMoveXY(Belkov, 89, 67);
    AddComTurnXY(Belkov, 88, 66);

    Say(Saseda, 'D7a-RSol1-1');
    Say(Johanna, 'D7a-FSol1-1');

    wait(0$12);

    Say(Belkov, 'D7b-Bel-1');
    Say(Jaworska, 'D7b-FSol1-1');
    Say(Belkov, 'D7b-Bel-2');
    Say(Saseda, 'D7b-RSol1-2');

    for unit in ruSolds do
        if (GetDistUnits(Burlak, unit)) < 7 then
        begin
            InGameOn;

            CenterNowOnUnits([Belkov, Saseda]);
            ComMoveXY(Burlak, 88, 66);
            ComTurnUnit(Belkov, Burlak);
            AddComTurnXY(Burlak, 90, 66);

            Say(Burlak, 'D7b-Bur-3');
            Say(Belkov, 'D7b-Bel-4');
            Say(Burlak, 'D7b-Bur-4');
            Say(Belkov, 'D7b-Bel-5');
            Say(Jaworska, 'D7b-FSol1-5');
            Say(Belkov, 'D7b-Bel-6');
            Say(Burlak, 'D7b-Bur-6');
            Say(Saseda, 'D7b-RSol1-6');

            InGameOff;
            SetSide(ruSolds, 3);
            SetSide(Belkov, 3);

            ChangeMissionObjectives('M3');

            secondAttack = true;
            
            exit;
        end;

    Say(Belkov, 'D7b-Bel-3');
    InGameOff;

    belkovWaitForGorky = true;
end;

// Belkov wait for Gorky
every 0$1 trigger belkovWaitForGorky do
var unit, ruSolds;
begin
    enable;

    ruSolds = FilterAllUnits([[f_side, 6], [f_type, unit_human]]);

    for unit in ruSolds do
        if (GetDistUnits(Burlak, unit)) < 7 then
        begin
            belkovWaitForGorky = false;
            InGameOn;

            CenterNowOnUnits([Belkov, Saseda]);
            ComMoveXY(Burlak, 88, 66);
            ComTurnUnit(Belkov, Burlak);
            AddComTurnXY(Burlak, 90, 66);

            Say(Belkov, 'D7b-Bel-3a');
            Say(Burlak, 'D7b-Bur-3');
            Say(Belkov, 'D7b-Bel-4');
            Say(Burlak, 'D7b-Bur-4');
            Say(Belkov, 'D7b-Bel-5');
            Say(Jaworska, 'D7b-FSol1-5');
            Say(Belkov, 'D7b-Bel-6');
            Say(Burlak, 'D7b-Bur-6');
            Say(Saseda, 'D7a-RSol1-6');

            InGameOff;

            SetSide(ruSolds, 3);
            SetSide(Belkov, 3);

            ChangeMissionObjectives('M3');

            secondAttack = true;
            
            exit;
        end;
end;

// Second attack  - execute in event On Contact
Export Function Dial_SecondAttack;
var nearRuUnit;
begin

    Say(Johanna, 'D8-FSol1-1');
    Say(Gladstone, 'D8-Glad-1');
    Say(Burlak, 'D8-Bur-1');

    secondAttack = false;

    nearRuUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), am_bun);

    ComExitBuilding(amApemans);
    wait(0$1);
    AddComAttackUnit(amApemans, nearRuUnit);
end;

// Dialog about camp in trees
every 0$1 trigger GetLives(am_bun) < 350 AND GetLives(am_bun) > 250 AND FilterUnitsInArea(TreeArea, [[f_side, 3], [f_type, unit_human]]) > 0  AND NOT amRetreat do
    Say(Johanna, 'D8-FSol1-2');


// Destroy bunker
every 0$1 trigger IsDying(am_bun) AND IsOK(am_lab) AND NOT amRetreat do
begin
    amRetreat = true;

    Say(Johanna, 'D8-FSol1-2a');

    ComMoveToArea([Gladstone, Johanna], AmericanExitArea);

    Say(Gladstone, 'D8-Glad-2');
    Say(Burlak, 'D8-Bur-2');
end;

// Destroy lab
every 0$1 trigger IsDying(am_lab) AND IsOK(am_bun) AND NOT amRetreat do
begin
    amRetreat = true;

    Say(Johanna, 'D8-FSol1-2a');

    ComMoveToArea([Gladstone, Johanna], AmericanExitArea);

    Say(Gladstone, 'D8-Glad-2');
    Say(Burlak, 'D8-Bur-2');
end;

// Retreat if bunker have less than 50% HP - only hard difficulty
every 0$1 trigger GetLives(am_bun) < 500 AND Difficulty = 3 AND NOT amRetreat do
begin
    amRetreat = true;

    Say(Johanna, 'D8-FSol1-2a');

    ComMoveToArea([Gladstone, Johanna], AmericanExitArea);

    Say(Gladstone, 'D8-Glad-2');
    Say(Burlak, 'D8-Bur-2');
end;

// Remove am from map
every 0$1 trigger FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]) > 0 do
var unit;
begin
    enable;

    for unit in FilterUnitsInArea(AmericanExitArea, [[f_side, 1], [f_type, unit_human]]) do
    begin
        if unit = Gladstone then
        begin
            gladstoneEscape = true;
            SaveCharacters(Gladstone, missionPrefix&'Gladstone');
        end
        else
        begin
            JohannaEscape = true;
            SaveCharacters(Johanna, missionPrefix&'Johanna');
        end;

        RemoveUnit(unit);
    end;
end;

// Kill Gladstone and Holland
every 0$1 trigger IsDead(Gladstone) AND IsDead(Johanna) do
    Say(Burlak, 'D9-Bur-1');

// Kill Holland but Gladstone escape
every 0$1 trigger IsDead(Johanna) AND gladstoneEscape AND NOT JohannaEscape do
    Say(Burlak, 'D9-Bur-1a');

// Kill Gladstone but Holland escape
every 0$1 trigger IsDead(Gladstone) AND NOT gladstoneEscape AND JohannaEscape do
    Say(Burlak, 'D9-Bur-1b');

// Americans escape
every 0$1 trigger gladstoneEscape AND JohannaEscape do
    Say(Burlak, 'D9-Bur-1c');

// Destroy all americans buildings
every 0$1 trigger FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_ok]]) = 0 do
var ruKirovSolds, ruKirovSoldsMaleRandom;
begin
    ruKirovSolds = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]) diff [Burlak, Gnyevko, Belkov];
    ruKirovSoldsMaleRandom = UnitFilter(ruKirovSolds, [f_sex, sex_male]);

    DialogueOn;

    if IsOK(Belkov) then
        Say(Belkov, 'D10-Bel-1');

    if ruKirovSolds > 0 AND (ruKirovSoldsMaleRandom > 0 OR IsOk(Belkov)) then
    begin
        Say(Burlak, 'D10-Bur-1');

        if IsOK(Belkov) then
            Say(Belkov, 'D10-Bel-1a')
        else
            Say(ruKirovSoldsMaleRandom[1], 'D10-RSol1-1');

        Say(Burlak, 'D10-Bur-2');
    end;

    DialogueOff;

    ChangeMissionObjectives('M4a');

    evacuationAvailable = true;

    SetAreaMapShow(ExitArea, 1);
end;

// Spawn Jelena
every 0$15 trigger evacuationAvailable AND NOT jelenaSpawned do
var randomSold;
begin
    PlaceUnitArea(Kirilenkova, JelenaSpawnArea, true);
    wait(0$3);

    jelenaSpawned = true;

    if GetDistUnits(NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]), Kirilenkova), Kirilenkova) < 9 then
       exit;

    DialogueOn;
    SayRadio(Kirilenkova, 'D11b-Kir-1');

    if IsOK(Belkov) then
    begin
        Say(Belkov, 'D11b-Bel-1');
        SayRadio(Belkov, 'D11b-Bel-1a');
    end
    else
    begin
        randomSold = DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]), 'D11b-RSol1-1', 'D11b-RFSol1-1', false);
    end;

    if FilterUnitsInArea(ApeLakeArea, [[f_side, 0], [f_class, class_apeman]]) > 0 then
        SayRadio(Kirilenkova, 'D11b1-Kir-1')
    else
        SayRadio(Kirilenkova, 'D11b2-Kir-1');

    if attackedByApemanNearLake AND FilterUnitsInArea(ApeLakeArea, [[f_side, 0], [f_class, class_apeman]]) > 0 then
    begin
        Say(Burlak, 'D11b1-Bur-1');

        if IsOk(Belkov) then
            Say(Belkov, 'D11b2-Bel-1')
        else
        begin
            if GetSex(randomSold) = sex_male then
                Say(randomSold, 'D11b1-RSol1-1')
            else
                Say(randomSold, 'D11b1-RFSol1-1');
        end;
            DialogRandom(FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]), 'D11b1-RSol1-1', 'D11b1-RFSol1-1', false);

        DialogueOff;
        exit;
    end;

    if spotWaterfall then
    begin
        Say(Burlak, 'D11b1-Bur-1a');

        if IsOk(Belkov) then
            Say(Belkov, 'D11b2-Bel-1')
        else
        begin
            if GetSex(randomSold) = sex_male then
                Say(randomSold, 'D11b1-RSol1-1')
            else
                Say(randomSold, 'D11b1-RFSol1-1');
        end;

        DialogueOff;
        exit;    
    end;

    if IsOk(Belkov) then
        Say(Belkov, 'D11b1-Bel-1')
    else
    begin
        if GetSex(randomSold) = sex_male then
            Say(randomSold, 'D11b1-RSol1-1')
        else
            Say(randomSold, 'D11b1-RFSol1-1');
    end;

    DialogueOff;
    exit;
end;

// Save Jelena
every 0$1 trigger See(3, Kirilenkova) AND jelenaSpawned do
var nearUnit;
begin
    SetSide(Kirilenkova, 3);

    jelenaSaved = true;

    CenterNowOnUnits(Kirilenkova);

    nearUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]) diff Kirilenkova, Kirilenkova);

    DialogueOn;

    Say(Kirilenkova, 'D11a-Kir-1');

    if nearUnit = Belkov then
    begin   
        Say(Belkov, 'D11a-Bel-1');
        Say(Kirilenkova, 'D11a-Kir-2');
        Say(Belkov, 'D11a-Bel-2');
    end
    else
    begin
        if GetSex(nearUnit) = sex_male then
            Say(nearUnit, 'D11a-RSol1-2')
        else
            Say(nearUnit, 'D11a-RFSol1-2');
    end;

    DialogueOff;

    wait(0$5);

    Say(Kirilenkova, 'D11c-Kir-1');
    Say(Burlak, 'D11c-Bur-1');
end;

// Communicat with end mission
every 0$1 trigger evacuationAvailable AND FilterUnitsInArea(ExitArea, [[f_side, 3], [f_type, unit_human]]) > 0 do
begin
    Query('QExit1');
end;
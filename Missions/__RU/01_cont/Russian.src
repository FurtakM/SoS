Export Burlak;
Export Pokryshkin, Saseda, Jaworska, Balei, Belkov, Jaunzeme;
Export Kirilenkova;
Export Gnyevko;
Export Kurin;

Export kirovSolds;

Export Function PrepareRussian;
begin
    PrepareHero;
    PreparePokryshkinTeams;
    PrepareJelena;
    PrepareGnyevko;
    PrepareKurin;
end;

Function PrepareHero;
begin
    uc_side = 3;

    Burlak := PrepareUnitEx('Burlak', false);

    PlaceUnitXY(Burlak, burlakSpawnPosition[1], burlakSpawnPosition[2], true);
end;

Function PrepareKurin;
begin
    uc_side = 6;

    Kurin := PrepareUnitEx('Kurin', false);
end;

Function PreparePokryshkinTeams;
begin
    uc_side = 6;

    Pokryshkin := PrepareUnitEx('Pokryshkin', false);
    Saseda := PrepareUnitEx('Saseda', false);
    Jaworska := PrepareUnitEx('Jaworska', false);
    Balei := PrepareUnitEx('Balei', false);
    Belkov := PrepareUnitEx('Belkov', false);
     
    PlaceUnitArea(Pokryshkin, PokryshkinSpawnArea, false);
    PlaceUnitArea(Saseda, Ru2SpawnArea, false);
    PlaceUnitArea(Jaworska, Ru2SpawnArea, false);
    PlaceUnitArea(Balei, Ru2SpawnArea, false);
    PlaceUnitArea(Belkov, Ru2SpawnArea, false);
    
    kirovSolds = [Pokryshkin, Saseda, Balei, Belkov, Jaworska];

    if Difficulty < 3 then
    begin
        Jaunzeme := PrepareUnitEx('Jaunzeme', false);

        PlaceUnitArea(Jaunzeme, Ru2SpawnArea2, false);

        kirovSolds = kirovSolds ^ Jaunzeme;
    end;
end;

Function PrepareJelena;
begin
    uc_side = 5;

    Kirilenkova := PrepareUnitEx('Kirilenkova', false);
end;

Function PrepareGnyevko;
var un, i, randomPosition;
begin
    uc_side = 5;

    Gnyevko := PrepareUnitEx('Gnyevko', false);

    randomPosition = gnyevkoSpawnPositions[rand(1, gnyevkoSpawnPositions)];

    PlaceUnitXY(Gnyevko, randomPosition[1], randomPosition[2], false);

    SetLives(Gnyevko, gnyevkoStartHP);
    ComHold(Gnyevko);


    // Spawn ape near Gnyevko
    for i := 1 to animalsAmount[1] - [3, 2, 1][Difficulty] do
    begin
        uc_side = 0;
        uc_nation = nation_nature;
        hc_class = class_apeman;
        hc_skills = [animalsStats[1], animalsStats[2], animalsStats[3], animalsStats[4]];     
        hc_agressivity = apeAgression; 

        un = CreateHuman;
        PlaceUnitXYR(un, GetX(Gnyevko), GetY(Gnyevko), 8, false);
    end;
end;
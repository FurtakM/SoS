On BuildingComplete(building) Do
begin
     if GetSide(building) = 3 AND GetBType(building) = b_depot then
        SetBname(building, 'bukharin');

    if GetSide(building) = 3 and GetBTYpe(building) in [b_lab, b_lab_basic, b_lab_full, b_lab_half] AND NOT buildedLab then
       buildedLab = true;

    if GetSide(building) = 2 then
    begin
        if GetBType(building) = b_lab then
            ComUpgradeLab(building, b_lab_weapon);

        if GetBType(building) = b_workshop then
            ComUpgrade(building);

        if GetBType(building) = b_ext_gun then
            InitAIManager(2, nation_arabian, arabianUnitsInBase, ArabianBaseArea, ArabianHealArea, ArabianParkingArea, ArabianDefenceArea, [2, 2, 3, 3][Difficulty], 1, [1, 1, 2, 2][Difficulty], true);
    end;
end;

On ApemanTamed(apeman, human) Do
begin
    if GetSide(human) = 3 AND GetNation(human) = 3 AND NOT dial_tameApeBlocker then
        Dial_TameApe(human);
end;

On ResearchComplete(research, building) Do
begin
    if research = tech_apelang AND GetSide(building) = 3 then
        Dial_ResearchApeLang;

    if research = tech_apepsych AND GetSide(building) = 3 then
        Dial_ResearchApePsych;

    if research = tech_apeagres AND GetSide(building) = 3 then
        Dial_ResearchApeAgress;
end;

On EnterBuilding(building, human) Do
begin
    if GetSide(human) = 3 AND GetClass(human) = class_apeman AND (GetBtype(building) = b_lab OR GetBType(building) = b_lab_half) AND NOT dial_apeEnterLabBlocker then
        Dial_ApeEnterLab;
end;

On UnitGoesToRed(unit) Do
begin
    if unit in arabianPatrolVehicles then
        arabianPatrolVehicles = arabianPatrolVehicles diff unit;

    if unit in arabianHovercrafts then
        arabianHovercrafts = arabianHovercrafts diff unit;

   if unit in arabianPatrolTroops then
      arabianPatrolTroops = arabianPatrolTroops diff unit;
end;

On UnitDestroyed(unit) Do
begin
    if unit in arabianUnitsInBase then
        arabianUnitsInBase = arabianUnitsInBase diff unit;

    if unit = Burlak then
        Lose_Burlak;

    if FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_nation, 3]]) < 6 then
        Lose_Survive;

    if GetType(unit) = unit_vehicle AND GetSide(unit) = 2 AND GetChassis(unit) = ar_hovercraft then
       achievement_DestroyedHovercrafts = achievement_DestroyedHovercrafts + 1;
end;

On Contact(side1, side2) Do
begin
    if (side1 = 3 AND side2 = 6 OR side1 = 6 AND side2 = 3) AND GetSide(Stevens) = 6 then
    begin
        stevensFollow = false;
        stevensCanExitMap = true;
        stevensWasInDepot = false;
        stevensDepot = false;   

        SetSide(Stevens, 1);

        ComMoveToArea(Stevens, StevensExitMapArea);
    end;
end;

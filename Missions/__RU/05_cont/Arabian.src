Export Heike, Omar;
Export arabianUnitsInBase, arabianPatrolVehicles, arabianHovercrafts, arabianPatrolTroops;
Export arabLeftExit, arabRightExit;
Export Function PrepareArabians;
begin
    arabianUnitsInBase = [];
    arabianHovercrafts = [];
    arabianPatrolTroops = [];

    arabLeftExit = true;

    PrepareHeroes;
    PrepareBuildings;   
    PrepareUnits;
    PrepareArabianPatrolVehicles(arabianPatrolVehiclesAmount, ArabianPatrolExitArea1);
    PrepareArabianHovercraft;
end;

Function PrepareHeroes;
begin
    uc_side = 2;

    Heike = PrepareUnitExt('Heike', false, 0);  
    Omar = PrepareUnitExt('Omar', false, class_desert_warior);   

    PlaceHumanInUnit(Omar, mastodon); 
end;

Function PrepareBuildings;
var depot;
begin

    uc_side = 2;
    uc_nation = nation_arabian;

    bc_level = enemySkillLevel;
    bc_type = b_breastwork;

    CreateAndPlaceBuildingXYD(118, 63, 1);
    CreateAndPlaceBuildingXYD(120, 66, 1);
    CreateAndPlaceBuildingXYD(123, 73, 1);

    if Difficulty = 2 then
        CreateAndPlaceBuildingXYD(145, 83, 0);

    if Difficulty = 3 then
    begin
        CreateAndPlaceBuildingXYD(145, 83, 0);
        CreateAndPlaceBuildingXYD(151, 81, 5);    
    end;

    bc_type = b_depot;

    depot = CreateAndPlaceBuildingXYD(124, 60, 2);

    SetResourceType(GetBase(depot), mat_cans, 3000);
    SetResourceVisibility(131, 51, 2);

    bc_type = b_oil_mine;

    CreateAndPlaceBuildingXYD(131, 51, 2);  


    // Techs

    case Difficulty of
        1:begin
            SetTech(tech_tech1, 2, state_researched);
        end;

        2:begin
            SetTech(tech_tech1, 2, state_researched);
            SetTech(tech_weap1, 2, state_researched);
        end;

        3:begin
            SetTech(tech_tech1, 2, state_researched);
            SetTech(tech_weap1, 2, state_researched);
            SetTech(tech_tech2, 2, state_researched);
            SetTech(tech_weap2, 2, state_researched);
        end;
    end;
end;


Function PrepareUnits;
var i, unit, bunker;
begin
    uc_side = 2;
    uc_nation = 2;

    for bunker in FilterAllUnits([[f_side, 2], [f_btype, b_breastwork]]) do
    begin
        PrepareSoldier(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceHumanInBunker(unit, bunker);
        arabianUnitsInBase = arabianUnitsInBase ^ unit;
    end;

    for i := 1 to arabianUnits[1] do
    begin
        PrepareSoldier(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceUnitArea(unit, ArabianSpawnArea1, false);  
        arabianUnitsInBase = arabianUnitsInBase ^ unit;  
    end;

    for i := 1 to arabianUnits[2] do
    begin
        PrepareEngineer(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceUnitArea(unit, ArabianSpawnArea1, false); 
        arabianUnitsInBase = arabianUnitsInBase ^ unit;   
    end;

    for i := 1 to arabianUnits[3] do
    begin
        PrepareMechanic(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceUnitArea(unit, ArabianSpawnArea1, false);    
        arabianUnitsInBase = arabianUnitsInBase ^ unit;
    end;

    for i := 1 to arabianUnits[4] do
    begin
        PrepareScientist(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceUnitArea(unit, ArabianSpawnArea1, false);  
        arabianUnitsInBase = arabianUnitsInBase ^ unit;  
    end;

end;

Export Function PrepareArabianPatrolSoldiers;
var i, unit;
begin
    uc_side = 2;
    uc_nation = 2;

    for i := 1 to arabianPatrolSoldiers do
    begin
        PrepareSoldier(0, enemySkillLevel);
        unit = CreateHuman;

        PlaceUnitArea(unit, ArabianSpawnArea2, false);  
        arabianPatrolTroops = arabianPatrolTroops ^ unit;
    end;

end;

Export Function PrepareArabianPatrolVehicles(amount, area);
var i, veh;
begin
    uc_side = 2;
    uc_nation = 2;

    for i := 1 to amount do
    begin
        PrepareVehicle(ar_half_tracked, engine_combustion, control_manual, [ar_flame_thrower, ar_gatling_gun, ar_gun][Rand(1, 3)], 100); 

        veh = CreateVehicle;
        arabianPatrolVehicles = arabianPatrolVehicles ^ veh;

        PlaceUnitArea(veh, area, false);     

        PrepareMechanic(0, enemySkillLevel);
        PlaceHumanInUnit(CreateHuman, veh);
    end;
end;

Export Function PrepareArabianHovercraft;
var i, veh;
begin
    uc_side = 2;
    uc_nation = 2;

    for i := 1 to arabianPatrolVehiclesAmount do
    begin
        PrepareVehicle(ar_hovercraft, engine_combustion, control_manual, [ar_double_machine_gun, ar_light_gun][Rand(1, 2)], 100); 

        veh = CreateVehicle;
        arabianHovercrafts = arabianHovercrafts ^ veh;

        PlaceUnitArea(veh, ArabianSpawnArea3, false);     

        PrepareMechanic(0, enemySkillLevel);
        PlaceHumanInUnit(CreateHuman, veh);
    end;

end;
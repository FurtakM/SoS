// Russian mission 3
// Map by Altar, extended by Serpent
// SAIL by Nefarem
// Consultation - SoSTeam

Starting
begin
    InitMission;
end;

// Global campaign variables
Export meetBelkovBrother;
Export cratesResources, oilResources;
Export vehiclesPreviousMission;
Export kurinOpinion, platonovOpinion;
Export vehiclesToSave;

// Current mission variables
Export spawnCrates;
Export scholtzeSpawned, scholtzeSaved;
Export dialog_workshop, dialog_amSupportTroops, dialog_track, dialog_stop;
Export americanTroopsArriveOmicronCounter;
Export kurinGiveMoreTime, extraTime, timeToArrive;
Export produceMoreVehicles, produceOnlyForYou, kurinCheckVehicles;
Export meetNearOmicronBase, attackOmicron;
Export kirovFactory, kirovCompleteFactory;
Export omicronSurrender, canExitBattlefield;
Export immortalSurvivors;

Export canDestroyBeriaBase;
Export enableOmicronAI;
Export kirovCanCaptureVehicles;
Export endCutscene;

Function InitMission;
begin
    ResetFog;
    RandomizeAll;
    InGameOn;

    // Init map config
    InitMapConfig;

    // Global campaign variables init values

    meetBelkovBrother = LoadVariable('02_MeetBelkovBrother_6', 0);
    cratesResources = LoadVariable('02_BeriaResourceCrates_7', 0);
    oilResources = LoadVariable('02_BeriaResourceOil_8', 0);

    vehiclesPreviousMission = LoadVariable('02_Vehicles_1', 0);

    platonovOpinion = LoadVariable('02_PlatonovOpinion_9', 0); // 0 - very bad, 1 - bad, 2 - good

    kurinOpinion = -1;  // 0 - bad,  1 - good

    vehiclesToSave = []; // [ [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i)];

    // Mission variables init values

    scholtzeSpawned = false;
    scholtzeSaved = false;

    americanTroopsArriveOmicronCounter = 0;

    spawnCrates = true;

    dialog_track = false;
    dialog_workshop = false;
    dialog_stop = false;

    counterEnable = false;

    extraTime = 7$00;
    kurinGiveMoreTime = false;

    timeToArrive = 2$00;

    produceOnlyForYou = false;
    produceMoreVehicles = false;
    kurinCheckVehicles = false;

    omicronSurrender = false;

    canExitBattlefield = true;

    immortalSurvivors = false;

    canDestroyBeriaBase = false;

    endCutscene = false;

    kirovCanCaptureVehicles = [];

    // AI variables
    kirovFactory = 0;
    kirovCompleteFactory = false;

    enableOmicronAI = true;

    // Init units
    PrepareRussians;
    PrepareAmericans;
    Nef_PrepareNature;

    // Init mission intro
    MissionIntro;
end;
Export Function CustomEvent(event);
begin

End;

On Command(com) do
var i, j, temp, order, x, y, building;
begin

    for i in FilterAllUnits([[f_side,3],[f_type,unit_human]]) do
        if GetTaskList(i) > 0 then
            for j = 1 to GetTaskList(i) do
            begin
                temp = GetTaskList(i)[j][4];
                building = GetTaskList(i)[j][5];
                x = GetTaskList(i)[j][2];
                y = GetTaskList(i)[j][3];

                if (GetSide(temp) = 6 and GetTaskList(i)[j][1] = '<') then
                    SetTaskList(i, []);
            end;

end;

On BuildingCaptured(building, side, human) Do
begin
    if side = 6 then
        SetSide(building, 6);
end;

On BuildingComplete(building) Do
var diffUnits, canTakeUnits;
begin
    diffUnits = [];

    if GetSide(building) = 3 AND GetBType(building) = b_depot AND IsInArea(building, SecondBurlakBase) then
        SetBName(building, 'kagan');

    if GetSide(building) = 6 then
    begin
        if GetBType(building) = b_ext_noncombat then
        begin
            SpawnKirovSupport;

        end;

        if GetBType(building) = b_depot then
        begin
            kirovOilDepot = building;
            SetResourceType(GetBase(kirovOilDepot), mat_oil, 3000);
            ComEnterUnit(Grishko, building);
        end;
            
        if (GetBType(building) = b_breastwork OR GetBType(building) = b_armoury) AND IsInArea(building, SecondKurinBase) then
            ComEnterUnit(UnitFilter(secondBaseTeam, [[f_class, class_soldier], [f_outside], [f_not, [f_hastask]]])[1], building);
    end;
end;

On UpgradeComplete(building) Do
var i, tmp;
begin
    if GetSide(building) = 3 AND GetBType(building) = b_factory AND NOT dialog_workshop then
        Dial_UpgradeWorkshop(building);
end;

On UnitDestroyed(un) do
begin
   if un = Burlak then
    Lose_Burlak;

   if un = beriaDepot AND NOT canDestroyBeriaBase then
    Lose_Omicron;
end;

On Contact(side1, side2) do
begin
    if side1 = 3 AND side2 = 6 then
        Lose_SelfAttack;
end;

On VehicleConstructed(vehicle, factory) do
begin
    if GetSide(vehicle) = 6 then
    begin
        if GetWeapon(vehicle) = ru_cargo_bay AND NOT kirovTransportCargo then
            kirovTransportCargo = vehicle
        else
        begin
            if GetWeapon(vehicle) = ru_cargo_bay then
            begin
                ComTransport(vehicle, kirovOilDepot, mat_oil);
                AddComMoveXY(vehicle, 137, 99);
                AddComHold(vehicle);

                kirovAttackCargo = kirovAttackCargo ^ vehicle;
            end;
                
        end;
        
        if GetWeapon(vehicle) in [ru_heavy_machine_gun, ru_gatling_gun, ru_gun] then
        begin
            kirovAttackVehicles = kirovAttackVehicles ^ vehicle;
            ComMoveXY(vehicle, 137, 99);
        end;
            
    end;
End;

On VehicleCaptured(new, old, side, capturing_unit) do
begin
    if side = 6 then
    begin
        ComExitVehicle(capturing_unit);
        SetSide(new, 6);
    end;
End;

On EnterVehicle(vehicle, human) Do
begin
    if GetSide(vehicle) = 6 AND meetNearOmicronBase then
    begin
        wait(0$2);
        AddComAgressiveMove(vehicle, 78, 39);
        KirovAttackOmicron;
    end;
end;

On ResearchComplete(research, building) Do
begin
   if GetSide(building) = 6 AND research = tech_gun then
   begin
      MountWeaponTurrent;
      wait(3$30);
      KirovProduceVehicles(FilterAllUnits([[f_side, 6], [f_type, unit_building], [f_btype, b_factory]])[1]);
   end;
end;


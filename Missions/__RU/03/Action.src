Export currentTeam, kirovBaseUnits, bukovMsg;
Export counterEnable, counterIndex, ticks, convoyTime, timeToPrepareAttack, timeToArabAttack;
Export kirovAttackVehicles;
Export Function MissionIntro;
var i, un, selectedPeople, minmax, tmp;
begin
if not debug then
InGameOn;
RevealFogArea(3, RevealMapPartArea);
CenterNowOnUnits([Burlak, Kurin]);

bukovMsg := ['D3a-RSol1-1', 'D3b-RSol1-1'];

//BELKOV TEAM DEAD
if NOT Belkov then
   begin                                          
   Say(Kurin, 'D1a-Kur-1');
   Say(Burlak, 'D1a-Bur-1');
   Say(Kurin, 'D1a-Kur-2');
   //ANGRY PLATO
   Say(Kurin, 'D1a-Kur-2a');
   Say(Kurin, 'D1a-Kur-2b'); 
   Say(Burlak, 'D1a-Bur-2');
   end
else
   begin
   Say(Kurin, 'D1b-Kur-1');
   Say(Belkov, 'D1b-Bel-1');
   Say(Kurin, 'D1b-Kur-2');
   end;

Say(Kurin, 'D2-Kur-1');
Say(Burlak, 'D2-Bur-1');
Say(Kurin, 'D2-Kur-2');
Say(Burlak, 'D2-Bur-2');
Say(Kurin, 'D2-Kur-3');
PrepareAmericansMissionIntro;
SetAttitude(1, 6, att_friend, true);
CenterNowOnUnits([Popov, Gaydar]);
wait(0$3);
SayRadio(Popov, 'D2-Pop-3');
wait(0$0.3);
CenterNowOnUnits([Burlak, Kurin]);
Say(Kurin, 'D2-Kur-4');
Say(Burlak, 'D2-Bur-4');
Say(Kurin, 'D2-Kur-5');
Say(Burlak, 'D2-Bur-5');
Say(Kurin, 'D2-Kur-6');
Say(Burlak, 'D2-Bur-4');


kirovBaseUnits = otherKirovUnits;
kirovBaseUnits = kirovBaseUnits ^ otherBeriaUnits;

tmp := FilterAllUnits([[f_side, 3], [f_type, unit_human]]);

minmax := 8 - Length(tmp);

selectedPeople = CharacterSelection('text', minmax, minmax,
                                [sel_change_class, Burlak, Gnyevko, Kovalyuk, Kirilenkova, Belkov,
                                 sel_dont_change_class,        
                                 sel_not_hired,sel_changeable, sel_change_class]^kirovBaseUnits^
                                [sel_dont_change_class, sel_not_changeable, Kurin, Popov],
                                [class_soldier, class_mechanic, [class_engineer, 1], [class_scientistic, 1]]);
SetSide(kirovBaseUnits, 6);
SetSide(selectedPeople, 3);

AddComMoveXY(Popov, 135, 67);
AddComMoveXY(Gaydar, 209, 164);

kirovBarracks := FilterAllUnits([[f_side, 6], [f_btype, b_armoury]]);
kirovBaseUnits := kirovBaseUnits diff selectedPeople;
SetClass(kirovBaseUnits[1], 2);
for i:=2 to 3 do
SetClass(kirovBaseUnits[i], 3);

for un in selectedPeople do
    ComExitBuilding(un);

currentTeam := selectedPeople union [Burlak, Gnyevko, Kovalyuk, Belkov, Kirilenkova];
AddComMoveXY(currentTeam, 113, 97);

solsKirov := FilterAllUnits([[f_side, 6], [f_type, unit_human], [f_class, class_soldier]]);
solsKirov := solsKirov diff [Kurin, Bukov, Gaydar, Popov];

for un in kirovBaseUnits do
    begin
    if IsInUnit(un) = 0 then
       ComEnterUnit(un, kirovBarracks[1]);
    end;

ChangeMissionObjectives('M1');

Query('QReadyInfo');
//counterEnable := true;
SaveForQuickRestart;
wait(0$8);
InGameOff;
spawnCrates := true;

for un in kirovBaseUnits do
    begin
    if IsInUnit(un) = 0 AND NOT GetClass(un) = 2 then
       ComEnterUnit(un, kirovBarracks[2]);
    end;

UpgradeKirovBase;

End;

//COUNTER ARAB
every 0$1 do
begin
timeToArabAttack = timeToArabAttack  - 0$01;
end;

//COUNTER
every 0$1 trigger counterEnable do
var veh, hum, i;
begin
       
enable;                

veh = FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_not, [f_weapon, ru_cargo_bay]]]);
hum = FilterAllUnits([[f_side, 3], [f_type, unit_human]]) + kurinVehNeeded;

ticks = ticks + 0$01;

VehNeeded = hum - veh;

if VehNeeded < 0 then
   enoughVehsForKirov = true
else
   enoughVehsForKirov = false;

case counterIndex of
1:begin

if VehNeeded <= 0 then
   display_strings = ['#Ru03-5a'] ^ ['#Ru03-4', timeToPrepareAttack]
else
   display_strings = ['#Ru03-5', VehNeeded] ^ ['#Ru03-4', timeToPrepareAttack];  //#Ru03-5'

timeToPrepareAttack = timeToPrepareAttack - 0$01;

end;

2:begin
display_strings=['#Ru03-1', timeToArrive];
timeToArrive = timeToArrive - 0$01;
end;

3:begin
display_strings=['#Ru03-2', timeToArrive];
timeToArrive = timeToArrive - 0$01;
end;

4:begin
display_strings=['#Ru03-3', extraTime];
extraTime = extraTime - 0$01;
end;

End;
End;

//CONVOYS
every 0$1 do
begin
enable;

if tick = convoyTime[1] then
   begin                                 
   SayRadio(Bukov, bukovMsg[1]);
   
   PrepareAmericanConvoy(amConvoyGroups[1][1], amConvoyGroups[1][2], amConvoyGroups[1][3], amConvoyGroups[1][4], amConvoyGroups[1][5], AmTroopsSpawn);
   if bukovMsg[1] = 'D3a-RSol1-1' then
   begin
      SayRadio(Burlak, 'D3a-Bur-1');
      if IsOk(Gnyevko) then
         Say(Gnyevko, 'D3b-Gny-1');
         Say(Burlak, 'D3b-Bur-1');
      if IsOK(Kovalyuk) then
         Say(Kovalyuk, 'D3b-Kov-1');
         SayRadio(Bukov, 'D3b-Rsol1-2');
   bukovMsg := bukovMsg diff bukovMsg[1];
   end;
                                     
//PrepareAmericanConvoy(amConvoyGroups[1][1], amConvoyGroups[1][2], amConvoyGroups[1][3], amConvoyGroups[1][4], amConvoyGroups[1][5], AmTroopsSpawn);
//ATTACKS ON BERIA/KAGAN
PrepareAttackOnRus();

wait(0$1);

amConvoyGroups := amConvoyGroups diff amConvoyGroups[1];
convoyTime := convoyTime diff convoyTime[1];
   end;
End;

// Find oil deposit
Every 0$1 trigger GetResourceVisibility(78, 112, 3) do
begin
CenterOnXY(78, 112);
DialogueOn;
SayRadio(Burlak, 'D2a-Bur-1');
SayRadio(Kurin, 'D2a-Kur-1');
DialogueOff;

ChangeMissionObjectives('M2');
end;

//AM ATTACK RETREAT
Every 0$1 trigger UnitFilter(amVehs, [f_not,  [f_lives, 700]]) or UnitFilter(amIntroUnits, [f_not,  [f_lives, 700]])  do
var i;
begin
enable;

for i := 1 to amVehs do
    begin
    if GetLives(amVehs[i]) <= 700 then
       ComMoveToArea(amVehs[i], amRetreatArea);
    end;

for i := 1 to amIntroUnits and GetLives(i) <= 700 do
    begin
    if GetLives(amIntroUnits[i]) <= 700 then
       ComMoveToArea(amIntroUnits[i], amRetreatArea);
    end;
           
for i in amIntroUnits do
begin
if IsInArea(i, amRetreatArea) and GetLives(i) <= 700 then
   SetLives(i, 1000);
end;

for i in amVehs do
begin
if IsInArea(i, amRetreatArea) then
   SetLives(i, 1000);
end;

end;

// Dialog track
every 0$1 trigger NOT dialog_track do
var vehicle;
begin
enable;

for vehicle in FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_or, [f_chassis, ru_medium_wheeled], [f_chassis, ru_heavy_wheeled]]]) do
    if GetDistUnitXY(vehicle, 48, 81) < 7 then
    begin
    dialog_track = true;

    CenterNowOnXY(48, 81);

    DialogueOn;
    Say(Burlak, 'D5a-Bur-1');
    DialogueOff;                                              

    // 82 - basic tools
    if GetTech(82, 3) = state_disabled then
       SetTech(82, 3, state_enabled);

    // 84 - track
    SetTech(84, 3, state_enabled);

    SetRestrict(b_ext_track, 3, state_enabled);

    exit;
    end;
end;

// Spawn Scholtze
every 5$30 do
begin
PrepareScholtze;

wait(0$2);

DialogueOn;
SayNoFaceRadio(Scholtze2, 'D4-Sch-1');
SayRadio(Burlak, 'D4-Bur-1');
DialogueOff;                    

ChangeMissionObjectives('M4');

ComHold(Scholtze);

scholtzeSpawned = true;
end;

// Contact with Scholtze
every 0$1 trigger See(3, Scholtze) and scholtzeSpawned and not scholtzeSaved do
begin
scholtzeSaved = true;
                                                           
CenterNowOnUnits(Scholtze);

SetSide(Scholtze, 3);

DialogueOn;

Say(Scholtze, 'D4a-Sch-1');
Say(Burlak, 'D4a-Bur-1');
Say(Scholtze, 'D4a-Sch-2');

DialogueOff;

ChangeMissionObjectives('MScholtzeOut');

SetTech(tech_Tech2, 3, state_enabled);    
SetTech(tech_Weap1, 3, state_enabled);

// 82 - basic tools
if GetTech(82, 3) = state_disabled then
   SetTech(82, 3, state_enabled);

// 83 - cargo
SetTech(83, 3, state_enabled);

SetRestrict(b_ext_noncombat, 3, state_enabled);
end;

// Kill Scholtze
every 5$0 trigger scholtzeSpawned and not scholtzeSaved do
begin
DialogueOn;

SayNoFaceRadio(Scholtze, 'D4b-Sch-1');
SayRadioNoFace(Scholtze2, 'D4b-Sol1-1');
SayRadioNoFace(Scholtze2, 'D4b-Sol2-1');
SayNoFaceRadio(Scholtze, 'D4b-Sch-2');
Say(Burlak, 'D4b-Bur-2');

DialogueOff;

SetLives(Scholtze, 0);
end;



//CRATES
every 0$30 trigger spawnCrates do
begin
enable;
CreateCratesArea(rand(3, 5), cratesSpawn, true);
end;


// Stop move near Omicron base
every 1 trigger NOT meetNearOmicronBase do
var i, unit, randDialog, currentDist, tmpDist, nearUnit, index;
begin
    enable;
    for unit in FilterUnitsInArea(areaOmicron, [[f_side, 1], [f_or, [f_type, unit_human], [f_type, unit_vehicle], [f_type, unit_building]]]) do
        if GetDistUnits(unit, NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), unit)) < 17 then
        begin
            ComMoveXY(NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), unit), 114, 98);
            if NOT dialog_stop then
            begin
                dialog_stop = true;
                if NOT IsOK(Gnyevko) then
                    SayRadio(Kurin, 'D11-Kur-1')
                else
                    Say(Gnyevko, 'D11-Gny-1');

                wait(0$5);
                dialog_stop = false;
            end;
        end;
end;

// CANT ENTER TO KIROV BASE
every 1 trigger SeeArea(3, areaKirov) >= 6 and tick > 0$10 do
var unit;
begin
    enable;

    for unit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
        if IsInArea(unit, areaKirov) AND (UnitFilter(unit, [f_type, unit_human]) OR UnitsInside(unit)) then
        begin
            ComMoveXY(unit, 120, 98);

            if NOT dialog_stop then
            begin
                dialog_stop = true;
                Say(Kurin, 'D12-Kur-1');
                wait(0$5);
                dialog_stop = false;
            end;
        end;
end;
                                   
//KURIN QUERY
every 0$1 trigger IsSelected(Kurin) do
begin
                 
If true then
   begin
   case Query('QReadyToAttack') of
   1:begin
                                        
   DeselectUnits(Kurin);
   SelectUnits(Burlak);
   Say(Burlak, 'D6a-Bur-1');
   SayRadio(Kurin, 'D6a-Kur-1');

   meetNearOmicronBase = true;
   kurinCheckVehicles = true;
   counterEnable := true;
   timeToArrive = 2$00;
   disable;


   end;

   2:begin

   DeselectUnits(Kurin);
   SelectUnits(Burlak);
   Say(Burlak,'D6b-Bur-1');
   Say(Kurin,'D6b-Kur-1');
   Say(Burlak,'D6b-Bur-2');
   counterEnable := true;
   timeToArrive = 2$00;
   meetNearOmicronBase = true;

   disable;
   end;

   3:begin           
   // continue;
   DeselectUnits(Kurin);
   SelectUnits(Burlak);
      end;
   end;
end;
enable;
end;

Export enoughVehsForKirov;
// Kurin ask about preparation to attack
every 0$1 trigger timeToPrepareAttack < 0$01 do
begin
CenterNowOnUnits(Kurin);
InGameOn;
DialogueOn;

SayRadio(Kurin, 'D5-Kur-1');

case Query('QVehicles') of
        1:begin
        SayRadio(Burlak, 'D6a-Bur-1');
        SayRadio(Kurin, 'D6a-Kur-1');

        meetNearOmicronBase = true;
        kurinCheckVehicles = true;
        timeToArrive = 2$00;
        end;

        2:begin
        SayRadio(Burlak,'D6b-Bur-1');
        SayRadio(Kurin,'D6b-Kur-1');
        SayRadio(Burlak,'D6b-Bur-2');

        meetNearOmicronBase = true;
        end;

        3:begin
        SayRadio(Burlak,'D6c-Bur-1');
        SayRadio(Kurin,'D6c-Kur-1');
        SayRadio(Burlak,'D6c-Bur-2');

        kurinGiveMoreTime = true;
        kurinOpinion = 0;
                                        
        counterIndex = 4;
        end;
    end;

InGameOff;
DialogueOff;
end;

// End more time
every 0$1 trigger extraTime < 0$01 and kurinGiveMoreTime do
begin
DialogueOn;
SayRadio(Kurin, 'D6c-Kur-2');
DialogueOff;

timeToArrive = 2$00;
meetNearOmicronBase = true;
end;

// Kurin check amount vehicles
every 0$1 trigger meetNearOmicronBase do
var i, mech, mechs, vehs, haveVehs, beriaUnits, freeVehs;
begin
mechs = [];
vehs = [];
freeVehs := FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_not, [f_weapon, ru_cargo_bay]]]) - FilterAllUnits([[f_side, 3], [f_type, unit_human]]);
                  
haveVehs = 0;

for i := 1 to freeVehs do
    begin
    uc_side = 6;
    uc_nation = 3;
    PrepareMechanic(false, 2);
    mech = CreateHuman;
    mechs = mechs ^ mech;      
    PlaceUnitArea(mech, kirovSpawn, false);
    ComExitBuilding(mech);
    wait(0$1);
    end;

    if enoughVehsForKirov and freeVehs > 0 then
    begin
        vehs = FilterAllUnits([[f_type, unit_vehicle], [f_nation, nation_russian], [f_not, [f_weapon, ru_cargo_bay]], [f_not, [f_occupied]]]);
        for i := 1 to freeVehs do
            kirovAttackVehicles = kirovAttackVehicles ^ vehs[i];

        SetSide(kirovAttackVehicles, 6);

        produceMoreVehicles = true;
        
        for i := 1 to freeVehs do
            AddComEnterUnit(mechs[i], kirovAttackVehicles[i]);

        if kurinCheckVehicles and produceMoreVehicles and not kurinOpinion = 0 then
        kurinOpinion = 1;
    end
    else
    begin
        if kurinCheckVehicles = false and produceMoreVehicles = false then
        begin
        DialogueOn;
        SayRadio(Kurin, 'D6d-Kur-1');
        DialogueOff;

        YouLost('Vehicles');
        end;
    end;
end;

// Meet with others near Omicron base
every 0$1 trigger meetNearOmicronBase do
var i;
begin
spawnCrates = false;

SetAttitude(6, 1, att_enemy, true);
SetRestrict(b_depot, 3, false);

ChangeMissionObjectives('M3a');
ChangeMissionObjectives('M5');

SetAreaMapShow(MeetArea, 1);        

for i := 1 to kirovAttackVehicles do
ComMoveXY(kirovAttackVehicles[i], 75, 38);
counterIndex = 2;

if NOT kurinOpinion = 0 then
   kurinOpinion = 1;

InitOmicronDef;
end;                               

// Delay meet with others near Omicron base
every 0$1 trigger (timeToArrive <= 0 and counterIndex = 2) do
begin
DialogueOn;
SayRadio(Kurin, 'D7a-Kur-1');
DialogueOff;

counterIndex = 3;
timeToArrive = 2$00;

kurinOpinion = 0;
end;

// Lose when player still don't arrive
every 0$1 trigger timeToArrive < 0$01 AND counterIndex = 3 do
YouLost('Delay');

// Attack Omicron
every 0$1 trigger meetNearOmicronBase AND FilterUnitsExceptArea(MeetArea, [[f_side, 3], [f_type, unit_human]]) = 0 do
begin

counterEnable = false;
startTheAttack = true;      
Display_strings = [];

SetAreaMapShow(MeetArea, 0);

canExitBattlefield = false;

DialogueOn;
SayRadio(Burlak, 'D7-Bur-1');
SayRadio(Kurin, 'D7-Kur-1');
DialogueOff;

ChangeMissionObjectives('M5a');

//Query('QRefuel');

KirovAttackOmicron;                  
end;

//KIROV ATTACK RETREAT
{
Every 0$1 trigger (UnitFilter(kirovAttackVehicles[KirovCurrentAttacker], [f_not,  [f_lives, 700]]) and startTheAttack) or not IsBusy(kirovAttackVehicles[KirovCurrentAttacker]) do
var i;
begin                                                                                
enable;                                   
                                         
if GetLives(kirovAttackVehicles[KirovCurrentAttacker]) <= 700 then
   begin                                                            
   ComMoveToArea(kirovAttackVehicles[KirovCurrentAttacker], MeetAreaRetreat);
   AddComRepairVehicleAndReturn(IsDrivenBy(kirovAttackVehicles[KirovCurrentAttacker]));
   SetFuel(kirovAttackVehicles[KirovCurrentAttacker], 100);
   KirovCurrentAttacker = KirovCurrentAttacker + 1;
   KirovAttackOmicron;
   end;
End;
}
                                                                              
// Omicron surrender
Export omicronNotDestroyed;
every 0$1 trigger FilterAllUnits([[f_side, 1], [f_type, unit_human]]) < 5 and startTheAttack do
var i, tmpUs, tmpRu, building, beriaBuildings, nearBuilding, turrets, nearTurrets, emptyVehicles;
begin
omicronSurrender = true;
canExitBattlefield = true;

endCutscene = true;

if timeToArabAttack = 0 and not IsDead(omikronDepot) then
   omicronNotDestroyed = true
else
    omicronNotDestroyed = false;

SetAttitude(1, 3, att_friend, false);
SetAttitude(2, 3, att_friend, true);

CenterNowOnUnits(Burlak);

SayRadioNoFace(Scholtze2, 'D8-Sol1-1');

if IsOk(Kovalyuk) then
   Say(Kovalyuk, 'D8-Kov-1');

SayRadio(Bukov, 'D8-RSol1-1');

if IsOk(Gnyevko) then
   Say(Gnyevko, 'D8-Gny-1');

tmpRu = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male]]) diff [Burlak, Gnyevko, Kovalyuk, Belkov];
Say(tmpRu[1], 'D8-RSol2-1');

DialogueOff;

//PrepareArabian;

music_nat = nation_arabian;
music_class = music_combat;
game_speed = 4;

DialogueOn;
SayRadio(Grishko, 'D9-Gri-1');
SayRadio(Kurin, 'D9-Kur-1');
DialogueOff;

CenterOnXY(162, 107);

wait(2$0);

DialogueOn;
SayRadio(Grishko, 'D9-Gri-2');
SayRadio(Kurin, 'D9-Kur-2');

wait(1$0);

SayRadio(Grishko, 'D9-Gri-3');
DialogueOff;

CenterNowOnUnits(Kurin);

SayRadio(Kurin, 'D9-Kur-3');

DialogueOn;
SayRadio(Kurin, 'D9a-Kur-1');
SayRadio(Burlak, 'D9a-Bur-1');
SayRadio(Kurin, 'D9a-Kur-2');
DialogueOff;

wait(0$1);

ComMoveUnit(Popov, Burlak);
repeat
      wait(0$1);
until GetDistUnits(Burlak, Popov) < 8 OR (IsInUnit(Burlak) < 8 AND IsInUnit(Burlak));

if IsInUnit(Burlak) then
   begin
   ComExitVehicle(Burlak);
   wait(0$1);
   end;                           

CenterNowOnUnits([Burlak, Popov]);

ComTurnUnit(Burlak, Popov);
ComTurnUnit(Popov, Burlak);

Say(Burlak, 'D10a-Bur-1');
Say(Popov, 'D10a-Pop-1');
Say(Burlak, 'D10a-Bur-2');
Say(Popov, 'D10a-Pop-2');
Say(Burlak, 'D10a-Bur-3');
Say(Popov, 'D10a-Pop-3');
Say(Burlak, 'D10a-Bur-4');
Say(Popov, 'D10a-Pop-4');
Say(Burlak, 'D10a-Bur-5');
Say(Popov, 'D10a-Pop-5');

EndMission;
end;

Export Function EndMission;
begin

if enoughVehsForKirov then
   AddMedal('Surplus',1)
else if produceOnlyForYou then
   AddMedal('Surplus',-1)
else
   AddMedal('Surplus',-2);

if scholtzeSaved and IsOK(Scholtze) then
   AddMedal('Scholtze',1)
else
    AddMedal('Scholtze', -1);

if omicronNotDestroyed = false then
   AddMedal('OmiRun',1)
else
    AddMedal('OmiRun', -1);

GiveMedals('Main');

RewardPeople(FilterAllUnits([[f_side, 3], [f_nation, 3], [f_type, unit_human]]));

SaveCharacters(Burlak, currentMissionPrefix & 'Burlak');

if IsLive(Gnyevko) then
   SaveCharacters(Gnyevko, currentMissionPrefix & 'Gnyevko');

if IsLive(Kovalyuk) then
   SaveCharacters(Kovalyuk, currentMissionPrefix & 'Kovalyuk');

if IsLive(Belkov) then
   SaveCharacters(Belkov, currentMissionPrefix & 'Belkov');

if IsLive(Kirilenkova) then
   SaveCharacters(Kirilenkova, currentMissionPrefix & 'Kirilenkova');

SaveCharacters(FilterAllUnits([[f_side, 3], [f_nation, 3], [f_type, unit_human]]) diff [Burlak, Gnyevko, Kovalyuk, Belkov, Kirilenkova], currentMissionPrefix & 'burlakTeam');
SaveCharacters(FilterAllUnits([[f_side, 6], [f_nation, 3], [f_type, unit_human]]) diff ([Kurin]), currentMissionPrefix & 'kirovTeam');

SaveVariable(kurinOpinion, '03_KurinOpinion');

YouWin;

End;


Export Function MissionIntro;
var i, unit, freeSlots, tmp;
begin
    CenterNowOnUnits([Burlak, Kurin]);

    ComTurnUnit([Burlak, BelkovDialog], Kurin);
    ComTurnUnit(Kurin, Burlak);

    RevealFogArea(3, RevealMapPartArea);

    if NOT IsPlaced(Belkov) AND NOT IsPlaced(Belkov2) then
    begin
        Say(Kurin, 'D1a-Kur-1');
        Say(Burlak, 'D1a-Bur-1');
        Say(Kurin, 'D1a-Kur-2');

        if platonovOpinion <= 1 then
            Say(Kurin, 'D1a-Kur-2a')
        else
            Say(Kurin, 'D1a-Kur-2b');
 
        Say(Burlak, 'D1a-Bur-2');
    end
    else
    begin
        Say(Kurin, 'D1b-Kur-1');
        Say(BelkovDialog, 'D1b-Bel-1');
        Say(Kurin, 'D1b-Kur-2');
    end;

    Say(Kurin, 'D2-Kur-1');
    Say(Burlak, 'D2-Bur-1');
    Say(Kurin, 'D2-Kur-2');
    Say(Burlak, 'D2-Bur-2');
    Say(Kurin, 'D2-Kur-3');

    SetAttitude(1, 6, att_friend, true);

    PrepareAmericansMissionIntro;
    ComMoveXY(amIntroUnits, 28, 35);

    CenterOnXY(93, 36);
    PlaceSeeing(93, 36, 3, -9);

    wait(0$6);

    CenterOnUnits(Popov);
    wait(0$1);

    SayRadio(Popov, 'D2-Pop-3');

    CenterNowOnUnits([Burlak, Kurin]);
    wait(0$1);

    for unit in amIntroUnits do
        RemoveUnit(unit);

    Say(Kurin, 'D2-Kur-4');
    Say(Burlak, 'D2-Bur-4');
    Say(Kurin, 'D2-Kur-5');
    Say(Burlak, 'D2-Bur-5');
    Say(Kurin, 'D2-Kur-6');

    if IsPlaced(Gnyevko) then
        baseUnitsToChoose = baseUnitsToChoose - 1;

    if IsPlaced(Kovalyuk) then
        baseUnitsToChoose = baseUnitsToChoose - 1;

    if IsPlaced(Kirilenkova) then
        baseUnitsToChoose = baseUnitsToChoose - 1;

    SelectCharacters(baseUnitsToChoose);

    RemoveSeeing(93, 36, 3);

    RevealFogArea(3, RevealMapPartArea);

    ComExitBuilding(FilterAllUnits([[f_side, 3], [f_type, unit_human]]));
    wait(0$1);
    AddComMoveXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), GetX(beriaDepot), GetY(beriaDepot));

    repeat
        wait(0$1);
        CenterNowOnUnits(Burlak);
    until GetDistUnits(Burlak, beriaDepot) < 6;

    InGameOff;

    ChangeMissionObjectives('M1');

    SaveForQuickRestart;

    counterEnable = true;
    counterIndex = 1;

    RevealFogArea(3, RevealMapPartArea);

    ResAllTechs(6, FilterAllUnits([[f_side, 6], [f_type, unit_building], [f_btype, b_lab]])[1], 3); 
    ResAllTechs(1, FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_btype, b_lab_half]])[1], 1);  

    AddComBuild(FilterUnitsInArea(KirovBaseArea, [[f_side, 6], [f_type, unit_human], [f_class, class_engineer]]), b_ext_gun, 159, 116, 0);
    AddComBuild(FilterUnitsInArea(KirovBaseArea, [[f_side, 6], [f_type, unit_human], [f_class, class_engineer]]), b_ext_noncombat, 161, 115, 5);
    AddComEnterUnit(FilterUnitsInArea(KirovBaseArea, [[f_side, 6], [f_type, unit_human], [f_class, class_engineer]]), kirovDepot);

    ComFree(survivorsArabianAttack);

    tmp = survivorsArabianAttack diff Popov;
    freeSlots = 6 - UnitsInside(ru2_bar1);

    AddComEnterUnit(survivorsArabianAttack diff Popov, ru2_bar1);

    for i = 1 to tmp do
    begin
      if freeSlots > 0 then
      begin
        ComEnterUnit(tmp[i], ru2_bar1);
        freeSlots = freeSlots - 1;
      end
      else
      break;
    end;

    wait(0$1);

    ComEnterUnit(UnitFilter(tmp, [[f_outside], [f_not, [f_hastask]]]), kirovDepot);

    ComWalk(ruScout);
    wait(0$1);
    AddComMoveXY([ruScout, Popov], 99, 37);
    AddComMoveXY([ruScout, Popov], 88, 60);
    AddComMoveXY([ruScout, Popov], 129, 99);
    AddComEnterUnit([Popov, ruScout], kirovDepot);
end;

// Kirov complete upgrade factory
every 0$1 trigger kirovCompleteFactory do
var i, tmp, pom, diffUnits, canTakeUnits;
begin

   diffUnits = diffUnits ^  Kurin;
   diffUnits = diffUnits ^ survivorsArabianAttack;
   diffUnits = diffUnits ^ kirovEng;

   canTakeUnits = FilterUnitsInArea(KirovBaseArea, [[f_side, 6],  [f_type, unit_human]]) diff diffUnits;

   pom = 6;
 
   if canTakeUnits then
     for i = 1 to canTakeUnits do
     begin
       if GetClass(canTakeUnits[i]) <> 3 then
       begin
             ComEnterUnit(canTakeUnits[i], kirovFactory);
             AddComChangeProfession(canTakeUnits[i], 1);
             wait(0$1);
       end;

       ComEnterUnit(canTakeUnits[i], kirovFactory);

       pom = pom - 1;
     end;

    if pom > 0 then
        neededUnitsSpawn = Replace(neededUnitsSpawn, 3, pom);

end;

// Kirov send cargo to transport oil
every 0$1 trigger kirovTransportCargo AND NOT endCutscene do
begin
    enable;

    AddComTransport(kirovTransportCargo, kirovOilDepot, mat_oil);
    AddComGive(kirovTransportCargo, kirovDepot);
    SetFuel(kirovTransportCargo, 100);
end;

// Find oil deposit
Every 0$1 trigger GetResourceVisibility(78, 112, 3) do
begin
    CenterOnXY(78, 112);
    DialogueOn;
    Say(Burlak, 'D2a-Bur-1');
    SayRadio(Kurin, 'D2a-Kur-1');
    DialogueOff;

    ChangeMissionObjectives('M2');
end;

// Dialog track
every 0$1 trigger NOT dialog_track do
var vehicle;
begin
  enable;

  for vehicle in FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_or, [f_chassis, ru_medium_wheeled], [f_chassis, ru_heavy_wheeled]]]) do
    if GetDistUnitXY(vehicle, 48, 81) < 7 then
    begin
        dialog_track = true;

        CenterNowOnXY(48, 81);

        DialogueOn;
        Say(Burlak, 'D5a-Bur-1');
        DialogueOff;

        // 82 - basic tools
        if GetTech(82, 3) = state_disabled then
           SetTech(82, 3, state_enabled);

        // 84 - track
        SetTech(84, 3, state_enabled);

        SetRestrict(b_ext_track, 3, state_enabled);

        exit;
    end;
end;

// Spawn Scholtze
every 5$30 do
begin
    PrepareScholtze;

    wait(0$2);

    DialogueOn;
    SayRadio(Scholtze, 'D4-Sch-1');
    Say(Burlak, 'D4-Bur-1');
    DialogueOff;

    ChangeMissionObjectives('M4');

    ComHold(Scholtze);

    scholtzeSpawned = true;
end;

// Contact with Scholtze
every 0$1 trigger See(3, Scholtze) AND scholtzeSpawned AND NOT scholtzeSaved do
begin
    scholtzeSaved = true;

    CenterNowOnUnits(Scholtze);
    
    SetSide(Scholtze, 3);

    DialogueOn;

    Say(Scholtze, 'D4a-Sch-1');
    Say(Burlak, 'D4a-Bur-1');
    Say(Scholtze, 'D4a-Sch-2');
    
    DialogueOff;

    ComMoveXY(amPatrol, 28, 36);

    ChangeMissionObjectives('MScholtzeOut');

    SetTech(tech_Tech2, 3, state_enabled);
    SetTech(tech_Weap1, 3, state_enabled);

    // 82 - basic tools
    if GetTech(82, 3) = state_disabled then
       SetTech(82, 3, state_enabled);

    // 83 - cargo
    SetTech(83, 3, state_enabled);

    SetRestrict(b_ext_noncombat, 3, state_enabled);
end;

// Spawn american patrol to kill Scholtze
every 2$30 trigger IsPlaced(Scholtze) AND NOT scholtzeSaved do
    PrepareAmericanPatrol;

// Animals get away from Scholtze position
every 0$1 trigger IsPlaced(Scholtze) AND GetSide(Scholtze) = 8 AND animalsSafeTime > 0$01 do
var ape, tiger;
begin
    enable;

    animalsSafeTime = animalsSafeTime - 0$01;

    for ape in FilterAllUnits([[f_side, 0], [f_class, class_apeman]]) do
        GetAwayFromXY(ape, GetX(Scholtze), GetY(Scholtze), 12, false);

    for tiger in FilterAllUnits([[f_side, 0], [f_class, class_tiger]]) do
        GetAwayFromXY(tiger, GetX(Scholtze), GetY(Scholtze), 12, false);
end;

// American patrol see Scholtze
every 0$1 trigger See(1, Scholtze) AND amPatrol AND NOT scholtzeSaved do
begin

    enable;

    if NOT IsPlaced(Scholtze) then
        exit;

    if  Attacks(amPatrol[1]) = Scholtze OR Attacks(amPatrol[2]) = Scholtze then
    begin
        DialogueOn;

        Say(Scholtze, 'D4b-Sch-1');
        Say(amPatrol[2], 'D4b-Sol1-1');
        Say(amPatrol[1], 'D4b-Sol2-1');
        Say(Scholtze, 'D4b-Sch-2');
        Say(Burlak, 'D4b-Bur-2');

        DialogueOff;

        Killunit(Scholtze);

        ChangeMissionObjectives('MScholtzeDel');
    end;

end;


// Upgrade workshop to factory - execute in event
Export Function Dial_UpgradeWorkshop(building);
var maleSci, maleOthers;
begin
    dialog_workshop = true;

    if NOT FilterAllUnits([[f_side, 3], [f_btype, b_oil_power]]) then
    begin
        CenterNowOnUnits(building);

        DialogueOn;

        Say(Burlak, 'D3aa-Bur-1');

        if IsOk(Scholtze) AND GetSide(Scholtze) = 3 then
            Say(Scholtze, 'D3aa-Sch-1')
        else
        begin
            maleSci = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_class, class_scientistic], [f_sex, sex_male], [f_ok]]) diff [Burlak, Gnyevko, Kovalyuk, Belkov, Belkov2];

            if maleSci then
                Say(maleSci[1], 'D3aa-Sci1-1')
            else
            begin
                maleOthers = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male], [f_ok]]) diff [Burlak, Gnyevko, Kovalyuk, Belkov, Belkov2];

                if maleOthers then
                    Say(maleOthers[1], 'D3aa-Sci1-1')
                else
                begin
                    maleOthers = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male], [f_ok]]) diff [Burlak];
                    Say(maleOthers[1], 'D3aa-Sci1-1');
                end;
            end;
        end;

        Say(Burlak, 'D3aa-Bur-2');

        DialogueOff;    
    end;

    ChangeMissionObjectives('M3');
end;

// Contact with american support troops
every 0$1 trigger NOT dialog_amSupportTroops do
var amUnit, randomMale;
begin
    enable;

    for amUnit in amSupportTroops do
    begin
        if See(3, amUnit) then
        begin
            dialog_amSupportTroops = true;

            CenterNowOnUnits(amUnit);
            DialogueOn;

            randomMale = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male], [f_ok]]) diff [Burlak, Gnyevko, Kovalyuk, Belkov, Belkov2];

            if randomMale then
                DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male], [f_ok]]), 'D3b-RSol1-1', '', false, true)
            else
            begin
                randomMale = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male], [f_ok]]) diff [Burlak];
                Say(randomMale[1], 'D3b-RSol1-1');          
            end;
 
            if IsOK(Gnyevko) then
                Say(Gnyevko, 'D3b-Gny-1');

            Say(Burlak, 'D3b-Bur-1');

            DialogueOff;

            exit;
        end;
    end;
end;

// Ask Burlak with decision how stop americans troops with help to Omicron base
every 2$0 trigger dialog_amSupportTroops do
begin
    if IsOK(Kovalyuk) then
        Say(Kovalyuk, 'D3b-Kov-1');

    DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male]]), 'D3b-RSol1-2', '', false, false);

end;

// Immortal Kurin
every 1 trigger NOT endCutscene do
begin
  enable;
  SetLives(Kurin, 1000);
end;

// Kurin ask about preparation to attack 
every 0$1 trigger timeToPrepareAttack < 0$01 do
begin
    CenterNowOnUnits(Kurin);
    InGameOn;
    DialogueOn;
    
    SayRadio(Kurin, 'D5-Kur-1');

    case Query('QVehicles') of
        1:begin
            Say(Burlak, 'D6a-Bur-1');
            SayRadio(Kurin, 'D6a-Kur-1');

            meetNearOmicronBase = true;
            kurinCheckVehicles = true;
        end;

        2:begin
            Say(Burlak,'D6b-Bur-1');
            Say(Kurin,'D6b-Kur-1');
            Say(Burlak,'D6b-Bur-2');

            meetNearOmicronBase = true;
        end;

        3:begin
            Say(Burlak,'D6c-Bur-1');
            Say(Kurin,'D6c-Kur-1');
            Say(Burlak,'D6c-Bur-2');

            kurinGiveMoreTime = true;
            kurinOpinion = 0;

            counterIndex = 4;
        end;
    end;

    InGameOff;
    DialogueOff;
end;

// End more time
every 0$1 trigger extraTime < 0$01 AND kurinGiveMoreTime do
begin
    DialogueOn;
    SayRadio(Kurin, 'D6c-Kur-2');
    DialogueOff;

    meetNearOmicronBase = true;
end;

// Kurin check amount vehicles
every 0$1 trigger meetNearOmicronBase do
var i, mech, mechs, vehs, haveVehs;
begin
    mechs = [];
    vehs = [];

    haveVehs = 0;

    uc_side = 6;
    uc_nation = 3;

    for i := 1 to 2 do
    begin
        PrepareMechanic(false, 2);
        mech = CreateHuman;
        mechs = mechs ^ mech;
        PlaceHumanInUnit(mech, kirovDepot);
        ComExitBuilding(mech);
        wait(0$1);
        AddComMoveXY(mech, GetX(beriaDepot), GetY(beriaDepot));
    end;

    if FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian]])= FilterAllUnits([[f_side, 3], [f_type, unit_human]]) then
        produceOnlyForYou = true;

    wait(0$20);

    if FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_not, [f_weapon, ru_cargo_bay]]]) >= FilterAllUnits([[f_side, 3], [f_type, unit_human]]) + 2 then
    begin
        vehs = vehs ^ FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_not, [f_weapon, ru_cargo_bay]], [f_not, [f_occupied]]])[1];
        vehs = vehs ^ FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_nation, nation_russian], [f_not, [f_weapon, ru_cargo_bay]], [f_not, [f_occupied]]])[2];

        kirovAttackVehicles = kirovAttackVehicles ^ vehs;

        SetSide(vehs, 6);

        produceMoreVehicles = true;

        ComEnterUnit(mechs[1], vehs[1]);
        ComEnterUnit(mechs[2], vehs[2]);

        if kurinCheckVehicles AND NOT kurinOpinion = 0 then
                kurinOpinion = 1;
    end
    else
    begin
        if kurinCheckVehicles then
        begin
            DialogueOn;
            SayRadio(Kurin, 'D6d-Kur-1');
            DialogueOff;

            Lose_Vehicles;
        end;
    end;
end;

// Meet with others near Omicron base
every 0$1 trigger meetNearOmicronBase do
begin
    spawnCrates = false;

    SetAttitude(6, 1, att_enemy, true);

    SetRestrict(b_depot, 3, false);

    ChangeMissionObjectives('M3a');
    ChangeMissionObjectives('M5');

    SetAreaMapShow(MeetArea, 1);

    ComMoveXY(kirovAttackVehicles, 75, 38);
    ComMoveXY(kirovAttackCargo, 75, 38);

    counterIndex = 2;

    if NOT kurinOpinion = 0 then
        kurinOpinion = 1;

    InitAIManager(1, 1, FilterAllUnits([[f_side, 1], [f_type, unit_human]]), OmicronBaseArea, OmicronHealArea, OmicronParkingArea, OmicronDefenceArea, [2, 3, 4][Difficulty], [2, 3, 4][Difficulty], 0, false);

end;

// Delay meet with others near Omicron base
every 0$1 trigger timeToArrive < 0$01 AND counterIndex = 2 do
begin
    DialogueOn;
    SayRadio(Kurin, 'D7a-Kur-1');
    DialogueOff;

    counterIndex = 3;
    timeToArrive = 2$00;

    kurinOpinion = 0;
end;

// Lose when player still don't arrive
every 0$1 trigger timeToArrive < 0$01 AND counterIndex = 3 do
    Lose_Delay;

// Attack Omicron
every 0$1 trigger meetNearOmicronBase AND FilterUnitsExceptArea(MeetArea, [[f_side, 3], [f_type, unit_human]]) = 0 do
begin

    counterEnable = false;
    Display_strings = [];

    SetAreaMapShow(MeetArea, 0);

    canExitBattlefield = false;

    DialogueOn;
    Say(Burlak, 'D7-Bur-1');
    SayRadio(Kurin, 'D7-Kur-1');
    DialogueOff;

    ChangeMissionObjectives('M5a');

    Query('QRefuel');

    KirovAttackOmicron;
end;

// Stop exit battlefield
every 1 trigger NOT canExitBattlefield do
var unit;
begin
    enable;

    for unit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
        if NOT IsInArea(unit, OmicronBattlefieldArea) AND (UnitFilter(unit, [f_type, unit_human]) OR UnitsInside(unit)) then
        begin
            ComMoveToArea(unit, OmicronBattlefieldArea);

            if NOT dialog_stop then
            begin
                dialog_stop = true;
                Say(Kurin, 'D12-Kur-1');
                wait(0$5);
                dialog_stop = false;
            end;   
        end;
end;

// Stop enter to second base
every 1 do
var unit;
begin
    enable;
    for unit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
        if IsInArea(unit, SecondKurinBase) then
        begin
            ComMoveXY(unit, 200, 114);

            if NOT dialog_stop then
            begin
                dialog_stop = true;
                Say(Kurin, 'D12-Kur-1');
                wait(0$5);
                dialog_stop = false;
            end;
            
        end;
end;

// Stop move near Omicron base
every 1 trigger NOT meetNearOmicronBase AND IsOK(Sikorski) do
var unit, randDialog;
begin
    enable;
    for unit in FilterUnitsInArea(OmicronBaseArea, [[f_side, 1], [f_or, [f_type, unit_human], [f_type, unit_vehicle], [f_type, unit_building]]]) do
        if GetDistUnits(unit, NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), unit)) < 17 then
        begin
            ComMoveXY(NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), unit), 69, 85);

            randDialog = rand(1, 2);

            if NOT dialog_stop then
            begin
                dialog_stop = true;
                if randDialog = 1 OR NOT IsOK(Gnyevko) then
                    SayRadio(Kurin, 'D11-Kur-1')
                else
                    Say(Gnyevko, 'D11-Gny-1');
                wait(0$5);
                dialog_stop = false;
            end;
            
        end;
end;

// Omicron surrender
every 0$1 trigger IsDead(Sikorski) AND FilterAllUnits([[f_side, 1], [f_type, unit_human]]) < 5 do
var i, tmpUs, tmpRu, building, beriaBuildings, nearBuilding, turrets, nearTurrets;
begin
    omicronSurrender = true;
    canExitBattlefield = true;

    enableOmicronAI = false;
    endCutscene = true;

    SetAttitude(1, 3, att_friend, false);
    SetAttitude(2, 3, att_friend, true);

    CenterNowOnUnits(Burlak);

    uc_side = 1;
    uc_nation = 1;

    hc_name := '';
    hc_gallery = '';

    PrepareSoldier(sex_male, 1);

    tmpUs = CreateHuman;

    ComStop(FilterAllUnits([f_side, 3]));

    InGameOn;
    DialogueOn;

    CenterOnXY(26, 33);

    SayRadio(tmpUs, 'D8-Sol1-1');

    if IsOk(Kovalyuk) then
        Say(Kovalyuk, 'D8-Kov-1');

    tmpRu = DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male]]), 'D8-RSol1-1', '', false, false);

    if IsOk(Gnyevko) then
        Say(Gnyevko, 'D8-Gny-1');

    DialogRandom(FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_sex, sex_male]]) diff tmpRu, 'D8-RSol2-1', '', false, false);

    DialogueOff;

    PrepareArabian;

    CenterNowOnXY(234, 122);

    ComAgressiveMove(northGroup, 221, 110);
    ComAgressiveMove(UnitFilter(southGroup, [f_type, unit_human]), 221, 110);
    wait(0$7);
    AddComAgressiveMove(southGroup, 221, 110);

    wait(0$8);

    music_nat = nation_arabian;
    music_class = music_combat;
    game_speed = 4;

    DialogueOn;
    SayRadio(Grishko, 'D9-Gri-1');
    SayRadio(Kurin, 'D9-Kur-1');
    DialogueOff;

    ComMoveXY(UnitFilter(secondBaseTeam, [f_class, class_engineer]), 158, 105);
    ComMoveXY(UnitFilter(secondBaseTeam, [f_class, class_scientistic]), 158, 105);

    AddComMoveXY(UnitFilter(secondBaseTeam, [f_class, class_engineer]), GetX(beriaDepot), GetY(beriaDepot));
    AddComMoveXY(UnitFilter(secondBaseTeam, [f_class, class_scientistic]), GetX(beriaDepot), GetY(beriaDepot));

    ComMoveXY(kirovTransportCargo, 158, 105);
    AddComMoveXY(kirovTransportCargo, GetX(beriaDepot), GetY(beriaDepot));

    wait(0$2.5);

    DialogueOn;
    SayRadio(Grishko, 'D9-Gri-2');
    SayRadio(Kurin, 'D9-Kur-2');
    DialogueOff;

    repeat
        wait(1);
    until GetLives(Grishko) < 1000;

    DialogueOn;
    SayRadio(Grishko, 'D9-Gri-3');
    DialogueOff;

    KillUnit(Grishko);

    wait(0$12);

    CenterNowOnUnits(Kurin);

    SayRadio(Kurin, 'D9-Kur-3');

    CenterNowOnXY(176, 110);

    ComAgressiveMove(UnitFilter(southGroup, [f_type, unit_human]), 221, 110);
    ComAgressiveMove(northGroup, 161, 109);
    wait(0$7);
    ComAgressiveMove(southGroup, 161, 109);

    wait(0$11);

    ComEnterUnit(Kurin, kirovDepot);

    DialogueOn;
    SayRadio(Kurin, 'D9a-Kur-1');
    Say(Burlak, 'D9a-Bur-1');
    SayRadio(Kurin, 'D9a-Kur-2');
    DialogueOff;

    ComExitBuilding(survivorsArabianAttack);
    wait(0$1);
    ComMoveXY(survivorsArabianAttack, GetX(beriaDepot), GetY(beriaDepot));

    immortalSurvivors = true;

    for tmpUS in FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_ok]]) do
        RemoveUnit(tmpUS);

    repeat
        wait(0$1);
    until IsDead(Kurin);

    wait(0$1);

    nearTurrets = [];
    turrets = FilterAllUnits([[f_side, 6], [f_btype, b_bunker], [f_ok]]);

    for i = 1 to 2 do
    begin
      nearTurrets = nearTurrets ^ NearestUnitToUnit(turrets, beriaDepot);
      turrets = turrets diff nearTurrets;
      tmpRu = UnitsInside(nearTurrets[i])[1];

      ComExitBuilding(tmpRu);
      AddComMoveXY(tmpRu, GetX(beriaDepot), GetY(beriaDepot));
    end;

    tmpRu = UnitsInside(FilterAllUnits([[f_side, 6], [f_type, unit_building], [f_or, [f_btype, b_lab], [f_btype, b_lab_half]]])[1]);
    ComExitBuilding(tmpRu);
    AddComMoveXY(tmpRu, GetX(beriaDepot), GetY(beriaDepot));

    wait(0$14);

    ComExitVehicle(FilterUnitsInArea(OmicronBattlefieldArea, [[f_side, 6], [f_type, unit_human]]));

    for i in FilterUnitsInArea(OmicronBattlefieldArea, [[f_side, 6], [f_type, unit_vehicle]]) do
    begin
       tmpRu = UnitsInside(i);
       ComExitVehicle(tmpRu[1]);
       AddComMoveXY(tmpRu[1], GetX(beriaDepot), GetY(beriaDepot));
    end;

    wait(0$1);

    AddComMoveXY(FilterUnitsInArea(OmicronBattlefieldArea, [[f_side, 6], [f_type, unit_human]]), GetX(beriaDepot), GetY(beriaDepot));
    ComExitVehicle(UnitsInside(kirovTransportCargo)[1]);

    ComExitVehicle(FilterAllUnits([[f_side, 3], [f_type, unit_human]]));
    wait(0$1);
    ComMoveXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), GetX(Popov), GetY(Popov));

    repeat
        ComMoveXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), GetX(Popov), GetY(Popov));
        CenterNowOnUnits(Burlak);
        wait(0$1);
    until GetDistUnits(Burlak, Popov) < 8;

    CenterNowOnUnits([Burlak, Popov]);

    ComTurnUnit(Burlak, Popov);
    ComTurnUnit(Popov, Burlak);

    Say(Burlak, 'D10a-Bur-1');
    Say(Popov, 'D10a-Pop-1');
    Say(Burlak, 'D10a-Bur-2');
    Say(Popov, 'D10a-Pop-2');
    Say(Burlak, 'D10a-Bur-3');
    Say(Popov, 'D10a-Pop-3');
    Say(Burlak, 'D10a-Bur-4');
    Say(Popov, 'D10a-Pop-4');
    Say(Burlak, 'D10a-Bur-5');
    Say(Popov, 'D10a-Pop-5');

    survivorsArabianAttack = [];
    survivorsArabianAttack = FilterAllUnits([[f_side, 6], [f_ok], [f_type, unit_human]]);

    ComMoveXY(FilterAllUnits([[f_side, 3], [f_type, unit_human]]), 83, 157);
    ComMoveXY(survivorsArabianAttack, 83, 157);

    repeat
        CenterNowOnUnits(Burlak);
        wait(0$1);
    until GetDistUnits(Burlak, beriaDepot) > 23;

    PlaceSeeing(113, 96, 3, -12);
    CenterNowOnXY(113, 96);

    canDestroyBeriaBase = true;

    beriaBuildings = FilterUnitsInArea(BeriaBaseArea, [[f_side, 3], [f_type, unit_building]]);

    for i = 1 to beriaBuildings do
    begin
         nearBuilding = NearestUnitToUnit(beriaBuildings, FilterAllUnits([f_side, 2])[1]);
         beriaBuildings = beriaBuildings diff nearBuilding;

         AddComAttackUnit(northGroup, nearBuilding);
         AddComAttackUnit(southGroup, nearBuilding);
    end;
        
    wait(0$35);

    FinishMission;
end;

every 1 trigger immortalSurvivors do
var unit;
begin
    enable;

    for unit in survivorsArabianAttack do
        SetLives(unit, 1000);
end;
// Russian mission 4
// Map by Altar, extended by Serpent
// SAIL by Nefarem
// Consultation - SoSTeam

Starting
begin
    InitMission;
end;


// Global campaign variables
Export platonovOpinion, kurinOpinion, meetBelkovBrother;
Export heikeHeavyLosses;

// Current mission variables
Export canExitMap, arabianCanExitMap;
Export startEvacuation, namedBase;
Export evacuatedCargos, canStealCargo, stealWarmCount, stealCargos, cargosToTriggerAttacks;
Export dial_labUpgradedBlock, dial_contactBlock;
Export enableAttacks, currentWave;
Export saveGaydar, gaydarWaitForBurlak;
Export killedArabianUnits, arabianKillers;
Export lostUnits;

// Achievement variables
Export achievement_ConstructedTurrets;

Function InitMission;
begin
    ResetFog;
    RandomizeAll;
    InGameOn;

    // Init map config
    InitMapConfig;

    // Global campaign variables init values
    platonovOpinion = LoadVariable('02_PlatonovOpinion_9', 0);  // 0 - very bad, 1 - bad, 2 - good
    kurinOpinion = LoadVariable('03_KurinOpinion_1', 0);  // 0 - bad,  1 - good
    meetBelkovBrother = LoadVariable('02_MeetBelkovBrother_6', 0); // 0 - no, 1 - yes

    heikeHeavyLosses = 0; // Heike suffered heavy losses,  0 - no, 1 - yes

    // Mission variables init values
    canExitMap = false;
    arabianCanExitMap = false;

    namedBase = false;
    startEvacuation = false;

    stealCargos = [];
    stealWarmCount = 0;
    evacuatedCargos = 0;

    canStealCargo = false;

    dial_labUpgradedBlock = false;
    dial_contactBlock = false;
  
    currentWave = 0;

    saveGaydar = false;
    gaydarWaitForBurlak =false;

    killedArabianUnits = 0;
    arabianKillers = 0;

    lostUnits = 0;

    // AI variables
    enableAttacks = false;

    // Achievement variables init values
    achievement_ConstructedTurrets = 0;

    // Init units
    PrepareRussians;
    Nef_PrepareNature;

    // Init mission intro
    MissionIntro;
end;
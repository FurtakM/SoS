Export Burlak, Karamazov, Petrovova, Gleb, Petrosyan, Titov, Dolgov, Lipshchin, Kirilenkova, Vsevolod; // Heroes
Export Platonov;
Export other_survivors;
Export survivorsToPlace;

Export Function PrepareRussians;
begin
    if savedVsevolod then
        PrepareHeroes(RussianSpawnArea1, 0)
    else
    begin
        PrepareHeroes(RussianSpawnArea2, 5);
        SpawnPlatonovReinforces;
   end;
end;

Function PrepareHeroes(area, dir);
var i, unit, car, cargoList;
begin

    uc_side = 3;
    uc_nation = 3;

    Burlak:=PrepareUnitExt('Burlak', true, 0);

    Titov := PrepareUnitExt('Titov', true, 0);
    Dolgov:= PrepareUnitExt('Dolgov', true, 0);
    Lipshchin:= PrepareUnitExt('Lipshchin', true, 0);
    Karamazov:= PrepareUnitExt('Karamazov', true, 0);
    Petrovova:= PrepareUnitExt('Petrovova', true, 0);
    Gleb:= PrepareUnitExt('Gleb', true, 0);
    Petrosyan:= PrepareUnitExt('Petrosyan', true, 0);
    if savedVsevolod then
    begin
      Kirilenkova := PrepareUnitExt('Kirilenkova', true, 0);
      Vsevolod := PrepareUnitExt('Vsevolod', true, class_soldier);
    end;

    Platonov = PrepareUnitExt('Platonov', false, 0);

    other_survivors = CreateCharacterSet('06_other_survivors');
    survivorsToPlace = other_survivors;
    
    for i:=1 to vehiclesFrom06 do
    begin
        uc_nation = vehiclesFrom06[i][1];
        vc_chassis:=vehiclesFrom06[i][2];
        vc_engine = vehiclesFrom06[i][3];
        vc_control = vehiclesFrom06[i][4];
        vc_weapon:=vehiclesFrom06[i][5];
        uc_direction:=dir;
        vc_fuel_battery:=Rand(60, 70);
        car:=CreateVehicle;
        PlaceUnitArea(car, area, false);

        case vehiclesFrom06[i][6] of
            'Bur': PlaceHumanInUnit(Burlak, car);
            'Kar': PlaceHumanInUnit(Karamazov, car);
            'Ptr': PlaceHumanInUnit(Petrovova, car);
            'Glb': PlaceHumanInUnit(Gleb, car);
            'Pet': PlaceHumanInUnit(Petrosyan, car);
            'Dol': PlaceHumanInUnit(Dolgov, car);
            'Lip': PlaceHumanInUnit(Lipshchin, car);
            'Tit': PlaceHumanInUnit(Titov, car);
            'Kir': PlaceHumanInUnit(Kirilenkova, car);
            'rand' : begin
                PlaceHumanInUnit(survivorsToPlace[1], car);
                survivorsToPlace = survivorsToPlace diff survivorsToPlace[1];
                end;    
        end;
     end;

    if Burlak AND NOT IsPlaced(Burlak) then PlaceUnitArea(Burlak, area, false);
    if Karamazov AND NOT IsPlaced(Karamazov) then PlaceUnitArea(Karamazov, area, false);
    if Petrovova AND NOT IsPlaced(Petrovova) then PlaceUnitArea(Petrovova, area, false);
    if Gleb AND NOT IsPlaced(Gleb) then PlaceUnitArea(Gleb, area, false);
    if Petrosyan AND NOT IsPlaced(Petrosyan) then PlaceUnitArea(Petrosyan, area, false);
    if Dolgov AND NOT IsPlaced(Dolgov) then PlaceUnitArea(Dolgov, area, false);
    if Lipshchin AND NOT IsPlaced(Lipshchin) then PlaceUnitArea(Lipshchin, area, false);
    if Titov AND NOT IsPlaced(Titov) then PlaceUnitArea(Titov, area, false);
    if Kirilenkova AND NOT IsPlaced(Kirilenkova) then PlaceUnitArea(Kirilenkova, area, false);

    for unit in survivorsToPlace do
        PlaceUnitArea(unit, area, false);

    cargoList = FilterAllUnits([[f_side, 3], [f_type, unit_vehicle], [f_or, [f_weapon, ru_cargo_bay], [f_weapon, ar_cargo_bay], [f_weapon, us_cargo_bay]]]);

    if cargoList then
        for i = 1 to cargoList do
            if resourcesFrom06 then
            begin
                AddCargo(cargoList[i], 1, resourcesFrom06[i][1]);
                AddCargo(cargoList[i], 2, resourcesFrom06[i][2]);
                AddCargo(cargoList[i], 3, resourcesFrom06[i][3]);
            end;
end;

Function SpawnPlatonovReinforces;
var i, un, veh;
begin
    uc_side = 3;
    uc_nation = 3;

    PrepareSoldier(false, 4);

    PrepareVehicle(ru_medium_tracked, engine_combustion, control_manual, [ru_gatling_gun, ru_gun][rand(1, 2)], rand(50, 60));

    veh = CreateVehicle;
    SetDir(veh, 0);
    PlaceUnitXY(veh, 134, 230, false);


    un = CreateHuman;
    PlaceHumanInUnit(un, veh);


    PrepareVehicle(ru_medium_tracked, engine_combustion, control_manual, ru_cargo_bay, rand(50, 60));

    veh = CreateVehicle;
    SetDir(veh, 0);
    PlaceUnitXY(veh, 134, 233, false);

    SetCargo(veh, mat_cans, 100);

    un = CreateHuman;
    PlaceHumanInUnit(un, veh);

    if Kirilenkova then
    begin
        PrepareVehicle(ru_medium_tracked, engine_combustion, control_manual, ru_cargo_bay, rand(50, 60));

        veh = CreateVehicle;
        SetDir(veh, 0);
        PlaceUnitXY(veh, 134, 236, false);
        SetCargo(veh, mat_cans, 100);

        PlaceHumanInUnit(Kirilenkova, veh);
    end;

end;
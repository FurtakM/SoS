Export Burlak, Gnyevko, Belkov, Kirilenkova; // Heroes with Burlak
Export other_survivors; // Other survivors from 01

Export Kurin, Belkov2, otherKirovUnits;
Export Kovalyuk, randomMech;
Export kirovVehicle;
Export belkovTeam; // 4 sold + Belkov + 1 sci (no Jelena)
Export removedBelkovUntisSaved;
Export removedBelkovVehicleSaved; // [Chassis, Engine, Control, Weapon, Fuel]

Export Platonov, platSoldiers;
Export Function PrepareRussian;
begin
    PrepareCharactersFromPreviousMission;
    PrepareKirovBase;
    PrepareBeriaBase;
end;

Export Function PrepareBelkovReturnTeam;
var unit, others;
begin

    belkovTeam = [];

    uc_side = 6;
    uc_nation = nation_russian;
    vc_chassis = removedBelkovVehicleSaved[1];
    vc_engine = removedBelkovVehicleSaved[2];
    vc_control = removedBelkovVehicleSaved[3];
    vc_weapon = removedBelkovVehicleSaved[4];
    vc_fuel_battery = removedBelkovVehicleSaved[5];

    kirovVehicle = CreateVehicle;

    SetDir(kirovVehicle, 3);

    PlaceUnitArea(kirovVehicle, BelkovReturnArea, false);

    belkovTeam = belkovTeam ^ kirovVehicle;

    if TestCharacters('02_TmpBelkov') then
    begin
        Belkov = CreateCharacter('02_TmpBelkov');
        PlaceHumanInUnit(Belkov, kirovVehicle);

        belkovTeam = belkovTeam ^ Belkov;
    end
    else
    begin
        Belkov2 = CreateCharacter('02_TmpBelkov2');
        PlaceHumanInUnit(Belkov2, kirovVehicle);

        belkovTeam = belkovTeam ^ Belkov2;
    end;
        
    others = CreateCharacterSet('02_TmpBelkovTeam');

    for unit in others do
    begin
        PlaceUnitArea(unit, BelkovReturnArea, false);

        belkovTeam = belkovTeam ^ unit;
    end;
    
end;

Function PrepareCharactersFromPreviousMission;
var i;
begin
    uc_side = 3;
    
    Burlak = PrepareUnit('Burlak', true);
    Gnyevko = PrepareUnit('Gnyevko', true);
    Belkov = PrepareUnit('Belkov', true);
    Kirilenkova = PrepareUnit('Kirilenkova', true);

    PlaceUnitArea(Burlak, BurlakSpawnArea, false);
    PlaceUnitArea(Gnyevko, BurlakTeamSpawnArea, false);
    PlaceUnitArea(Belkov, BurlakTeamSpawnArea, false);
    PlaceUnitArea(Kirilenkova, BurlakTeamSpawnArea, false);

    CenterNowOnUnits(Burlak);

    other_survivors = CreateCharacterSet('01_other_survivors');

    for i in other_survivors do
        PlaceUnitArea(i, BurlakTeamSpawnArea,  false); 
end;

Function PrepareKirovBase;
var i, un;
begin

    SetBName(kirov, 'kirov');

    uc_side = 6;

    Kurin = PrepareUnit('Kurin', false);
    Kovalyuk = PrepareUnit('Kovalyuk', false);
    Belkov2 = PrepareUnit('Belkov2', false);

    PlaceUnitArea(Kurin, KurinSpawnArea, false);
    PlaceUnitXYR(Kovalyuk, 161, 108, 6,  false);

    ComHold(Kurin);

    otherKirovUnits = [];

    kirovAmountPeople = Replace(kirovAmountPeople, 1, kirovAmountPeople[1] - UnitFilter(other_survivors, [f_class, class_soldier]));

    for i := 1 to kirovAmountPeople[1] do
    begin
        uc_nation = 3;

        hc_gallery = '';
        hc_name = '';
        PrepareHuman(false, 1, rand(3, 5));

        un = CreateHuman;

        otherKirovUnits = otherKirovUnits ^ un;

        PlaceUnitXYR(un, 161, 108, 6,  false);
    end;

    for i := 1 to kirovAmountPeople[2] do
    begin
        uc_nation = 3;

        hc_gallery = '';
        hc_name = '';
        PrepareHuman(false, 2, rand(4, 5));

        un = CreateHuman;

        otherKirovUnits = otherKirovUnits ^ un;

        PlaceUnitXYR(un, 161, 108, 6,  false);
    end;

    for i := 1 to kirovAmountPeople[3] do
    begin
        uc_nation = 3;

        hc_gallery = '';
        hc_name = '';
        PrepareHuman(false, 3, rand(3, 5));

        un = CreateHuman;

        otherKirovUnits = otherKirovUnits ^ un;

        PlaceUnitXYR(un, 161, 108, 6,  false);
    end;

    for i := 1 to kirovAmountPeople[4] do
    begin
        uc_nation = 3;

        hc_gallery = '';
        hc_name = '';
        PrepareHuman(false, 4, rand(3, 5));

        un = CreateHuman;

        otherKirovUnits = otherKirovUnits ^ un;

        PlaceUnitXYR(un, 161, 108, 6,  false);
    end;

    if NOT TestCharacters('01_Gnyevko') then
    begin
        PrepareMechanic(sex_male, 3);
        randomMech = CreateHuman;

        PlaceUnitXYR(randomMech, 161, 108, 6,  false);
    end;

    if NOT belkovReturn then
        PlaceHumanInUnit(Belkov2, ru2_arm);

end;

Function PrepareBeriaBase;
var i, un;
begin
    SetBName(beria, 'beria');

    uc_side = 6;

    Platonov = PrepareUnit('Platonov', false);

    PlaceUnitXY(Platonov, 112, 97, false);
    ComHold(Platonov);

    platSoldiers = [];

    for i := 1 to 4 do
    begin
        uc_nation = 3;

        hc_gallery = '';
        hc_name = '';

        if i = 4 AND NOT UnitFilter(platSoldiers, [f_sex, sex_male]) then
            PrepareHuman(sex_male, 1, rand(1, 3))
        else
            PrepareHuman(false, 1, rand(1, 3));

        un = CreateHuman;

        platSoldiers = platSoldiers ^ un;

        PlaceUnitXYR(un, 110, 97, 6,  false);

        if i < 4 then
            ComEnterUnit(un, FilterUnitsInArea(BeriaBaseArea, [[f_side, 6], [f_btype, b_breastwork]])[i]);
    end;
end;


Export Function MissionIntro;
var yourSolds, nearUnit;
var i;
begin

    CenterNowOnUnits(Burlak);

    ComMoveXY(Burlak, 163, 108);
    AddComTurnUnit(Burlak, Kurin);

    yourSolds = FilterAllUnits([[f_side, 3], [f_type, unit_human]]) diff Burlak;

    for i := 1 to meetWithKurinPositions do
    begin
        nearUnit = NearestUnitToXY(yourSolds, meetWithKurinPositions[i][1], meetWithKurinPositions[i][2]);
        ComMoveXY(nearUnit, meetWithKurinPositions[i][1], meetWithKurinPositions[i][2]);
        AddComTurnUnit(nearUnit, Kurin);

        yourSolds = yourSolds diff nearUnit;
    end;

    repeat
        CenterNowOnUnits(Burlak);
        wait(0$1);
    until GetDistUnits(Burlak, Kurin) < 4;

    CenterNowOnUnits([Burlak, Kurin]);

    ComTurnUnit(Kurin, Burlak);

    Say(Kurin, 'D1-Kur-1');
    Say(Burlak, 'D1-Bur-1');
    Say(Kurin, 'D1-Kur-2');
    Say(Burlak, 'D1-Bur-2');
    Say(Kurin, 'D1-Kur-3');

    if IsOK(Gnyevko) then
        Say(Kurin, 'D1-Kur-3a');

    InGameOff;

    SetSide(FilterAllUnits([[f_side, 3], [f_type, unit_human]]) diff [Burlak, Gnyevko], 6);
    SetSide(Kovalyuk, 3);
    
    ComFree(FilterUnitsInArea(KirovBaseArea, [[f_side, 6], [f_type, unit_human]]) diff Kurin);

    ComEnterUnit(other_survivors, ru2_arm1);
    ComEnterUnit(Belkov, ru2_arm1);

    if IsPlaced(Kirilenkova) then
       ComEnterUnit(Kirilenkova, ru_lab);

    if NOT TestCharacters('01_Gnyevko') then
        SetSide(randomMech, 3);

    ChangeMissionObjectives('M1');

    HideArea(3, AllMapArea);
    ShowArea(3, VisibleMapPartArea);
    HideArea(6, AllMapArea);
    ShowArea(6, VisibleMapPartArea);

    SaveForQuickRestart;

    ComEnterUnit(other_survivors, ru2_arm1);
    ComEnterUnit(Belkov, ru2_arm1);

    if IsPlaced(Kirilenkova) then
       ComEnterUnit(Kirilenkova, ru_lab);
end;

// Block move in far north
every 1 trigger NOT canExploreMap AND FilterUnitsInArea(NorthMoveLimitArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) > 0 do
var unit;
begin
    enable;
    
    for unit in FilterUnitsInArea(NorthMoveLimitArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
    begin
        //InGameOn;

        ComMoveXY(unit, GetX(unit), GetY(unit) + 5);

        if NOT meetPlatonov AND NOT dialogLoopBlocker then
        begin
            if unit <> Burlak AND IsOk(Kovalyuk) then
                Say(Kovalyuk, 'D1b-Kov-1');

            if unit = Burlak then
                Say(Burlak, 'D1b-Bur-1');
        end
        else if NOT dialogLoopBlocker then
            Say(Platonov, 'D8-Pla-1');

        if NOT dialogLoopBlocker then
           dialogLoopBlocker = true;

        repeat
           ComMoveXY(FilterUnitsInArea(NorthMoveLimitArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), GetX(unit), GetY(unit) + 5);
           wait(0$1);
        until FilterUnitsInArea(NorthMoveLimitArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) = 0;

        //InGameOff;
    end;

    if dialogLoopBlocker then
    begin
         wait(0$5);

         dialogLoopBlocker = false;
    end;

end;

// Block exit Beria base
every 1 trigger NOT canExitBase AND FilterUnitsExceptArea(BeriaBaseArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) > 0 do
var unit;
begin
    enable;

    for unit in FilterUnitsExceptArea(BeriaBaseArea, [[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
    begin

        ComMoveXY(unit, 114, 98);

        if dialogLoopBlocker then
            exit;

        dialogLoopBlocker = true;

        Say(Platonov, 'D8-Pla-1');
        exit;
    end;

    wait(0$5);
    
    dialogLoopBlocker = false;
end;


// Kurin dialog if player stay in Kirov base
every 1$30 trigger NOT meetPlatonov and FilterUnitsInArea(KirovBaseArea, [[f_side, 3], [f_type, unit_human]]) > 0 do
    Say(Kurin, 'D1a-Kur-1');

// First visit Beria base
every 0$1 trigger NOT meetPlatonov AND IsOK(Burlak) AND FilterUnitsInArea(BeriaBaseArea, [[f_side, 3], [f_type, unit_human]]) > 0 do
var i, unit, yourUnits, randomMaleSoldier;
begin

    enable;

    for unit in FilterAllUnits([[f_side, 3], [f_type, unit_human]]) do
        if See(6, unit) then
        begin
            meetPlatonov = true;

            InGameOn;
            DialogueOn;

            randomMaleSoldier = DialogRandom(UnitFilter(platSoldiers, [f_sex, sex_male]), 'D2-RSol1-1', '', false);
            Say(Burlak, 'D2-Bur-1');
            Say(randomMaleSoldier, 'D2-RSol1-2');

            DialogueOff; 

            yourUnits = FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_ok]]);

            for i := 1 to yourUnits do
            begin
                ComMoveXY(yourUnits[i],  meetWithPlatonovPositions[i][1], meetWithPlatonovPositions[i][2]);
                AddComTurnUnit(yourUnits[i], Platonov);
            end;  

            repeat
                CenterNowOnUnits(Burlak);
                wait(0$1);
            until GetDistUnits(Burlak, Platonov) < 4;   

            ComTurnUnit(Platonov, Burlak);
            Say(Platonov, 'D2-Pla-2');
            Say(Burlak, 'D2-Bur-2');
            Say(Platonov, 'D2-Pla-3');
            Say(Burlak, 'D2-Bur-3');
            Say(Platonov, 'D2-Pla-4');
            Say(Burlak, 'D2-Bur-4');
            Say(Platonov, 'D2-Pla-5');

            Question1([1,2,3,4]);

            Say(Platonov,'D3-Pla-1');
            Say(Burlak,'D3-Bur-1');

            InGameOff;

            ChangeMissionObjectives('M2a');

            ComFree(Platonov);

            SetSide(beria, 3);

            canExitBase = false;

            exit;
        end;
end;

// Dialog after finish construct laboratory - executed in event
Export Function Dial_LabConstructed;
begin
    labConstructed = true;

    DialogueOn;

    Say(Burlak, 'D5a-Bur-1');

    if workshopConstructed then
        Say(Platonov, 'D5a-Pla-1')
    else
        Say(Platonov, 'D5a-Pla-1a');

    DialogueOff;

    ChangeMissionObjectives('M2b');
end;

// Dialog after finish construct workshop - executed in event
Export Function Dial_WorkshopConstructed;
begin
    workshopConstructed = true;

    DialogueOn;

    Say(Burlak, 'D5b-Bur-1');

    if labConstructed then
        Say(Platonov, 'D5b-Pla-1')
    else
        Say(Platonov, 'D5b-Pla-1a');

    DialogueOff;

    ChangeMissionObjectives('M2c');
end;

// Dialog after start construct again same building or other buildings - executed in event
Export Function Dial_StopConstructAgainSameBuilding(building);
begin
    wait(0$1);
    
    CenterNowOnUnits(building);

    ComStop(FilterAllUnits([[f_side, 3], [f_type, unit_human]]));

    DialogueOn;

    Say(Platonov, 'D6-Pla-1');
    Say(Burlak, 'D6-Bur-1');

    DialogueOff;

    case GetBType(building) of
        b_lab: SetResourceType(GetBase(beria), mat_cans, GetResourceType(GetBase(beria), mat_cans) + labCratesRefund);
        b_workshop: SetResourceType(GetBase(beria), mat_cans, GetResourceType(GetBase(beria), mat_cans) + workshopCratesRefund);
        b_oil_power: SetResourceType(GetBase(beria), mat_cans, GetResourceType(GetBase(beria), mat_cans) + oilPowerCratesRefund);
        b_armoury: SetResourceType(GetBase(beria), mat_cans, GetResourceType(GetBase(beria), mat_cans) + armouryCratesRefund);
        b_breastwork: SetResourceType(GetBase(beria), mat_cans, GetResourceType(GetBase(beria), mat_cans) + bunkerCratesRefund);
    end;

    RemoveUnit(building);
end;

// All buindings constructed
every 0$2 trigger workshopConstructed AND labConstructed do
begin

    DialogueOn;

    if engineResearched then
    begin
        Say(Burlak, 'D7b-Bur-1');
        Say(Platonov, 'D7b-Pla-1');
    end
    else
    begin
        Say(Burlak, 'D7a-Bur-1');
        Say(Platonov, 'D7a-Pla-1');    
    end;

    DialogueOff;

    ChangeMissionObjectives('M2');

    CreateCratesXYR(3, GetX(Platonov), GetY(Platonov), 5, true);
end;

// "Towarzyszu Burlak! Znalezlismy nowa dostawe." ;_;
Export Function Dial_NewCrates(x, y);
begin
    wait(0$18);

    if NOT spawnCrates then
       exit;

    ComMoveXY(Platonov, x, y);
    Say(Platonov, 'D4-Pla-1');
end;

// Dialog after constructed first vehicle - executed in event
Export Function Dial_FirstVehConstructed(factory);
begin

    firstVeh = FilterAllUnits([[f_side, 3], [f_type, unit_vehicle]])[1];

    constructedFirstVehicle = true;

    InGameOn;
    CenterNowOnUnits(factory);
    
    wait(0$3);

    Say(Burlak, 'D9-Bur-1');

    ComMoveXY(Platonov, GetX(firstVeh), GetY(firstVeh));

    repeat
        wait(0$1);
    until GetDistUnits(Platonov, firstVeh) < 4;

    ComTurnUnit(Platonov, firstVeh);

    Say(Platonov, 'D9-Pla-1');

    InGameOff;

    ChangeMissionObjectives('M3');

    canExitBase = true;

    ComMoveXY(Platonov, 112, 97);
end;

// Deliver vehicle for Kurin
Every 0$1 trigger IsInArea(firstVeh, KirovBaseArea) AND NOT vehicleForKurin do
var i, eng, mech;
begin
    vehicleForKurin = true;

    eng = UnitFilter(otherKirovUnits, [f_class, class_engineer])[1];
    mech = UnitFilter(otherKirovUnits, [f_class, class_mechanic])[1];

    InGameOn;

    CenterNowOnUnits(Kurin);
    ComMoveXY(firstVeh, GetX(Kurin), GetY(Kurin));

    Say(Kurin, 'D10-Kur-1');

    repeat
        wait(0$1);
        ComMoveXY(firstVeh, GetX(Kurin), GetY(Kurin));
    until GetDistUnits(firstVeh, Kurin) < 9;

    ComExitVehicle(UnitsInside(firstVeh));
    wait(0$1);

    // Check veh status, fuel level and HP
    if GetLives(firstVeh) < 1000 then
    begin
        ComExitBuilding(mech);
        wait(0$1);
        AddComRepairVehicle(mech, firstVeh);
    end;

    if GetFuel(firstVeh) < 90 then
    begin
        ComExitBuilding(eng);
        wait(0$1);

        ExtAddComRefuel(eng, kirov, firstVeh);
    end;

    repeat
        wait(0$1);
    until GetLives(firstVeh) = 1000 AND GetFuel(firstVeh) > 90;

    if UnitsInside(firstVeh) then
       ComExitVehicle(UnitsInside(firstVeh)[1]);

    ComEnterUnit(eng, kirov);
    SetSide(firstVeh, 6);

    if belkovReturn then
    begin
        if IsInUnit(Belkov) then
            ComExitBuilding(Belkov);

        repeat
            wait(0$1);
            AddComEnterUnit(Belkov, firstVeh);
        until IsInUnit(Belkov) = firstVeh;

        Say(Belkov, 'D10-Bel-1');
        Say(Kurin, 'D10-Kur-2');
    end
    else
    begin
        if IsInUnit(Belkov2) then
            ComExitBuilding(Belkov2);

        repeat
            wait(0$1);
            AddComEnterUnit(Belkov2, firstVeh);
        until IsInUnit(Belkov2) = firstVeh;

        meetBelkovBrother = true;

        Say(Belkov2, 'D10-Bel-1');
        Say(Burlak, 'D10-Bur-1');
        Say(Belkov2, 'D10-Bel-2');
        Say(Burlak, 'D10-Bur-2');
        Say(Kurin, 'D10-Kur-2a');
    end;

    removedBelkovUntisSaved = [];
    removedBelkovVehicleSaved = [];

    canRemoveBelkovUnits = true;

    CreateBelkovTeam;

    ComExitBuilding(belkovTeam);
    wait(0$1);

    kirovVehicle = FilterAllUnits([[f_side, 6], [f_type, unit_vehicle]])[1];

    belkovTeam = belkovTeam ^ kirovVehicle;

    ComMoveXY(belkovTeam, 147, 102);
    AddComMoveXY(belkovTeam, 133, 94);
    AddComMoveXY(belkovTeam, 123, 74);
    AddComMoveXY(belkovTeam, 111, 48);
    AddComMoveXY(belkovTeam, 122, 43);
    AddComMoveXY(belkovTeam, 129, 32);
    AddComMoveXY(belkovTeam, 132, 22);

    wait(0$8);

    CenterNowOnUnits(Kurin);
    Say(Kurin, 'D10-Kur-2b');

    InGameOff;

    ChangeMissionObjectives('M4');

    backToBeria1 = true;
end;

// Kurin dialog when player deliver next vehicle
Every 0$1 trigger FilterUnitsInArea(KirovBaseArea, [[f_side, 3], [f_type, unit_vehicle]]) AND vehicleForKurin do
    Say(Kurin, 'D1a-Kur-1a');

// Remove Belkov from map
every 1 trigger canRemoveBelkovUnits do
var unit, driver;
begin
    enable;

    if FilterUnitsInArea(BelkovExitArea, [f_side, 6]) = 0 then
        exit;

    for unit in FilterUnitsInArea(BelkovExitArea, [f_side, 6]) do
    begin

        if GetType(unit) = unit_human AND (NOT unit = Belkov AND NOT unit = Belkov2 AND NOT unit = Kirilenkova) then
        begin
            removedBelkovUntis = removedBelkovUntis + 1;
            removedBelkovUntisSaved = removedBelkovUntisSaved ^ unit;

            RemoveUnit(unit);
        end;

        if unit = Kirilenkova then
        begin
            SaveCharacters(Kirilenkova, '02_TmpKirilenkova');
            RemoveUnit(unit);
        end;

        if unit = Belkov2 then
        begin
            SaveCharacters(Belkov2, '02_TmpBelkov2');
            RemoveUnit(unit);
        end;

        if GetType(unit) = unit_vehicle then
        begin
            driver = UnitsInside(unit);

            if driver[1] = Belkov then
            begin  
                SaveCharacters(Belkov, '02_TmpBelkov');
                RemoveUnit(unit);
            end
            else
            begin
                SaveCharacters(Belkov2, '02_TmpBelkov2');
                RemoveUnit(unit);
            end;  

            removedBelkovVehicleSaved = removedBelkovVehicleSaved ^ [GetChassis(unit), GetEngine(unit), GetControl(unit), GetWeapon(unit), GetFuel(unit)];

            RemoveUnit(unit);
        end;
    end;

    SaveCharacters(removedBelkovUntisSaved, '02_TmpBelkovTeam');
end;

// Back to Beria base
every 0$1 trigger FilterUnitsExceptArea(BeriaBaseArea, [[f_side, 3], [f_type, unit_human]]) = 0 AND backToBeria1 do
var engs;
begin
    engs = UnitFilter(otherKirovUnits, [f_class, class_engineer]);

    backToBeria1 = false;
    canExitBase = false;

    Say(Platonov, 'D11-Pla-1');

    if FilterAllUnits([[f_side, 3], [f_type, unit_human]]) > 1 then
        Say(Burlak, 'D11-Bur-1a')
    else
        Say(Burlak, 'D11-Bur-1');

    ChangeMissionObjectives('M5');

    construcMoreVehicles = true;
    spawnCrates = true;

    AddComExitBuilding(engs);
    AddComBuild(engs, b_workshop, 159, 112, 0);
    AddComEnterUnit(engs, kirov);
end;

// Constructed vehicles for your solds
every 0$1 trigger constructedVehiclesCounter >= FilterAllUnits([[f_side, 3], [f_type, unit_human]]) do
var tmpBelkov, nearRuUnit;
begin
    canRemoveBelkovUnits = false;
    spawnCrates = false;

    ComStop(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]));

    InGameOn;

    CenterOnUnits(Platonov);

    DialogueOn;

    Say(Burlak, 'D12-Bur-1');
    Say(Platonov, 'D12-Pla-1');
    Say(Burlak, 'D12-Bur-2');
    Say(Platonov, 'D12-Pla-2');
    Say(Burlak, 'D12-Bur-3');
    Say(Platonov, 'D12-Pla-3');

    DialogueOff;

    SetAttitude(6, 1, att_friend, true);
    PrepareAmerican;
    PrepareBelkovReturnTeam;

    if IsOK(Belkov) then
        tmpBelkov = Belkov
    else
        tmpBelkov = Belkov2;

    CenterNowOnUnits(kirovVehicle);

    SayRadio(trapCommander, 'D13-Sol1-1');
    SayRadio(tmpBelkov, 'D13-Bel-1');
    SayRadio(Kurin, 'D13-Kur-1');
    SayRadio(tmpBelkov, 'D13-Bel-2');

    ComMoveXY(belkovTeam, 121, 29);

    repeat
        wait(0$1);

        CenterNowOnUnits(kirovVehicle);

        nearRuUnit = NearestUnitToUnit(UnitFilter(belkovTeam, [[f_type, unit_human], [f_outside]]), kirovVehicle);
        
        if GetDistUnits(kirovVehicle, nearRuUnit) < 5 then
            ComStop(kirovVehicle)
        else
            ComMoveXY(belkovTeam, 121, 29);
    until GetDistUnits(NearestUnitToUnit(belkovTeam, trapCommander), trapCommander) < 5;
 
    CenterNowOnUnits([kirovVehicle, trapCommander]);

    ComWalk(trapTeam);
    AddComTurnUnit(trapTeam, kirovVehicle);

    ComStop(belkovTeam);
    AddComTurnUnit(belkovTeam, trapCommander);

    AddComMoveXY(FilterUnitsInArea(TrapNorthGroupArea, [[f_side, 1], [f_type, unit_human]]), 111, 13);

    music_class = music_combat;

    wait(0$1);

    DialogueOn;
    Say(trapCommander, 'D14-Sol1-1');
    Say(tmpBelkov, 'D14-Bel-1');
    DialogueOff;

    immortalBelkovTeam = true;

    SetAttitude(6, 1, att_enemy, true);

    ComMoveXY(belkovTeam, 107, 5);
    ComMoveXY(kirovVehicle, 107, 5);

    wait(0$2);

    ComMoveXY(belkovTeam, 101, 19);
    ComMoveXY(kirovVehicle, 101, 19);

    wait(0$2);
    immortalVehLive = 400;
    SetLives(kirovVehicle, 500);

    wait(0$3);

    ComExitVehicle(tmpBelkov);
    wait(0$1);
    ComEnterUnit(trapCommander, kirovVehicle);
    AddComHold(trapTeam);

    ComMoveXY(belkovTeam, 88, 18);

    repeat
        wait(0$1);
        CenterNowOnUnits(tmpBelkov);
    until IsInArea(tmpBelkov, BelkovRetreatArea);

    amVeh = FilterAllUnits([[f_side, 1], [f_type, unit_vehicle]])[1];

    ComMoveXY(FilterUnitsInArea(NearHillArea, [[f_side, 1], [f_type, unit_human]]), 113, 29);
    ComMoveXY(amVeh, 118, 25);

    SayRadio(tmpBelkov, 'D14-Bel-1a');
    SayRadio(Kurin, 'D14-Kur-1');
    SayRadio(tmpBelkov, 'D14-Bel-2');
    SayRadio(Kurin, 'D14-Kur-2');

    CenterOnUnits(Platonov);
    wait(0$2);

    Say(Burlak, 'D14a-Bur-1');
    Say(Platonov, 'D14a-Pla-1');
    Say(Burlak, 'D14a-Bur-2');
    Say(Platonov, 'D14a-Pla-2');
    Say(Burlak, 'D14a-Bur-3');
    Say(Platonov, 'D14a-Pla-3');
    Say(Burlak, 'D14a-Bur-4');
    Say(Platonov, 'D14a-Pla-4');

    InGameOff;

    immortalBelkovTeam = false;
    activateAMAI = true;
    canExitBase = true;
    canExploreMap = true;
    construcMoreVehicles = false;
    activateBelkovAI = true;

    ChangeMissionObjectives('M6');

    music_class = 0;

    ComFree(trapTeam);
end;

// Spawn am mech
every 0$1 trigger GetDistUnitXY(amVeh, 118, 25) < 6 do
begin
    ComExitVehicle(trapCommander);

    uc_side = 1;
    uc_nation = 1;
    
    PrepareMechanic(0, enemySkillLevel);

    amMech = CreateHuman;
    PlaceUnitArea(amMech, AmMechSpawnArea, false);

    ComRepairVehicle(amMech, amVeh);
    AddComEnterUnit(amMech, amVeh);       
end;

// Am mech enter veh if near is enemy
every 0$1 trigger IsOK(amMech) AND FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_ok]]) do
var nearUnit;
begin
    enable;

    nearUnit = NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]), amMech);

    if GetDistUnits(nearUnit, amMech) < 13 then
        ComEnterUnit(amMech, FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_ok]])[1] )
    else
    begin
        if GetLives(FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_ok]])[1]) = 1000 AND NOT IsInUnit(amMech) then
            ComEnterUnit(amMech, FilterAllUnits([[f_side, 1], [f_type, unit_vehicle], [f_ok]])[1] );
    end;
end;

// Immortal Belkov team
every 0$1 trigger immortalBelkovTeam do
begin
    enable;

    SetLives(UnitFilter(belkovTeam, [f_type, unit_human]), 1000);

    if UnitFilter(belkovTeam, [f_type, unit_vehicle]) then
        SetLives(UnitFilter(belkovTeam, [f_type, unit_vehicle])[1], immortalVehLive);
end;

// Start counter
every 0$1 trigger canExploreMap AND NOT belkovSaved AND belkovAttackTime > 0$00 do
begin
    enable;

    belkovAttackTime = belkovAttackTime - 0$01;
end;

// Some americans attack Belkov
every 0$15 trigger canExploreMap do
var i, tmp;
begin
    tmp = trapTeam;
    tmp = tmp diff trapCommander;

    for i := 1 to rand(2, 3) do
        ComAgressiveMove(tmp[i], 87, 18);
end;

// Belkvo attack american forces
every 0$1 trigger belkovAttackTime < 0$02 AND trapTeam do
begin
    enable;

    activateBelkovAI = false;
    ComAgressiveMove(belkovTeam, 120, 28);
end;

// Saw bunkers on hill
every 0$1 trigger (NOT sawLeftBunker OR NOT sawRightBunker)  AND canExploreMap do
var bunker;
begin
    enable;

    for bunker in FilterAllUnits([[f_side, 1], [f_type, unit_building], [f_btype, b_breastwork]]) do
    begin
        if See(3, bunker) then
        begin
           // Left bunkers
            if bunker IN leftBunkers AND NOT sawLeftBunker AND NOT FilterUnitsInArea(WeakPositionArea, [[f_side, 3]]) then
            begin
                sawLeftBunker = true;

                CenterOnUnits(bunker);

                DialogueOn;

                Say(Burlak, 'D15-Bur-1');

                if IsOK(Gnyevko) OR IsOK(Kovalyuk) then
                begin
                     if IsOK(Gnyevko) then
                        Say(Gnyevko, 'D15-Gny-1');

                     if IsOK(Kovalyuk) then
                        Say(Kovalyuk, 'D15-Kov-1');

                     Say(Burlak, 'D15-Bur-2');

                     if IsOK(Kovalyuk) then
                        Say(Kovalyuk, 'D15-Kov-2');

                     Say(Burlak, 'D15-Bur-3');
                end;

                DialogueOff;

                if raportForPlatonov = 2 then
                    raportForPlatonov = 1
                else
                    raportForPlatonov = 3;

                ChangeMissionObjectives('M7');

                exit;
            end;

            // Right bunkers
            if bunker IN rightBunkers AND NOT sawRightBunker then
            begin
                sawRightBunker = true;

                CenterOnUnits(bunker);
                DialogueOn;
                
                if IsOK(Kovalyuk) then
                begin
                    if sawLeftBunker then
                        Say(Kovalyuk, 'D16-Kov-1a')
                    else
                        Say(Kovalyuk, 'D16-Kov-1');

                Say(Burlak, 'D16-Bur-1');

                if IsOK(Gnyevko) then
                    Say(Gnyevko, 'D16-Gny-1');

                end;

                DialogueOff;      

                if raportForPlatonov = 3 then
                    raportForPlatonov = 1
                else
                    raportForPlatonov = 2;

                exit;     
            end;
        end;
    end;
end;

// Dialog when player attack right bunkers
every 0$2 trigger NOT dialogAttackRightBunkers AND rightBunkers AND canExploreMap AND (IsOK(Kovalyuk) OR IsOK(Gnyevko)) do
var unit;
begin
    enable;

    for unit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
    begin
        if Attacks(unit) = rightBunkers[1] OR Attacks(unit) = rightBunkers[2] then
        begin
            dialogAttackRightBunkers = true;
            DialogueOn;

            if IsOK(Gnyevko) then
                Say(Gnyevko,'D16A-Gny-1');

            if IsOK(Kovalyuk) then
                Say(Kovalyuk,'D16A-Kov-1');

            Say(Burlak, 'D16A-Bur-1');

            DialogueOff;
            exit;
        end;
    end;
end;

// Dialog on hill
every 0$3 trigger FilterUnitsInArea(WeakPositionArea, [[f_side, 3]]) AND NOT sawLeftBunker do
var decsToChoose;
begin

    sawLeftBunker = true;

    if playerGetSupport then
        decsToChoose = [1, 3]
    else
        decsToChoose = [1, 2, 3];

    case SelectiveQuery('QAttackBw', decsToChoose) of
        1:begin
            DialogueOn;

            Say(Burlak, 'D17-Bur-1');
            SayRadio(Platonov, 'D17-Pla-1');
            Say(Burlak, 'D17-Bur-2');
            SayRadio(Platonov, 'D17-Pla-2');

            DialogueOff;

            if NOT (initiative in [2, 3]) then
            begin
                initiative = 1;
                platonovOpinion = 2;
            end;
        end;

        2:begin
            DialogueOn;

            Say(Burlak, 'D18-Bur-1');
            SayRadio(Platonov, 'D18-Pla-1');
            Say(Burlak, 'D18-Bur-2');
            SayRadio(Platonov, 'D18-Pla-2');

            DialogueOff;
            
            playerGetSupport = true;
            medalForGetSupport = false;
            PlatonovSendSupport;

            if initiative <> 3 then
            begin
                initiative = 1;
                platonovOpinion = 2;
            end;
        end;

        3: begin
          initiative = 3;
          platonovOpinion = 0;
        end;
    end;

    if IsOK(Gnyevko) then
    begin
        Say(Gnyevko, 'D19-Gny-1');
        Say(Burlak, 'D19-Bur-1');
    end;

    if IsOK(Kovalyuk) then
    begin
        Say(Kovalyuk, 'D19-Kov-1');
        Say(Burlak, 'D19-Bur-2');
    end;

end;

// Dialog when player attack left bunkers but don't from hill
every 0$2 trigger NOT dialogAttackLeftBunkers AND leftBunkers AND canExploreMap AND FilterUnitsInArea(WeakPositionArea, [[f_side, 3]]) = 0 AND (IsOK(Kovalyuk) OR IsOK(Gnyevko)) do
var unit;
begin
    enable;

    for unit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
    begin
        if Attacks(unit) = leftBunkers[1] OR Attacks(unit) = leftBunkers[2] then
        begin
            dialogAttackLeftBunkers = true;

            DialogueOn;

            if IsOK(Gnyevko) then
                Say(Gnyevko,'D19A-Gny-1');

            if IsOK(Kovalyuk) then
                Say(Kovalyuk,'D19A-Kov-1');
            
            Say(Burlak, 'D16A-Bur-1');
            DialogueOff;
            exit;
        end;
    end;
end;

// Destroy left bunkers
every 0$1 trigger (NOT leftBunkers OR FilterUnitsInArea(LeftHillArea, [[f_side, 1], [f_type, unit_building], [f_btype, b_breastwork], [f_ok]]) = 0) AND canExploreMap do
begin
    leftBunkers = [];

    DialogueOn;

    Say(Burlak,'D20-Bur-1');
    Say(Platonov,'D20-Pla-1');

    DialogueOff;    

    ChangeMissionObjectives('M8');

end;

// Destroy right bunkers
every 0$1 trigger (NOT rightBunkers OR FilterUnitsInArea(rightHillArea, [[f_side, 1], [f_type, unit_building], [f_btype, b_breastwork], [f_ok]]) = 0) AND canExploreMap do
begin
    rightBunkers = [];

    DialogueOn;

    Say(Burlak,'D20a-Bur-1');
    Say(Platonov,'D20a-Pla-1');

    DialogueOff;    
end;

// Near the americans who surrounded Belkov
every 0$1 trigger NOT saveBelkovDecision AND belkovTeam do
var yourUnit, trapUnit, decsToChoose;
begin
    enable;

    for yourUnit in FilterAllUnits([[f_side, 3], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
        for trapUnit in trapTeam do
            if GetDistUnits(yourUnit, trapUnit) < 16 then
            begin
                saveBelkovDecision = true;

                if playerGetSupport then
                    decsToChoose = [1, 3]
                else
                    decsToChoose = [1, 2, 3];


                case SelectiveQuery('QAttackAm', decsToChoose) of
                    1:begin
                        DialogueOn;

                        Say(Burlak, 'D21-Bur-1');
                        SayRadio(Platonov, 'D21-Pla-1');

                        DialogueOff;

                        if NOT (initiative in [2, 3]) then
                        begin
                            initiative = 1;
                             platonovOpinion = 2;
                        end;
                    end;

                    2:begin
                        DialogueOn;

                        Say(Burlak, 'D22-Bur-1');

                        if playerGetSupport AND (leftBunkers = 0 OR rightBunkers = 0) then
                            Say(Platonov, 'D22-Pla-1b');

                        if playerGetSupport AND (leftBunkers OR rightBunkers) then
                            Say(Platonov, 'D22-Pla-1a');

                        if NOT playerGetSupport then
                        begin
                            Say(Platonov, 'D22-Pla-1');

                            playerGetSupport = true;
                            medalForGetSupport = false;
                            PlatonovSendSupport;
                        end;

                        DialogueOff;
                        
                        if initiative <> 3 then
                        begin
                            initiative = 2;
                            platonovOpinion = 1;
                        end;
                    end;

                    3: begin
                         initiative = 3;
                         platonovOpinion = 0;
                    end;
                end;

                if IsOK(Gnyevko) then
                begin
                    Say(Gnyevko, 'D23-Gny-1');
                    Say(Burlak, 'D23-Bur-1');
                end;

                saveBelkovObjective = true;
                ChangeMissionObjectives('M9');

                exit;
            end;
end;

// Save Belkov
every 0$1 trigger canExploreMap AND NOT trapTeam AND NOT belkovSaved do
begin
    belkovSaved = true;
    activateBelkovAI = false;
    
    SetSide(belkovTeam, 3);

    ChangeMissionObjectives('M9a');
end;

// No save Belkov
every 0$1 trigger saveBelkovDecision AND (IsDead(Belkov) OR IsDead(Belkov2) AND (GetSide(Belkov) = 6 OR GetSide(Belkov2) = 6)) do
begin
    ChangeMissionObjectives('M9a');
    saveBelkovObjective = false;
end;

// Dialog with Belkov
every 0$5 trigger belkovSaved AND ( (GetDistUnits(Burlak, Belkov) < 14 AND belkovReturn) OR (GetDistUnits(Burlak, Belkov2) < 14 AND NOT belkovReturn)) do
begin
    InGameOn;

    if IsOk(Belkov) AND belkovReturn then
    begin
        CenterNowOnUnits([Burlak, Belkov]);
        ComTurnUnit(Burlak, Belkov);
        ComTurnUnit(Belkov, Burlak);

        Say(Belkov, 'D24-Bel-1');
        Say(Burlak, 'D24-Bur-1');
        Say(Belkov, 'D24-Bel-2');
        Say(Burlak, 'D24-Bur-2');
    end;

    if IsOk(Belkov2) AND NOT belkovReturn then
    begin
        CenterNowOnUnits([Burlak, Belkov2]);
        ComTurnUnit(Burlak, Belkov2);
        ComTurnUnit(Belkov2, Burlak);

        Say(Belkov2, 'D24-Bel-1');
        Say(Burlak, 'D24-Bur-1');
        Say(Burlak, 'D24-Bur-1a');
        Say(Belkov2, 'D24-Bel-2');
        Say(Burlak, 'D24-Bur-2');     
    end;

    if captureBelkovVehicle then
        Say(Burlak, 'D24-Bur-2a');

    SayRadio(Platonov, 'D24-Pla-2');

    if leftBunkers then
        SayRadio(Platonov, 'D24-Pla-2a');

    InGameOff;
end;

// Track dialog
every 0$1 trigger FilterUnitsInArea(TrackDialogArea, [[f_side, 3], [f_type, unit_vehicle]]) AND (IsOk(Kovalyuk) OR IsOK(Gnyevko) ) do
begin
    DialogueOn;

    Say(Burlak, 'D26-Bur-1');

    if IsOk(Kovalyuk) then
        Say(Kovalyuk, 'D26-Kov-1');

    if IsOk(Gnyevko) then
        Say(Gnyevko, 'D26-Gny-1');

    Say(Burlak, 'D26-Bur-2');

    DialogueOff;
end;

// Raport for Platonov, change value from -1 to 0
every 0$1 trigger canExploreMap AND ( NOT IsInArea(Burlak, BeriaBaseArea) AND NOT IsInArea(IsInUnit(IsInUnit(Burlak)), BeriaBaseArea) ) AND raportForPlatonov = -1 do
begin
    enable;
    raportForPlatonov = 0;
end;

// Raports for Platonov
// 0 - nothing, 1- Explore two bunkers, 2 - explore right bunkers, 3 - explore left bunkers
every 0$1 trigger canExploreMap AND raportForPlatonov >= 0 AND
NOT backToBeria2 AND
(IsInArea(Burlak, BeriaBaseArea) OR IsInArea(IsInUnit(IsInUnit(Burlak)), BeriaBaseArea) AND
NOT backToBeria2 ) do
begin
    enable;

    DialogueOn;

    case raportForPlatonov of
        0: begin
            Say(Platonov, 'D25a-Pla-1');
            Say(Burlak, 'D25a-Bur-1');
        end;

        1: begin
            Say(Platonov, 'D25-Pla-1');
            Say(Burlak, 'D25-Bur-1');
            Say(Burlak, 'D25b-Bur-1');
            Say(Platonov, 'D25b-Pla-1');

            playerGetSupport = true;
            PlatonovSendSupport;
        end;

        2: begin
            Say(Platonov, 'D25-Pla-1');
            Say(Burlak, 'D25-Bur-1a');

            if NOT sawLeftBunker then
                Say(Platonov, 'D25c-Pla-1');
        end;

        3: begin
            Say(Platonov, 'D25-Pla-1');
            Say(Burlak, 'D25a-Bur-1b');
            Say(Burlak, 'D25b-Bur-1');
            Say(Platonov, 'D25b-Pla-1');

            playerGetSupport = true;
            PlatonovSendSupport;

            if NOT sawRightBunker then
                Say(Platonov, 'D25c-Pla-1');

        end;
    end;

    raportForPlatonov = -1;

    Say(Platonov, 'D25c-Pla-1a');

    DialogueOff;
end;

// Explore north map part
every 0$1 trigger FilterUnitsInArea(NorthArea, [[f_side, 3]]) > 0 do
begin
    northMapExplored = true;
    ChangeMissionObjectives('M10');
end;

// All missions complete
every 0$1 trigger NOT leftBunkers AND northMapExplored AND (belkovSaved OR NOT saveBelkovObjective) do
begin
    backToBeria2 = true;

    ChangeMissionObjectives('M10a');
end;

// Immortal Platonov and Kurin
every 0$1 do
begin
   enable;
   SetLives([Kurin, Platonov], 1000);
end;
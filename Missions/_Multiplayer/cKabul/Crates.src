Every 0$20 trigger game do
SpawnCrates([cratesWestArea, cratesEastArea, cratesMiddleArea, cratesSouthArea, arBaseArea], multi_crates_spawn[2], multi_crates_spawn[1]);

Export Function SpawnCrates(areas, time, amount);
var i, x, playersAreas;
begin
if not areas then
   exit;

playersAreas := [cratesPlayerWestArea, cratesPlayerMiddleArea, cratesPlayerEastArea];
x := 5;

repeat
 wait(time);

 if tick < [30$00, 25$00, 20$00][Difficulty] then
    begin
    for i := 1 to multi_sides do
        if multi_sides[i] then
           begin
           if GetResourceArea(playersAreas[multi_sides[i]], mat_cans) < [32, 25, 23][Difficulty] then
              begin
              amount := amount - (x * 10);
              CreateCratesArea(x, playersAreas[multi_sides[i]], true);
              end;
           end;

    wait(0$2);
    end;

 for i in areas do
     begin
     if GetResourceArea(i, mat_cans) < [25, 22, 20][Difficulty] then
        begin
        wait(0$5);
        CreateCratesArea(x, i, true);
        amount := amount - (x * 10);
        end;
     end;

 time := time + 0$1;
 x := x - 1;

 if x = 0 then
    x := 5;

 if time > 1$00 then
    time := 0$30;

until not game or amount <= 0;
End;

Every 0$40 trigger GetResourceArea(cratesSouthArea, mat_cans) < [32, 25, 23][Difficulty] do
begin
enable;
CreateCratesArea(5, cratesSouthArea, true);
End;
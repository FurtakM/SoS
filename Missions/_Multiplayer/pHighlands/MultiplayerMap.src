// Multiplayer map module
// Each map has his own
// SOS_Multiplayer required
Export function InitMultiplayerSettings;
begin
{
  mpBaseLevel,            // (0)
  mpAmountOfPeople,       // (1)
  mpSkillLevel,           // (2)
  mpStartingResources,    // (3)
  mpShipments,            // (4)
  mpOilSources,           // (5)
  mpSibSources,           // (6)
  mpPoints                // (7)
  mpVehicles              // (8)
  mpEmissions             // (9)
  mpSharedVision,         // (10)
  mpFlags,                // (11)
  mpSibDetection,         // (12)
  mpPeopleRespawn,        // (13)
  mpApemans,              // (14)
  mpSibBomb,              // (17)
  mpBuildUpTime,          // (20)
}

mpGameSettingsKeys := [
  1, // 0
  1, // 1
  1, // 2
  1, // 3
  1, // 4
  1, // 5
  1, // 6
  1, // 7
  1, // 8
  1, // 9
  1, // 10
  1, // 11
  1, // 12
  1, // 13
  1, // 14
  0, // 15
  0, // 16
  1, // 17
  0, // 18
  1, // 19
  1  // 20
];

mpGameSettingsValues := [
  [0, 1], // base level (only depot, breastworks, breastworks + armoury)
  [9, 12, 15, 24], // amount of people
  [1, 3, 5, 7], // people skill
  [[0,0,0], [100,30,0], [200,75,10], [500,150,30]], // resources
  [1, 2, 3, 4], // shipments
  [0, 1, 2], // oil sources
  [0, 1, 2], // sib sources
  [250, 350, 500, 1000, 2000], // points
  [0, 2], // vehicles
  [0, 5$00, 10$00, 15$00, 20$00, 30$00], // emissions
  [0, 1], // shared vision
  [-1, 0$10, 0$20, 0$40, 0$0], // flag (morale time)
  [0, 1], // sib detection
  [0, 6$30, 3$30], // people respawn
  [0, 2, 4, 6], // apemans
  [],
  [],
  [0, 1, 15$0, 25$0, 30$0, 45$0, 60$0, 90$0, 100$0, 120$0, 180$0], // sib bomb
  [],
  [0, 1, 2, 4], // tree
  [0, 3$0, 5$0, 8$0, 10$0, 12$0, 15$0, 20$0, 30$0] // bt
];

// default settings
mpGameSettings := [
  1,   // base 0
  15,  // people 1
  4,   // skill  2
  [100,30,0], // resources 3
  3,   // shipments  4
  1,   // oil 5
  2,   // sib 6
  250, // points 7
  0,   // vehicles 8
  0,   // emission 9
  1,   // vision 10
  0$20, // morale 11
  1,   // detection 12
  0$20,   // respawn 13
  3,   // apemans  14
  0,   // - 15
  0,   // - 16
  15$0, // sib bomb 17
  0,   // - 18
  0,   // tree 19
  3$0  // bt 20
];

mpGameType := GameTypeNormal;
your_side  := 1;

mpSidePositions := [1,2,3,4,0,0,0,0];
mpSideTeams     := [1,1,2,2,0,0,0,0];
mpSideNations   := [3,3,3,3,0,0,0,0];
mpTeams         := [[1,2],[3,4]];
mpTeamGame      := true;
mpMapSize       := [100, 120];
End;

Export kingArea, kingCamArea;
Export function GetMultiplayerMapData(gameType);
var i, all, area, hasOil, hasSib, baseLevel, startPos;
begin
mpBuildUpAreas := [
  BaseArea1,
  BaseArea2,
  BaseArea3,
  BaseArea4
];

mpMarkedAreas := [] union mpBuildUpAreas;

if gameType = GameTypeSurvival then
   baseLevel := 1
else
   baseLevel := mpGameSettings[1];

mpPeopleAmount := mpGameSettings[2];
mpPeopleSkill := mpGameSettings[3];
mpStartingResources := mpGameSettings[4];
mpShipments := mpGameSettings[5];
hasOil := mpGameSettings[6] > 0;
hasSib := mpGameSettings[7] > 0;

mpKingArea := 0;
mpPoints := 0;
mpEmissions := 0;

kingArea := 0;
kingCamArea := 0;

if mpGameSettings[9] then
   begin
   mpVehicles := [
     [
        [us_light_wheeled, engine_combustion, control_manual, us_machine_gun],
        [us_light_wheeled, engine_combustion, control_manual, us_light_gun]
     ], // american
     [
        [ar_light_trike, engine_combustion, control_manual, ar_double_machine_gun],
        [ar_light_trike, engine_combustion, control_manual, ar_light_gun]
     ], // arabian
     [
        [ru_medium_wheeled, engine_combustion, control_manual, ru_heavy_machine_gun],
        [ru_medium_wheeled, engine_combustion, control_manual, ru_heavy_machine_gun]
     ]  // russian
   ];
   end;

mpSharedVision := mpGameSettings[11];
mpBattleFlag := mpGameSettings[12];
mpSibDetection := mpGameSettings[13];
mpRespawn := mpGameSettings[14];
mpApemans := mpGameSettings[15];
mpSibBomb := mpGameSettings[18];
mpBuildUpTime := mpGameSettings[21];

mpApemansArea := [
  ApeArea1,
  ApeArea2,
  ApeArea3,
  ApeArea4
];

startPos := [
  GetBuildingFromArea(DepotArea1, 2),
  GetBuildingFromArea(DepotArea2, 0),
  GetBuildingFromArea(DepotArea3, 3),
  GetBuildingFromArea(DepotArea4, 5)
];

for i := 1 to Count(startPos) do
    mpStartPos := Replace(mpStartPos, i, [ShiftX(startPos[i][1], startPos[i][3], 10), ShiftY(startPos[i][2], startPos[i][3], 10)]);

if baseLevel >= 1 then
   mpDepots := startPos;

if hasOil then
   begin
   all := (mpGameSettings[6] = 2);

   mpOilDeposits := [
     GetSourcesFromArea(OilArea1, all),
     GetSourcesFromArea(OilArea2, all),
     GetSourcesFromArea(OilArea3, all),
     GetSourcesFromArea(OilArea4, all)
   ];
   end;

if hasSib then
   begin
   all := (mpGameSettings[7] = 2);

   mpSibDeposits := [
     GetSourcesFromArea(SibArea1, all),
     GetSourcesFromArea(SibArea2, all),
     GetSourcesFromArea(SibArea3, all),
     GetSourcesFromArea(SibArea4, all)
   ];
   end;

End;


// Crates drop
Every 0$2 marked mpActive do
var i, x, tmp, time, areas, normalTime, delayTime, maxTime;
begin
if not mpShipments then
   exit;

case mpShipments of
     1: begin // rare
        time := 0$20;
        normalTime := 0$20;
        maxTime := 1$20;
        delayTime := 0$4;
        end;
     2: begin // medium
        time := 0$17;
        normalTime := 0$17;
        maxTime := 1$10;
        delayTime := 0$3;
        end;
     3: begin  // high
        time := 0$15;
        normalTime := 0$15;
        maxTime := 1$00;
        delayTime := 0$2;
        end;
     4: begin  // very high
        time := 0$12;
        normalTime := 0$12;
        maxTime := 0$55;
        delayTime := 0$1;
        end;
     end;

tmp := [BaseArea1, BaseArea2, BaseArea3, BaseArea4];
areas := [CratesArea1, CratesArea2, CratesArea3, CratesArea4];

for i := 1 to 8 do
    begin
    if not mpSidePositions[i] then
       continue;

    areas := areas ^ tmp[mpSidePositions[i]];
    end;

x := 1;

repeat
 wait(time);
 x := Inc(x);

 if x > 5 then
    x := 1;

 for i in areas do
     begin
     if i = CenterArea then
        begin
        if GetResourceArea(i, mat_cans) < 30 then
           CreateCratesArea(x, i, true);
        end
     else if GetResourceArea(i, mat_cans) < 20 then
        begin
        CreateCratesArea(x, i, true);
        end;
     end;

 time := time + delayTime;

 if time > maxTime then
    time := normalTime;

until false;
End;

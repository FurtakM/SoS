Export ar_force_west, ar_force_south, ar_force_north, ar_force_north2, ar_force_east;
Export ar_force_tmp, ar_vehicles_tmp, ar_defenders_tmp;
Export Saliba, Gensher;

Export Function PrepareArabian(side);
var i, d, xy, un, un2, skill, tmp, techs;
begin
tmp := FilterAllUnits([f_side, 2]);

side_bot := side;
 
if tmp and side <> 2 then
   begin
   for i in tmp do
       SetSide(i, side);

   ResetFog;
   end;

techs := [tech_ApeAgres, tech_ApeBrain, tech_ApeNeural,
          tech_StimDrugs, tech_Bio1, tech_Bio2,
          tech_Weap3, tech_RemCharge, tech_rocket,
          tech_SibEng, 72, tech_Sib2];

for i in techs do
    SetTech(i, side, state_researched);

skill := [6, 7, 8][Difficulty];

ComLinkToBase([ar_dep_e], 141); 
Wait(1);

uc_side := side;
uc_nation := 2;

ar_force_north := PrepareBase(ar_dep_n, area_base_north, 'dammam', skill, [10000, 1000, 300], [12, 4, -1, 4]);
ar_force_north2 := PrepareBase(ar_dep_n2, area_base_north2, '', skill, [1000, 100, 0], [12, 4, 2, 0]);
ar_force_west := PrepareBase(ar_dep_w, area_base_west, 'jeddah', skill, [700, 300, 10], [12, 4, 3, 2]);
ar_force_south := PrepareBase(ar_dep_s, area_base_south, 'riyadh', skill, [500, 60, 0], [4, 2, 3, 1]);
ar_force_east := PrepareBase(ar_dep_e, area_base_east, '', skill, [500, 50, 0], [11, 2, 3, 1]);

mc_bases := [ar_force_north, ar_force_west, ar_force_east, ar_force_south, ar_force_north2];

// control_towers
vc_chassis := ar_half_tracked;
vc_engine := engine_siberite;
vc_control := control_manual;
vc_weapon := ar_control_tower;

for i = 1 to 5 do
    begin
    xy := [[156, 15], [12,7]][i mod 2 + 1];
    PrepareHuman(false, class_mechanic, skill);

    un := CreateVehicle;
    SetDir(un, rand(0, 5));
    PlaceUnitXYR(un, xy[1], xy[2], 6, false);

    un2 := CreateHuman;
    PlaceHumanInUnit(un2, un);

    mc_bases := ReplaceIn(mc_bases, [i mod 2 + 1, mc_bases[i mod 2 + 1]+1], un);
    mc_bases := ReplaceIn(mc_bases, [i mod 2 + 1, mc_bases[i mod 2 + 1]+1], un2);
    end;

for i in FilterAllUnits([[f_side, side_bot], [f_btype, b_breastwork], [f_empty]]) do
    begin
    if GetBase(i) then
       continue;

    d := GetDir(i);

    if d < 3 then
       d := d + 3
      else
       d := d - 3;

    PrepareHuman(false, class_mortar, skill);
    un := CreateHuman;

    SetDir(un, d);
    PlaceHumanInUnit(un, i);
    end;


ar_force_tmp := [];

PrepareHuman(sex_male, 1, skill);
hc_gallery := 'SecondCharsGal';
hc_face_number := 4;
ar_force_tmp := Replace(ar_force_tmp, 1, CreateHuman);

PrepareHuman(sex_female, 4, skill);
hc_gallery := 'SecondCharsGal';
hc_face_number := 15;
ar_force_tmp := Replace(ar_force_tmp, 2, CreateHuman);


PrepareHuman(sex_female, 4, skill);
hc_gallery := '';
ar_force_tmp := Replace(ar_force_tmp, 3, CreateHuman);

hc_sex := sex_male;
hc_class = 11;
hc_gallery = 'sandar';
hc_face_number = 33;
hc_name = 'Thabit Muhair Saliba';
hc_skills = [5, 5, 3, 3];

Saliba = CreateHuman;
PlaceHumanInUnit(Saliba, ar_dep_s);

hc_name := 'Dietrich Gensher';
hc_class := 1;
hc_gallery := 'sandar';
hc_face_number := 2;
hc_skills := [10, 6, 5, 4];

Gensher := CreateHuman;
PlaceHumanInUnit(Gensher, ar_dep_n);

InitHc;

// prepare vehicles
ar_defenders_tmp := [ [], [], [], [] ];

for d = 1 to [3, 3, 4][Difficulty] do
for i = 1 to 4 do
    begin
    PrepareVehicle(ar_half_tracked,
                   [engine_siberite, engine_solar][rand(1,2)],
                   [control_manual, control_apeman][rand(1,2)],
                   [ar_gatling_gun, ar_gun, ar_flame_thrower, ar_rocket_launcher][rand(1,4)],
                   100);
    un := CreateVehicle;
    ar_defenders_tmp := ReplaceIn(ar_defenders_tmp, [i, ar_defenders_tmp[i]+1], un);
    SetDir(un, rand(0,5));
    PlaceUnitArea(un, [parking_north, parking_west, parking_east, parking_south][i], false);
    end;

InitHc;
End;


Every 13$00+10$00 trigger IsOk(ar_dep_n) and GetSide(ar_dep_n) = side_bot do
var i, t, x, base, tmp, coords, flags, danger, target, _target;
begin
enable;

base := 1;
flags := [0, 0, 0, 0, 1, 0, 0, 0, 1, 0];
coords := [ [28,14], [107,119], [127,97], [185,127] ];
target := 0;
x := 0;

tmp := [
         [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
         [ar_half_tracked, engine_combustion, control_remote, ar_gatling_gun],
         [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
         [ar_half_tracked, engine_combustion, control_remote, ar_selfpropelled_bomb]
       ];

if Difficulty > 1 then
   for i = 1 to Difficulty+2 do
       tmp := Insert(tmp, tmp+1, [ar_half_tracked, engine_combustion, control_remote, [ar_rocket_launcher, ar_gun, ar_gun, ar_selfpropelled_bomb][rand(1, 4)]]);

MC_SetProduceList(base, tmp);

repeat
 wait(0$1);
until MC_GetProduceList(base) = 0;

wait(0$10);

for i = 1 to coords do
    begin
    t := DangerAtRangeXY(side_bot, coords[i][1], coords[i][2], 35)[4];

    if t > x then
       begin
       x := t;
       target := i;
       end;

    end;

case target of
1: _target := [[60,8], [28,15]];
2: _target := [[52,11], [76,90]];
3: _target := [[129,66], [130,97]];
4: _target := [[156,87], [183,127]];
end;

if not _target then
   _target := [[52,11], [76,90]];

MC_PrepareAttack(base, mc_vehicles[base], _target, flags);
End;

Every 13$00+10$00 trigger (IsOk(ar_dep_w) and GetSide(ar_dep_w) = side_bot) and (multi_players_amount = 3 or IsDead(ar_dep_n) or GetSide(ar_dep_n) <> side_bot)  do
var i, t, x, base, tmp, coords, flags, danger, target, _target;
begin
enable;

base := 2;
flags := [0, 0, 0, 0, 1, 0, 0, 0, 1, 0];
coords := [ [101,110], [100,21] ];
target := 0;
x := 0;

tmp := [
         [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
         [ar_half_tracked, engine_combustion, control_remote, ar_gatling_gun],
         [ar_half_tracked, engine_combustion, control_remote, ar_rocket_launcher],
       ];

if Difficulty > 1 then
   for i = 1 to Difficulty+1 do
       tmp := Insert(tmp, tmp+1, [ar_half_tracked, engine_combustion, control_remote, [ar_rocket_launcher, ar_gun, ar_gun][i mod 2 + 1]]);

MC_SetProduceList(base, tmp);

repeat
 wait(0$1);
until MC_GetProduceList(base) = 0;

wait(0$10);

for i = 1 to coords do
    begin
    t := DangerAtRangeXY(side_bot, coords[i][1], coords[i][2], 35)[4];

    if t > x then
       begin
       x := t;
       target := i;
       end;

    end;

case target of
1: _target := [[49,35], [76,90]];
2: _target := [[79,13], [100,22]];
end;

if not _target then
   _target := [[49,35], [76,90]];

MC_PrepareAttack(base, mc_vehicles[base], _target, flags);
End;
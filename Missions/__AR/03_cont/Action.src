Export function SetMovie;
begin
ChangeSideFog(2, 5);

CenterNowOnUnits(Louis);

InGameOn;

PlaceSeeing(134, 12, 2, 5);
RemoveSeeing(134, 12, 2);

ComMoveXY(Louis, 94, 160);
Wait(0$03);
ComExitBuilding(Jan);
AddComMoveXY(Jan, 92, 155);
AddComTurnUnit(Jan, Louis);


Wait(0$05);
CenterNowOnUnits(Jan);

ComExitVehicle(Louis);
AddComMoveXY(Louis, 94, 156);
AddComTurnUnit(Louis, Jan);

Wait(0$0.3);

Say(Louis, 'DDLc1');
Say(Jan, 'DDJc1');
Say(Louis, 'DDLc2');
Say(Jan, 'DDJc2');
Say(Louis, 'DDLc3');
Say(Jan, 'DDJc3');
Say(Louis, 'DDLc4');
Say(Jan, 'DDJc4');
Say(Jan, 'DDJc5');
Say(Louis, 'DDLc5');
Say(Jan, 'DDJc6');
Say(Jan, 'DDJc6a'); // oil
Say(Louis, 'DDLc5a');
Say(Jan, 'DDJc6b');
Say(Louis, 'DDLc6');

InGameOff;


ChangeMissionObjectives('FindBase');
CameraPlaced;

Wait(0$02);

DialogueOn;
Say(Jan, 'DDJc7');
Say(Louis, 'DDLc7');
DialogueOff;

Wait(0$01);

SaveForQuickRestart;

Wait(0$20);

SetArabianAI;
SendPatrol;
End;


// InBase
Every 0$01 trigger IsInArea(Louis, Base1) do
begin
InGameOn;

CenterNowOnUnits(Louis);

If See(2, Hubert) then
begin
DialogueOn;

Say(Louis, 'DDLcb1');
Say(Hubert, 'DDHcb1');

DialogueOff;
end;

ComMoveXY(Louis, 139, 24);
ChangeSideFog(2, 8);

Wait(0$05);

PlaceSeeing(GetX(Gali), GetY(Gali), 2, 10);

CenterOnUnits(Gali);

If IsInUnit(Louis) then
begin
AddComExitVehicle(Louis);
end;

AddComMoveXY(Louis, 139, 24);
AddComTurnUnit(Louis, Gali);

Wait(1);

ComTurnUnit(Gali, Louis);
ComTurnUnit(Mike, Louis);

Say(Gali, 'DDGcb1');
Say(Louis, 'DDLcb2');
Say(Gali, 'DDGcb2');
Say(Mike, 'DDMcb1');
Say(Louis, 'DDLcb3');
Say(Gali, 'DDGcb3');
Say(Mike, 'DDMcb2');
Say(Gali, 'DDGcb4');
Say(Louis, 'DDLcb3n');
Say(Mike, 'DDMcb3');
Say(Louis, 'DDLcb4');
Say(Mike, 'DDMcb4');

Wait(0$02);

ComExitBuilding([Jenna, apeman1]);

ComMoveXY([Jenna, apeman1], GetX(Louis) - 3, GetY(Louis));
AddComTurnUnit(Jenna, Louis);

ComTurnUnit(Louis, Jenna);

Say(Louis, 'DDLape1');
Say(Jenna, 'DDJape1');
Say(Louis, 'DDLape2');
Say(Jenna, 'DDJape2');
Say(Louis, 'DDLape3');

InGameOff;

ChangeSideFog(2, 5);

If Difficulty = 1 then
begin
ChangeMissionObjectives('TransportDiff1');
end;

If Difficulty = 2 then
begin
ChangeMissionObjectives('TransportDiff2');
end;

If Difficulty = 3 then
begin
ChangeMissionObjectives('TransportDiff3');
end;

CounterStart = true;
SetAreaMapShow(crat_coll, 1);

PrepareEng;

SetSide([Abdul, ar_cargo], 6);
SetSide(FilterAllUnits([f_side, 8]), 2);
SetSide([Abdul, ar_cargo], 8);

Wait(0$03);

BorderActive = false;

SetCargo(ar_cargo, mat_oil, 100);
Hint('Collecter');

Wait(0$05);

DialogueOn;

CenterNowOnUnits(ar_shit);

Say(Louis, 'DDLsolar1');
Say(Mike, 'DDMsolar1');
Say(Gali, 'DDGsolar1');

DialogueOff;

Hint('Solar');

SaveForQuickRestart;
end;

Every 0$02 marked 01 do
var un;
begin
enable;

for un in FilterAllUnits([f_side, 2]) diff [Louis] do
begin

If GetDistUnits(Abdul, un) < 8 then
begin
DialogueOn;
CenterNowOnUnits(Abdul);

If GetSex(un) = sex_male then
begin
Say(un, 'DFoundSheikM');
end else
begin
Say(un, 'DFoundSheikF');
end;

DialogueOff;
Disable(01);
end;
end;
end;

Every 0$01 trigger GetDistUnits(Louis, Abdul) < 8 do
begin
InGameOn;

Disable(01);

CenterNowOnUnits(Abdul);

ComExitVehicle(Louis); // if he in veh

Wait(0$03);

ComTurnUnit(Louis, Abdul);
ComTurnUnit(Abdul, Louis);


Say(Louis, 'DDLele1');
Say(Abdul, 'DDAele1');
Say(Louis, 'DDLele2');
Say(Abdul, 'DDAele2');
Say(Louis, 'DDLele3');
Say(Abdul, 'DDAele3');
Say(Louis, 'DDLele4');
Say(Abdul, 'DDAele4');
Say(Louis, 'DDLele5');
Say(Abdul, 'DDAele5');
Say(Louis, 'DDLele6');
Say(Abdul, 'DDAele6');
Say(Louis, 'DDLele7');

Wait(0$0.2);

ComFree(Elephant);

InGameOff;
end;

Every 0$01 trigger IsInArea(Elephant, MastodontArea) do
begin

ComStop(Elephant);
SetSide(Elephant, 8);

Wait(0$0.2);

ComEnterUnit(Abdul, Elephant);

Wait(0$5);

InGameOn;

ComTurnUnit(Louis, Abdul);
AddComTurnUnit(Abdul, Louis);

Say(Abdul, 'DDeles1');
Say(Louis, 'DDeles2');
Say(Abdul, 'DDeles3');
Say(Louis, 'DDeles4');
Say(Abdul, 'DDeles5');
Say(Louis, 'DDeles6');

InGameOff;

ComFree(Abdul);

Locked = true;

ChangeMissionObjectives('SheikFree');
limit = limit + 6$00;

SetSide(ar_cargo, 2);

Wait(0$03);

ComMoveXY([Abdul, Elephant], 202, 177);

Wait(0$20);

RemoveUnit(Abdul);
RemoveUnit(Elephant);
end;



Every 0$10 trigger AmAngry > 20 do
begin
SayRadio(dow, 'DS1');
am_diff = am_diff + 2;
end;

Every 0$10 trigger AmAngry > 40 do
begin
SayRadio(dow, 'DS2');
Say(Louis, 'DLS3');
SayRadio(dow, 'DS3');
am_diff = am_diff + 2;
end;


// Oil can work
Every 0$01 trigger Time > 6$00 do
begin
ComUpgrade(ar_deps);
end;


// Convoy
Every 0$01 trigger Time > 15$00 do
begin
DialogueOn;
SayRadio(Jan, 'CnvDJ1');
Say(Louis, 'CnvDL1');
DWait(0$0.2);
Say(Gali, 'CnvDG1');
Say(Louis, 'CnvDL2');
Say(Jenna, 'CnvDJn1');
Say(Louis, 'CnvDL3');
DialogueOff;
Hint('Convoy');
SetTech(42, 2, state_enabled);

Wait(1$20);

PrepareRick;

Wait(0$03);

DialogueOn;
SayRadio(Rick, 'DR1');
Say(Louis, 'DRL1');
DWait(0$0.3);
Say(Louis, 'DRL2');
SayRadio(Rick, 'DRa2');
Say(Louis, 'DRL3');
DialogueOff;

Wait(1$00);

PlaceUnitXY(Rick, 127, 2, false);
SetLives(Rick, 500);
ComMoveXY(Rick, 131, 13);

Wait(0$05);

DialogueOn;
CenterNowOnUnits(Rick);
Say(Rick, 'DR3');
Say(Louis, 'DRL4');
Say(Rick, 'DR4');
Say(Louis, 'DRL4a');
Say(Jenna, 'DRJ1');
Say(Rick, 'DR5');
DialogueOff;

SetSide(Rick, 2);

ComEnterUnit(Rick, ar_lab1);
end;


Every 0$10 trigger Time > 18$00 do
begin
PrepareConvoyA;
end;


Every 6$30 trigger Time > 23$00 do
begin
enable;

SayRadio(Jan, 'DrCn1');
Wait(0$35);
PrepareConvoyB;
end;

Every 0$01 trigger Time > 21$10 do
begin
DialogueOn;
Say(Jenna, 'DJ-Solar1');
DialogueOff;

SetLives(ar_el1, 10);
SetLives(ar_el2, 10);
SetLives(ar_el3, 10);

CenterNowOnUnits(ar_el2);
end;


Every 0$01 trigger Time > 31$00 do
begin
InGameOn;
DialogueOn;

PrepareShariff;

SayRadio(Shariff, 'DShar1');

Case Query('ShariffQuestion') of
1:begin // tell you need help
Say(Louis, 'DL-help1');
SayRadio(Shariff, 'DShar2');
DialogueOff;
InGameOff;

Wait(3$00);

PrepareHelp;
end;
2:begin // tell you dont need help
Say(Louis, 'DL-nonhelp1');
Say(Shariff, 'DShar3');

DialogueOff;
InGameOff;

limit = limit + 10$0;   // time bonus
end;
end;

end;

// Dialogs from event
Every 0$01 trigger InJ = false do
begin
Say(Louis, 'DWeap1');
Say(Jenna, 'DWeap2');
end;


Every 0$01 trigger InR = false do
begin
Say(Rick, 'DRi1');
Say(Louis, 'DRiL1');
Say(Rick, 'DRi2');

ChangeMissionObjectives('Radar');

SetRestrict(b_lab_full, 2, state_enabled);
SetRestrict(b_lab_opto, 2, state_enabled);
SetRestrict(b_ext_radar, 2, state_enabled);
end;

Every 0$01 trigger ArRa = false do
begin
Say(Rick, 'RiRar1');
Say(Louis, 'RiRarL1');
ChangeMissionObjectives('RadarOut');
end;

// Added in ver 0.4
Every 0$01 trigger GetResourceType(GetBase(ar_deps), mat_cans) >= 500 do // player dont send any crates yet
begin
Say(Gali, 'DGad1');
end;

Every 0$03 trigger FilterAllUnits([[f_side, 2], [f_class, class_apeman_soldier]]) > 0 do
begin
Say(Hubert, 'DHad1');
Say(Louis, 'DLad1');
Say(Jenna, 'DJad1');
end;

Every 0$01 trigger FilterAllUnits([[f_side, 2], [f_class, class_apeman_engineer]]) > 0 do
begin
Say(Gali, 'DGad2');
end;


Every 0$02 trigger FilterUnitsInArea(AMCR, [f_control, control_remote]) > 0 do
begin
DialogueOn;

CenterNowOnXY(129, 35);

DWait(0$0.2);

Say(Hubert, 'DHam1');
Say(Jenna, 'DJam2');
Say(Louis, 'DLam3');

DialogueOff;
end;


Every 0$01 trigger FilterUnitsInArea(AmBase, [f_side, 2]) > 0 do
begin
SayRadio(dow, 'Dapp');
End;

Every 0$01 trigger FilterAllUnits([[f_side, 2], [f_or, [f_class, class_apeman], [f_class, class_apeman_soldier], [f_class, class_apeman_engineer]]]) = Rand(3,5) do
begin
Say(Gali, 'Dapp1');
End;

Every 0$01 trigger See(1, Mike) do
begin
Say(Mike, 'Dapp2');
End;


Every 0$05 trigger FilterAllUnits([[f_side, 2], [f_weapon, us_bulldozer]]) > 0 do
begin
SayRadio(dow, 'Dapp3');
End;


Every 0$01 trigger time >= [90$00, 87$00, 85$00][Difficulty] do
begin
InGameOn;
DialogueOn;

SayRadio(Shariff, 'DS-dissm');

YouLost('timeout');
DialogueOff;
End;


Every 0$13 trigger cameras >= [9, 8, 6][Difficulty] do
begin
SetTech(tech_HidCam, 2, state_disabled);
Say(FilterAllUnits([[f_side, 2], [f_class, 2]])[1], 'DCam');
End;













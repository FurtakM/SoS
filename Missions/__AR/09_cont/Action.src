Export Shariff;
Export Function Action;
var un, un2, filter;
begin

     uc_side := 5;
     Shariff := NewCharacter('Abdul');
     SetClass(Shariff, class_desert_warior);

     CenterNowOnUnits(Louis);

     filter := FilterAllUnits([[f_side, 2], [f_type, unit_human]]);

     if not IsLive(Rolf) then
        un := filter[1]
     else
        un := Rolf;

     if not IsLive(Gali) then
        un2 := filter[2]
     else
        un2 := Gali;


        InGameOn;

        ComTurnUnit(Louis, un);
        ComTurnUnit(un, Louis);

        Say(un, '09_DRolf1');
        Say(Louis, '09_DLouis1');

        Wait(0$0.5);

        SayRadio(Shariff, '09_DShariff1');
        Say(Louis, '09_DLouis2');
        SayRadio(Shariff, '09_DShariff2');
        Say(Louis, '09_DLouis3');
        SayRadio(Shariff, '09_DShariff3');

        Say(un, '09_DRolf2');
        Say(un2, '09_DGali1');
        Say(un, '09_DRolf3');
        Say(Louis, '09_DLouis4');

        if rus_var = 0 then
           Say(Louis, '09_DLouis-cargo');

        InGameOff;

        if rus_var = 0 then
           ChangeMissionObjectives('UsACar')
        else
           ChangeMissionObjectives('Us');

        game_counter := 1;

        SaveForQuickRestart;

End;


Every 0$01 trigger game_counter > 0 do
begin
enable;
game_counter := game_counter + 1;
end;


// american reaction
// 0 1
Every 0$01 trigger (rus_var = 0 and game_counter >= Rand(86, 120)) or (us_contact) do
begin
DialogueOn;
SayRadio(us_comander, '09_DUs1');
DialogueOff;

SpawnUSF(0);
us_attack := 2; // defense on line
End;


Every 0$01 trigger tick > 5$00 and us_contact = false do
var tmp;
begin
DialogueOn;

if IsLive(Gali) then
   tmp := Gali
    else
     tmp := ar_pep[3];

Say(tmp, '09_usnotattack');

if IsLive(Rolf) then
   Say(Rolf, '09_DRusno')
    else
     Say(Louis, '09_DLusno');

DialogueOff;
End;


Every 0$01 trigger tick > [13$00, 12$00, 11$00][Difficulty] and us_contact = false do
begin
us_contact := true;
End;

// see sniper
Every 0$01 trigger SeeSniper(1) do
begin
Say(Louis, '09_DLsniper');
End;

Every 0$01 trigger game_counter >= 62 do
var i, un, veh, filter;
begin
uc_side := 5;
uc_nation := nation_arabian;
filter := [];

    for i = 1 to 2 do
        begin
        PrepareHuman(sex_male, i, 6);
        RandFacesAndNames();
        un := CreateHuman;
        filter := filter ^ un;

        vc_chassis := ar_hovercraft;
        vc_engine := engine_combustion;
        vc_control := control_manual;
        vc_weapon := ar_light_gun;
        veh := CreateVehicle;
        filter := filter ^ veh;

        SetDir(veh, 4);
        PlaceUnitArea(veh, ar_water, false);
        PlaceHumanInUnit(un, veh);
        SetFuel(veh, 44);
        end;

     repeat
      begin
      Wait(0$01);
      for i = 1 to filter do
          if not HasTask(filter[i]) then
             ComMoveXY(filter[i], GetX(Louis), GetY(Louis));
      end
     until GetDistUnits(filter[1], Louis) < 7;

     Say(ar_pep[Rand(2,4)], '09_dwarn');

     Wait(0$01);

     SetSide(filter, 2);

     CenterNowOnUnits(filter[1]);

     DialogueOn;
     
     Say(filter[1], '09_DArR');
     DWait(0$0.2);
     Say(Louis, '09_DL-hov1');
     Say(filter[1], '09_DI-hov1');
     Say(Louis, '09_DL-hov2');
     if IsLive(Rolf) then Say(Rolf, '09_DR-hov1')
                     else Say(ar_pep[3], '09_DR-hov1');
     Say(Louis, '09_DL-hov3');
     if IsLive(Mike) then
        Say(Mike, '09_DI-hov2');
     Say(Louis, '09_DL-hov4');
     if IsLive(Rolf) then Say(Rolf, '09_DR-hov2')
                     else Say(ar_pep[3], '09_DR-hov2');
     if IsLive(Gali) then
        Say(Gali, '09_DI-hov3');
      
     DialogueOff;

     ChangeMissionObjectives('hover');
     Hint('hovercraft');
End;


Export prisons;
Every 0$01 trigger GetDistUnits(Louis, us_prison) < 6 do
begin
prisons := FilterAllUnits([[f_side, 5], [f_type, unit_human]]);

if GetSex(prisons[1]) = sex_male then
   Say(prisons[1], '09_DPrm')
    else
     Say(prisons[1], '09_DPrw');

SetSide(FilterAllUnits([f_side, 5]), 2);
prison_event := true;
End;

Every 0$01 trigger FilterUnitsInArea(beach, [f_chassis, ar_hovercraft]) > 0 do
dday_event := true;


Every 0$01 trigger FilterAllUnits([f_side, 1]) = 0 do
begin
InGameOn;
DialogueOn;
Say(Louis, '09_DL-win');

if IsLive(Rolf) then
   Say(Rolf, '09_DR-win');
   
END_MISSION;
End;
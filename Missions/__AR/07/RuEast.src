// RuEastBase side = 7;
Export ru7_sold, ru7_eng, ru7_mech, ru7_sci, ru7_bazz, ru7_veh, ru7_lab, ru7_fac, ru7_blist, ru7_bweap;
Every 0$01 trigger FilterAllUnits([f_side, 7]) > 0 do
var i, filter, list, p, l, range, al_build, buildings, towers, is_hurt, un, vehicles, cranes;
begin
Enable;

     ru7_sold := FilterAllUnits([[f_side, 7], [f_class, 1]]);
     ru7_eng  := FilterAllUnits([[f_side, 7], [f_class, 2]]);
     ru7_mech := FilterAllUnits([[f_side, 7], [f_class, 3]]);
     ru7_sci  := FilterAllUnits([[f_side, 7], [f_class, 4]]);
     ru7_bazz := FilterAllUnits([[f_side, 7], [f_class, 9]]);
     ru7_veh  := FilterAllUnits([[f_side, 7], [f_type, unit_vehicle], [f_not, [f_weapon, ru_crane]]]);

     ru7_lab := FilterAllUnits([[f_side, 7], [f_or, [f_btype, b_lab_full], [f_btype, b_lab_half]]]);
     ru7_fac := FilterAllUnits([[f_side, 7], [f_or, [f_btype, b_workshop], [f_btype, b_factory]]]);

     towers    := FilterAllUnits([[f_side, 7], [f_or, [f_btype, b_breastwork], [f_btype, b_bunker]]]);
     buildings := FilterAllUnits([[f_side, 7], [f_or, [f_btype, b_armoury], [f_btype, b_barracks]]]);
     al_build  := FilterAllUnits([[f_side, 7], [f_type, unit_building]]);
     vehicles  := FilterAllUnits([[f_side, 7], [f_type, unit_vehicle]]);

     // sta³e
     is_hurt := [500, 510, 520][Difficulty];

     
     if FilterAllUnits([[f_side, 7], [f_bweapon, 0], [f_or, [f_btype, b_bunker], [f_btype, b_turret]]]) > 0 and ru7_fac > 0 then
        begin
        filter := FilterAllUnits([[f_side, 7], [f_bweapon, 0], [f_or, [f_btype, b_bunker], [f_btype, b_turret]]]);

        for i = 1 to filter do
        ComPlaceWeapon(filter[i], ru7_bweap[1]);
        end;

     if UnitsInside(ru7_fac[1]) = 0 and BuildingStatus(ru7_fac) = bs_working then
        ComCancel(ru7_fac[1]);



  // Zolnierze
     if ru7_sold > 0 then
        begin

             for i = 1 to ru7_sold do
                 begin

                 if GetLives(ru7_sold[i]) <= is_hurt then
                    begin

                    if IsInUnit(ru7_sold[i]) then
                       ComExitBuilding(ru7_sold[i]);

                    ComMoveXY(ru7_sold[i], GetX(ru_east_dep)-1, GetY(ru_east_dep)+2);
                    end;


                 if UnitFilter(towers, [[f_lives, 300], [f_empty]]) > 0 and UnitFilter(ru7_sold, [f_outside]) = 0 then
                    begin
                    filter := UnitsInside(buildings[1]);
                    ComExitBuilding(filter[1]);
                    end;


                 if GetLives(ru7_sold[i]) > is_hurt and not HasTask(ru7_sold[i]) and not IsInUnit(ru7_sold[i]) then // Jednostka moze walczyc
                    begin

                    // szukanie pustej wiezyczki
                       if UnitFilter(towers, [[f_lives, 300], [f_empty]]) > 0 then // znaleziono pust¹ wie¿e!
                          begin
                          ComEnterUnit(ru7_sold[i], UnitFilter(towers, [[f_lives, 300], [f_empty]])[1])
                          end
                           else
                    // szukanie pustych koszar
                            if UnitFilter(buildings, [f_lives, 251]) > 0 then
                               begin

                               for l = 1 to UnitFilter(buildings, [f_lives, 251]) do
                                   if UnitsInside(buildings[l]) < 6 then
                                      ComEnterUnit(ru7_sold[i], buildings[l]);
                               end
                                else  // nie znaleziono miejsca, udaj sie pod magazyn
                                 if GetDistUnits(ru7_sold[i], ru_east_dep) >= 12 then
                                    ComMoveXY(ru7_sold[i], GetX(ru_east_dep)-1, GetX(ru_east_dep)+1);


                    end;

                 end;
        end;


  // bazzoki
     if ru7_bazz > 0 then
        begin

        for i = 1 to ru7_bazz do
            begin

            if GetLives(ru7_bazz) <= is_hurt then
               begin
               if IsInUnit(ru7_bazz[i]) then
                  ComExitBuilding(ru7_bazz[i]);

               ComMoveXY(ru7_bazz[i], GetX(ru_east_dep)-1, GetY(ru_east_dep)+2);
               end;

            if GetLives(ru7_bazz[i]) > is_hurt then
               if not IsInUnit(ru7_bazz[i]) then
                  // szukanie pustych koszarów
                     if UnitFilter(buildings, [f_lives, 251]) > 0 then
                        begin

                        for l = 1 to UnitFilter(buildings, [f_lives, 251]) do
                            if UnitsInside(buildings[l]) < 6 then
                               ComEnterUnit(ru7_bazz[i], buildings[l]);
                        end
                         else  // nie znaleziono miejsca, udaj sie pod magazyn
                         if GetDistUnits(ru7_bazz[i], ru_east_dep) < 12 then
                            ComMoveXY(ru7_bazz[i], GetX(ru_east_dep)-1, GetX(ru_east_dep)+1);


            end;

        end;


  // sci 
     if ru7_sci > 0 then
        begin

        for i = 1 to ru7_sci do
            begin

            if not HasTask(ru7_sci[i]) and FilterAllUnits([[[f_side, 7], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]) = 0 then
               begin

               if ru7_lab > 0 then
                  ComEnterUnit(ru7_sci[i], ru7_lab[1])
                   else
                    if GetDistUnits(ru7_sci[i], ru_east_dep) > 10 then
                       ComMoveXY(ru7_sci[i], GetX(ru_east_dep)-2, GetY(ru_east_dep)-2);

               if ru7_mech = 0 and ru7_fac > 0 and ru7_sci > 2 then
                  begin

                  if IsInUnit(ru7_sci[1]) then
                     ComExitBuilding(ru7_sci[1]);

                  ComEnterUnit(ru7_sci[1], ru7_fac[1]);
                  AddComChangeProfession(ru7_sci[1], class_mechanic);
                  end;


               end;

            if not HasTask(ru7_sci[i]) and FilterAllUnits([[[f_side, 7], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]) > 0 then
               begin

               if IsInUnit(ru7_sci[i]) then
                  ComExitBuilding(ru7_sci[i]);

               filter := FilterAllUnits([[[f_side, 7], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]);

               if filter > 0 then
                  ComHeal(ru7_sci[i], NearestUnitToUnit(filter, ru7_sci[i]));

               end;

            end;

        end;


  // mechy
     if ru7_mech > 0 then
        begin

        for i = 1 to ru7_mech do
            begin

            if not HasTask(ru7_mech) then
               begin

               if ru7_bweap > 0 and ru7_fac > 0 then
                  begin

                  if GetDistUnits(ru7_mech[i], ru_east_dep) > 11 then
                     ComMoveXY(ru7_mech[i], GetX(ru_east_dep)-2, GetY(ru_east_dep)-2)
                      else
                       if IsDriver(ru7_mech[i]) then
                          ComExitVehicle(ru7_mech[i]);

                  ComEnterUnit(ru7_mech[i], NearestUnitToUnit(ru7_fac, ru7_mech[i]));
                  end;


               if UnitFilter(vehicles, [f_empty]) > 0 and ru7_bweap = 0 then
                  begin

                  if not IsDriver(ru7_mech[i]) then
                     begin

                     if UnitFilter(vehicles, [f_not, [f_lives, is_hurt]]) > 0 then
                        ComRepairVehicle(ru7_mech[i], NearestUnitToUnit(ru7_mech[i], UnitFilter(vehicles, [f_not, [f_lives, is_hurt]])));

                     if IsInUnit(ru7_mech[i]) then
                        ComExitBuilding(ru7_mech[i]);

                     if UnitFilter(vehicles, [f_not, [f_lives, 1000]]) = 0 then
                        ComEnterUnit(ru7_mech[i], NearestUnitToUnit(UnitFilter(vehicles, [f_empty]), ru7_mech[i]))

                     end;

                  end
                   else
                    if ru7_fac > 0 then
                       ComEnterUnit(ru7_mech[i], ru7_fac[1])
                        else
                         begin
                         ComEnterUnit(ru7_mech[i], NearestUnitToUnit(buildings, ru7_mech[i]));
                         AddComChangeProfession(ru7_mech[i], class_soldier);
                         end;


               end;


            end;

        end;


     if ru7_eng > 0 then
        begin

        for i = 1 to ru7_eng do
            begin

            if UnitFilter(al_build, [f_not, [f_lives, 1000]]) > 0 then
               begin

               if IsInUnit(ru7_eng[i]) then
                  ComExitBuilding(ru7_eng[i]);

               if not HasTask(ru7_eng[i]) then
                  ComRepairBuilding(ru7_eng[i], NearestUnitToUnit(UnitFilter(al_build, [f_not, [f_lives, 1000]]), ru7_eng[i]));

               if FilterAllUnits([[[f_side, 7], [f_weapon, ru_crane], [f_not, [f_empty]]]]) > 0 then
                  begin
                  filter := FilterAllUnits([[f_side, 7], [f_weapon, ru_crane]]);


                  for p = 1 to filter do
                      if GetLives(filter[p]) >= is_hurt+100 then
                         ComRepairBuilding(filter[p], NearestUnitToUnit(UnitFilter(al_build, [f_not, [f_lives, 1000]]), filter[p]))
                          else
                           begin
                           ComMoveXY(filter[p], GetX(ru_east_dep)-2, GetY(ru_east_dep)-2);

                           un := IsDrivenBy(filter[p]);

                           if GetDistUnits(filter[p], ru_east_dep) <= 8 and GetLives(filter[p]) < is_hurt then
                              begin
                              SetFuel(filter[p], 100);
                              ComExitVehicle(un);
                              end;

                           end;
                  end;

               end
                else
                 begin

                 if not IsInUnit(ru7_eng[i]) and not HasTask(ru7_eng[i]) and ru7_blist = 0 then
                    ComEnterUnit(ru7_eng[i], ru_east_dep);

                 if ru7_blist > 0 and GetResourceType(GetBase(ru_east_dep), mat_cans >= 100) and FilterAllUnits([[f_enemy, 7], [f_distxy, GetX(ru_east_dep), GetY(ru_east_dep), 70]]) = 0 then
                    begin
                    ComBuild(ru7_eng[i], ru7_blist[1], ru7_blist[2], ru7_blist[3], ru7_blist[4]);
                    end;

                 end;

            end;

        end;


     if vehicles > 0 then
        begin
        filter := UnitFilter(vehicles, [[f_not, [f_empty]], [f_not, [f_weapon, ru_crane]]]);

        if filter > 0 then
           begin

           for i = 1 to filter do
               begin

               if GetDistUnits(filter[i], ru_east_dep) <= 8 and GetFuel(filter[i]) <= 67 then
                  SetFuel(filter[i], 100);

               if GetLives(filter[i]) > is_hurt then
                  ProtectArea(filter[i], ru_east_dep)
                   else
                    begin
                    ComMoveXY(filter[i], GetX(ru_east_dep)-2, GetY(ru_east_dep)-2);

                    un := IsDrivenBy(filter[i]);

                    if GetDistUnits(filter[i], ru_east_dep) <= 8 and GetLives(filter[i]) < is_hurt then
                       begin
                       ComExitVehicle(un);
                       //AddComRepairVehicle(un, filter[i]);
                       end;

                    end;
               end;

           end;

        end;


End;
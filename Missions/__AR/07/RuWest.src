// RuWestBase side = 6;
Export ru6_sold, ru6_eng, ru6_mech, ru6_sci, ru6_bazz, ru6_veh, ru6_lab, ru6_fac, ru6_blist, ru6_bweap;
Every 0$01 trigger FilterAllUnits([f_side, 6]) > 0 do
var i, filter, list, p, l, range, al_build, buildings, towers, is_hurt, un, vehicles, cranes;
begin
Enable;

     ru6_sold := FilterAllUnits([[f_side, 6], [f_class, 1]]);
     ru6_eng  := FilterAllUnits([[f_side, 6], [f_class, 2]]);
     ru6_mech := FilterAllUnits([[f_side, 6], [f_class, 3]]);
     ru6_sci  := FilterAllUnits([[f_side, 6], [f_class, 4]]);
     ru6_bazz := FilterAllUnits([[f_side, 6], [f_class, 9]]);
     ru6_veh  := FilterAllUnits([[f_side, 6], [f_type, unit_vehicle], [f_not, [f_weapon, ru_crane]]]);

     ru6_lab := FilterAllUnits([[f_side, 6], [f_or, [f_btype, b_lab_full], [f_btype, b_lab_half]]]);
     ru6_fac := FilterAllUnits([[f_side, 6], [f_or, [f_btype, b_workshop], [f_btype, b_factory]]]);

     towers    := FilterAllUnits([[f_side, 6], [f_or, [f_btype, b_breastwork], [f_btype, b_bunker]]]);
     buildings := FilterAllUnits([[f_side, 6], [f_or, [f_btype, b_armoury], [f_btype, b_barracks]]]);
     al_build  := FilterAllUnits([[f_side, 6], [f_type, unit_building]]);
     vehicles  := FilterAllUnits([[f_side, 6], [f_type, unit_vehicle]]);

     // sta³e
     is_hurt := [500, 510, 520][Difficulty];

     {Za³o¿enia:
      - ¯o³nierze wchodz¹ do wolnych budynków w kolejnosci: wieze, koszary; 
      - Jezeli brak tych budynkow wowczas bronia magazynu; 
      - Inzynierowie naprawiaja budynki i w razie potrzeby buduja nowe;
      - Mechanicy walcza i naprawiaja pojazdy; 
      - Naukowcy lecza rannych; 
      - Bazzoki strzelaja ;d 
      - Pojazdy walcza w danym zasiegu od bazy; 
      - Komputer otrzymymuje posilki;
      }

  
     if FilterAllUnits([[f_side, 6], [f_bweapon, 0], [f_or, [f_btype, b_bunker], [f_btype, b_turret]]]) > 0 and ru6_fac > 0 then
        begin
        filter := FilterAllUnits([[f_side, 6], [f_bweapon, 0], [f_or, [f_btype, b_bunker], [f_btype, b_turret]]]);

        for i = 1 to filter do
        ComPlaceWeapon(filter[i], ru6_bweap[1]);
        end;

     if UnitsInside(ru6_fac[1]) = 0 and BuildingStatus(ru6_fac) = bs_working then
        ComCancel(ru6_fac[1]);



  // Zolnierze
     if ru6_sold > 0 then
        begin

             for i = 1 to ru6_sold do
                 begin

                 if GetLives(ru6_sold[i]) <= is_hurt then
                    begin

                    if IsInUnit(ru6_sold[i]) then
                       ComExitBuilding(ru6_sold[i]);

                    ComMoveXY(ru6_sold[i], GetX(ru_west_dep)-1, GetY(ru_west_dep)+2);
                    end;


                 if UnitFilter(towers, [[f_lives, 300], [f_empty]]) > 0 and UnitFilter(ru6_sold, [f_outside]) = 0 then
                    begin
                    filter := UnitsInside(buildings[1]);
                    ComExitBuilding(filter[1]);
                    end;


                 if GetLives(ru6_sold[i]) > is_hurt and not HasTask(ru6_sold[i]) and not IsInUnit(ru6_sold[i]) then // Jednostka moze walczyc
                    begin

                    // szukanie pustej wiezyczki
                       if UnitFilter(towers, [[f_lives, 300], [f_empty]]) > 0 then // znaleziono pust¹ wie¿e!
                          begin
                          ComEnterUnit(ru6_sold[i], UnitFilter(towers, [[f_lives, 300], [f_empty]])[1])
                          end
                           else
                    // szukanie pustych koszar
                            if UnitFilter(buildings, [f_lives, 251]) > 0 then
                               begin

                               for l = 1 to UnitFilter(buildings, [f_lives, 251]) do
                                   if UnitsInside(buildings[l]) < 6 then
                                      ComEnterUnit(ru6_sold[i], buildings[l]);
                               end
                                else  // nie znaleziono miejsca, udaj sie pod magazyn
                                 if GetDistUnits(ru6_sold[i], ru_west_dep) >= 12 then
                                    ComMoveXY(ru6_sold[i], GetX(ru_west_dep)-1, GetX(ru_west_dep)+1);
                               

                    end;

                 end;
        end;


  // bazzoki
     if ru6_bazz > 0 then
        begin

        for i = 1 to ru6_bazz do
            begin

            if GetLives(ru6_bazz) <= is_hurt then
               begin
               if IsInUnit(ru6_bazz[i]) then
                  ComExitBuilding(ru6_bazz[i]);

               ComMoveXY(ru6_bazz[i], GetX(ru_west_dep)-1, GetY(ru_west_dep)+2);
               end;

            if GetLives(ru6_bazz[i]) > is_hurt then
               if not IsInUnit(ru6_bazz[i]) then
                  // szukanie pustych koszarów
                     if UnitFilter(buildings, [f_lives, 251]) > 0 then
                        begin

                        for l = 1 to UnitFilter(buildings, [f_lives, 251]) do
                            if UnitsInside(buildings[l]) < 6 then
                               ComEnterUnit(ru6_bazz[i], buildings[l]);
                        end
                         else  // nie znaleziono miejsca, udaj sie pod magazyn
                         if GetDistUnits(ru6_bazz[i], ru_west_dep) < 12 then
                            ComMoveXY(ru6_bazz[i], GetX(ru_west_dep)-1, GetX(ru_west_dep)+1);


            end;

        end;


  // sci {}
     if ru6_sci > 0 then
        begin

        for i = 1 to ru6_sci do
            begin

            if not HasTask(ru6_sci[i]) and FilterAllUnits([[[f_side, 6], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]) = 0 then
               begin

               if ru6_lab > 0 then
                  ComEnterUnit(ru6_sci[i], ru6_lab[1])
                   else
                    if GetDistUnits(ru6_sci[i], ru_west_dep) > 10 then
                       ComMoveXY(ru6_sci[i], 96, 104);

               if ru6_mech = 0 and ru6_fac > 0 and ru6_sci > 2 then
                  begin

                  if IsInUnit(ru6_sci[1]) then
                     ComExitBuilding(ru6_sci[1]);

                  ComEnterUnit(ru6_sci[1], ru6_fac[1]);
                  AddComChangeProfession(ru6_sci[1], class_mechanic);
                  end;


               end;

            if not HasTask(ru6_sci[i]) and FilterAllUnits([[[f_side, 6], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]) > 0 then
               begin

               if IsInUnit(ru6_sci[i]) then
                  ComExitBuilding(ru6_sci[i]);

               filter := FilterAllUnits([[[f_side, 6], [f_type, unit_human], [f_not, [f_lives, is_hurt]], [f_not, [f_class, 4]]]]);

               if filter > 0 {and filter < ru6_sci} then
                  ComHeal(ru6_sci[i], NearestUnitToUnit(filter, ru6_sci[i]));
                  {else
                    ComHeal(ru6_sci[i], filter[i]);}
               end;

            end;

        end;


  // mechy
     if ru6_mech > 0 then
        begin

        for i = 1 to ru6_mech do
            begin

            if not HasTask(ru6_mech) then
               begin

               if ru6_bweap > 0 and ru6_fac > 0 then
                  begin

                  if GetDistUnits(ru6_mech[i], ru_west_dep) > 11 then
                     ComMoveXY(ru6_mech[i], 96, 104)
                      else
                       if IsDriver(ru6_mech[i]) then
                          ComExitVehicle(ru6_mech[i]);

                  ComEnterUnit(ru6_mech[i], NearestUnitToUnit(ru6_fac, ru6_mech[i]));
                  end;


               if UnitFilter(vehicles, [f_empty]) > 0 and ru6_bweap = 0 then
                  begin

                  if not IsDriver(ru6_mech[i]) then
                     begin

                     if UnitFilter(vehicles, [f_not, [f_lives, is_hurt]]) > 0 then
                        ComRepairVehicle(ru6_mech[i], NearestUnitToUnit(ru6_mech[i], UnitFilter(vehicles, [f_not, [f_lives, is_hurt]])));

                     if IsInUnit(ru6_mech[i]) then
                        ComExitBuilding(ru6_mech[i]);

                     if UnitFilter(vehicles, [f_not, [f_lives, 1000]]) = 0 then
                        ComEnterUnit(ru6_mech[i], NearestUnitToUnit(UnitFilter(vehicles, [f_empty]), ru6_mech[i]))

                     end;

                  end
                   else
                    if ru6_fac > 0 then
                       ComEnterUnit(ru6_mech[i], ru6_fac[1])
                        else
                         begin
                         ComEnterUnit(ru6_mech[i], NearestUnitToUnit(buildings, ru6_mech[i]));
                         AddComChangeProfession(ru6_mech[i], class_soldier);
                         end;


               end;


            end;

        end;


     if ru6_eng > 0 then
        begin

        for i = 1 to ru6_eng do
            begin

            if UnitFilter(al_build, [f_not, [f_lives, 1000]]) > 0 then
               begin

               if IsInUnit(ru6_eng[i]) then
                  ComExitBuilding(ru6_eng[i]);

               if not HasTask(ru6_eng[i]) then
                  ComRepairBuilding(ru6_eng[i], NearestUnitToUnit(UnitFilter(al_build, [f_not, [f_lives, 1000]]), ru6_eng[i]));

               if FilterAllUnits([[[f_side, 6], [f_weapon, ru_crane], [f_not, [f_empty]]]]) > 0 then
                  begin
                  filter := FilterAllUnits([[f_side, 6], [f_weapon, ru_crane]]);

                  
                  for p = 1 to filter do
                      if GetLives(filter[p]) >= is_hurt+100 then
                         ComRepairBuilding(filter[p], NearestUnitToUnit(UnitFilter(al_build, [f_not, [f_lives, 1000]]), filter[p]))
                          else
                           begin
                           ComMoveXY(filter[p], 94, 103);

                           un := IsDrivenBy(filter[p]);

                           if GetDistUnits(filter[p], ru_west_dep) <= 8 and GetLives(filter[p]) < is_hurt then
                              begin
                              SetFuel(filter[p], 100);
                              ComExitVehicle(un);
                              end;

                           end; 
                  end;

               end
                else
                 begin

                 if not IsInUnit(ru6_eng[i]) and not HasTask(ru6_eng[i]) and ru6_blist = 0 then
                    ComEnterUnit(ru6_eng[i], ru_west_dep);

                 if ru6_blist > 0 and GetResourceType(GetBase(ru_west_dep), mat_cans >= 100) and FilterAllUnits([[f_enemy, 6], [f_distxy, GetX(ru_west_dep), GetY(ru_west_dep), 40]]) = 0 then
                    begin
                    ComBuild(ru6_eng[i], ru6_blist[1], ru6_blist[2], ru6_blist[3], ru6_blist[4]);
                    end;

                 end;

            end;

        end;


     if vehicles > 0 then
        begin
        filter := UnitFilter(vehicles, [[f_not, [f_empty]], [f_not, [f_weapon, ru_crane]]]);

        if filter > 0 then
           begin

           for i = 1 to filter do
               begin

               if GetDistUnits(filter[i], ru_west_dep) <= 8 and GetFuel(filter[i]) <= 67 then
                  SetFuel(filter[i], 100);

               if GetLives(filter[i]) > is_hurt then
                  ProtectArea(filter[i], ru_west_dep)
                   else
                    begin
                    ComMoveXY(filter[i], 94, 103);
                                       
                    un := IsDrivenBy(filter[i]);

                    if GetDistUnits(filter[i], ru_west_dep) <= 8 and GetLives(filter[i]) < is_hurt then
                       begin
                       ComExitVehicle(un);
                       //AddComRepairVehicle(un, filter[i]);
                       end;

                    end;
               end;

           end;

        end;


End;
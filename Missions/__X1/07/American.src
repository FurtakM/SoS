Export usCommander, usForces, usCargo, usToBuild, usAdditionalForces, usCratesCollector;
Export Function PrepareAmerican;
var i, b, veh;
begin
uc_side := 1;
uc_nation := 1;

SetResourceType(GetBase(usDepot), mat_cans, [1500, 2200, 3500, 3900][Difficulty]);
SetResourceType(GetBase(usDepot), mat_oil, 800);

usForces := [];
usToBuild := [];
usAdditionalForces := [];
usCratesCollector := [];

for i := 1 to 3 do
    begin
    PrepareApemanEngineer();
    usCratesCollector := Join(usCratesCollector, CreateHuman);
    PlaceUnitXYR(Last(usCratesCollector), 81, 142, 4, false);
    end;

InitHc;

for i in FilterAllUnits([f_type, unit_building]) do
    SetBLevel(i, [6, 7, 8, 8][Difficulty]);

for i in FilterAllUnits([f_btype, b_bunker]) do
    begin
    if GetBWeapon(i) = us_radar then
       begin
       uc_nation := 0;
       PrepareHuman(false, class_apeman_engineer, 2);
       end
    else
       PrepareHuman(false, 1, [5, 6, 7, 8][Difficulty]);

    PlaceHumanInUnit(CreateHuman, i);
    end;

b := HexInfo(74, 120);

uc_nation := 1;

for i := 1 to [1, 2, 3, 4][Difficulty] do
    begin
    PrepareHuman(false, 1, [5, 6, 7, 8][Difficulty]);
    PlaceHumanInUnit(CreateHuman, b);
    end;

SetClass(UnitsInside(b), class_sniper);

for i := 1 to 6 do
    begin
    PrepareHuman(false, 4, 6);
    PlaceHumanInUnit(CreateHuman, [amLab1, amLab2][i mod 2 + 1]);
    end;

for i := 1 to 3 do
    begin
    PrepareHuman(false, 3, 6);
    PlaceHumanInUnit(CreateHuman, amFac);
    end;

for i := 1 to 3 do
    begin
    PrepareHuman(false, 3, [6, 7, 7, 7][Difficulty]);
    PlaceHumanInUnit(CreateHuman, HexInfo(78, 130));
    end;

for i := 1 to [1, 2, 3, 3][Difficulty] do
    begin
    PrepareVehicle(us_medium_wheeled, engine_solar, control_manual, us_crane, 100);
    veh := CreateVehicle;
    SetDir(veh, 3);
    PlaceUnitArea(veh, usCraneArea, false);
    PrepareHuman(false, 3, 6);
    PlaceHumanInUnit(CreateHuman, veh);
    end;   

InitHc;

hc_gallery := 'sandar';
hc_face_number := 38;
hc_name := 'Doug Callahan';
hc_class := class_engineer;
hc_sex := sex_male;
hc_attr := [10, 11];
hc_basic_skills := [2, 1, 0, 0];
hc_skills := [4, 6, 3, 1];

usCommander := CreateHuman;
PlaceHumanInUnit(usCommander, usDepot);

DoNotAttack(2, usCommander);

InitHC_All();

PrepareVehicle(us_medium_wheeled, engine_combustion, control_remote, us_cargo_bay, 100);
usCargo := CreateVehicle;
PlaceUnitXY(usCargo, 81, 143, false);
Connect(usCargo);

DoNotAttack(5, usCargo);
End;

Every 6$00 trigger usAttacksAllowed and IsOk(amFac) and BuildingStatus(amFac) = bs_idle do
var i, chassis, weapon;
begin
enable;

for i := 1 to [4, 5, 6, 6][Difficulty] do
    begin
    chassis := [us_medium_wheeled, us_medium_tracked, us_heavy_tracked][rand(1, 3)];

    if chassis = us_heavy_tracked then
       weapon := us_heavy_gun
    else
       weapon := [us_double_gun, us_rocket_launcher, us_gatling_gun][rand(1, 3)];

    AddComConstruct(amFac, chassis, engine_combustion, control_remote, weapon);
    end;
End;

Every 0$01 do
var i, un, tmp;
begin
if Difficulty < 2 then
   exit;

tmp := [];

for i := 1 to Difficulty do
    begin
    uc_side := 4;
    uc_nation := nation_american;

    PrepareSoldier(false, [3, 4, 5, 6][Difficulty]);
    un := CreateHuman;
    tmp := Join(tmp, un);
    PlaceUnitArea(un, westSpawnArea, false);
    end;

for i in tmp do
    begin
    ComAgressiveMove(i, 54, 51);
    AddComWait(i, 0$20);
    AddComAgressiveMove(i, 35, 6);
    end;

repeat
 wait(7$00);
 tmp := UnitFilter(tmp, [f_ok]);

 if not tmp then
    exit;

 for i in tmp do
     begin
     ComAgressiveMove(i, 54, 51);
     AddComWait(i, 0$20);
     AddComAgressiveMove(i, 35, 6);
     end;
until not tmp;
End;

Every 0$10 trigger usForces >= [4, 5, 6, 6][Difficulty] do
var i, tmp;
begin
enable;

tmp := usForces;
usForces := [];

if not tmp then
   exit;

if FilterAllUnits([[f_side, 2], [f_distxy, 123, 141, 20]]) then
   begin
   ComAgressiveMove(tmp, 126, 143);
   wait(0$30);
   end;

repeat
 wait(0$1);
 tmp := AgressiveMove(tmp, [ [51,50], [98,50], [114,50], [127,51], [131,35] ]);
until not tmp;
End;

// player cheat while scout
Every 0$1 trigger scoutActive and not scoutSuccess and FilterUnitsInArea(southScoutArea, [f_side, 2]) do
var i, un, tmp;
begin
uc_side := 1;
uc_nation := 1;

tmp := [];

for i := 1 to 2 do
    begin
    PrepareSoldier(false, 7);
    un := CreateHuman;
    PlaceUnitXYR(un, 130, 153, 3, false);
    SetClass(un, class_sniper);

    AddComAgressiveMove(un, 108, 108);
    end;
End;

Every 0$1 trigger scoutActive and not scoutSuccess and FilterUnitsInArea(northScoutArea, [f_side, 2]) do
var i, un, tmp;
begin
uc_side := 1;
uc_nation := 1;

tmp := [];

for i := 1 to 2 do
    begin
    PrepareSoldier(false, 7);
    un := CreateHuman;
    PlaceUnitXYR(un, 56, 101, 3, false);
    SetClass(un, class_sniper);

    AddComAgressiveMove(un, 72, 92);
    end;
End;

Every 0$2 trigger not usSurrender do
var i, tmp, cr, enemyInArea;
begin
enable;

enemyInArea := FilterAllUnits([[f_enemy, 1], [f_distxy, 113, 139, 15]]);
cr := GetListOfCratesInArea(americanCratesArea);
tmp := UnitFilter(usCratesCollector, [f_ok]);

if not tmp then
   begin
   disable;
   exit;
   end;

for i in tmp do
    begin
    if enemyInArea and GetDistUnits(i, usDepot) > 12 then
       ComMoveXY(i, 83, 147)
    else if not HasTask(i) and cr then
       ComCollect(i, cr[1], cr[2]);
    end;
End;

Every 0$2 trigger IsOk(usCargo) and not usSurrender do
var cr, enemyInArea;
begin
enable;

enemyInArea := FilterUnitsInArea(americanBaseArea, [[f_enemy, 1], [f_ok]]);
cr := GetListOfCratesInArea(usCratesArea);

if GetFuel(usCargo) < 10 and not See(2, usCargo) then
   SetFuel(usCargo, 100);

if not HasTask(usCargo) and GetCargo(usCargo, mat_cans) < 100 and not enemyInArea then
   begin
   if cr then
      ComCollect(usCargo, cr[1], cr[2])
   else if not IsInArea(usCargo, usCratesArea) then
      ComMoveToArea(usCargo, usCratesArea);

   exit;
   end;

if enemyInArea and not IsInArea(usCargo, americanBaseArea) then
   ComMoveXY(usCargo, 80, 143)
else if GetCargo(usCargo, mat_cans) then
   ComGive(usCargo, usDepot);
End;

Every 0$3 trigger not usSurrender and FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_not, [f_ok]]]) and FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_class, 4]]) do
var i, sci;
begin
enable;

for i in FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_not, [f_ok]]]) do
    begin
    sci := FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_class, 4]]);

    if not sci then
       break;

    sci := sci[1];

    if IsInUnit(sci) then
       ComExit(sci)
    else
       begin
       ComHeal(sci, i);
       AddComEnterUnit(sci, [amLab1, amLab2][rand(1, 2)]);
       end;
    end;
End;

Every 0$2 trigger not usSurrender and FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_ok], [f_weapon, us_crane]]) and FilterAllUnits([[f_side, 1], [f_not, [f_lives, 1000]], [f_type, unit_building], [f_not, [f_btype, b_oil_mine]]]) do
var i, tmp, tmp2;
begin
enable;

tmp := FilterAllUnits([[f_side, 1], [f_not, [f_lives, 1000]], [f_type, unit_building], [f_not, [f_btype, b_oil_mine]]]);
tmp2 := FilterAllUnits([[f_side, 1], [f_not, [f_lives, 600]], [f_type, unit_building], [f_not, [f_btype, b_oil_mine]]]);

for i in FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_ok], [f_weapon, us_crane]]) do
    begin
    if tmp2 then
       ComRepairBuilding(i, NearestUnitToUnit(tmp2, i))
    else
       ComRepairBuilding(i, NearestUnitToUnit(tmp, i));
    end;
End;

Every 0$1 trigger not usSurrender and not FilterUnitsInArea(americanBaseArea, [[f_enemy, 1], [f_ok]]) and usToBuild and FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_ok], [f_weapon, us_crane]]) do
begin                                                                                                           
enable;
ComCraneBuild(FilterAllUnits([[f_side, 1], [f_not, [f_hastask]], [f_ok], [f_weapon, us_crane]]), usToBuild[1][1], usToBuild[1][2], usToBuild[1][3], usToBuild[1][4]);
End;

Every 0$10 trigger not usSurrender and FilterAllUnits([[f_side, 1], [f_ok], [f_or, [f_class, class_soldier], [f_class, class_sniper], [f_class, class_apeman_engineer]]]) do
var i, tmp, btmp, barracks, inside;
begin
enable;

tmp := FilterAllUnits([[f_side, 1], [f_ok], [f_or, [f_class, class_soldier], [f_class, class_sniper], [f_class, class_apeman_engineer]]]) diff usCratesCollector;
btmp := FilterAllUnits([[f_side, 1], [f_ok], [f_type, unit_building], [f_btype, b_bunker], [f_empty]]);
barracks := FilterAllUnits([[f_side, 1], [f_ok], [f_btype, b_barracks]]);

if not tmp then
   exit;

for i in tmp do
    begin
    inside := IsInUnit(i);

    if inside and GetBType(inside) = b_bunker then
       continue;

    if GetTag(i) = 1 and not HasTask(i) then
       SetTag(i, 0);

    btmp := FilterByTag(btmp, 0);

    if not btmp then
       begin          
       if not inside and GetClass(i) <> class_apeman_engineer and barracks and not GetTag(i) then
          ComEnterUnit(i, barracks[1]);
       end
    else
       begin
       SetTag(btmp[1], 1);

       ComEnterUnit(i, btmp[1]);
       SetTag(i, 1);
       end;
    end;
End;

Every 0$1 trigger FilterAllUnits([[f_side, 1], [f_control, control_remote], [f_ok], [f_not, [f_linked]]]) do
var i, tmp;
begin
enable;

tmp := FilterAllUnits([[f_side, 1], [f_control, control_remote], [f_ok], [f_not, [f_linked]]]);

for i in tmp do
    Connect(i);
End;

Every 1$00 trigger usAttacksAllowed and Difficulty > 1 do
var i, un, veh, chassis, weapon;
begin
wait([15$00, 15$00, 14$00, 13$00][Difficulty]);

if usSurrender then
   exit;

for i := 1 to [4, 5, 5, 6][Difficulty] do
    begin
    uc_side := 1;
    uc_nation := 1;

    InitHc_All();

    PrepareHuman(false, 3, [5, 6, 7, 8][Difficulty]);
    un := CreateHuman;

    chassis := [us_medium_wheeled, us_medium_tracked, us_heavy_tracked][i mod 3 + 1];

    if chassis = us_heavy_tracked then
       weapon := [us_heavy_gun, us_gatling_gun, us_double_gun][rand(1,3)]
    else
       weapon := [us_double_gun, us_double_gun, us_rocket_launcher, us_gatling_gun][rand(1,4)];

    PrepareVehicle(chassis, engine_combustion, control_manual, weapon, 30);
    veh := CreateVehicle;

    usAdditionalForces := Replace(usAdditionalForces, usAdditionalForces + 1, veh);

    SetDir(veh, 4);

    repeat
     wait(0$1);
    until PlaceUnitArea(veh, americanEastArea, false);

    PlaceHumanInUnit(un, veh);

    ComMoveXY(veh, 153, 45);

    wait(0$2);
    end;

repeat
 wait(0$1);
 usAdditionalForces := AgressiveMove(usAdditionalForces, [ [150,36], [138,25] ]);
until not usAdditionalForces;
End;

Every 0$2 trigger FilterAllUnits([[f_side, 1], [f_see, 2]]) do
begin
wait(0$30);
usAttacksAllowed := true;
End;
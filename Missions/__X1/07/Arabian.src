Export Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Shariff, Olaf,
       Kurt, Aviradze,
       heikeSecondSquad, heikeApes, heikeVehicles;
Export Function PrepareArabian;
var i, tmp, counter;
begin
uc_nation := 2;
uc_side := 5;
Shariff := PrepareUnit('Abdul', false, '');

uc_side := 2;

Heike := PrepareUnit('Heike', (not debug), prefix);
Ibrahim := PrepareUnit('Ibrahim', (not debug), prefix);
Givi := PrepareUnit('Givi', (not debug), prefix);
Kamil := PrepareUnit('Kamil', (not debug), prefix);
Kaia := PrepareUnit('Kaia', (not debug), prefix);
Sophia := PrepareUnit('Sophia', (not debug), prefix);
Markov := PrepareUnit('Markov', (not debug), prefix);
Aviradze := PrepareUnit('Aviradze', (not debug), prefix);
Kurt := PrepareUnit('Kurt', (not debug), prefix);
Mike := PrepareUnit('Mike', (not debug), prefix);
Olaf := PrepareUnit('Olaf', false, '');

heikeSecondSquad := CreateCharacterSet('06_others');
heikeApes := CreateCharacterSet('06_apes');

if debug then
   begin
   tmp := [
            [ar_half_tracked, engine_combustion, control_remote, ar_gatling_gun, 2, 1000, 0],
            [ar_half_tracked, engine_combustion, control_manual, ar_control_tower, 2, 1000, 0],
            [ar_half_tracked, engine_combustion, control_manual, ar_gun, 2, 750, 0],
            [ar_half_tracked, engine_combustion, control_manual, ar_gun, 2, 900, 0],
            [ar_half_tracked, engine_combustion, control_manual, ar_cargo_bay, 2, 1000, [100, 0]],
            [ar_half_tracked, engine_combustion, control_manual, ar_cargo_bay, 2, 1000, [50, 50]],
            [ar_half_tracked, engine_combustion, control_remote, ar_gun, 2, 1000, 0],
            [ar_half_tracked, engine_combustion, control_manual, ar_mortar, 2, 1000, 0]
          ];
   end
else
   tmp := [];

heikeVehicles := LoadVariable('06_Vehicles', tmp);

counter := 0;

if heikeVehicles then
   begin
   for i := heikeVehicles downto 1 do
       begin
       if heikeVehicles[i][3] = control_remote then
          begin
          if counter < remoteControlLimit then
             begin
             counter := Inc(counter);
             heikeVehicles := Insert(heikeVehicles, heikeVehicles, heikeVehicles[i]);
             end;

          heikeVehicles := Delete(heikeVehicles, i);
          end;
       end;
   end;
End;

Every 0$1 trigger scoutActive do
var i, tmp;
begin
tmp := [
         [ [96,50], [49,52], [34,55], [41,70], [53,96], [103,64] ],
         [ [104,70], [111,103], [131,147], [130,153], [110,93], [103,65] ]
       ];


for i in tmp[1] do
    begin
    AddComAgressiveMove(Mike, i[1], i[2]);
    AddComHold(Mike);
    AddComWait(Mike, 0$5);
    end;

for i in tmp[2] do
    begin
    AddComAgressiveMove(Kurt, i[1], i[2]);
    AddComHold(Kurt);
    AddComWait(Kurt, 0$5);
    end;

wait(2$00);

repeat
 wait(0$1);
until (not HasTask(Mike) or IsDead(Mike))
and (not HasTask(Kurt) or IsDead(Kurt));

if IsDead(Kurt) and IsDead(Mike) then
   begin
   scoutFailed := true;
   exit;
   end;

ComMoveXY([Kurt, Mike], 132, 52);
AddComMoveUnit([Kurt, Mike], FilterAllUnits([f_side, 2])[1]);

repeat
 wait(0$1);
until See(2, Kurt) or See(2, Mike);

if IsLive(Kurt) then
   SetSide(Kurt, 2);

if IsLive(Mike) then
   SetSide(Mike, 2);

scoutSuccess := true;
End;
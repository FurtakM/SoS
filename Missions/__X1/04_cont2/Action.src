Export Function Action;
var i, j, un, veh, tmp, rutmp;
begin
Video(true);

ChangeSideFog(5, 2);

music_nat := 4;
music_class := music_combat;

PlaceSeeing(133, 2, 2, 10);
CenterNowOnXY(133, 2);

wait(0$1);

// russian troops
uc_side := 6;
uc_nation := 3;

rutmp := [];

PrepareTank(6, 3, ru_medium_wheeled, engine_combustion, control_manual, ru_gun, 66);
veh := CreateVehicle;
SetDir(veh, 3);

PlaceUnitArea(veh, spawnArea, false);
SetLives(veh, 500);

PrepareHuman(false, 3, 3);
PlaceHumanInUnit(CreateHuman, veh);

ComMoveXY(veh, 133, 11);

rutmp := [veh];

for i := 1 to 4 do
    begin
    PrepareHuman(false, i, 2);
    un := CreateHuman;
    SetDir(un, 3);

    PlaceUnitArea(un, spawnArea, false);
    rutmp := Replace(rutmp, rutmp + 1, un);

    ComMoveXY(un, 133, 11);
    wait(0$0.5);
    end;

for i in rutmp do
    begin
    AddComMoveXY(i, 99, 65);
    AddComMoveXY(i, 76, 108);
    AddComMoveXY(i, 92, 176);
    end;

wait(0$10);

tmp := [Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Kurt, Aviradze] union heikeSecondSquad;
tmp := tmp diff 0;

// [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), cargo]
// cargo := [GetCargo(i, mat_cans), GetCargo(i, mat_oil)];
for i in heikeVehicles do
    begin

    if not tmp then
       continue;

    PrepareTank(2, i[5], i[1], i[2], i[3], i[4], 34);
    veh := CreateVehicle;

    if i[4] in [ru_cargo_bay, ar_cargo_bay, us_cargo_bay, ru_big_cargo_bay] then
       begin
       if i[7][1] then
          AddCargo(veh, mat_cans, i[7][1]);
       if i[7][2] then
          AddCargo(veh, mat_oil, i[7][2]);
       end;

    SetDir(veh, 3);
    PlaceUnitArea(veh, spawnArea, false);

    for j := 1 to tmp do
        begin
        if i[8] = GetClass(tmp[j]) then
           begin
           PlaceHumanInUnit(tmp[j], veh);
           tmp := Delete(tmp, j);
           break;
           end;
        end;

    if IsEmpty(veh) then
       begin
       PlaceHumanInUnit(tmp[1], veh);
       tmp := Delete(tmp, 1);
       end;

    if GetWeapon(veh) in [ru_cargo_bay, ar_cargo_bay, us_cargo_bay, ru_big_cargo_bay] then
       ComMoveXY(veh, 130, 8)
    else
       ComMoveXY(veh, 134, 15);

    wait(0$3);
    end;

if tmp then
   for i in tmp do
       begin
       PlaceUnitArea(i, spawnHumanArea, false);
       ComAgressiveMove(i, 146, 7);
       wait(0$0.5);
       end;

if heikeApes then
   for i in heikeApes do
       begin
       PlaceUnitArea(i, spawnHumanArea, false);
       ComAgressiveMove(i, 146, 7);
       wait(0$0.5);
       end;

DialogueOn;    
Say(Heike, 'DH-outpost-a-1'); 
DialogueOff;

RemoveSeeing(133, 2, 2);

activeInvincible := false;

Video(false);

music_nat := 0;
music_class := 0;

ChangeMissionObjectives('Outposts');

SaveForQuickRestart;
End;

// ibrahim betrayal
Every 0$2 trigger FilterAllUnits([[f_side, 2], [f_dist, Ibrahim, 20]]) and ibrahimOnRuSide and IsOk(Ibrahim) do
var x, y, d, lives;
begin
IbrahimBetrayal;

repeat
 wait(0$1);
until IsDead(Ibrahim) or (FilterAllUnits([[f_side, 2], [f_dist, Ibrahim, 6]]) and IsOk(Ibrahim));

if IsDead(Ibrahim) then
   exit;

x := GetX(Ibrahim);
y := GetY(Ibrahim);
d := GetDir(Ibrahim);
lives := GetLives(Ibrahim);

RemoveUnit(Ibrahim);

uc_side := 2;
uc_nation := 2;

Ibrahim := PrepareUnit('Ibrahim', false, '');
SetDir(Ibrahim, d);
PlaceUnitXY(Ibrahim, x, y, false);
SetLives(Ibrahim, lives);
End;

Export Function IbrahimBetrayal;
var i, tmp;
begin
PlaceSeeing(122, 77, 2, -6);

ComMoveXY(Ibrahim, 123, 79);
AddComTurnXY(Ibrahim, 125, 82);

wait(0$2);

ComTurnUnit(ruSol, Ibrahim);

Say(ruSol, 'DRum-outpost-b-3');

tmp := [
   HexInfo(125, 76),
   HexInfo(125, 79),
   HexInfo(125, 82)
];

for i in tmp do
    SetLives(i, 200);

Say(Ibrahim, 'DI-outpost-b-4');

SetSide(Ibrahim, 5);
ComFree([Ibrahim, ruSol]);

RemoveSeeing(122, 77, 2);

if Givi then
   Say(Givi, 'DG-1-outpost-b-5')
else
   if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-outpost-b-a-5') then
      Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-outpost-b-a-5');

repeat
 wait(0$1);
until IsDead(Ibrahim) or (GetDistUnits(Heike, Ibrahim) < 6 and IsOk(Ibrahim));

if IsDead(Ibrahim) then
   begin
   Say(Heike, 'DH-ibrahimdead-1');
   exit;
   end;

CenterNowOnUnits(Ibrahim);

DialogueOn;

Say(Ibrahim, 'DI-ibrahimlives-1');
Say(Heike, 'DH-Ibrahimlives-2');

DialogueOff;
End;

// second depot
Every 0$1 trigger FilterUnitsInArea(outerArea, [f_side, 2]) or tick >= [9$00, 8$00, 8$00, 7$30][Difficulty] do
begin
outerAreaAchived := true;

wait(0$20);

DialogueOn;

SayRadio(Shariff, 'DShar-radio-1');
SayRadio(Shariff, 'DShar-radio-2');
Say(Heike, 'DH-radio-2');

DialogueOff;

ChangeMissionObjectives('Gaidar');

if Difficulty < 3 then
   begin
   DialogueOn;
   PlaceSeeing(11, 14, 2, -6);
   RemoveSeeing(11, 14, 2);
   CenterOnXY(11, 14);
   dwait(0$1);
   CenterNowOnUnits(Heike);
   DialogueOff;
   end;

wait(1$20 + rand(0$10, 0$15) * Difficulty);

repeat
 wait(0$1);
until HexInfo(2, 1) = 0;

SetDir(Gaidar, 2);
PlaceUnitXY(Gaidar, 2, 1, false);

ComMoveXY(Gaidar, 19, 24);

if Difficulty < 3 then
   begin
   PlaceSeeing(11, 14, 2, -6);
   wait(0$2);
   RemoveSeeing(11, 14, 2);
   end;

AddComWait(Gaidar, 0$6);
AddComMoveXY(Gaidar, 38, 47);
AddComMoveXY(Gaidar, 32, 59);
AddComWait(Gaidar, 0$10);
AddComMoveXY(Gaidar, 62, 116);
AddComMoveXY(Gaidar, 92, 177);

repeat
 wait(0$1);
until IsDead(Gaidar) or IsAt(Gaidar, 92, 177);

if IsAt(Gaidar, 92, 177) then
   begin
   DialogueOn;

   PlaceSeeing(92, 177, 2, -3);
   CenterNowOnUnits(Gaidar);
   dwait(0$2);
   RemoveSeeing(92, 177, 2);

   DialogueOff;

   RemoveUnit(Gaidar);
   exit;
   end;

gaidarKilled := true;
ChangeMissionObjectives('GaidarKilled');
SetAchievement('ACH_COMMISAR');
End;

// gaidar see arabs
Every 0$1 trigger See(2, Gaidar) do
Say(Gaidar, 'DT-objective-1');

// heike near main base
Every 0$5 trigger FilterAllUnits([[f_side, 2], [f_or, [f_inarea, zone2], [f_inarea, zone3], [f_inarea, zone4]]]) do
begin
DialogueOn;
Say(Heike, 'DH-outpost-last-1');
SayRadio(Burlak, 'DB-outpost-last-2');
DialogueOff;
End;

Every 0$1 trigger GetLives(ruMainBase) < 999 or GetSide(ruMainBase) = 2 do
var i, tmp;
begin
if not earlyEscape then
   begin
   SayRadio(Burlak, 'DB-end-1');
   Say(Heike, 'DH-end-2');
   end;

if tick <= 15$00 then
   SetAchievement('ACH_FIST');

tmp := FilterAllUnits([[f_side, 3], [f_not, [f_type, unit_building]]]);

if not tmp then
   exit;

for i in tmp do
    begin
    SetTag(i, tEscape);

    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveXY(i, 92, 177);
    end;
End;

// hannibal
Every 0$1 trigger FilterUnitsInArea(zone1, [f_side, 2]) and not outerAreaAchived and IsOk(Kurt) do
begin
DialogueOn;

PlaceSeeing(111, 54, 2, -16);

CenterNowOnXY(111, 53);

dwait(0$0.3);

Say(Kurt, 'DKs-Hannibal-1');
Say(Kurt, 'DKs-Hannibal-2');
Say(Kurt, 'DKs-Hannibal-3');
Say(Kurt, 'DKs-Hannibal-4');

dwait(0$1);

RemoveSeeing(111, 54, 2);
PlaceSeeing(173, 76, 2, -12);
RemoveSeeing(173, 76, 2);

CenterNowOnXY(173, 76);

Say(Kurt, 'DKs-Hannibal-5');

if Kamil and not FilterAllUnits([[f_side, 2], [f_weapon, ar_flame_thrower]]) then
   begin
   Say(Kamil, 'DKam-Hannibal-a-6');
   Say(Kamil, 'DKam-Hannibal-a-7');
   Say(Givi, 'DG-Hannibal-a-8');
   end;

DialogueOff;
End;

Every 0$1 trigger IsOk(ruSecBase) and IsOk(ruMainBase) and FilterUnitsInArea(showDefenceArea, [f_type, unit_building]) > 2 and FilterUnitsInArea(forestArea, [f_side, 2]) > 3 do
hannibalPath := true;

Every 0$3 trigger hannibalPath and FilterUnitsInArea(zone3, [f_side, 2]) do
var i, tmp, hasFlameThrower;
begin
tmp := FilterUnitsInArea(zone3, [f_side, 2]);
hasFlameThrower := UnitFilter(tmp, [f_weapon, ar_flame_thrower]) > 0;

DialogueOn;

Say(Heike, 'DH-Cleverattack-1');
Say(Heike, 'DH-Cleverattack-2');

if hasFlameThrower then
   begin
   SayRadio(Burlak, 'DB-Cleverattack-3');
   Say(Heike, 'DH-Cleverattack-4');
   end
else
   begin
   SayRadio(Burlak, 'DB-Cleverattack-a-3');
   Say(Heike, 'DH-Cleverattack-a-4');
   end;

DialogueOff;

if not hasFlameThrower then
   exit;

earlyEscape := true;

wait(0$4);

tmp := FilterAllUnits([[f_side, 3], [f_or, [f_class, 1], [f_class, 2], [f_class, 4]]]) diff [Ibrahim, Kuzmov, Gaidar];

for i in tmp do
    begin
    if Prob(40 + 10 * Difficulty) then
       continue;

    SetTag(i, tEscape);

    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveXY(i, 92, 177);
    end;
End;

// remove all escapist
Every 0$1 trigger FilterUnitsInArea(escapeArea, [f_or, [f_side, 3], [f_side, 6]]) do
var i;
begin
enable;

for i in FilterUnitsInArea(escapeArea, [f_or, [f_side, 3], [f_side, 6]]) do
    if GetTag(i) = tEscape or GetSide(i) = 6 then
       RemoveUnit(i);
End;

Every 0$1 trigger Kaia and GetLives(Kaia) < 3 do
begin
DialogueOn;
CenterNowOnUnits(Kaia);
ForceSay(Kaia, 'DK-Kkilled-1');
Say(Heike, 'DH-Kkilled-2');
DialogueOff;
End;

Every 0$1 trigger kaiaKilledByRussian do
Say(Heike, 'DH-Kkilled-3');

Every 0$1 trigger tick >= 24$00 do
begin
DialogueOn;
SayRadio(Shariff, 'DShar-slow-1');
SayRadio(Shariff, 'DShar-slow-2');
DialogueOff;
End;

Every 0$1 trigger tick >= [40$00, 36$00, 32$00, 30$00][Difficulty] do
begin
DialogueOn;
SayRadio(Shariff, 'DShar-fail-1');
SayRadio(Shariff, 'DShar-fail-2');
DialogueOff;

YouLost('Time');
End;

Every 0$1 trigger FilterAllUnits([[f_side, 3], [f_or, [f_btype, b_warehouse], [f_btype, b_depot]]]) = 0 do
var tmp, m1, m2, m3;
begin
m1 := false;
m2 := false;
m3 := false;

if Count(FilterAllUnits([[f_side, 2], [f_nation, 2], [f_type, unit_human], [f_ok]])) < 5 then
   begin
   YouLost('Troops');
   exit;
   end;

if (tick + gameTime) <= 55$00 then
   SetAchievement('ACH_HSPEED_4');

if (tick + gameTime) <= [85$00, 75$00, 65$00, 60$00][Difficulty] then
   begin
   m1 := true;
   AddMedal('med1', 1);
   end
else
   AddMedal('med1', -1);

if (flamethrowers >= 3) * baseHill * hannibalPath * (loseCounter < [6, 5, 4, 3][Difficulty]) then
   begin
   m2 := true;
   AddMedal('med2', 1);
   end
else if flamethrowers < 3 then
   AddMedal('med2', -1)
else if not baseHill then
   AddMedal('med2', -2)
else if not hannibalPath then
   AddMedal('med2', -3)
else
   AddMedal('med3', -2);

SaveVariable(gaidarKilled, '04_2_GaidarKilled');

if gaidarKilled then
   begin
   m3 := true;
   AddMedal('med3', 1);
   end
else
   AddMedal('med3', -1);

if m1 and m2 and m3 and Difficulty >= 3 then
   SetAchievementEX('ACH_ARES', 4);

GiveMedals('MAIN');
 
RewardPeople(FilterAllUnits([[f_side, 2], [f_not, [f_nation, 0]]]));

SaveCharacters(Heike, '04_2_Heike');

if Aviradze then
   SaveCharacters(Aviradze, '04_2_Aviradze');

if Ibrahim and GetSide(Ibrahim) = 2 then
   begin
   if ibrahimOnRuSide then
      SaveVariable(true, '04_2_IbrahimJoined');

   SaveCharacters(Ibrahim, '04_2_Ibrahim');
   end;

if Kamil then
   SaveCharacters(Kamil, '04_2_Kamil');

if Sophia then
   SaveCharacters(Sophia, '04_2_Sophia');

if Kaia then
   SaveCharacters(Kaia, '04_2_Kaia');

if Givi then
   SaveCharacters(Givi, '04_2_Givi');

if Mike then
   SaveCharacters(Mike, '04_2_Mike');

if Markov then
   SaveCharacters(Markov, '04_2_Markov');

if Kurt then
   SaveCharacters(Kurt, '04_2_Kurt');

if heikeSecondSquad then
   SaveCharacters(heikeSecondSquad, '04_2_others');

tmp := FilterAllUnits([[f_side, 2], [f_nation, 0]]);

if tmp then                
   SaveCharacters(tmp, '04_2_apes');

YouWin;
End;
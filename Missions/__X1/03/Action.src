Export Function Action;
var hasAll, i, tmp;
begin
hasAll := FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, nation_arabian]]) >= 7;

InGameOn;

CenterNowOnUnits(Heike);

wait(0$2);

Say(Farmer, 'DF-1-start');

ComTurnUnit(Heike, Farmer);

Say(Heike, 'DH-1-start');

ComMoveXY(Omar, 92, 21);
ComMoveXY(Aviradze, 94, 23);

ComMoveXY(omarSquad[1], 90, 23);
ComMoveXY(omarSquad[2], 93, 25);

AddComTurnUnit(omarSquad, Omar);
AddComTurnUnit([Omar, Aviradze], Heike);

AddComTurnUnit(Heike, Omar);

repeat
 wait(0$1);
until IsAt(Omar, 92, 21);

Say(Omar, 'DO-1-start');
Say(Heike, 'DH-2-start');

if hasAll then
   begin
   Say(Omar, 'DO-2-start');
   Say(Omar, 'DO-3-start');
   Say(Heike, 'DH-3-start');
   Say(Farmer, 'DF-2-start');

   ComTurnUnit(Omar, Farmer);

   Say(Omar, 'DO-4-start');
   Say(Farmer, 'DF-3-start');
   Say(Omar, 'DO-5-start');
   Say(Farmer, 'DF-4-start');

   ComTurnUnit(Omar, Heike);
   end
else
   begin
   Say(Omar, 'DO-2-start-a');
   Say(Omar, 'DO-3-start-a');
   Say(Heike, 'DH-3-start-a');

   Say(Farmer, 'DF-2-start-a');

   ComTurnUnit(Omar, Farmer);

   Say(Omar, 'DO-4-start-a');
   Say(Farmer, 'DF-3-start-a');

   ComTurnUnit(Omar, Heike);
   end;

wait(0$0.3);

Say(Omar, 'DO-1-mission');
Say(Omar, 'DO-2-mission');

if not hasAll then
   Say(Omar, 'DO-3-mission');

Say(Omar, 'DO-4-mission');
Say(Heike, 'DH-1-mission');
Say(Farmer, 'DF-1-mission');

Say(Omar, 'DO-5-mission');

// give heike some units
if not hasAll then
   begin
   tmp := FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, nation_arabian]]);

   for i := 7 downto tmp do
       begin
       if omarSquad < 3 then
          break;

       SetSide(omarSquad[3], 2);
       omarSquad := Delete(omarSquad, 3);
       end;
   end;

// omar go out
ComMoveXY(Omar ^ omarSquad, 103, 9);

if hasAll and IsOk(Kaia) then
   begin
   wait(0$3);

   ComTurnUnit(Kaia, Farmer);

   Say(Kaia, 'DK-1-side');

   ComTurnUnit(Farmer, Kaia);

   Say(Farmer, 'DF-1-side');
   end;

InGameOff;

ChangeMissionObjectives('BuildBase');

ComFree(FilterAllUnits([f_side, 2]));

SaveForQuickRestart;

// farmer go build some base
ComMoveXY(Farmer ^ farmerSquad, 108, 62);

gameStarted := true;
End;

// talk about base
Every 0$3 trigger gameStarted do
begin
Video(true);

CenterOnUnits(Heike);

ComTurnUnit(Heike, Aviradze);
ComTurnUnit(FilterAllUnits([f_side, 2]) diff Heike, Heike);

Say(Heike, 'DH-1-explore');

if Givi then
   Say(Givi, 'DG-1-explore')
else if heikeSecondSquad then
   Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-1-explore');

Say(Aviradze, 'DA-1-explore');

if Sophia then
   begin
   Say(Sophia, 'DS-2-explore');
   Say(Aviradze, 'DA-2-explore');

   ComTurnUnit(Sophia, Aviradze);
   ComTurnUnit(Aviradze, Sophia);

   Say(Sophia, 'DS-3-explore');
   Say(Aviradze, 'DA-3-explore');
   end;

if Mike then
   begin
   Say(Mike, 'DM-1-explore');
   Say(Heike, 'DH-2-explore');
   Say(Mike, 'DM-2-explore');

   if Kaia then
      Say(Kaia, 'DK-1-explore');
   end;

ComFree(FilterAllUnits([f_side, 2]));

Video(false);
End;

// see hill
Every 0$1 trigger not americanBaseSpoted and FilterUnitsInArea(hillArea, [f_side, 2]) and not americanBaseCaptured do
begin
if Mike then
   Say(Mike, 'DM-1-scout')
else
   Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-1-explore');

PlaceSeeing(54, 35, 2, 1);
RemoveSeeing(54, 35, 2);
End;

// on hill
Every 0$1 trigger FilterUnitsInArea(upHillArea, [f_side, 2]) do
var randomMen, randomWomen, speakerOk;
begin
randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);
randomWomen := UnitFilter(heikeSecondSquad, [f_sex, sex_female]);

DialogueOn;

dwait(0$1);

speakerOk := false;

if Mike then
   speakerOk := Say(Mike, 'DM-1-spot')
else if randomMen then
   speakerOk := Say(randomMen[1], 'DArm-1-spot-a');

if speakerOk then
   begin
   if Givi then
      Say(Givi, 'DG-1-spot')
   else if randomWomen then
      Say(randomWomen[1], 'DArf-1-spot-a');
   end;

if Mike then
   Say(Heike, 'DH-1-spot')
else
   Say(Heike, 'DH-1-spot-a');

DialogueOff;

americanBaseSpoted := true;
End;

// See american base
Every 0$1 trigger not americanBaseSpoted and FilterAllUnits([[f_side, 2], [f_see, 1]]) and not americanBaseCaptured do
var randomMen;
begin
americanBaseSpoted := true;

randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);

if not randomMen then
   exit;

DialogueOn;
Say(randomMen[1], 'DArm-1-spot-a');
Say(Heike, 'DH-1-spot-a');
DialogueOff;
End;

// american base captured
Every 0$1 trigger GetSide(usDepot) = 2 or FilterAllUnits([[f_side, 1], [f_not, [f_ok]], [f_btype, b_breastwork]]) or FilterUnitsInArea(americanBaseArea, [f_side, 2]) > 1 do
var i, p;
begin
americanBaseCaptured := true;

wait(0$2);

if IsOk(usCommander) then
   begin
   usForces := usForces union usCommander;
   Say(usCommander, 'DUsm-1-assault');
   end;

for i in usForces do
    begin
    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveXY(i, 34, 67);
    end;

Say(Heike, 'DH-1-assault');

repeat
 wait(0$1);
until not usForces;

if not americanHasEscaped then
   Say(Heike, 'DH-2-assault');

wait(0$2);

repeat
 wait(0$1);
until not FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_not, [f_ok]]]);

Video(true);

if not GetSide(usDepot) = 2 then
   SetSide(usDepot, 2);

ComMoveXY(Heike, 35, 22);
AddComTurnXY(Heike, 38, 25);
ComMoveXY(heikeSquad union heikeSecondSquad, 39, 26);
AddComTurnUnit(heikeSquad union heikeSecondSquad, Heike);

p := 0;

repeat
 wait(0$1);
 p := p + 1;
until not HasTask(Heike) or p > 10;

Say(Heike, 'DH-1-capture');

if Markov then
   Say(Markov, 'DMar-1-capture');

if Sophia then
   begin
   Say(Heike, 'DH-2-capture');
   Say(Sophia, 'DS-1-capture');
   end;

Video(false);

ChangeMissionObjectives('ConstructBase');
End;

// workshop
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_btype, b_workshop], [f_not, [f_constructed]]]) do
begin
workshopBuilded := true;
ChangeMissionObjectives('ConstructVeh');

if not IsOk(Markov) then
   exit;

Say(Heike, 'DH-1-shop');

if not (GetTech(2, tech_OilEng) = state_researched and GetTech(2, tech_SolEng) = state_researched) then
   Say(Markov, 'DMar-1-shop-a');
End;

// engines
Every 0$1 trigger IsOk(Markov) and workshopBuilded and GetTech(2, tech_OilEng) = state_researched and GetTech(2, tech_SolEng) = state_researched do
begin
DialogueOn;

Say(Markov, 'DMar-1-shop');
Say(Heike, 'DH-1-shop');
Say(Markov, 'DMar-2-shop');
Say(Markov, 'DMar-3-shop');
Say(Heike, 'DH-2-shop');
Say(Markov, 'DMar-4-shop');
Say(Heike, 'DH-3-shop');
Say(Markov, 'DMar-5-shop');
Say(Heike, 'DH-4-shop');

DialogueOff;
End;

// first balista
Every 0$3 trigger IsOk(Markov) and FilterAllUnits([[f_side, 2], [f_or, [f_weapon, ar_multimissile_ballista], [f_bweapon, ar_multimissile_ballista]]]) do
begin
DialogueOn;
CenterNowOnUnits(FilterAllUnits([[f_side, 2], [f_or, [f_weapon, ar_multimissile_ballista], [f_bweapon, ar_multimissile_ballista]]]));
dwait(0$0.3);

Say(Heike, 'DH-1-bal');
Say(Markov, 'DMar-1-bal');
Say(Heike, 'DH-2-bal');

DialogueOff;
End;

// lab builded
Every 0$1 trigger IsOk(Aviradze) and FilterAllUnits([[f_side, 2], [f_btype, b_lab]]) do
begin
DialogueOn;

Say(Aviradze, 'DA-1-lab');
Say(Heike, 'DH-1-lab');
Say(Aviradze, 'DA-2-lab');
Say(Heike, 'DH-2-lab');

DialogueOff;
End;

// first ape sold
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_class, class_apeman_soldier]]) do
begin

End;

// first ape eng
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_class, class_apeman_engineer]]) do
begin

End;

// remove base squad
Every 0$1 trigger UnitFilter(usForces, [f_inarea, westRoad]) do
var i;
begin
enable;

for i in UnitFilter(usForces, [f_inarea, westRoad]) do
    begin
    usForces := usForces diff i;
    RemoveUnit(i);
    end;

if not americanHasEscaped then
   begin
   americanHasEscaped := true;
   Say(Heike, 'DH-2-assault-a');
   end;
End;

// Farmer depot captured
Every 0$1 trigger GetSide(arDepot) = 2 and FarmerOnMap do
YouLost('Attack');


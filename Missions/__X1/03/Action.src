Export Function Action;
var hasAll, i, tmp;
begin
hasAll := FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, nation_arabian]]) >= 7;

InGameOn;

CenterNowOnUnits(Heike);

wait(0$2);

Say(Farmer, 'DF-1-start');

ComTurnUnit(Heike, Farmer);

Say(Heike, 'DH-1-start');

ComMoveXY(Omar, 92, 21);
ComMoveXY(Aviradze, 94, 23);

ComMoveXY(omarSquad[1], 90, 23);
ComMoveXY(omarSquad[2], 93, 25);

AddComTurnUnit(omarSquad, Omar);
AddComTurnUnit([Omar, Aviradze], Heike);

AddComTurnUnit(Heike, Omar);

repeat
 wait(0$1);
until IsAt(Omar, 92, 21);

Say(Omar, 'DO-1-start');
Say(Heike, 'DH-2-start');

if hasAll then
   begin
   Say(Omar, 'DO-2-start');
   Say(Omar, 'DO-3-start');
   Say(Heike, 'DH-3-start');
   Say(Farmer, 'DF-2-start');

   ComTurnUnit(Omar, Farmer);

   Say(Omar, 'DO-4-start');
   Say(Farmer, 'DF-3-start');
   Say(Omar, 'DO-5-start');
   Say(Farmer, 'DF-4-start');

   ComTurnUnit(Omar, Heike);
   end
else
   begin
   Say(Omar, 'DO-2-start-a');
   Say(Omar, 'DO-3-start-a');
   Say(Heike, 'DH-3-start-a');

   Say(Farmer, 'DF-2-start-a');

   ComTurnUnit(Omar, Farmer);

   Say(Omar, 'DO-4-start-a');
   Say(Farmer, 'DF-3-start-a');

   ComTurnUnit(Omar, Heike);
   end;

wait(0$0.3);

Say(Omar, 'DO-1-mission');
Say(Omar, 'DO-2-mission');

if not hasAll then
   Say(Omar, 'DO-3-mission');

Say(Omar, 'DO-4-mission');
Say(Heike, 'DH-1-mission');
Say(Farmer, 'DF-1-mission');

Say(Omar, 'DO-5-mission');

// give heike some units
if not hasAll then
   begin
   tmp := FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, nation_arabian]]);

   for i := 7 downto tmp do
       begin
       if omarSquad < 3 then
          break;

       SetSide(omarSquad[3], 2);
       heikeSecondSquad := heikeSecondSquad union omarSquad[3];
       omarSquad := Delete(omarSquad, 3);
       end;
   end;

// omar go out
ComMoveXY(Omar ^ omarSquad, 103, 9);

if hasAll and IsOk(Kaia) then
   begin
   wait(0$3);

   ComTurnUnit(Kaia, Farmer);

   Say(Kaia, 'DK-1-side');

   ComTurnUnit(Farmer, Kaia);

   Say(Farmer, 'DF-1-side');
   end;

InGameOff;

ChangeMissionObjectives('BuildBase');

ComFree(FilterAllUnits([f_side, 2]));

SaveForQuickRestart;

// farmer go build some base
ComMoveXY(Farmer ^ farmerSquad, 108, 62);

gameStarted := true;
End;

// talk about base
Every 0$3 trigger gameStarted do
begin
Video(true);

CenterOnUnits(Heike);

ComTurnUnit(Heike, Aviradze);
ComTurnUnit(FilterAllUnits([f_side, 2]) diff Heike, Heike);

Say(Heike, 'DH-1-explore');

if Givi then
   Say(Givi, 'DG-1-explore')
else if heikeSecondSquad then
   Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-1-explore');

Say(Aviradze, 'DA-1-explore');

if Sophia then
   begin
   Say(Sophia, 'DS-2-explore');
   Say(Aviradze, 'DA-2-explore');

   ComTurnUnit(Sophia, Aviradze);
   ComTurnUnit(Aviradze, Sophia);

   Say(Sophia, 'DS-3-explore');
   Say(Aviradze, 'DA-3-explore');
   end;

if Mike then
   begin
   Say(Mike, 'DM-1-explore');
   Say(Heike, 'DH-2-explore');
   Say(Mike, 'DM-2-explore');

   if Kaia then
      Say(Kaia, 'DK-1-explore');
   end;

ComFree(FilterAllUnits([f_side, 2]));

Video(false);
End;

// see hill
Every 0$1 trigger not americanBaseSpoted and FilterUnitsInArea(hillArea, [f_side, 2]) and not americanBaseCaptured do
begin
if Mike then
   Say(Mike, 'DM-1-scout')
else
   Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-1-explore');

PlaceSeeing(54, 35, 2, 1);
RemoveSeeing(54, 35, 2);
End;

// on hill
Every 0$1 trigger FilterUnitsInArea(upHillArea, [f_side, 2]) do
var randomMen, randomWomen, speakerOk;
begin
randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);
randomWomen := UnitFilter(heikeSecondSquad, [f_sex, sex_female]);

DialogueOn;

dwait(0$1);

speakerOk := false;

if Mike then
   speakerOk := Say(Mike, 'DM-1-spot')
else if randomMen then
   speakerOk := Say(randomMen[1], 'DArm-1-spot-a');

if speakerOk then
   begin
   if Givi then
      Say(Givi, 'DG-1-spot')
   else if randomWomen then
      Say(randomWomen[1], 'DArf-1-spot-a');
   end;

if Mike then
   Say(Heike, 'DH-1-spot')
else
   Say(Heike, 'DH-1-spot-a');

DialogueOff;

americanBaseSpoted := true;
End;

// See american base
Every 0$1 trigger not americanBaseSpoted and FilterAllUnits([[f_side, 2], [f_see, 1]]) and not americanBaseCaptured do
var randomMen;
begin
americanBaseSpoted := true;

randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);

if not randomMen then
   exit;

DialogueOn;
Say(randomMen[1], 'DArm-1-spot-a');
Say(Heike, 'DH-1-spot-a');
DialogueOff;
End;

// american base captured
Every 0$1 trigger GetSide(usDepot) = 2 or FilterAllUnits([[f_side, 1], [f_not, [f_ok]], [f_btype, b_breastwork]]) or FilterUnitsInArea(americanBaseArea, [f_side, 2]) > 1 do
var i, p;
begin
americanBaseCaptured := true;

wait(0$2);

if IsOk(usCommander) then
   begin
   usForces := usForces union usCommander;
   Say(usCommander, 'DUsm-1-assault');
   end;

for i in usForces do
    begin
    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveXY(i, 34, 67);
    end;

Say(Heike, 'DH-1-assault');

repeat
 wait(0$1);
until not usForces;

if not americanHasEscaped then
   Say(Heike, 'DH-2-assault');

wait(0$2);

repeat
 wait(0$1);
until not FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_not, [f_ok]]]);

Video(true);

if not GetSide(usDepot) = 2 then
   SetSide(usDepot, 2);

CenterOnXY(35, 22);

ComMoveXY(Heike, 35, 22);
AddComTurnXY(Heike, 38, 25);
ComMoveXY(heikeSquad union heikeSecondSquad, 39, 26);
AddComTurnUnit(heikeSquad union heikeSecondSquad, Heike);

p := 0;

repeat
 wait(0$1);
 p := p + 1;
until not HasTask(Heike) or p > 10;

Say(Heike, 'DH-1-capture');

if Markov then
   Say(Markov, 'DMar-1-capture');

if Sophia then
   begin
   Say(Heike, 'DH-2-capture');
   Say(Sophia, 'DS-1-capture');
   end;

Video(false);

ChangeMissionObjectives('ConstructBase');
End;

// workshop
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_btype, b_workshop], [f_not, [f_constructed]]]) do
begin
workshopBuilded := true;
ChangeMissionObjectives('ConstructVeh');

if not IsOk(Markov) then
   exit;

Say(Heike, 'DH-1-shop');

if not (GetTech(2, tech_OilEng) = state_researched and GetTech(2, tech_SolEng) = state_researched) then
   Say(Markov, 'DMar-1-shop-a');
End;

// engines
Every 0$1 trigger IsOk(Markov) and workshopBuilded and GetTech(2, tech_OilEng) = state_researched and GetTech(2, tech_SolEng) = state_researched do
begin
DialogueOn;

Say(Markov, 'DMar-1-shop');
Say(Heike, 'DH-1-shop');
Say(Markov, 'DMar-2-shop');
Say(Markov, 'DMar-3-shop');
Say(Heike, 'DH-2-shop');
Say(Markov, 'DMar-4-shop');
Say(Heike, 'DH-3-shop');
Say(Markov, 'DMar-5-shop');
Say(Heike, 'DH-4-shop');

DialogueOff;
End;

// first balista
Every 0$3 trigger IsOk(Markov) and FilterAllUnits([[f_side, 2], [f_or, [f_weapon, ar_multimissile_ballista], [f_bweapon, ar_multimissile_ballista]]]) do
begin
DialogueOn;
CenterNowOnUnits(FilterAllUnits([[f_side, 2], [f_or, [f_weapon, ar_multimissile_ballista], [f_bweapon, ar_multimissile_ballista]]]));
dwait(0$0.3);

Say(Heike, 'DH-1-bal');
Say(Markov, 'DMar-1-bal');
Say(Heike, 'DH-2-bal');

DialogueOff;
End;

// lab builded
Every 0$1 trigger IsOk(Aviradze) and FilterAllUnits([[f_side, 2], [f_btype, b_lab]]) do
begin
DialogueOn;

Say(Aviradze, 'DA-1-lab');
Say(Heike, 'DH-1-lab');
Say(Aviradze, 'DA-2-lab');
Say(Heike, 'DH-2-lab');

DialogueOff;
End;

// first ape sold
Every 0$1 trigger IsOk(Kaia) and FilterAllUnits([[f_side, 2], [f_class, class_apeman_soldier]]) do
var ape;
begin
ape := FilterAllUnits([[f_side, 2], [f_class, class_apeman_soldier]]);

Video(true);

CenterNowOnUnits(ape[1]);

ComExitBuilding(ape[1]);
AddComMoveXY(ape[1], 35, 28);
AddComMoveXY(ape[1], 36, 17);

Say(Kaia, 'DK-1-apesol');
Say(Aviradze, 'DA-1-apesol');
Say(Kaia, 'DK-2-apesol');
Say(Aviradze, 'DA-2-apesol');
Say(Kaia, 'DK-3-apesol');

Video(false);
End;

// first ape eng
Every 0$1 trigger IsOk(Sophia) and FilterAllUnits([[f_side, 2], [f_class, class_apeman_engineer]]) do
var ape;
begin
ape := FilterAllUnits([[f_side, 2], [f_class, class_apeman_engineer]]);

Video(true);

CreateCratesXY(5, 35, 22, true);

ComTurnUnit(Sophia, ape[1]);
CenterNowOnUnits(ape[1]);
ComExitBuilding(ape[1]);

wait(0$2);

Say(Sophia, 'DS-1-apeeng');

if IsOk(Kamil) then
   begin
   Say(Kamil, 'DKam-1-apeeng');
   Say(Sophia, 'DS-2-apeeng');
   Say(Kamil, 'DKam-2-apeeng');
   Say(Sophia, 'DS-3-apeeng');

   if not IsOK(Kaia) then
      begin
      Video(false);
      exit;
      end;

   Say(Kaia, 'DK-1-apeeng');

   if not IsOk(Givi) then
      begin
      Video(false);
      exit;
      end;

   Say(Givi, 'DG-1-apeeng');
   Say(Kaia, 'DK-2-apeeng');
   end;

Video(false);
End;

// farmer is ready
Every 0$1 trigger farmerBaseReady do
begin
DialogueOn;

CenterNowOnUnits(Farmer);

SayRadio(Farmer, 'DF-1-distribution');
Say(Heike, 'DH-2-distribution');

if IsOk(Kaia) then
   begin
   Say(Kaia, 'DK-1-distribution');
   Say(Heike, 'DH-1-distribution');

   if IsOk(Givi) then
      begin
      Say(Givi, 'DG-1-distribution');
      Say(Heike, 'DH-3-distribution');
      end;
   end;

DialogueOff;
ChangeMissionObjectives('Crates1');

farmerRequestedCrates := 300;

Query('FarmerCrates');
SetAreaMapShow(collectFarmerArea, 1);

CenterNowOnXY(108, 61);
End;

// base constructed
Every 0$1 trigger americanBaseCaptured and FilterAllUnits([[f_side, 2], [f_not, [f_constructed]], [f_btype, b_lab]]) and FilterAllUnits([[f_side, 2], [f_not, [f_constructed]], [f_btype, b_armoury]]) do
var eng;
begin
wait(0$10);

eng := UnitFilter(heikeSecondSquad, [[f_sex, sex_male], [f_class, 2]]);

Say(Heike, 'DH-1-task');

if IsOk(Sophia) then
   begin
   Say(Sophia, 'DS-1-task');
   Say(Heike, 'DH-2-task');
   end
else if eng then
   begin
   Say(eng[1], 'DArm-1-task');
   Say(Heike, 'DH-2-task');
   end;

ChangeMissionObjectives('BaseConstructed');
allowConvoys := true;
End;

// farmer wants more crates.. #2
Every 0$1 trigger farmerBaseReady and farmerCrates >= 300 do
begin
DialogueOn;

SayRadio(Farmer, 'DF-1-delivery-2');
Say(Heike, 'DH-1-delivery-2');
SayRadio(Farmer, 'DF-2-delivery-2');

DialogueOff;

ChangeMissionObjectives('Crates2');

farmerCratesCounter := 4$00;
farmerRequestedCrates := 600;
End;

// farmer wants more crates.. #3
Every 0$1 trigger farmerBaseReady and farmerCrates >= 600 and Difficulty > 1 do
begin
DialogueOn;

SayRadio(Farmer, 'DF-1-delivery-3');
Say(Heike, 'DH-1-delivery-3');
SayRadio(Farmer, 'DF-2-delivery-3');

if IsOk(Kaia) then
   begin
   Say(Kaia, 'DK-1-delivery-3');
   Say(Heike, 'DH-2-delivery-3');
   Say(Givi, 'DG-1-delivery-3');
   Say(Mike, 'DM-1-delivery-3');
   end;

DialogueOff;

if Difficulty = 2 then
   begin
   ChangeMissionObjectives('Crates3');
   farmerCratesCounter := 5$00;
   farmerRequestedCrates := 900;
   end
else
   begin
   ChangeMissionObjectives('Crates4');
   farmerCratesCounter := 6$00;
   farmerRequestedCrates := 1200;
   end;
End;

// crates collected
Every 0$2 trigger farmerBaseReady and farmerCrates >= [600, 900, 1200][Difficulty] and farmerRequestedCrates <= farmerCratesCounter do
begin
retreatAllowed := true;
display_strings := [];

DialogueOn;
Video(true);

SayRadio(Omar, 'DO-radio-end');
Say(Heike, 'DH-1-radio-end');

Video(false);
DialogueOff;

ChangeMissionObjectives('Retreat');
End;

Every 0$1 trigger retreatAllowed do
StartCargoEvacuation;

// farmer depot destroyed
Every 0$1 trigger not retreatAllowed and IsDead(arDepot) do
begin
SayRadio(Farmer, 'DF-1-failure');
YouLost('FarmerDepot');
End;

// talk with Farmer
Every 0$1 trigger retreatAllowed and GetDistUnits(Heike, Farmer) < 6 do
var i, max, tmp;
begin
Video(true);

ComTurnUnit(Heike, Farmer);
ComTurnUnit(Farmer, Heike);

CenterNowOnUnits(Farmer);

Say(Heike, 'DH-1-end');
Say(Farmer, 'DF-1-end');
Say(Farmer, 'DF-2-end');
Say(Heike, 'DH-2-end');
Say(Farmer, 'DF-3-end');
Say(Heike, 'DH-3-end');
Say(Givi, 'DG-1-end');
Say(Heike, 'DH-4-end');
Say(Farmer, 'DF-4-end');
Say(Heike, 'DH-5-end');
Say(Farmer, 'DF-5-end');

Video(false);

max := [4, 3, 2][Difficulty];

if farmerSquad < max then
   max := farmerSquad;

for i := 1 to max do
    begin
    SetSide(farmerSquad[1], 2);
    farmerSquad := Delete(farmerSquad, 1);
    end;

for i in Farmer ^ farmerSquad do
    begin
    if IsInUnit(i) then
       ComExitBuilding(i);
    AddComMoveXY(i, 102, 7);
    end;

for i in FilterAllUnits([[f_side, 5], [f_type, unit_building]]) diff arDepot do
    SetSide(i, 2);

repeat
 wait(0$1);
 tmp := UnitFilter(Farmer ^ farmerSquad, [f_inarea, northRoad]);

 if tmp then
    for i in tmp do
        begin
        if i in farmerSquad then
           farmerSquad := farmerSquad diff i;

        RemoveUnit(i);
        end;
until not Farmer and not farmerSquad;
End;

// farmer angry
Every 0$2 trigger farmerCratesCounter <= 0$00 do
begin
DialogueOn;
SayRadio(Farmer, 'DF-1-distribution-a');
Say(Heike, 'DH-1-distribution-a');
SayRadio(Farmer, 'DF-2-distribution-a');
DialogueOff;
End;

// first cargo captured
Every 0$3 trigger FilterAllUnits([[f_side, 2], [f_weapon, us_cargo_bay]]) do
begin
DialogueOn;

Say(Heike, 'DH-1-truck');

if IsOk(Markov) then
   begin
   Say(Markov, 'DMar-1-truck');
   Say(Heike, 'DH-2-truck');
   end;

DialogueOff;
End;

Export Function EmptyCargoDialog;
var randomMen, randomWomen;
begin
randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);
randomWomen := UnitFilter(heikeSecondSquad, [f_sex, sex_female]);

if randomMen then
   Say(randomMen[1], 'DArm-1-nocargo')
else if randomWomen then
   Say(randomWomen[1], 'DArf-1-nocargo');
End;

Export Function IncomingAttack;
begin
if attackWave = 1 then
   begin
   DialogueOn;

   SayRadio(Omar, 'DO-1-radio-1');
   Say(Heike, 'DH-1-radio-1-');

   if IsOk(Markov) then
      begin
      Say(Markov, 'DMar-1-radio-1');
      Say(Heike, 'DH-2-radio-1');
      end;

   DialogueOff;
   end
else
   begin
   SayRadio(Omar, 'DO-1-radio-u');
   Say(Heike, 'DH-1-radio-u');
   end;
End;

// remove base squad
Every 0$1 trigger UnitFilter(usForces, [f_inarea, westRoad]) do
var i;
begin
enable;

for i in UnitFilter(usForces, [f_inarea, westRoad]) do
    begin
    usForces := usForces diff i;
    RemoveUnit(i);
    end;

if not americanHasEscaped and attackWave = 1 then
   begin
   americanHasEscaped := true;
   Say(Heike, 'DH-2-assault-a');
   end;
End;

// Farmer depot captured
Every 0$1 trigger GetSide(arDepot) = 2 do
YouLost('Attack');

// ibrahim
Every 0$30 trigger not FilterAllUnits([[f_side, 1], [f_type, unit_human]]) and not retreatAllowed and farmerCrates > 100 and FilterUnitsInArea(americanBaseArea, [[f_side, 2], [f_type, unit_human]]) do
var randomMen, randomWomen, speaker, place;
begin
randomMen := UnitFilter(heikeSecondSquad, [f_sex, sex_male]);
randomWomen := UnitFilter(heikeSecondSquad, [f_sex, sex_female]);

if IsOk(Kaia) then
   speaker := [Kaia, 'DK']
else if randomMen then
   speaker := [randomMen[rand(1, randomMen)], 'DArm']
else if randomWomen then
   speaker := [randomWomen[rand(1, randomWomen)], 'DArf']
else
   exit;

if IsInUnit(speaker[1]) then
   place := IsInUnit(speaker[1])
else
   place := speaker[1];

CenterNowOnUnits(place);
PrepareIbrahim(GetX(place), GetY(place));

wait(0$2);

if not IsPlaced(Ibrahim) then
   exit;

Video(true);

wait(0$1);

Say(Ibrahim, 'DI-1-land');

if IsInUnit(speaker[1]) then
   ComExit(speaker[1]);

AddComMoveUnit(speaker[1], Ibrahim);
AddComTurnUnit(speaker[1], Ibrahim);

ComTurnUnit(Ibrahim, speaker[1]);

wait(0$1);

Say(speaker[1], speaker[2] & '-1-land');
Say(Ibrahim, 'DI-2-land');
Say(speaker[1], speaker[2] & '-2-land');

if IsInUnit(Heike) then
   ComExitBuilding(Heike);

AddComMoveXY(Heike, 34, 21);
AddComTurnUnit(Heike, speaker[1]);

ComMoveXY(Ibrahim, 36, 22);
ComMoveXY(speaker[1], 35, 22);

AddComTurnUnit([Ibrahim, speaker[1]], Heike);

repeat
 wait(0$1);
until GetDistUnits(speaker[1], usDepot) < 8;

Say(speaker[1], speaker[2] & '-3-land');
Say(Heike, 'DH-1-land');

if UnitsInside(usDepot) = 6 then
   ComExitBuilding(UnitsInside(usDepot)[1]);

SetSide(Ibrahim, 2);

ComEnterUnit(Ibrahim, usDepot);

Video(false);

ibrahimInDepot := true;
End;

Export Function IbrahimQuery;
begin

case Query('IbrahimQuery') of
1: begin // accept
   Video(true);

   ibrahimInDepot := false;

   Say(Heike, 'DH-1-interrogation');
   Say(Ibrahim, 'DI-1-interrogation');
   Say(Heike, 'DH-2-interrogation');
   Say(Ibrahim, 'DI-2-interrogation');
   Say(Heike, 'DH-3-interrogation');
   Say(Ibrahim, 'DI-3-interrogation');
   Say(Heike, 'DH-4-interrogation');
   Say(Ibrahim, 'DI-4-interrogation');
   Say(Heike, 'DH-5-interrogation');
   Say(Ibrahim, 'DI-5-interrogation');

   wait(0$1);

   case Query('IbrahimDecisionQuery') of
   1: begin // kill
      Say(Ibrahim, 'DI-1-kill');
      KillUnit(Ibrahim); 
      end;

   2: begin // let him go
      ComExitBuilding(Ibrahim);
      AddComMoveXY(Ibrahim, 32, 5);

      wait(0$3);

      ComExitBuilding(Heike);
      AddComMoveXY(Heike, 36, 21);

      AddComWait(Ibrahim, 0$2);
      AddComHold(Ibrahim);

      ibrahimIsFree := true;
      end;
   end;

   Video(false);
   end;
2:;
end;

End;

Every 0$10 trigger ibrahimIsFree and IsInArea(Ibrahim, americanBaseArea) and See(6, Heike) and not FilterAllUnits([[f_side, 1], [f_type, unit_human]]) do
var changeClass;
begin
Video(true);

CenterNowOnUnits(Ibrahim);

ComTurnUnit(Heike, Ibrahim);
ComTurnUnit(Ibrahim, Heike);

Say(Ibrahim, 'DI-1-free');
Say(Heike, 'DH-1-free');
Say(Ibrahim, 'DI-2-free');

changeClass := 0;

case Query('IbrahimJoinQuery') of
1: begin // accept
   changeClass := 1;
   end;

2: begin // decline
   Say(Heike, 'DH-1-decline');
   Say(Ibrahim, 'DI-1-decline');

   ComMoveXY(Ibrahim, 30, 1);

   repeat
    wait(0$1);
   until IsAt(Ibrahim, 30, 1) or not FilterAllUnits([[f_side, 2], [f_see, 6]]);

   RemoveUnit(Ibrahim);
   SaveVariable(1, 'IbrahimHasEscaped');
   end;

3: begin // ask Omar
   Say(Heike, 'DH-1-radio-hq');
   Say(Ibrahim, 'DO-1-radio-hq');
   Say(Heike, 'DH-2-radio-hq');
   Say(Ibrahim, 'DO-2-radio-hq');
   Say(Heike, 'DH-3-radio-hq');

   changeClass := 1;
   end;
end;

if changeClass then
   begin
   Say(Heike, 'DH-1-agree');

   if IsOk(Givi) then
      begin
      Say(Givi, 'DG-1-agree');
      Say(Heike, 'DH-2-agree');
      Say(Givi, 'DG-2-agree');
      end;

   if UnitsInside(usDepot) = 6 then
      ComExitBuilding(UnitsInside(usDepot)[1]);

   ComEnterUnit(Ibrahim, usDepot);

   repeat
    wait(0$1);
   until IsInUnit(Ibrahim);

   RemoveUnit(Ibrahim);

   uc_side := 2;
   Ibrahim := PrepareUnit('Ibrahim', false, '');
   PlaceHumanInUnit(Ibrahim, usDepot);
   end;

Video(false);
End;

// end mission
Every 0$1 trigger retreatAllowed and omarCargoCounter < 1 do
begin
DialogueOn;

Say(Heike, 'DH-1-final');

if tick <= [60$00, 50$00, 45$00][Difficulty] then
   AddMedal('med1', 1)
else
   AddMedal('med1', -1);

if vehCounter >= 2 then
   AddMedal('med2', 1)
else
   AddMedal('med2', -1);

if deadCounter = 0 then
   AddMedal('med3', 1)
else
   AddMedal('med3', -1);

GiveMedals('MAIN');

RewardPeople(FilterAllUnits([[f_side, 2], [f_nation, 2], [f_type, unit_human]]));

SaveCharacters(Heike, '03_Heike');
SaveCharacters(Aviradze, '03_Aviradze');

if Givi then
   SaveCharacters(Givi, '03_Givi');

if Mike then
   SaveCharacters(Mike, '03_Mike');

if Kamil then
   SaveCharacters(Kamil, '03_Kamil');

if Kaia then
   SaveCharacters(Kaia, '03_Kaia');

if Sophia then
   SaveCharacters(Sophia, '03_Sophia');

if Markov then
   SaveCharacters(Markov, '03_Markov');

if Ibrahim and GetSide(Ibrahim) = 2 then
   SaveCharacters(Markov, '03_Ibrahim');

if heikeSecondSquad then
   SaveCharacters(heikeSecondSquad, '03_others');

YouWin;

DialogueOff;
End;


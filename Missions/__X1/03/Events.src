On UnitDestroyed(un) do
begin
if un = Heike then
   YouLost('Heike');
if un = Aviradze then
   YouLost('Aviradze');
if un = usDepot and not retreatAllowed then
   YouLost('Depot');
if un = arDepot then
   YouLost('FarmerDepot');
if un = Farmer then
   YouLost('Farmer');
if un in usForces then
   usForces := usForces diff un;
if un in FilterAllUnits([[f_side, 2], [f_nation, 2], [f_type, unit_human]]) then
   deadCounter := deadCounter + 1;
if un in FilterAllUnits([[f_side, 4], [f_weapon, us_cargo_bay]]) and not firstConvoyDestroyed then
   firstConvoyDestroyed := true;
if un in usAttackers then
   usAttackers := usAttackers diff un;
End;

On LeaveBuilding(building, unit) do
begin
if unit = Ibrahim and ibrahimInDepot then
   begin
   Video(true);

   ComEnterUnit(Ibrahim, usDepot);

   repeat
    wait(3);
   until IsInUnit(Ibrahim);

   Video(false);
   end;
End;

On EnterBuilding(building, unit) do
begin
if unit = Heike and building = usDepot and ibrahimInDepot then
   IbrahimQuery;
End;

On VehicleCaptured(newId, oldId, oldSide, captureUnit) do
begin
if oldId in usAttackers then
   usAttackers := usAttackers diff oldId;

if oldSide = 4 and not firstConvoyDestroyed then
   firstConvoyDestroyed := true;

if GetWeapon(oldId) = us_cargo_bay and GetCargo(oldId, mat_cans) = 0 then
   EmptyCargoDialog;
End;

On Contact(s1, s2) do
begin
if s1 = 2 and s2 = 5 then
   YouLost('Attack');
End;

On BuildingComplete(b) do
begin
if GetSide(b) = 1 then
   begin
   if GetBType(b) = b_lab then
      begin
      ComEnterUnit(UnitFilter(usForces, [f_class, 4]), b);
      AmLabResearch(b);
      end;

   if GetBType(b) = b_armoury then
      ComEnterUnit(UnitFilter(usForces, [[f_class, 1], [f_not, [f_inside]]]), b);

   americanBuildList := Delete(americanBuildList, 1);
   end;

if GetSide(b) = 5 then
   SetBLevel(b, 5);
End;

On VehicleConstructed(veh, fac) do
begin
if GetSide(veh) = 2 then
   vehCounter := vehCounter + 1;
End;

On SailEvent(event) do
begin
if event = eventAttack then
   AmericanAttack;
End;
On VehicleConstructed(vehicle, factory) do
begin
SOS_VehicleConstructed(vehicle, factory);

if GetControl(vehicle) = control_apeman then
   begin
   apeDriver := true;
   exit;
   end;

if GetSide(factory) = 6 then
   begin
   ruConstructList := Delete(ruConstructList, 1);
   ruTanks := Join(ruTanks, vehicle);
   end;
End;

On EnterVehicle(vehicle, unit) do
begin
if GetSide(unit) = 6 and not See(2, unit) then
   SetLives(unit, 1000);
End;

On EvacuateVehicle(driver, abandoned_vehicle, unknown1, unknown2) do
var tag, area, fac, arm;
begin
if GetSide(driver) = 3 then
   begin
   tag := GetTag(driver);

   area := [ruSpawn1AArea, ruSpawn2AArea, ruSpawn3AArea][tag];

   if not area then
      exit;

   repeat
    ComMoveToArea(driver, area);
    wait(0$3);

    //if not See(2, driver) then
      // break;
    
    if not IsOk(driver) then
       exit;
   until IsInArea(driver, ruSpawn1Area) or IsInArea(driver, ruSpawn2Area) or IsInArea(driver, ruSpawn3Area);

   RemoveUnit(driver);
   end;

if GetSide(driver) = 6 then
   begin
   ComMoveXY(driver, 97, 139);

   if GetLives(driver) < 1000 then
      SetTag(driver, tHeal);

   repeat
    wait(0$1);

    if IsDead(driver) then
       exit;

   until GetLives(driver) = 1000;

   SetTag(driver, 0);

   fac := FilterUnitsInArea(ruBaseArea, [[f_side, 6], [f_btype, b_factory], [f_ok]]);

   if fac then
      begin
      ComEnterUnit(driver, fac[1]);
      exit;
      end;

   arm := FilterUnitsInArea(ruBaseArea, [[f_side, 6], [f_btype, b_barracks], [f_not, [f_full]], [f_ok]]);

   if arm then
      begin
      ComEnterUnit(driver, arm[1]);
      AddComChangeProfession(driver, class_soldier);
      end;
   end;
End;

On ClassChanged(un, oldClass, newClass) do
begin
if newClass = class_apeman_kamikaze then
   kamikazeTrained := true;
End;

On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

if un in ruTanks then
   ruConstructList := Join(ruConstructList, [GetChassis(un), GetEngine(un), GetControl(un), GetWeapon(un)]);

if un = Schulz then
   YouLost('Schulz');

if un = Heike and GetSide(un) = 2 then
   YouLost('Heike');
End;


On VehicleConstructed(vehicle, factory) do
begin
SOS_VehicleConstructed(vehicle, factory);

if GetControl(vehicle) = control_apeman then
   apeDriver := true;
End;

On EvacuateVehicle(driver, abandoned_vehicle, unknown1, unknown2) do
var tag, area;
begin
if GetSide(driver) = 3 then
   begin
   tag := GetTag(driver);

   area := [ruSpawn1AArea, ruSpawn2AArea, ruSpawn3AArea][tag];

   if not area then
      exit;

   repeat
    ComMoveToArea(driver, area);
    wait(0$3);

    //if not See(2, driver) then
      // break;
    
    if not IsOk(driver) then
       exit;
   until IsInArea(driver, ruSpawn1Area) or IsInArea(driver, ruSpawn2Area) or IsInArea(driver, ruSpawn3Area);

   RemoveUnit(driver);
   end;
End;

On ClassChanged(un, oldClass, newClass) do
begin
if newClass = class_apeman_kamikaze then
   kamikazeTrained := true;
End;

On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

if un = Schulz then
   YouLost('Schulz');

if un = Heike and GetSide(un) = 2 then
   YouLost('Heike');
End;

Export function PrepareRussianAttack(spot, strength);
var i, tmp, un, veh, skill, area, parking, dir, control;
begin
result := [];

skill := [6, 7, 8, 9][Difficulty];
area := [ruSpawn1Area, ruSpawn2Area, ruSpawn3Area][spot];
parking := [ruSpawn1Area, ruSpawn2Area, ruSpawn3Area][spot];
dir := [5, 0, 2][spot];

if FilterUnitsInArea(area, [f_side, 0]) then
   for i in FilterUnitsInArea(area, [f_side, 0]) do
       RemoveUnit(i);

if FilterUnitsInArea(area, [[f_not, [f_type, unit_human]], [f_side, 2]]) then
   for i in FilterUnitsInArea(area, [[f_not, [f_type, unit_human]], [f_side, 2]]) do
       KillUnit(i);

if FilterUnitsInArea(area, [[f_type, unit_human], [f_side, 2]]) then
   for i in FilterUnitsInArea(area, [[f_type, unit_human], [f_side, 2]]) do
       ComMoveXY(i, 91, 56);

repeat
 wait(0$1);
until not FilterUnitsInArea(area, [f_ok]);

// tanks
for i := 1 to [3, 4, 5, 6][Difficulty] + strength do
    begin
    uc_side := 3;
    uc_nation := nation_russian;

    control := [control_manual, control_computer, control_computer][rand(1,3)];

    PrepareVehicle(
       [ru_medium_tracked, ru_heavy_tracked, ru_heavy_wheeled][i mod 3 + 1],
       [engine_siberite, engine_combustion][rand(1,2)],
       control,
       [ru_rocket_launcher, ru_heavy_gun, ru_gatling_gun][i mod 3 + 1],
       100
    );

    veh := CreateVehicle;
    SetDir(veh, dir);

    PlaceUnitArea(veh, area, false);

    if control = control_manual then
       begin
       uc_side := 3;
       uc_nation := nation_russian;

       PrepareMechanic(false, skill);
       PlaceHumanInUnit(un, veh);
       SetTag(un, spot);
       end;                   

    ComMoveToArea(veh, area);

    repeat
     wait(0$3);
    until not FilterUnitsInArea(area, [f_ok]);

    result := Join(result, veh);
    end;

// infantry
for i := 1 to [1, 2, 2, 3][Difficulty] + strength do
    begin
    uc_side := 3;
    uc_nation := nation_russian;

    PrepareSoldier(false, skill);
    un := CreateHuman;

    if Prob([30, 40, 50, 50][Difficulty]) then
       SetClass(un, class_bazooker);

    SetDir(un, dir);
    SetTag(un, spot);

    PlaceUnitArea(un, area, false);
    ComMoveToArea(un, parking);

    wait(0$1);

    result := Join(result, un);
    end;
End;

Every 0$10 do
var i, e, attackers, extraAttackers, timeToAttack, wave, strength, spot, extraSpot, paths, enemy;
begin
timeToAttack := [8$00, 7$30, 7$20, 7$10][Difficulty];
attackers := [];
extraAttackers := [];
wave := 0;
strength := [0, 1, 1, 1][Difficulty];
paths := [
   [ [176,152], [157,122], [146,89], [125,46] ],
   [ [86,133], [76,111], [61,85], [38,44], [48,31] ],
   [ [25,21], [47,30] ]
];

repeat
 wait(timeToAttack);
 wave := Inc(wave);
 spot := rand(1,3);

 if wave mod 3 = 0 then
    strength := Inc(strength);

 attackers := PrepareRussianAttack(spot, strength);

 if wave = [5, 6, 6, 7][Difficulty] then
    begin
    extraSpot := spot - 1;

    if extraSpot < 1 then
       extraSpot := 3;

    extraAttackers := PrepareRussianAttack(extraSpot, 0);
    end;

 timeToAttack := timeToAttack - 0$20;

 if timeToAttack < 4$00 then
    timeToAttack := 4$00;

 repeat
  wait(0$1);
  enemy := FilterAllUnits([f_side, 2]);

  for i in attackers do
      begin
      if not IsOk(i) then
         attackers := attackers diff i;

      if GetType(i) = unit_vehicle then
         AgressiveMove(i, paths[spot])
      else
         begin
         if GetClass(i) = class_bazooker then
            begin
            if GetDistUnits(i, NearestUnitToUnit(enemy, i)) > 12 then
               ComAttackUnit(i, NearestUnitToUnit(enemy, i))
             else
               begin
               e := NearestUnitToUnit(enemy, i);

               case rand(1,3) of
                    1: ComAttackUnit(i, e);
                    2: ComSpaceShift(i, GetX(e) + rand(-1, 1), GetY(e) + rand(-1, 1));
                    3: ComTimeShift(i, GetX(e), GetY(e));
                    end;
               end;
            end
         else
            ComAttackUnit(i, NearestUnitToUnit(enemy, i));
         end;
      end;

  if extraAttackers then
     begin
     for i in extraAttackers do
         begin
         if not IsOk(i) then
            extraAttackers := extraAttackers diff i;

         if GetType(i) = unit_vehicle then
            AgressiveMove(i, paths[spot])
         else
            begin
            if GetClass(i) = class_bazooker then
               begin
               if GetDistUnits(i, NearestUnitToUnit(enemy, i)) > 12 then
                  ComAttackUnit(i, NearestUnitToUnit(enemy, i))
               else
                  begin
                  e := NearestUnitToUnit(enemy, i);

                  case rand(1,3) of
                       1: ComAttackUnit(i, e);
                       2: ComSpaceShift(i, GetX(e) + rand(-1, 1), GetY(e) + rand(-1, 1));
                       3: ComTimeShift(i, GetX(e), GetY(e));
                       end;
                  end;
               end
            else
               ComAttackUnit(i, NearestUnitToUnit(enemy, i));
            end;
         end;
     end;
 until not attackers and not extraAttackers;

until wave >= [6, 7, 7, 8][Difficulty];

ruDefeated := true;
End;
Export Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf, Kurt, Aviradze,
       Schulz, Basim, Lemmy, Mia, Nia, Jenna,
       heikeSecondSquad, schulzSecondSquad, rolfSquad,
       Rolf;
Export function PrepareArabian;
var i, apes, tmp, un, b, j, veh, vehicles;
begin
if LoadVariable('05_DrugsTested', debug) then
   SetTech(tech_StimDrugs, 2, state_researched);
         
uc_side := 2;
uc_nation := nation_nature;

apes := CreateCharacterSet('09_apes');
b := FilterAllUnits([[f_side, 2], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]);

if apes then
   for i in apes do
       if UnitsInside(b[1]) < 6 then
          PlaceHumanInUnit(i, b[1])
       else
          PlaceUnitXYR(i, 82, 27, 6, false);

InitHC_All();

uc_nation := nation_arabian;

b := FilterAllUnits([[f_side, 2], [f_type, unit_building], [f_or, [f_btype, b_bunker], [f_btype, b_armoury], [f_btype, b_barracks]]]);

Rolf := PrepareUnit('Rolf', (not debug), '06_');

if Rolf then
   begin
   rolfSquad := CreateCharacterSet('06_RolfSquad');

   if rolfSquad < 3 then
      for i := 1 to 3 - Count(rolfSquad) do
          begin
          PrepareSoldier(false, 4);
          rolfSquad := Join(rolfSquad, CreateHuman);
          end;

   if b then
      begin
      for i in rolfSquad union Rolf do
          begin
          SetClass(i, 1);
          PlaceHumanInUnit(i, UnitFilter(b, [f_not, [f_full]])[1]);
          end;
      end
   else
      for i in rolfSquad union Rolf do
          PlaceUnitXYR(i, 82, 27, 6, false);
   end;

Heike := PrepareUnit('Heike', (not debug), prefix);
Ibrahim := PrepareUnit('Ibrahim', (not debug), prefix);
Givi := PrepareUnit('Givi', (not debug), prefix);
Kamil := PrepareUnit('Kamil', (not debug), prefix);
Kaia := PrepareUnit('Kaia', (not debug), prefix);
Sophia := PrepareUnit('Sophia', (not debug), prefix);
Markov := PrepareUnit('Markov', (not debug), prefix);
Aviradze := PrepareUnit('Aviradze', (not debug), prefix);
Kurt := PrepareUnit('Kurt', (not debug), prefix);
Mike := PrepareUnit('Mike', (not debug), prefix);
Olaf := PrepareUnit('Olaf', (not debug), prefix);

heikeSecondSquad := CreateCharacterSet('09c_heikeSecondSquad');

Schulz := PrepareUnit('Schulz', (not debug), prefix);
Basim := PrepareUnit('Basim', (not debug), prefix);
Lemmy := PrepareUnit('Lemmy', (not debug), prefix);
Mia := PrepareUnit('Mia', (not debug), prefix);
Nia := PrepareUnit('Nia', (not debug), prefix);
Jenna := PrepareUnit('Jenna', (not debug), prefix);

schulzSecondSquad := CreateCharacterSet('09c_schulzSecondSquad');

if debug then
   begin
   schulzSecondSquad := [];

   for i := 1 to 3 do
       begin
       uc_side := 2;
       uc_nation := nation_arabian;

       PrepareHuman(false, i, 0);
       schulzSecondSquad := Join(schulzSecondSquad, CreateHuman);
       end;
   end;

//    1            2               3               4            5             6            7           8
// GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetFuel(i), GetMark(i), IsDrivenBy(i)
vehicles := LoadVariable('09c_vehicles', [
               [13,3,1,28,2,0,0,36],
               [13,3,1,28,2,0,0,41],
               [13,3,1,28,2,0,0,37],
               [13,3,1,28,2,0,0,35],
               [13,3,1,28,2,0,0,34],
               [13,3,1,28,2,0,0,38],
               [13,3,1,28,2,0,0,40],
               [13,3,1,28,2,0,0,39]
            ]);

// Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf, Kurt, Aviradze
vehicles := vehicles diff SpawnInto(Heike, LoadVariable('09c_Heike_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Givi, LoadVariable('09c_Givi_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Mike, LoadVariable('09c_Mike_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Ibrahim, LoadVariable('09c_Ibrahim_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Kamil, LoadVariable('09c_Kamil_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Kaia, LoadVariable('09c_Kaia_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Sophia, LoadVariable('09c_Sophia_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Markov, LoadVariable('09c_Markov_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Olaf, LoadVariable('09c_Olaf_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Kurt, LoadVariable('09c_Kurt_ID', -1), vehicles);
vehicles := vehicles diff SpawnInto(Aviradze, LoadVariable('09c_Aviradze_ID', -1), vehicles);

vehicles := vehicles diff SpawnInto(Schulz, LoadVariable('09c_Schulz_ID', 34), vehicles);
vehicles := vehicles diff SpawnInto(Lemmy, LoadVariable('09c_Lemmy_ID', 35), vehicles);
vehicles := vehicles diff SpawnInto(Mia, LoadVariable('09c_Mia_ID', 36), vehicles);
vehicles := vehicles diff SpawnInto(Nia, LoadVariable('09c_Nia_ID', 37), vehicles);
vehicles := vehicles diff SpawnInto(Jenna, LoadVariable('09c_Jenna_ID', 38), vehicles);
vehicles := vehicles diff SpawnInto(Basim, LoadVariable('09c_Basim_ID', -1), vehicles);

for i := 1 to schulzSecondSquad do
    vehicles := vehicles diff SpawnInto(schulzSecondSquad[i], LoadVariable('09c_random_ID_' & i, 38 + i), vehicles);

for i := 1 to heikeSecondSquad do
    vehicles := vehicles diff SpawnInto(heikeSecondSquad[i], LoadVariable('09c_hrandom_ID_' & i, -1), vehicles);

if FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]) then
   begin
   for j := Count(vehicles) downto 1 do
       begin
       i := vehicles[j];

       if i[3] = control_remote then
          begin
          uc_side := 2;
          uc_nation := i[5];
          PrepareVehicle(i[1], i[2], i[3], i[4], i[6]);
          veh := CreateVehicle;
          SetDir(veh, 5);
          PlaceUnitArea(veh, startArea, false);

          vehicles := Delete(vehicles, j);

          Connect(veh);
          wait(0$0.3);
          end;
       end;
   end;

//  1              2                3             4             5              6            7        8         9        10           11
// GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetFuel(i), GetMark(i), GetX(i), GetY(i), GetDir(i), GetLives(i)
vehicles := LoadVariable('09_otherVehicles', []);

if vehicles then
   begin
   for i in vehicles do
       begin
       uc_side := 2;
       uc_nation := i[5];
       PrepareVehicle(i[1], i[2], i[3], i[4], i[6]);
       veh := CreateVehicle;
       SetDir(veh, 5);

       if i[7] then
          SetMark(veh, i[7]);

       PlaceUnitXYD(veh, i[8], i[9], i[10], false);

       SetFuel(veh, i[6]);
       end;
   end;
End;

Function SpawnInto(unit, number, vehicles);
var i, veh;
begin
for i in vehicles do
    if i[8] = number and i[3] = control_manual then
       begin
       result := i;

       uc_side := 2;
       uc_nation := i[5];
       PrepareVehicle(i[1], i[2], i[3], i[4], i[6]);
       veh := CreateVehicle;
       SetDir(veh, 5);

       if i[7] < 0 then
          SetMark(veh, i[7]);

       if not PlaceUnitInArea(veh, startArea, false) then
          begin
          SetDir(unit, 5);
          PlaceUnitArea(unit, startInfArea, false);
          exit;
          end;

       PlaceHumanInUnit(unit, veh);
       exit;
       end;

SetDir(unit, 5);
PlaceUnitArea(unit, startInfArea, false);
End;

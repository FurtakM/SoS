Export Function Action;
var i, un, veh, tmp;
begin
InGameOn;

CenterNowOnXY(179, 5);

veh := PrepareTank(2, 2, ar_light_trike, engine_combustion, control_manual, ar_double_machine_gun, 33);
SetDir(veh, 2);
PlaceUnitXY(veh, 178, 2, false);

PlaceHumanInUnit(Heike, veh);

if Kamil then
   begin
   PlaceUnitXY(Kamil, 207, 37, false);
   ComTurnXY(Kamil, 211, 38);
   end;

if Sophia then
   begin
   PlaceUnitXY(Sophia, 209, 40, false);
   ComTurnXY(Sophia, 211, 38);
   end;

if Givi then
   begin
   PlaceUnitXY(Givi, 201, 34, false);
   ComTurnXY(Givi, 206, 36);
   end;

if Ibrahim then
   begin
   PlaceUnitXY(Ibrahim, 202, 39, false);
   ComTurnXY(Ibrahim, 206, 36);
   end;

if Markov then
   begin
   veh := PrepareTank(2, 2, ar_medium_trike, engine_combustion, control_manual, ar_gatling_gun, 80 - 20 * Difficulty);
   SetDir(veh, 2);
   PlaceUnitXY(veh, 197, 35, false);

   PlaceHumanInUnit(Markov, veh);
   end;

tmp := Kaia union heikeSecondSquad;

for i in tmp do
    begin
    PlaceUnitXYR(i, 203, 41, 6, false);
    ComHold(i);
    end;

if not FilterAllUnits([[f_side, 2], [f_class, 2]]) then
   SetClass(Heike, 2);

ComMoveXY(Heike, 183, 12);
AddComMoveXY(Heike, 188, 20);
AddComMoveXY(Heike, 191, 25);
AddComMoveXY(Heike, 203, 29);
AddComExitVehicle(Heike);

repeat
 wait(0$1);
until not IsInUnit(Heike);

CenterOnUnits(Heike);

tmp := FilterAllUnits([[f_side, 2], [f_type, unit_human]]) diff [Heike, Markov];

ComTurnUnit(tmp, Heike);

if Givi then
   ComTurnUnit(Heike, Givi)
else
   ComTurnUnit(Heike, tmp[1]);

Say(Heike, 'DH-Start-1');

if Givi then
   begin
   Say(Givi, 'DG-Start-2');

   if not Mike then
      begin
      Say(Givi, 'DG-Start-b-3');
      Say(Heike, 'DH-Start-b-4');
      end;
   end
else
   if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-Start-a-2') then
      Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-Start-a-2');

ComTurnXY(Heike, 211, 38);

wait(0$1);

Say(Heike, 'DH-Start-3');
Say(Heike, 'DH-Start-4');

InGameOff;

ChangeMissionObjectives('Prepare');

SaveForQuickRestart;

wait(0$10);

if not Aviradze then
   exit;

SetDir(Aviradze, 4);
PlaceUnitXY(Aviradze, 225, 33, false);

for i := 1 to 2 do
    begin
    uc_side := 2;
    uc_nation := 0;                       

    PrepareHuman(false, class_apeman, 1);
    hc_gallery := '';
    hc_name := ['Artudito', 'Textur'][i];
    un := CreateHuman;
    PlaceUnitXY(un, [224, 226][i], [31, 35][i], false);
    ComMoveXY(un, 215, 36);
    end;

hc_name := '';

InitHc;

ComMoveXY(Aviradze, 215, 36);

Say(Aviradze, 'DA-Arrive-1');
Say(Heike, 'DH-Arrive-2');
Say(Aviradze, 'DA-Arrive-3');
End;

Every 0$2 trigger baseConstructed do
var i, un, veh, p;
begin
DialogueOn;

Say(Heike, 'DH-base-1');

if Markov then
   begin
   Say(Markov, 'DMar-base-2');
   Say(Markov, 'DMar-base-3');
   Say(Heike, 'DH-base-4');
   Say(Markov, 'DMar-base-5');
   end
else
   begin
   if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-base-a-2') then
      Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-base-a-2');

   Say(Heike, 'DH-base-a-3');
   end;

Say(Aviradze, 'DA-base-6');
Say(Aviradze, 'DA-base-7');

DialogueOff;

ChangeMissionObjectives('Flamethrower');

wait(2$00);

// move Kurt to Heike
ComMoveXY([Kurt, Mike], 203, 37);

if not FilterAllUnits([[f_side, 2], [f_distxy, 203, 37, 10]]) then
   AddComMoveXY([Kurt, Mike], GetX(Heike), GetY(Heike));

repeat
 wait(0$1);
until See(2, Kurt) and (GetDistUnits(Kurt, Heike) < 8 or GetDistUnits(Kurt, NearestUnitToUnit(FilterAllUnits([[f_side, 2], [f_type, unit_building]]), Kurt)) < 7) and not IsInArea(Kurt, hillArea);

InGameOn;

CenterNowOnUnits(Kurt);

if IsInUnit(Heike) then
   ComExitBuilding(Heike);

ComMoveUnit([Kurt, Mike], Heike);
AddComTurnUnit(Heike, Kurt);

if FilterAllUnits([[f_side, 2], [f_dist, Kurt, 10], [f_type, unit_building]]) then
   begin
   Say(Kurt, 'DKs-scout-1');
   Say(Heike, 'DH-scout-2');
   end;

CenterOnUnits(Heike);

if IsInUnit(Heike) then
   ComExit(Heike);

repeat
 wait(0$1);
 ComMoveUnit(Heike, Kurt); 
until GetDistUnits(Heike, Kurt) < 6;

ComTurnUnit([Kurt, Mike], Heike);

Say(Kurt, 'DKs-scout-3');

if Kaia and GetDistUnits(Kaia, Kurt) < 15 then
   begin
   p := 1;

   if IsInUnit(Kaia) then
      ComExit(Kaia);

   wait(0$1);

   ComMoveUnit(Kaia, Kurt);

   repeat
    wait(0$1);
    p := Inc(p);
   until GetDistUnits(Kaia, Kurt) < 6 or p > 12;

   ComTurnUnit(Kaia, Kurt);

   Say(Kaia, 'DK-scout-4');

   ComTurnUnit(Kurt, Kaia);

   Say(Kurt, 'DKs-scout-5');
   Say(Kaia, 'DK-scout-6');

   if Mike then
      begin
      Say(Mike, 'DM-scout-7');

      ComTurnUnit(Kaia, Mike);

      Say(Kaia, 'DK-scout-8');
      Say(Mike, 'DM-scout-9');
      end;
   end;

PlaceSeeing(184, 100, 2, -12);
RemoveSeeing(184, 100, 2);

CenterOnXY(184, 100);

Say(Kurt, 'DKs-scout-10');

wait(0$1);

PlaceSeeing(144, 65, 2, -12);
RemoveSeeing(144, 65, 2);

CenterOnXY(144, 65);

Say(Kurt, 'DKs-scout-11');

wait(0$1);

CenterNowOnUnits(Kurt);

if Mike and not Givi then
   begin                      
   Say(Mike, 'DM-scout-a-1');
   Say(Heike, 'DH-scout-a-2');
   end;

SetSide([Kurt, Mike], 2);

InGameOff;

ChangeMissionObjectives('Hill');

Hint('BunkerHill');

SetAreaMapShow(dontGoArea, 1);

allowToPatrol := true;

wait(1$30);

InitHc;

for i := 1 to [3, 3, 2, 2][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareHuman(false, [1, 3, 1][i mod 3 + 1], [3, 2, 2, 2][Difficulty]);
    un := CreateHuman;
    heikeSecondSquad := heikeSecondSquad union un;

    PrepareTank(2, 2, ar_medium_trike, [engine_solar, engine_combustion][i mod 2 + 1], control_manual, [ar_gun, ar_gatling_gun, ar_gun, ar_light_gun, ar_light_gun][Difficulty + rand(0, 1)], 77);
    veh := CreateVehicle;
    SetDir(veh, 2);
    PlaceUnitXYR(veh, 178, 2, 2, false);
    PlaceHumanInUnit(un, veh);

    ComMoveXY(un, 183, 11);

    wait(0$2);
    end;
End;

Every 0$2 trigger labConstructed and IsOk(Aviradze) do
begin
DialogueOn;

Say(Aviradze, 'DA-lab-1');
Say(Aviradze, 'DA-lab-2');
Say(Heike, 'DH-lab-3');

DialogueOff;
End;

Every 0$2 trigger GetTech(tech_HidCam, 2) = state_researched do
var i;
begin
DialogueOn;

Say(Aviradze, 'DA-Cam-1');
Say(Aviradze, 'DA-Cam-2');
Say(Aviradze, 'DA-Cam-3');
Say(Heike, 'DH-Cam-4');
Say(Kaia, 'DK-Cam-5');
Say(Kaia, 'DK-Cam-6');

DialogueOff;

Query('Camera');

hiddenCameraAllowed := true;

for i in camAreas do
    SetAreaMapShow(i, 1);
End;

Every 0$1 trigger not camAreas and hiddenCameraAllowed do
var i;
begin
stop_talking := true;

for i in FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, 2]]) do
    AddExperience(i, 2, 450);

stop_talking := false;

SetAchievement('ACH_CAMERA');
End;

// Spotted
Every 0$1 trigger russianAlert and not allowHeikeAttack do
begin
PlaceSeeing(GetX(russianAlert), GetY(russianAlert), 2, -6);
CenterNowOnUnits(russianAlert);
wait(0$1);
YouLost('Spotted');
End;

Every 0$5 trigger SeeEnemy([3, 6], 2, russianPatrol) do
russianAlert := SeeEnemy([3, 6], 2, russianPatrol);

Every 0$1 trigger FilterAllUnits([[f_side, 6], [f_btype, b_bunker], [f_not, [f_lives, 1000]]]) do
russianAlert := FilterAllUnits([[f_side, 6], [f_btype, b_bunker], [f_not, [f_lives, 1000]]])[1];

Every 0$1 trigger russianAlert and FilterUnitsInArea(kirovBaseArea, [[f_type, unit_building], [f_not, [f_lives, 1000]]]) do
var i, tmp, towers, noncombat;
begin
noncombat := Popov union UnitFilter(kirovBaseSquad union beriaBaseSquad, [f_not, [f_class, 1]]);

for i in noncombat do
    begin
    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveXY(i, 114, 97);
    AddComMoveXY(i, 99, 128);
    AddComMoveXY(i, 91, 145);
    AddComMoveXY(i, 82, 153);
    AddComMoveToArea(i, westEscapeArea);
    end;

if not allowHeikeAttack then
   exit;

wait(0$05);

SayRadio(Kurin, 'D9a-Kur-1');
SayRadio(Burlak, 'D9a-Bur-1');
SayRadio(Kurin, 'D9a-Kur-2');

omicronFall := true;

wait(0$3);

retreatFromOmicron := true;
End;

// escapist
Every 0$1 trigger FilterUnitsInArea(westEscapeArea, [[f_not, [f_side, 2]], [f_nation, 3]]) do
var i;
begin
enable;

for i in FilterUnitsInArea(westEscapeArea, [[f_not, [f_side, 2]], [f_nation, 3]]) do
    begin
    escapeCounter := escapeCounter + 1;
    RemoveUnit(i);
    end;
End;

// american reinforcements
Every 0$1 trigger tick >= 6$30 + rand(0, 1$30) do
SmallAmericanReinforcements;

// russian starts to attack omicron
Every 0$1 trigger tick >= [15$00, 14$00, 13$00, 12$00][Difficulty] do
begin
ComAgressiveMove(UnitFilter(russianOmicronAttackers, [f_type, unit_vehicle]), 62, 22);
ComMoveXY(russianCargo, 69, 27);

wait(1$00);

startAttackOnOmicron := true;
End;

// omar scene
Every 0$2 trigger omarCountdown = 0 do
var i, driver, veh, un, tmp, tmp2, enemy;
begin
ChangeSideFog(5, 2);

PrepareOmarForces;

wait(0$03);

allowHeikeAttack := true;
activeGrishkoAI := false;

SetAreaMapShow(dontGoArea, 0);

if not hillBase then
   ChangeMissionObjectives('HillDone');

CenterOnUnits(Omar);

for i in FilterAllUnits([[f_side, 2], [f_type, unit_human]]) do
    if HasTask(i) then
       ComHold(i);

DialogueOn;
SayRadio(Omar, 'DO-assault-1');
DialogueOff;

game_speed := 4;

for i in UnitFilter(grishkoSquad, [f_not, [f_inside]]) do
    ComMoveXY(i, 226, 114);

ComEnterUnit(Grishko, NearestUnitToUnit(FilterAllUnits([[f_side, 6], [f_btype, b_armoury]]), Grishko));

ChangeMissionObjectives('Attack');

music_class := music_combat;
music_nat := 4;

DoNotAttack(5, Grishko);
DoNotAttack(6, UnitFilter(omarSquad, [f_class, 1])[4]);

for i in UnitFilter(omarSquad, [f_not, [f_class, 4]]) do
    ComAgressiveMove(i, 221, 110);

ComMoveXY(UnitFilter(omarSquad, [f_class, 4]), 214, 124);

ComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 214, 114, 0);

if IsOk(Grishko) then
   begin
   SayRadio(Grishko, 'D9-Gri-1');
   SayRadio(Kurin, 'D9-Kur-1');
   end;

wait(0$1);

ComAgressiveMove(Omar, 233, 130);

if IsOk(Grishko) then
   begin
   SayRadio(Grishko, 'D9-Gri-2');
   SayRadio(Kurin, 'D9-Kur-2');
   end;

ComExitBuilding(Grishko);
AddComMoveXY(Grishko, 211, 104);
AddComHold(Grishko);

repeat
 wait(3);
until not IsInUnit(Grishko);

ComAttackUnit(UnitFilter(omarSquad, [f_weapon, ar_flame_thrower]), Grishko);
AddComAgressiveMove(UnitFilter(omarSquad, [f_weapon, ar_flame_thrower]), 215, 106);

repeat
 wait(3);
until GetLives(Grishko) < 1000;

NormalAttack(5, Grishko);
NormalAttack(6, UnitFilter(omarSquad, [f_class, 1])[4]);

if IsOk(Grishko) then
   begin
   SayRadio(Grishko, 'D9-Gri-3');
   SayRadio(Kurin, 'D9-Kur-3');
   end;

music_class := music_auto;
music_nat := music_auto;

for i in omarSquad do
    begin
    if GetType(i) = unit_vehicle and GetLives(i) < 1000 then
       begin
       driver := IsDrivenBy(i);
       veh := i;
       ComExitVehicle(driver);
       AddComRepairVehicle(driver, veh);
       AddComEnterUnit(driver, veh);
       end
    else if GetLives(i) < 500 then
       begin
       AddComHeal(UnitFilter(omarSquad, [f_class, 4]), i);
       end;
    end;

repeat
 wait(0$1);
until not UnitFilter(omarSquad, [[f_not, [f_lives, 1000]], [f_type, unit_vehicle]]) and not UnitFilter(omarSquad, [f_not, [f_lives, 250]]);

wait(0$2);

tmp := UnitFilter(omarSquad, [[f_empty], [f_type, unit_vehicle]]);
tmp2 := UnitFilter(omarSquad, [[f_class, 3], [f_not, [f_driving]]]);

for i := 1 to tmp do
    ComEnterUnit(tmp2[i], tmp[i]);

wait(0$5);

ComAgressiveMove(omarSquad, 199, 141);
ComAgressiveMove(Omar, 190, 143);
AddComHold(Omar);

// attack on kirov
AddComAgressiveMove(UnitFilter(omarSquad, [f_or, [f_class, 1], [f_class, 4]]), 161, 130);
AddComAgressiveMove(UnitFilter(omarSquad, [f_class, 1]), 163, 122);
AddComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 165, 118, 0);
AddComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 159, 116, 0);

AddComAgressiveMove(UnitFilter(omarSquad, [f_type, unit_vehicle]), 169, 114);

repeat
 wait(0$1);

 tmp := UnitFilter(omarSquad, [f_class, 1]);

 for i in tmp do
     begin
     if GetLives(i) < 600 and GetDistUnitXY(i, 161, 130) > 6 then
        ComMoveXY(i, 161, 130);

     if GetLives(i) = 1000 and not HasTask(i) then
        ComAgressiveMove(i, 160, 109);
     end;

 tmp := UnitFilter(omarSquad, [f_type, unit_vehicle]);

 for i in tmp do
     begin
     tmp2 := IsDrivenBy(i);

     if not tmp2 then
        continue;

     if GetLives(i) >= 600 and GetTag(i) = 1 then
        SetTag(i, 0);

     if GetLives(i) >= 600 and not GetTag(i) then
        ComAttackUnit(i, NearestUnitToUnit(enemy, i));

     if GetLives(i) < 600 and not GetTag(i) then
        begin
        SetTag(i, 1);
        ComMoveXY(i, 202, 150);
        end;
                                       
     if GetTag(i) = 1 and GetDistUnitXY(i, 202, 150) < 6 and tmp2 then
        begin
        ComExitVehicle(tmp2);
        AddComRepairVehicle(tmp2, i);
        AddComWait(tmp2, 0$20);
        AddComEnterUnit(tmp2, i);
        end;
     end;
until kirovDestroyed;

repeat
 wait(0$1);

 for i in omarSquad union Omar do
     if IsInArea(i, southRoadArea) then
        RemoveUnit(i)
     else
        begin
        if GetClass(i) = 3 and not IsInUnit(i) and FilterAllUnits([[f_side, 5], [f_lives, 251], [f_type, unit_vehicle], [f_empty]]) then
           ComEnterUnit(i, FilterAllUnits([[f_side, 5], [f_type, unit_vehicle], [f_empty]])[1])
        else
           ComMoveXY(i, 208, 162);
        end;
until not FilterAllUnits([[f_side, 5], [f_type, unit_human]]);
End;

// kirov
Every 0$1 trigger IsDead(kirov) do
Say(Heike, 'DH-1-Kirov');

// beria
Every 0$1 trigger See(2, beria) do
Say(Heike, 'DH-1-Beria');

// kagan
Every 0$1 trigger See(2, kagan) do
begin
DialogueOn;
CenterNowOnUnits(kagan);

Say(Heike, 'DH-1-Kagan');

if Kamil then
   begin
   Say(Kamil, 'DKam-Kagan-2');
   Say(Givi, 'DG-Kagan-3');
   Say(Givi, 'DG-Kagan-4');
   end;

case Query('KaganBase') of
1: begin
   Say(Heike, 'DH-Kagan-5');
   Say(Heike, 'DH-Kagan-6');
   SayRadio(Shariff, 'DShar-Kagan-7');
   Say(Heike, 'DH-Kagan-8');
   Say(Heike, 'DH-Kagan-9');
   SayRadio(Shariff, 'DShar-Kagan-10');
   Say(Heike, 'DH-Kagan-11');
   kaganCaptured := true;
   end;
2: begin
   if FilterAllUnits([f_side, 8]) and Aviradze then
      Say(Heike, 'DH-Kagan-a-5');

   Say(Heike, 'DH-Kagan-a-6');
   Say(Heike, 'DH-Kagan-a-7');
   Say(Givi, 'DG-Kagan-a-8');
   end;
end;

DialogueOff;
End;

Every 0$1 trigger kaganDestroyed or GetSide(kagan) = 2 do
ChangeMissionObjectives('KaganDone');

// see omicron attackers
Every 0$1 trigger startAttackOnOmicron and UnitFilter(russianOmicronAttackers, [f_inarea, russianOmParkingArea]) do
begin
DialogueOn;

CenterNowOnXY(63, 32);
PlaceSeeing(63, 32, 2, -12);

Say(Heike, 'DH-Omicron-1');
Say(Mike, 'DM-Omicron-2');

RemoveSeeing(63, 32, 2);
DialogueOff;
End;

// see enemy lab
Every 0$3 trigger (See(2, ruLab) or GetSide(ruLab) = 2 or GetSide(beria) = 2) and IsOk(ruLab) and IsOk(Heike) and IsOk(Aviradze) do
var i, tmp, tmp2, q, settings;
begin
DialogueOn;

PlaceSeeing(111, 90, 2, -6);
CenterNowOnXY(111, 90);

Say(Aviradze, 'DA-Cap-1');
Say(Heike, 'DH-Cap-2');

RemoveSeeing(111, 90, 2);

DialogueOff;

repeat
 wait(0$1);
until (beriaDestroyed or GetSide(beria) = 2 or GetSide(ruLab) = 2) and IsOk(ruLab) and IsOk(Aviradze) and not InBattle(2) and not FilterAllUnits([[f_side, 3], [f_type, unit_human], [f_distxy, 110, 95, 12]]) and not FilterAllUnits([[f_side, 3], [f_btype, b_barracks], [f_distxy, 110, 95, 12]]);

tmp := heikeSecondSquad union [Ibrahim, Kamil, Sophia, Kaia];
tmp := UnitFilter(tmp, [f_ok]);

if tmp < 2 then
   q := 2
else
   q := Query('LabCap');

case q of
1: begin
   DoNotAttack(2, ruLab);

   labCaptured := true;
   Say(Heike, 'DH-Cap-a-1');
   Say(Aviradze, 'DA-Cap-a-2');

   settings := [Aviradze, sel_changeable] ^
               [sel_not_hired, sel_not_changeable, Heike, Givi, Kurt, Markov] ^
               [sel_changeable] ^ tmp;
   tmp2 := CharacterSelection('Select two characters to go with Aviradze', 2, 2, settings, []);

   SetSide(ruLab, 8);
   SetSide(Aviradze, 8);

   aviradzeSquad := [Aviradze];

   if IsInUnit(Aviradze) then
      ComExit(Aviradze);

   wait(3);

   ComEnterUnit(Aviradze, ruLab);

   for i := 1 to 2 do
       begin
       aviradzeSquad := aviradzeSquad union tmp2[i];
       SetSide(tmp2[i], 8);

       if IsInUnit(tmp2[i]) then
          ComExit(tmp2[i]);

       wait(3);

       ComEnterUnit(tmp2[i], ruLab);
       end;

   SetAchievement('ACH_LAB');
   end;
2: begin
   Say(Heike, 'DH-Cap-b-1');
   Say(Aviradze, 'DA-Cap-b-2');
   end;
End;

End;

Every 0$1 trigger FilterUnitsInArea(hillArea, [[f_side, 2], [f_btype, b_breastwork]]) >= 2 do
SetAchievement('ACH_KING');

Every 0$1 trigger not allowHeikeAttack and FilterUnitsInArea(hillArea, [[f_side, 2], [f_or, [f_btype, b_breastwork], [f_btype, b_armoury]]]) do
begin
hillBase := true;
ChangeMissionObjectives('HillDone');
End;

Every 0$1 trigger Kaia and GetLives(Kaia) < 3 do
begin
DialogueOn;
CenterNowOnUnits(Kaia);
ForceSay(Kaia, 'DK-Kkilled-1');
Say(Heike, 'DH-Kkilled-2');
DialogueOff;
End;

Every 0$1 trigger kaiaKilledByRussian do
Say(Heike, 'DH-Kkilled-3');

// go to west
Every 0$1 trigger (kirovDestroyed and (GetSide(beria) = 2 or beriaDestroyed) and (kaganDestroyed or (kaganCaptured and GetSide(kagan) = 2))) do
var i, tmp, tmp2, cargo, driver;
begin
ChangeMissionObjectives('GoToWest');
SetAreaMapShow(goToWestArea, 1);

repeat
 wait(0$1);
 tmp := FilterUnitsInArea(goToWestArea, [[f_side, 2], [f_or, [f_type, unit_human], [[f_type, unit_vehicle], [f_not, [f_empty]]]]]);
 tmp2 := FilterAllUnits([[f_side, 2], [f_not, [f_driving]], [f_or, [f_type, unit_human], [[f_type, unit_vehicle], [f_not, [f_empty]]]]]);
until tmp + 0 = tmp2 + 0;

SaveCharacters(Heike, '04_1_Heike');

if Aviradze and not Aviradze in aviradzeSquad then
   SaveCharacters(Aviradze, '04_1_Aviradze')
else if Aviradze then
   SaveCharacters(Aviradze, '04_1_Aviradze_L');

// Aviradze squad possible: [Ibrahim, Kamil, Sophia, Kaia]

if Ibrahim and not Ibrahim in aviradzeSquad then
   SaveCharacters(Ibrahim, '04_1_Ibrahim')
else if Ibrahim then
   SaveCharacters(Ibrahim, '04_1_Ibrahim_L');

if Kamil and not Kamil in aviradzeSquad then
   SaveCharacters(Kamil, '04_1_Kamil')
else if Kamil then
   SaveCharacters(Kamil, '04_1_Kamil_L');

if Sophia and not Sophia in aviradzeSquad then
   SaveCharacters(Sophia, '04_1_Sophia')
else if Sophia then
   SaveCharacters(Sophia, '04_1_Sophia_L');

if Kaia and not Kaia in aviradzeSquad then
   SaveCharacters(Kaia, '04_1_Kaia')
else if Kaia then
   SaveCharacters(Kaia, '04_1_Kaia_L');

if Givi then
   SaveCharacters(Givi, '04_1_Givi');

if Mike then
   SaveCharacters(Mike, '04_1_Mike');

if Markov then
   SaveCharacters(Markov, '04_1_Markov');

if Kurt then
   begin
   SaveCharacters(Kurt, '04_1_Kurt');
   ToLua('enableBioCharacter(BIO_KURT)');
   end;

if heikeSecondSquad diff aviradzeSquad then
   SaveCharacters(heikeSecondSquad diff aviradzeSquad, '04_1_others');

if aviradzeSquad and heikeSecondSquad then
   begin
   tmp := [];

   for i in heikeSecondSquad do
       if i in aviradzeSquad then
          tmp := Replace(tmp, tmp + 1, i);

   if tmp then
      SaveCharacters(tmp, '04_1_others_L');
   end;

tmp := FilterAllUnits([[f_side, 2], [f_nation, 0]]);

if tmp then
   SaveCharacters(tmp, '04_1_apes');

tmp := FilterUnitsInArea(goToWestArea, [f_type, unit_vehicle]);

if tmp then
   begin
   tmp2 := [];

   for i in tmp do
       begin
       driver := IsDrivenBy(i);

       if not driver then
          continue;

       if GetWeapon(i) in [ar_cargo_bay, us_cargo_bay, ru_cargo_bay, ru_big_cargo_bay] then
          begin
          cargo := [GetCargo(i, mat_cans), GetCargo(i, mat_oil)];
          tmp2 := Replace(tmp2, tmp2 + 1, [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), cargo, GetClass(driver)]);
          end
       else
          tmp2 := Replace(tmp2, tmp2 + 1, [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), [], GetClass(driver)]);
       end;

   SaveVariable(tmp2, '04_1_Vehicles');
   end;

SaveVariable(labCaptured, '04_1_RuLabCaptured');
SaveVariable(escapeCounter, '04_1_EscapeCounter');
SaveVariable(hillBase, '04_1_BaseHill');
SaveVariable(tick, '04_1_Time');
SaveVariable(loseCounter, '04_1_DeadCounter');
SaveVariable(kaganCaptured * GetSide(kagan) = 2, '04_1_KaganCaptured');
SaveVariable(FilterAllUnits([[f_side, 2], [f_weapon, ar_flame_thrower]]) + 0, '04_1_Flamethrower');

ChangeMap(1, '__x1\04_cont2');
End;


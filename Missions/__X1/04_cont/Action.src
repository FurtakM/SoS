Export Function Action;
var i, un, veh, tmp;
begin
InGameOn;

CenterNowOnXY(179, 5);

veh := PrepareTank(2, 2, ar_light_trike, engine_combustion, control_manual, ar_double_machine_gun, 33);
SetDir(veh, 2);
PlaceUnitXY(veh, 178, 2, false);

PlaceHumanInUnit(Heike, veh);

if Kamil then
   begin
   PlaceUnitXY(Kamil, 207, 37, false);
   ComTurnXY(Kamil, 211, 38);
   end;

if Sophia then
   begin
   PlaceUnitXY(Sophia, 209, 40, false);
   ComTurnXY(Sophia, 211, 38);
   end;

if Givi then
   begin
   PlaceUnitXY(Givi, 201, 34, false);
   ComTurnXY(Givi, 206, 36);
   end;

if Ibrahim then
   begin
   PlaceUnitXY(Ibrahim, 202, 39, false);
   ComTurnXY(Ibrahim, 206, 36);
   end;

if Markov then
   begin
   veh := PrepareTank(2, 2, ar_medium_trike, engine_combustion, control_manual, ar_gatling_gun, 80 - 20 * Difficulty);
   SetDir(veh, 2);
   PlaceUnitXY(veh, 197, 35, false);

   PlaceHumanInUnit(Markov, veh);
   end;

tmp := Kaia union heikeSecondSquad;

for i in tmp do
    begin
    PlaceUnitXYR(i, 203, 41, 6, false);
    ComHold(i);
    end;

if not FilterAllUnits([[f_side, 2], [f_class, 2]]) then
   SetClass(Heike, 2);

ComMoveXY(Heike, 183, 12);
AddComMoveXY(Heike, 188, 20);
AddComMoveXY(Heike, 191, 25);
AddComMoveXY(Heike, 203, 29);
AddComExitVehicle(Heike);

repeat
 wait(0$1);
until not IsInUnit(Heike);

CenterOnUnits(Heike);

tmp := FilterAllUnits([[f_side, 2], [f_type, unit_human]]) diff [Heike, Markov];

ComTurnUnit(tmp, Heike);

if Givi then
   ComTurnUnit(Heike, Givi)
else
   ComTurnUnit(Heike, tmp[1]);

Say(Heike, 'DH-Start-1');

if Givi then
   begin
   Say(Givi, 'DG-Start-2');

   if not Mike then
      begin
      Say(Givi, 'DG-Start-b-3');
      Say(Heike, 'DH-Start-b-4');
      end;
   end
else
   if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-Start-a-2') then
      Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-Start-a-2');

ComTurnXY(Heike, 211, 38);

wait(0$1);

Say(Heike, 'DH-Start-3');
Say(Heike, 'DH-Start-4');

InGameOff;

ChangeMissionObjectives('Prepare');

wait(0$10);

if not Aviradze then
   exit;

SetDir(Aviradze, 4);
PlaceUnitXY(Aviradze, 225, 33, false);

for i := 1 to 2 do
    begin
    uc_side := 2;
    uc_nation := 0;                       

    PrepareHuman(false, class_apeman, 1);
    hc_gallery := '';
    hc_name := ['Artudito', 'Textur'][i];
    un := CreateHuman;
    PlaceUnitXY(un, [224, 226][i], [31, 35][i], false);
    ComMoveXY(un, 215, 36);
    end;

hc_name := '';

InitHc;

ComMoveXY(Aviradze, 215, 36);
End;

Every 0$2 trigger baseConstructed do
var i, un, veh;
begin
DialogueOn;

Say(Heike, 'DH-base-1');

if Markov then
   begin
   Say(Markov, 'DMar-base-2');
   Say(Markov, 'DMar-base-3');
   Say(Heike, 'DH-base-4');
   Say(Markov, 'DMar-base-5');
   end
else
   begin
   if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-base-a-2') then
      Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-base-a-2');

   Say(Heike, 'DH-base-a-3');
   end;

Say(Aviradze, 'DA-base-6');
Say(Aviradze, 'DA-base-7');

DialogueOff;

ChangeMissionObjectives('Flamethrower');

wait(2$00);

// move Kurt to Heike
ComMoveXY([Kurt, Mike], 203, 37);

if not FilterAllUnits([[f_side, 2], [f_distxy, 203, 37, 10]]) then
   AddComMoveXY([Kurt, Mike], GetX(Heike), GetY(Heike));

repeat
 wait(0$1);
until See(2, Kurt) or GetDistUnits(Kurt, Heike) < 8;

InGameOn;

CenterNowOnUnits(Kurt);

if IsInUnit(Heike) then
   ComExitBuilding(Heike);

ComMoveUnit([Kurt, Mike], Heike);
AddComTurnUnit(Heike, Kurt);

if FilterAllUnits([[f_side, 2], [f_dist, Kurt, 10], [f_type, unit_building]]) then
   begin
   Say(Kurt, 'DKs-scout-1');
   Say(Heike, 'DH-scout-2');
   end;

repeat
 wait(0$1);
until GetDistUnits(Heike, Kurt) < 6;

ComTurnUnit([Kurt, Mike], Heike);

Say(Kurt, 'DKs-scout-3');

if Kaia then
   begin
   if IsInUnit(Kaia) then
      ComExitBuilding(Kaia);

   AddComMoveUnit(Kaia, Kurt);

   repeat
    wait(0$1);
   until GetDistUnits(Kaia, Kurt) < 6;

   ComTurnUnit(Kaia, Kurt);

   Say(Kaia, 'DK-scout-4');

   ComTurnUnit(Kurt, Kaia);

   Say(Kurt, 'DKs-scout-5');
   Say(Kaia, 'DK-scout-6');

   if Mike then
      begin
      Say(Mike, 'DM-scout-7');

      ComTurnUnit(Kaia, Mike);

      Say(Kaia, 'DK-scout-8');
      Say(Mike, 'DM-scout-9');
      end;
   end;

PlaceSeeing(184, 100, 2, -12);
RemoveSeeing(184, 100, 2);

CenterOnXY(184, 100);

Say(Kurt, 'DKs-scout-10');

wait(0$1);

PlaceSeeing(144, 65, 2, -12);
RemoveSeeing(144, 65, 2);

CenterOnXY(144, 65);

Say(Kurt, 'DKs-scout-11');

wait(0$1);

CenterNowOnUnits(Kurt);

if Mike and not Givi then
   begin                      
   Say(Mike, 'DM-scout-a-1');
   Say(Heike, 'DH-scout-a-2');
   end;

SetSide([Kurt, Mike], 2);

InGameOff;

allowToPatrol := true;

wait(1$30);

InitHc;

for i := 1 to [3, 3, 2][Difficulty] do
    begin
    uc_side := 2;
    uc_nation := 2;

    PrepareHuman(false, [1, 3, 1][i mod 3 + 1], [3, 2, 2][Difficulty]);
    un := CreateHuman;
    heikeSecondSquad := heikeSecondSquad union un;

    PrepareTank(2, 2, ar_medium_trike, [engine_solar, engine_combustion][i mod 2 + 1], control_manual, [ar_gun, ar_gatling_gun, ar_gun, ar_light_gun][Difficulty + rand(0, 1)], 77);
    veh := CreateVehicle;
    SetDir(veh, 2);
    PlaceUnitXYR(veh, 178, 2, 2, false);
    PlaceHumanInUnit(un, veh);

    ComMoveXY(un, 183, 11);

    wait(0$2);
    end;
End;

Every 0$2 trigger labConstructed and IsOk(Aviradze) do
begin
DialogueOn;

Say(Aviradze, 'DA-lab-1');
Say(Aviradze, 'DA-lab-2');
Say(Heike, 'DH-lab-3');

DialogueOff;
End;

Every 0$2 trigger GetTech(tech_HidCam, 2) = state_researched do
var i;
begin
DialogueOn;

Say(Aviradze, 'DA-Cam-1');
Say(Aviradze, 'DA-Cam-2');
Say(Aviradze, 'DA-Cam-3');
Say(Heike, 'DH-Cam-4');
Say(Kaia, 'DK-Cam-5');
Say(Kaia, 'DK-Cam-6');

DialogueOff;

Query('Camera');

hiddenCameraAllowed := true;

for i in camAreas do
    SetAreaMapShow(i, 1);
End;

Every 0$1 trigger not camAreas and hiddenCameraAllowed do
var i;
begin
stop_talking := true;

for i in FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_nation, 2]]) do
    AddExperience(i, 2, 450);

stop_talking := false;
End;

// Spotted
Every 0$2 trigger russianAlert and not allowHeikeAttack do
begin
if spottedByPatrol then
   Say(Heike, 'DH-Spot-f-3');

YouLost('Spotted');
End;

Every 0$3 trigger FilterAllUnits([[f_or, [f_side, 3], [f_side, 6]], [f_see, 2]]) diff russianPatrol do
russianAlert := true;

Every 0$1 trigger russianAlert do
var i, tmp, towers, noncombat;
begin
tmp := UnitFilter(kirovBaseSquad, [[f_class, 1], [f_inside]]);
towers := FilterAllUnits([[f_side, 6], [f_btype, b_bunker]]);

for i := 1 to tmp do
    begin
    ComExitBuilding(tmp[i]);
    AddComEnterUnit(tmp[i], towers[i]);
    end;

noncombat := Popov union UnitFilter(kirovBaseSquad union beriaBaseSquad, [f_not, [f_class, 1]]);

for i in noncombat do
    begin
    if IsInUnit(i) then
       ComExitBuilding(i);

    AddComMoveToArea(i, westEscapeArea);
    end;

if not allowHeikeAttack then
   exit;

SayRadio(Kurin, 'D9a-Kur-1');
SayRadio(Burlak, 'D9a-Bur-1');
SayRadio(Kurin, 'D9a-Kur-2');

omicronFall := true;
retreatFromOmicron := true;
End;

// escapist
Every 0$1 trigger FilterUnitsInArea(westEscapeArea, [[f_not, [f_side, 2]], [f_nation, 3]]) do
var i;
begin
enable;

for i in FilterUnitsInArea(westEscapeArea, [[f_not, [f_side, 2]], [f_nation, 3]]) do
    begin
    escapeCounter := escapeCounter + 1;
    RemoveUnit(i);
    end;
End;

// american reinforcements
Every 0$1 trigger tick >= 6$30 + rand(0, 1$30) do
SmallAmericanReinforcements;

// russian starts to attack omicron
Every 0$1 trigger tick >= 9$15 do
begin
ComAgressiveMove(UnitFilter(russianOmicronAttackers, [f_type, unit_vehicle]), 62, 22);
ComMoveXY(russianCargo, 69, 27);

wait(1$00);

startAttackOnOmicron := true;
End;

// omar scene
Every 0$2 trigger tick >= 20$00 do
var i, driver, veh, un, tmp, tmp2, enemy;
begin
ChangeSideFog(5, 2);

PrepareOmarForces;

wait(0$03);

allowHeikeAttack := true;
activeGrishkoAI := false;
CenterOnUnits(Omar);

for i in FilterAllUnits([[f_side, 2], [f_type, unit_human]]) do
    if HasTask(i) then
       ComHold(i);

DialogueOn;
SayRadio(Omar, 'DO-assault-1');
DialogueOff;

for i in UnitFilter(grishkoSquad, [f_not, [f_inside]]) do
    ComMoveXY(i, 226, 114);

ComEnterUnit(Grishko, NearestUnitToUnit(FilterAllUnits([[f_side, 6], [f_btype, b_armoury]]), Grishko));

ChangeMissionObjectives('Attack');

music_class := music_combat;
music_nat := 4;

DoNotAttack(5, Grishko);
DoNotAttack(6, UnitFilter(omarSquad, [f_class, 1])[4]);

for i in UnitFilter(omarSquad, [f_not, [f_class, 4]]) do
    ComAgressiveMove(i, 221, 110);

ComMoveXY(UnitFilter(omarSquad, [f_class, 4]), 214, 124);

ComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 214, 114, 0);

SayRadio(Grishko, 'D9-Gri-1');
SayRadio(Kurin, 'D9-Kur-1');

wait(0$1);

ComAgressiveMove(Omar, 233, 130);

SayRadio(Grishko, 'D9-Gri-2');
SayRadio(Kurin, 'D9-Kur-2');

ComExitBuilding(Grishko);
AddComMoveXY(Grishko, 211, 104);
AddComHold(Grishko);

repeat
 wait(3);
until not IsInUnit(Grishko);

ComAttackUnit(UnitFilter(omarSquad, [f_weapon, ar_flame_thrower]), Grishko);
AddComAgressiveMove(UnitFilter(omarSquad, [f_weapon, ar_flame_thrower]), 215, 106);

repeat
 wait(3);
until GetLives(Grishko) < 1000;

NormalAttack(5, Grishko);
NormalAttack(6, UnitFilter(omarSquad, [f_class, 1])[4]);

SayRadio(Grishko, 'D9-Gri-3');
SayRadio(Kurin, 'D9-Kur-3');

music_class := music_auto;
music_nat := music_auto;

for i in omarSquad do
    begin
    if GetType(i) = unit_vehicle and GetLives(i) < 1000 then
       begin
       driver := IsDrivenBy(i);
       veh := i;
       ComExitVehicle(driver);
       AddComRepairVehicle(driver, veh);
       AddComEnterUnit(driver, veh);
       end
    else if GetLives(i) < 500 then
       begin
       AddComHeal(UnitFilter(omarSquad, [f_class, 4]), i);
       end;
    end;

repeat
 wait(0$1);
until not UnitFilter(omarSquad, [[f_not, [f_lives, 1000]], [f_type, unit_vehicle]]) and not UnitFilter(omarSquad, [f_not, [f_lives, 250]]);

wait(0$2);

tmp := UnitFilter(omarSquad, [[f_empty], [f_type, unit_vehicle]]);
tmp2 := UnitFilter(omarSquad, [[f_class, 3], [f_not, [f_driving]]]);

for i := 1 to tmp do
    ComEnterUnit(tmp2[i], tmp[i]);

wait(0$5);

ComMoveXY(omarSquad, 199, 141);
ComMoveXY(Omar, 190, 143);
AddComHold(Omar);

// attack on kirov
AddComAgressiveMove(UnitFilter(omarSquad, [f_or, [f_class, 1], [f_class, 4]]), 161, 130);
AddComAgressiveMove(UnitFilter(omarSquad, [f_class, 1]), 163, 122);
AddComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 165, 118, 0);
AddComPlaceDelayedCharge(UnitFilter(omarSquad, [f_class, 1])[4], 159, 116, 0);

AddComAgressiveMove(UnitFilter(omarSquad, [f_type, unit_vehicle]), 169, 114);

repeat
 wait(0$1);

 tmp := UnitFilter(omarSquad, [f_class, 1]);

 for i in tmp do
     begin
     if GetLives(i) < 600 and GetDistUnitXY(i, 161, 130) > 6 then
        ComMoveXY(i, 161, 130);

     if GetLives(i) = 1000 and not HasTask(i) then
        ComAgressiveMove(i, 160, 109);
     end;

 tmp := UnitFilter(omarSquad, [f_type, unit_vehicle]);

 for i in tmp do
     begin
     tmp2 := IsDrivenBy(i);

     if not tmp2 then
        continue;

     if GetLives(i) >= 600 and GetTag(i) = 1 then
        SetTag(i, 0);

     if GetLives(i) >= 600 and not GetTag(i) then
        ComAttackUnit(i, NearestUnitToUnit(enemy, i));

     if GetLives(i) < 600 and not GetTag(i) then
        begin
        SetTag(i, 1);
        ComMoveXY(i, 202, 150);
        end;
                                       
     if GetTag(i) = 1 and GetDistUnitXY(i, 202, 150) < 6 and tmp2 then
        begin
        ComExitVehicle(tmp2);
        AddComRepairVehicle(tmp2, i);
        AddComWait(tmp2, 0$20);
        AddComEnterUnit(tmp2, i);
        end;
     end;
until kirovDestroyed;

repeat
 wait(0$1);

 for i in omarSquad union Omar do
     if IsInArea(i, southRoadArea) then
        RemoveUnit(i)
     else
        ComMoveXY(i, 208, 162);
until not FilterAllUnits([[f_side, 5], [f_type, unit_human]]);
End;

// kirov
Every 0$1 trigger IsDead(kirov) do
Say(Heike, 'DH-1-Kirov');

// beria
Every 0$1 trigger See(2, beria) do
Say(Heike, 'DH-1-Beria');

// kagan
Every 0$1 trigger See(2, kagan) do
Say(Heike, 'DH-1-Kagan');

// see enemy lab
Every 0$3 trigger See(2, ruLab) and IsOk(ruLab) and IsOk(Heike) and IsOk(Aviradze) and not InBattle(2) do
var i, tmp, q;
begin
DialogueOn;

PlaceSeeing(111, 90, 2, -6);
CenterNowOnXY(111, 90);

Say(Aviradze, 'DA-Cap-1');
Say(Heike, 'DH-Cap-2');

DialogueOff;

repeat
 wait(0$1);
until beriaDestroyed and IsOk(ruLab);

tmp := heikeSecondSquad union [Ibrahim, Kamil, Sophia, Kaia];

if tmp < 2 then
   q := 2
else
   q := Query('LabCap');

case q of
1: begin
   DoNotAttack(2, ruLab);

   labCaptured := true;
   Say(Heike, 'DH-Cap-a-1');
   Say(Aviradze, 'DA-Cap-a-2');

   SetSide(ruLab, 8);
   SetSide(Aviradze, 8);

   aviradzeSquad := [Aviradze];

   ComEnterUnit(Aviradze, ruLab);

   for i := 1 to 2 do
       begin
       aviradzeSquad := aviradzeSquad union tmp[i];
       SetSide(tmp[i], 8);
       ComEnterUnit(tmp[i], ruLab);
       end;
   end;
2: begin
   Say(Heike, 'DH-Cap-b-1');
   Say(Aviradze, 'DA-Cap-b-2');
   end;
End;

End;

// go to west
Every 0$1 trigger kirovDestroyed and beriaDestroyed and kaganDestroyed do
var i, tmp, tmp2, cargo;
begin
ChangeMissionObjectives('GoToWest');
SetAreaMapShow(goToWestArea, 1);

repeat
 wait(0$1);
 tmp := FilterUnitsInArea(goToWestArea, [[f_side, 2], [f_or, [f_type, unit_human], [[f_type, unit_vehicle], [f_not, [f_empty]]]]]);
until FilterAllUnits([[f_side, 2], [f_or, [f_type, unit_human], [[f_type, unit_vehicle], [f_not, [f_empty]]]]]) + 0
      = tmp + 0;

SaveCharacters(Heike, '04_1_Heike');

if Aviradze and not Aviradze in aviradzeSquad then
   SaveCharacters(Aviradze, '04_1_Aviradze')
else if Aviradze then
   SaveCharacters(Aviradze, '04_1_Aviradze_L');

// Aviradze squad possible: [Ibrahim, Kamil, Sophia, Kaia]

if Ibrahim and not Ibrahim in aviradzeSquad then
   SaveCharacters(Ibrahim, '04_1_Ibrahim')
else if Ibrahim then
   SaveCharacters(Ibrahim, '04_1_Ibrahim_L');

if Kamil and not Kamil in aviradzeSquad then
   SaveCharacters(Kamil, '04_1_Kamil')
else if Kamil then
   SaveCharacters(Kamil, '04_1_Kamil_L');

if Sophia and not Sophia in aviradzeSquad then
   SaveCharacters(Sophia, '04_1_Sophia')
else if Sophia then
   SaveCharacters(Sophia, '04_1_Sophia_L');

if Kaia and not Kaia in aviradzeSquad then
   SaveCharacters(Kaia, '04_1_Kaia')
else if Kaia then
   SaveCharacters(Kaia, '04_1_Kaia_L');

if Givi then
   SaveCharacters(Givi, '04_1_Givi');

if Mike then
   SaveCharacters(Mike, '04_1_Mike');

if Markov then
   SaveCharacters(Markov, '04_1_Markov');

if Kurt then
   SaveCharacters(Kurt, '04_1_Kurt');

if heikeSecondSquad diff aviradzeSquad then
   SaveCharacters(heikeSecondSquad diff aviradzeSquad, '04_1_others');

if aviradzeSquad and heikeSecondSquad then
   begin
   tmp := [];

   for i in heikeSecondSquad do
       if i in aviradzeSquad then
          tmp := Replace(tmp, tmp + 1, i);

   if tmp then
      SaveCharacters(tmp, '04_1_others_L');
   end;

tmp := FilterAllUnits([[f_side, 2], [f_nation, 0]]);

if tmp then
   SaveCharacters(tmp, '04_1_apes');

tmp := FilterUnitsInArea(goToWestArea, [f_type, unit_vehicle]);

if tmp then
   begin
   tmp2 := [];

   for i in tmp do
       if GetWeapon(i) in [ar_cargo_bay, us_cargo_bay, ru_cargo_bay] then
          begin
          cargo := [GetCargo(i, mat_cans), GetCargo(i, mat_oil)];
          tmp2 := Replace(tmp2, tmp2 + 1, [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), cargo]);
          end
       else
          tmp2 := Replace(tmp2, tmp2 + 1, [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), []]);

   SaveVariable(tmp2, '04_1_Vehicles');
   end;

SaveVariable(labCaptured, '04_1_RuLabCaptured');
SaveVariable(escapeCounter, '04_1_EscapeCounter');
SaveVariable(FilterUnitsInArea(hillArea, [[f_side, 2], [f_type, unit_building]]) > 0, '04_1_BaseHill');
SaveVariable(tick, '04_1_Time');
SaveVariable(loseCounter, '04_1_DeadCounter');
SaveVariable(FilterAllUnits([[f_side, 2], [f_weapon, ar_flame_thrower]]) + 0, '04_1_Flamethrower');

ChangeMap(1, '__x1\04_cont2');
End;


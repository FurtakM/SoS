Export Function Action;
var i, un, veh, tmp;
begin
InGameOn;

PauseMusic := true;

music_nat := 2;
music_class := music_recon;

CenterNowOnXY(7, 6);
RevealFogOnXY(7, 6, 2, -20);

tmp := [Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Aviradze];
tmp := tmp diff 0;
tmp := SortBySkill(tmp, 3);

// [GetChassis(i), GetEngine(i), GetControl(i), GetWeapon(i), GetNation(i), GetLives(i), cargo]
// cargo := [GetCargo(i, mat_cans), GetCargo(i, mat_oil)];
for i in heikeVehicles do
    begin
    PrepareTank(5, i[5], i[1], i[2], i[3], i[4], 34);
    veh := CreateVehicle;

    if i[4] in [ru_cargo_bay, ar_cargo_bay, us_cargo_bay] then
       begin
       if i[7][1] then
          AddCargo(veh, mat_cans, i[7][1]);
       if i[7][2] then
          AddCargo(veh, mat_oil, i[7][2]);
       end;

    SetDir(veh, 2);
    PlaceUnitArea(veh, spawnStartArea, false);
    end;

if tmp then
   for i in tmp union heikeApes do
       begin
       PlaceUnitArea(i, spawnHumanArea, false);
       ComTurnXY(i, 26, 9);
       end;

if heikeSecondSquad then
   for i in heikeSecondSquad do
       begin
       PlaceUnitArea(i, spawnHuman2Area, false);
       ComTurnXY(i, 26, 9);
       end;

if heikeApes then
   for i in heikeApes do
       begin
       PlaceUnitArea(i, spawnApeArea, false);
       ComHold(i);
       end;

if Kurt then
   begin
   PlaceUnitArea(Kurt, spawnGuardArea, false);
   ComTurnXY(Kurt, 42, 32);
   end;

wait(0$2);

Say(Heike, 'DH-Start-1');

if Givi then
   begin
   Say(Givi, 'DG-Start-2-5');
   Say(Heike, 'DH-Start-3');
   Say(Heike, 'DH-Start-4');
   Say(Givi, 'DG-Start-5');
   Say(Heike, 'DH-Start-6');
   Say(Givi, 'DG-Start-7');

   if Mike then
      begin
      Say(Mike, 'DM-Start-8');
      Say(Mike, 'DM-Start-9');
      Say(Givi, 'DG-Start-10');
      Say(Givi, 'DG-Start-11');
      Say(Heike, 'DH-Start-12');
      Say(Givi, 'DG-Start-13');
      Say(Givi, 'DG-Start-14');

      if Kaia then
         begin
         Say(Kaia, 'DK-Start-15');
         Say(Kaia, 'DK-Start-16');
         Say(Kaia, 'DK-Start-17');

         wait(0$0.3);
         ComTurnUnit(Givi, Kaia);

         Say(Givi, 'DG-Start-18');
         Say(Kaia, 'DK-Start-19');
         end;
      end;

   Say(Heike, 'DH-Start-20');
   end;

wait(0$2);

SayRadio(Shariff, 'DShar-Start-21');
Say(Heike, 'DH-Start-22');
SayRadio(Shariff, 'DShar-Start-23');
SayRadio(Shariff, 'DShar-Start-24');
SayRadio(Shariff, 'DShar-Start-25');
SayRadio(Shariff, 'DShar-Start-26');
Say(Heike, 'DH-Start-27');

if Kaia then
   begin
   ComTurnUnit(Heike, Kaia);
   Say(Kaia, 'DK-Start-28');
   Say(Heike, 'DH-Start-29');
   end
else
   Say(Heike, 'DH-Start-a-28');

if Givi then
   begin
   ComTurnUnit(Heike, Givi);
   ComTurnUnit(Givi, Heike);

   Say(Heike, 'DH-Start-30');
   Say(Heike, 'DH-Start-31');
   Say(Givi, 'DG-Start-32');
   Say(Heike, 'DH-Start-33');
   Say(Givi, 'DG-Start-34');
   Say(Givi, 'DG-Start-35');

   if Kurt and not dialogue_skipped then
      begin
      ComMoveXY(Kurt, 28, 13);
      AddComTurnUnit(Kurt, Heike);
      end;

   Say(Givi, 'DG-Start-36');

   if Kurt then
      begin
      if not Givi then
         Say(Kurt, 'DKs-Start-37-a')
      else
         Say(Kurt, 'DKs-Start-37');

      Say(Kurt, 'DKs-Start-38');

      ComTurnUnit(Heike, Kurt);

      Say(Heike, 'DH-Start-39');
      end;
   end
else
   if SayRandom(heikeSecondSquad, 'DArm-Start-a-37', 'DArf-Start-a-37') then
      Say(Heike, 'DH-Start-39');

if HexInfo(44, 25) then
   RemoveUnit(HexInfo(44, 25));

SetDir(Olaf, 5);
PlaceUnitXY(Olaf, 44, 25, false);
ComMoveXY(Olaf, 29, 12);

repeat
 wait(0$1);
until dialogue_skipped or GetDistUnits(Heike, Olaf) < 8 or GetDistUnitXY(Olaf, 26, 9) < 6;

Say(Olaf, 'DO-Scout-1');

ComTurnUnit(Olaf, Heike);

for i in tmp union heikeSecondSquad do
    ComTurnUnit(i, Olaf);

wait(0$0.3);

ComMoveUnit(Heike, Olaf);
AddComTurnUnit(Heike, Olaf);

Say(Heike, 'DH-Scout-2');
Say(Olaf, 'DO-Scout-3');

if Mike and Givi then // both alive
   Say(Olaf, 'DO-Scout-4')
else
   begin
   if Mike then // only mike
      begin
      Say(Olaf, 'DO-Scout-a-4');
      Say(Heike, 'DH-Scout-a-5');
      end
   else if Givi then // only givi
      begin
      Say(Olaf, 'DO-Scout-b-4');
      Say(Heike, 'DH-Scout-b-5');
      end
   else
      begin  // none
      Say(Olaf, 'DO-Scout-f-4');
      Say(Heike, 'DH-Scout-f-5');
      Say(Olaf, 'DO-Scout-f-6');
      end;
   end;

Say(Heike, 'DH-Scout-5');
Say(Olaf, 'DO-Scout-6');
Say(Olaf, 'DO-Scout-7');
Say(Heike, 'DH-Scout-8');
Say(Heike, 'DH-Scout-9');
Say(Olaf, 'DO-Scout-10');
Say(Olaf, 'DO-Scout-12');
Say(Olaf, 'DO-Scout-13');
Say(Olaf, 'DO-Scout-14');

if Givi then
   begin
   Say(Givi, 'DG-Scout-15');
   Say(Kaia, 'DK-Scout-16');
   end;

if not dialogue_skipped then
   begin
   async;
   Say(Olaf, 'DO-Scout-17');

   RevealFogOnXY(55, 77, 2, -6);
   wait(10);
   CenterOnXY(55, 77);

   wait(0$5);

   Say(Olaf, 'DO-Scout-18');

   RevealFogOnXY(204, 107, 2, -12);
   wait(10);
   CenterOnXY(204, 107);

   wait(0$5);
   sync;

   CenterNowOnUnits(Heike);
   end
else
   begin
   RevealFogOnXY(55, 77, 2, -6);
   RevealFogOnXY(204, 107, 2, -12);
   end;

Say(Olaf, 'DO-Scout-19');
Say(Olaf, 'DO-Scout-20');
Say(Heike, 'DH-Scout-21');
Say(Heike, 'DH-Scout-22');

InGameOff;

if Givi then
   SetRestrict(b_lab_biological, 2, state_enabled);

ChangeMissionObjectives('Road');

PauseMusic := false;

for i in FilterAllUnits([[f_side, 5], [f_type, unit_vehicle]]) do
    SetSide(i, 2);

SaveForQuickRestart;
music_class := music_auto;
End;

// see crossroad
Every 0$1 trigger SeeArea(2, crossRoadArea) do
begin
DialogueOn;

PlaceSeeing(111, 72, 2, -20);
RemoveSeeing(111, 72, 2);

CenterNowOnXY(111, 72);

if IsOk(Kamil) then
   begin
   Say(Kamil, 'DKam-Cross-1');
   Say(Kamil, 'DKam-Cross-2');
   Say(Kamil, 'DKam-Cross-3');
   Say(Kamil, 'DKam-Cross-4');

   if IsOk(Ibrahim) then
      begin
      if ibrahimJoinedIn04 then
         Say(Ibrahim, 'DI-Cross-b-5');

      Say(Ibrahim, 'DI-Cross-b-6');
      Say(Ibrahim, 'DI-Cross-b-7');
      Say(Kamil, 'DKam-Cross-8');
      end;
   end
else if IsOk(Sophia) then
   begin
   Say(Sophia, 'DS-Cross-1');
   Say(Sophia, 'DS-Cross-2');
   Say(Sophia, 'DS-Cross-3');
   Say(Sophia, 'DS-Cross-4');

   if IsOk(Ibrahim) then
      begin
      if ibrahimJoinedIn04 then
         Say(Ibrahim, 'DI-Cross-b-5');

      Say(Ibrahim, 'DI-Cross-b-6');
      Say(Ibrahim, 'DI-Cross-b-7');
      Say(Sophia, 'DS-Cross-8');
      end;
   end
else if UnitFilter(heikeSecondSquad, [f_ok]) then
   SayRandom(heikeSecondSquad, 'DArm-Cross-c-1', 'DArf-Cross-c-1');

SetTech(tech_Crane, 2, state_enabled);

DialogueOff;

ChangeMissionObjectives('Crane');

spottedCrossRoad := true;
End;

// dialog about radar
Every 0$10 trigger FilterAllUnits([[f_side, 2], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]) and (IsOk(Kaia) or heikeSecondSquad) do
begin
DialogueOn;

if Kaia then
   CenterNowOnUnits(Kaia)
else
   CenterNowOnUnits(Heike);

if IsOk(Kaia) then
   Say(Kaia, 'DK-Cam-1')
else
   SayRandom(heikeSecondSquad, 'DArm-Cam-1', 'DArf-Cam-1');

Say(Heike, 'DH-Cam-2');
Say(Heike, 'DH-Cam-3');
Say(Heike, 'DH-Cam-4');

if IsOk(Kaia) then
   Say(Kaia, 'DK-Cam-5')
else
   SayRandom(heikeSecondSquad, 'DArm-Cam-5', 'DArf-Cam-5');

Say(Olaf, 'DO-Cam-6');
Say(Olaf, 'DO-Cam-7');

DialogueOff;

ChangeMissionObjectives('Radar');

SetTech(tech_radar, 2, state_enabled);
SetRestrict(b_ext_radar, 2, true);
End;

Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_or, [f_weapon, ar_radar], [f_bweapon, ar_radar]]]) do
ChangeMissionObjectives('RadarDone');

Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_weapon, ar_crane]]) do
ChangeMissionObjectives('CraneDone');

// dialog about remote control
Every 0$30 trigger IsOk(Olaf) and IsOk(Aviradze) and GetTech(tech_radar, 2) = state_researched and (GetDistUnits(Aviradze, Heike) < 10 or IsInUnit(Aviradze)) do
begin
DialogueOn;

Say(Aviradze, 'DA-RC-1');
Say(Aviradze, 'DA-RC-2');

if IsOk(Kaia) then
   begin
   Say(Kaia, 'DK-RC-3');
   Say(Aviradze, 'DA-RC-4');
   Say(Kaia, 'DK-RC-5');
   Say(Aviradze, 'DA-RC-6');
   Say(Kaia, 'DK-RC-7');
   Say(Aviradze, 'DA-RC-8');
   end;

Say(Heike, 'DH-RC-9');

if IsOk(Markov) then
   begin
   Say(Markov, 'DMar-RC-10');
   Say(Markov, 'DMar-RC-11');
   end
else
   begin
   Say(Olaf, 'DO-RC-a-10');
   end;

DialogueOff;

ChangeMissionObjectives('RemoteControl');

SetTech(tech_remCont, 2, state_enabled);
SetRestrict(b_ext_radio, 2, true);
End;

// west base
Every 0$1 trigger FilterUnitsInArea(westBaseArea, [f_side, 2]) do
var i;
begin
PrepareWestBase;
westBaseSpotted := true;

wait(0$2);

InGameOn;

CenterOnXY(56, 77);
PlaceSeeing(56, 77, 2, -12);

wait(0$2);

DialogueOn;

Say(Heike, 'DH-Rbase-1');
Say(Kaia, 'DK-Rbase-2');
Say(Kaia, 'DK-Rbase-3');
Say(Kaia, 'DK-Rbase-4');

DialogueOff;

RemoveSeeing(56, 77, 2);
InGameOff;

repeat
 wait(0$1);
until not apeSoldGroup and not apeEngGroup;

wait(0$2);

if IsOk(Kaia) then
   begin
   DialogueOn;  
   CenterNowOnXY(56, 81);

   Say(Kaia, 'DK-Rbase-5');

   if IsOk(Givi) then
      begin
      Say(Givi, 'DG-Rbase-6');
      Say(Givi, 'DG-Rbase-7');
      Say(Kaia, 'DK-Rbase-8');
      Say(Givi, 'DG-Rbase-9');
      Say(Givi, 'DG-Rbase-10');
      Say(Kaia, 'DK-Rbase-11');
      end;

   DialogueOff;
   end;

for i in FilterUnitsInArea(westBaseArea, [[f_side, 3], [f_type, unit_building]]) do
    SetSide(i, 2);

westBaseCaptured := true;

SetRestrict(b_depot, 2, true);
End;

// east base
Every 0$1 trigger not oilBaseGuard or GetSide(ruEastBase) = 2 do
begin
DialogueOn;

Say(Heike, 'DH-Goal-1');
SayRadio(Shariff, 'DShar-Goal-2');
Say(Heike, 'DH-Goal-3');

if reinforcementsAllowed then
   begin
   SayRadio(Shariff, 'DShar-Goal-4');
   Say(Heike, 'DH-Goal-5');
   end;

DialogueOff;

eastBaseCaptured := true;

ChangeMissionObjectives('BaseCaptured');

SetSide(ruEastBase, 2);

if not reinforcementsAllowed then
   exit;

wait(10$00 + rand(-0$30, 0$30));

PrepareReinforcements;
End;


// drugs
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_btype, b_lab_biological]]) do
begin
Say(Givi, 'DG-Lab-1');
End;

Every 0$1 trigger GetTech(tech_StimDrugs, 2) = state_researched and IsOk(Givi) do
begin
Say(Givi, 'DG-Stim-1');
Say(Heike, 'DH-Stim-2');
Say(Givi, 'DG-Stim-3');
Say(Heike, 'DH-Stim-4');

drugsResearched := true;
End;

Every 0$1 trigger drugsResearched do
var i, tmp, tag;
begin
tmp := [Heike, Givi, Olaf, Markov, Kaia, Sophia, Kurt, Kamil, Mike, Ibrahim, Aviradze];
tag := ['H', 'G', 'O', 'Mar', 'K', 'S', 'Ks', 'Kam', 'M', 'I', 'A'];

repeat
 wait(0$1);

 for i := tmp downto 1 do
     if GetLives(tmp[i]) < 250 then
        begin
        stimDrugsCounter := stimDrugsCounter + 1;

        DialogueOn;

        CenterNowOnUnits(tmp[i]);
        ForceSay(tmp[i], 'D' & tag[i] & '-Stim-a-1');

        tmp := Delete(tmp, i);
        tag := Delete(tag, i);

        drugsTested := true;
        DialogueOff;
        break;
        end;
until not tmp;
End;

// remote control
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]) and FilterAllUnits([[f_side, 2], [f_control, control_remote]]) do
remoteControlConstructed := true;

Every 0$1 trigger remoteControlConstructed do
var i, tmp, tag;
begin
tmp := [Heike, Givi, Olaf, Markov, Kaia, Sophia, Kurt, Kamil, Mike, Ibrahim, Aviradze];
tag := ['H', 'G', 'O', 'Mar', 'K', 'S', 'Ks', 'Kam', 'M', 'I', 'A'];

repeat
 wait(0$1);

 for i := tmp downto 1 do
     if GetClass(tmp[i]) = 3 and UnitsLinked(tmp[i]) then
        begin
        DialogueOn;
        Say(tmp[i], 'D' & tag[i] & '-RC-a-1');

        tmp := Delete(tmp, i);
        tag := Delete(tag, i);
        DialogueOff;

        remoteControlTested := true;
        break;
        end;
until not tmp;
End;

Every 0$1 trigger remoteControlTested and IsOk(Markov) do
begin
Say(Markov, 'DMar-RC-a-2');
Say(Markov, 'DMar-RC-a-3');
End;

// Heike & Aviradze after Beria
Every 0$1 trigger reinforcementsAllowed and GetDistUnits(Heike, Aviradze) < 8 and not InBattle(2) and IsOk(Aviradze) and IsOk(Heike) do
begin
if not IsInUnit(Aviradze) then
   ComTurnUnit(Aviradze, Heike);

if not IsInUnit(Heike) then
   ComTurnUnit(Heike, Aviradze);

wait(0$1);

DialogueOn;

CenterNowOnUnits(Aviradze);

Say(Heike, 'DH-Beria-1');
Say(Aviradze, 'DA-Beria-2');
Say(Heike, 'DH-Beria-3');
Say(Aviradze, 'DA-Beria-4');
Say(Aviradze, 'DA-Beria-5');
Say(Aviradze, 'DA-Beria-6');
Say(Heike, 'DA-Beria-7');

DialogueOff;

SetTech(tech_weap2, 2, state_enabled);
End;

// Kaia Death
Every 0$1 trigger IsLive(Kaia) and GetLives(Kaia) < 10 do
begin
DialogueOn;
ForceSay(Kaia, 'DK-Kdeath-1');
Say(Heike, 'DH-Kdeath-2');
DialogueOff;
End;

Every 0$1 trigger SeeGroup(2, ruNorthGroup) do
Say(Heike, 'DH-Obj-1');

Every 0$1 trigger SeeGroup(2, ruSouthGroup) do
Say(Heike, 'DH-Obj-a-1');

Every 0$1 trigger eastBaseCaptured or tick >= [7$00, 6$30, 6$00][Difficulty] do
begin
wait(rand(2$00, 2$30));
convoyStarted := true;
End;

Every 0$1 trigger convoyStarted do
begin
DialogueOn;
SayRadio(Shariff, 'DShar-Convoy-1');
Say(Heike, 'DH-Convoy-2');
DialogueOff;

PrepareConvoy;

repeat
 wait(rand(2$30, 5$30));
 PrepareConvoy;
 convoyCounter := convoyCounter - 1;
until convoyCounter <= 0;
End;

Every 0$1 trigger convoySavedCounter >= 10 do
SayRadio(Omar, 'DOm-Fail-1');

Every 0$1 trigger not ruNorthGroup and not ruSouthGroup do
ChangeMissionObjectives('RaiderDone');

Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_control, control_remote]]) >= 3 and FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]) >= 2 do
ChangeMissionObjectives('RemoteControlDone');

Every 0$1 trigger (convoyCounter < 1 and not UnitFilter(ruConvoySquad, [[f_placed], [f_type, unit_human]])) or tick >= 60$00 do
var tmp;
begin
ChangeMissionObjectives('ReinforcementsDone');
music_class := music_victory;

InGameOn;

Say(Heike, 'DH-end-1');

wait(0$2);

SayRadio(Shariff, 'DShar-end-2');

if not ruNorthGroup and not ruSouthGroup then
   begin
   Say(Heike, 'DH-end-3');
   end
else
   begin
   Say(Heike, 'DH-end-a-3');
   SayRadio(Shariff, 'DShar-end-a-4');
   Say(Heike, 'DH-end-a-5');
   InGameOff;

   repeat
    wait(0$1);
   until FilterAllUnits([[f_side, 3], [f_type, unit_human]]) = 0;

   InGameOn;

   Say(Heike, 'DH-end-a-6');
   end;

SayRadio(Shariff, 'DShar-end-3');

if not flamethrowerBuilt then
   SetAchievement('ACH_FIRE');

if drugsTested then
   AddMedal('med1', 1)
else if GetTech(tech_stimDrugs, 2) = state_researched then
   AddMedal('med1', -1)
else
   AddMedal('med1', -2);

if FilterAllUnits([[f_side, 2], [f_control, control_remote]]) >= 3 and FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]) >= 1 then
   AddMedal('med2', 1)
else
   AddMedal('med2', -1);

if convoySavedCounter < [10, 8, 6][Difficulty] then
   AddMedal('med3', 1)
else
   AddMedal('med3', -1);

GiveMedals('MAIN');

RewardPeople(FilterAllUnits([[f_side, 2], [f_nation, 2], [f_type, unit_human]]));

SaveVariable(convoySavedCounter, '05_ConvoySavedCounter');
SaveVariable(radarConstructed, '05_Radar');
SaveVariable(drugsTested, '05_DrugsTested');
SaveVariable(FilterAllUnits([[f_side, 2], [f_control, control_remote]]) >= 3 and FilterAllUnits([[f_side, 2], [f_weapon, ar_control_tower]]) >= 2, '05_RemoteControl');

SaveCharacters(Heike, '05_Heike');

if Aviradze then
   SaveCharacters(Aviradze, '05_Aviradze');

if Ibrahim  then
   SaveCharacters(Ibrahim, '05_Ibrahim');

if Kamil then
   SaveCharacters(Kamil, '05_Kamil');

if Sophia then
   SaveCharacters(Sophia, '05_Sophia');

if Kaia then
   SaveCharacters(Kaia, '05_Kaia');

if Givi then
   SaveCharacters(Givi, '05_Givi');

if Mike then
   SaveCharacters(Mike, '05_Mike');

if Markov then
   SaveCharacters(Markov, '05_Markov');

if Kurt then
   SaveCharacters(Kurt, '05_Kurt');

if heikeSecondSquad then
   SaveCharacters(heikeSecondSquad, '05_others');

tmp := FilterAllUnits([[f_side, 2], [f_nation, 0]]);

if tmp then
   SaveCharacters(tmp, '05_apes');

YouWin;
End;
Export Function PrepareArabian;
var i, un, lives;
begin
SetBName(ar_dep, 'newkabul');

uc_side := 5;
uc_nation := 2;

for i in FilterAllUnits([[f_side, 2], [f_type, unit_building]]) do
    begin
    SetSide(i, 5);

    lives := rand(248, 510);
    SetLives(i, lives);

    if GetBType(i) = b_bunker then
       begin
       PlaceWeaponTurret(i, [ar_double_machine_gun, ar_gun, ar_gatling_gun, ar_rocket_launcher][rand(1,4)]);

       if lives > 250 then
          begin
          PrepareSoldier(false, 6);
          PlaceHumanInUnit(CreateHuman, i);
          end;
       end;
    end;

for i := 1 to 9 do
    begin
    PrepareHuman(false, rand(1, 4), 0);
    un := CreateHuman;
    SetDir(un, rand(0, 5));
    PlaceUnitArea(un, kabulDeadArea, false);
    KillUnit(un);
    end;

for i := 1 to 3 do
    begin
    vc_chassis := ar_medium_trike;
    vc_engine := [engine_solar, engine_combustion][rand(1, 2)];
    vc_control := [control_manual, control_remote][rand(1, 2)];
    vc_weapon := [ar_double_machine_gun, ar_gatling_gun, ar_gun][rand(1, 3)];
    un := CreateVehicle;
    SetDir(un, rand(0, 5));
    PlaceUnitArea(un, kabulVDeadArea, false);
    SetLives(un, 200);
    end;

for i := 1 to 3 do
    begin
    PrepareSoldier(false, 6);
    un := CreateHuman;
    PlaceUnitXYR(un, 106, 47, 4, false);
    ComAgressiveMove(un, 71, 53);
    end;
End;

Export Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf, Kurt, Aviradze,
       heikeSecondSquad;
Export Function PrepareHeike;
var i, tmp, un, veh;
begin
uc_side := 2;
uc_nation := 2;

Heike := PrepareUnit('Heike', (not debug), prefix);
Ibrahim := PrepareUnit('Ibrahim', (not debug), prefix);
Givi := PrepareUnit('Givi', (not debug), prefix);
Kamil := PrepareUnit('Kamil', (not debug), prefix);
Kaia := PrepareUnit('Kaia', (not debug), prefix);
Sophia := PrepareUnit('Sophia', (not debug), prefix);
Markov := PrepareUnit('Markov', (not debug), prefix);
Aviradze := PrepareUnit('Aviradze', (not debug), prefix);
Kurt := PrepareUnit('Kurt', (not debug), prefix);
Mike := PrepareUnit('Mike', (not debug), prefix);
Olaf := PrepareUnit('Olaf', false, '');

heikeSecondSquad := CreateCharacterSet('07_others');

veh := [
    [ar_half_tracked, engine_combustion, control_manual, ar_gatling_gun],
    [ar_half_tracked, engine_combustion, control_manual, ar_gun],
    [ar_half_tracked, engine_combustion, control_manual, ar_gun],
];

for i in veh do
    begin
    vc_chassis := i[1];
    vc_engine := i[2];
    vc_control := i[3];
    vc_weapon := i[4];
    vc_fuel_battery := 25;

    un := CreateVehicle;
    SetDir(un, 0);
    PlaceUnitArea(un, heikeVehArea, false);
    end;

tmp := [ Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf,
       Kurt, Aviradze ] union heikeSecondSquad;

for i in tmp do
    begin
    if GetClass(i) in [class_mechanic, class_engineer, class_scientistic] and FilterAllUnits([[f_side, 2], [f_empty]]) then
       PlaceHumanInUnit(i, FilterAllUnits([[f_side, 2], [f_empty]])[1])
    else
       PlaceUnitArea(i, heikePeopleArea, false);

    ComHold(i);
    end;

PlaceUnitArea(Heike, heikeArea, false);
ComTurnUnit(Heike, ar_dep);

if Givi then
   begin
   PlaceUnitXY(Givi, 129, 158, false);
   ComTurnUnit(Givi, ar_dep);
   end;
End;
Export kabulAttackers, heikeAttackers, Burlak;
Export Function PrepareRussian;
var i, un;
begin
uc_side := 3;
uc_nation := 3;

Burlak := NewCharacter('Burlak');

kabulAttackers := [];
heikeAttackers := [];

for i := 1 to 9 do
    begin
    PrepareHuman(false, class_soldier, 0);
    un := CreateHuman;

    if Prob(40) then
       SetClass(un, class_bazooker);

    SetDir(un, rand(0, 5));

    PlaceUnitArea(un, KabulRuDead, false);
    KillUnit(un);
    end;

for i := 1 to 9 do
    begin
    if rand(0, 1) then
       begin
       PrepareHuman(false, class_soldier, 7);
       un := CreateHuman;
       SetDir(un, 1);
       PlaceUnitArea(un, kabulRuArea, false);
       ComHold(un);
       end
    else
       begin
       vc_chassis := ru_medium_tracked;
       vc_engine := engine_combustion;
       vc_control := control_manual;
       vc_weapon := [ru_gun, ru_gatling_gun, ru_rocket_launcher][rand(1, 3)];
       un := CreateVehicle;
       SetDir(un, 1);
       PlaceUnitArea(un, kabulRuArea, false);
       ComHold(un);

       PlaceHumanInUnit(CreateHuman, un);
       end;

    kabulAttackers := Replace(kabulAttackers, kabulAttackers + 1, un);
    end;

for i := 1 to 5 do
    begin
    if rand(0, 1) then
       begin
       PrepareHuman(false, class_soldier, 7);
       un := CreateHuman;
       SetDir(un, 1);
       SetClass(un, class_bazooker);
       PlaceUnitArea(un, kabulRu2Area, false);
       ComHold(un);
       end
    else
       begin
       vc_chassis := ru_heavy_wheeled;
       vc_engine := engine_combustion;
       vc_control := control_manual;
       vc_weapon := [ru_gun, ru_gatling_gun, ru_rocket_launcher][rand(1, 3)];
       un := CreateVehicle;
       SetDir(un, 4);
       PlaceUnitArea(un, kabulRu2Area, false);
       ComHold(un);

       PlaceHumanInUnit(CreateHuman, un);
       end;

    kabulAttackers := Replace(kabulAttackers, kabulAttackers + 1, un);
    end;

for i := 1 to 5 do
    begin
    PrepareHuman(false, class_soldier, 7);
    un := CreateHuman;
    SetDir(un, 0);
    SetClass(un, class_bazooker);
    PlaceUnitArea(un, kabulRu3Area, false);
    ComHold(un);

    kabulAttackers := Replace(kabulAttackers, kabulAttackers + 1, un);
    end;


for i := 1 to 12 do
    begin
    if Prob(22) then
       begin
       PrepareHuman(false, class_soldier, 10);
       un := CreateHuman;
       SetDir(un, 3);
       PlaceUnitArea(un, heikeRuArea, false);
       ComHold(un);
       end
    else
       begin
       vc_chassis := [ru_medium_tracked, ru_heavy_tracked][rand(1, 2)];
       vc_engine := engine_combustion;
       vc_control := control_manual;
       vc_weapon := [ru_gun, ru_gatling_gun, ru_gatling_gun, ru_rocket_launcher][rand(1, 4)];
       un := CreateVehicle;
       SetDir(un, 3);
       PlaceUnitArea(un, heikeRuArea, false);
       ComHold(un);

       PrepareMechanic(false, 10);
       PlaceHumanInUnit(CreateHuman, un);
       end;

    heikeAttackers := Replace(heikeAttackers, heikeAttackers + 1, un);
    end;
End;

Every 0$2 trigger startAttackOnKabul and kabulAttackers do
var i;
begin
enable;

for i in kabulAttackers do
    ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 5]), i));
End;

Every 0$1 trigger startAttackOnHeike and heikeAttackers do
var i;
begin
enable;

for i in heikeAttackers do
    ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 2]), i));
End;
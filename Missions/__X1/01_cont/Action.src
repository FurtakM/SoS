Export Function Action;
begin
InGameOn;

wait(0$4);

Say(Heike, 'DH-start-1');
ComMoveXY(Heike, 71, 29);

wait(0$4);

ComMoveXY(Heike, 71, 33);
AddComTurnXY(Heike, 71, 34);

wait(0$4);

Say(Heike, 'DH-start-2');

wait(0$4);

ComMoveXY(Heike, 65, 26);
Say(Heike, 'DH-start-3');

InGameOff;

ChangeMissionObjectives('O1_cont');

SaveForQuickRestart;

missionStarted := true;

PrepareJohnny;
End;

// meet johnny
Every 0$1 trigger not olafSpotted and not mercenariesSpotted and See(2, Johnny) and not See(3, Heike) and NearestUnitToUnit(FilterAllUnits([f_side, 1]), Heike) = Johnny and GetAttitude(1, 2) = att_neutral do
begin
InGameOn;

CenterNowOnUnits(Heike);

Say(Johnny, 'DJ-ambush-1');

wait(0$0.3);

ComTurnUnit(Heike, Johnny);

Say(Heike, 'DH-ambush-1');

wait(0$1);

ComWalk(Johnny);
AddComMoveUnit(Johnny, Heike);

repeat
 wait(0$1);
until GetDistUnits(Johnny, Heike) < 4;

ComTurnUnit(Johnny, Heike);

Say(Johnny, 'DJ-ambush-2');
Say(Heike, 'DH-ambush-2');
Say(Johnny, 'DJ-ambush-3');
Say(Heike, 'DH-ambush-3');
Say(Johnny, 'DJ-ambush-4');

ComTurnXY(Heike, 39, 22);

Say(Heike, 'DH-ambush-4');

PrepareRussianPatrol;

PlaceSeeing(39, 22, 2, -12);
CenterOnXY(39, 22);

ComMoveXY(russianPatrol, 39, 23);
ComMoveXY([Heike, Johnny], 39, 7);
AddComTurnUnit([Heike, Johnny], russianPatrol[1]);

Say(russianPatrol[1], 'DR-ambush-5');
Say(russianPatrol[2], 'DR1-ambush-5');

repeat
 wait(0$1);
until GetDistUnitXY(Heike, 39, 7) < 6;

AddComTurnUnit(Heike, russianPatrol[1]);
ComTurnUnit(Johnny, Heike);

Say(Johnny, 'DJ-ambush-6');
Say(Heike, 'DH-ambush-6');

InGameOff;

meetJohnny := true;

ComFree(Johnny);

ChangeMissionObjectives('O2_ambush_cont');

ComMoveXY(russianPatrol[russianPatrol], 29, 15);

wait(0$2);

if not See(3, Heike) and not See(3, Johnny) then
   ComMoveXY(russianPatrol[1], 58, 32);

wait([0$50, 0$40, 0$35][Difficulty]);

ComAgressiveMove(russianPatrol, 43, 13);

repeat
 wait(0$1);
until not russianPatrol or not IsOk(Johnny);

RemoveSeeing(39, 22, 2);

if GetAttitude(1, 2) = att_enemy then
   exit;

if not IsOk(Johnny) then
   begin
   ChangeMissionObjectives('O2_ambush_out_cont');
   exit;
   end;

if GetDistUnits(Heike, Johnny) > 10 then
   begin
   ComMoveUnit(Johnny, Heike);

   repeat
    wait(0$1);
   until GetDistUnits(Johnny, Heike) < 6;
   end;

CenterNowOnUnits(Heike);

InGameOn;

ComTurnUnit(Heike, Johnny);
ComTurnUnit(Johnny, Heike);

Say(Johnny, 'DJ-thanks-1');
Say(Heike, 'DH-thanks-1');
Say(Johnny, 'DJ-thanks-2');

ComMoveXY(Johnny, 4, 3);

wait(0$1);

Say(Heike, 'DH-thanks-2');

repeat
 wait(0$1);
until not See(2, Johnny);

Say(Heike, 'DH-thanks-2a');

RemoveUnit(Johnny);
InGameOff;

ChangeMissionObjectives('O2_ambush_out_cont');

johnnySaved := true;
AddExperience(Heike, 1, 600);

if IsOk(Olaf) then
   ComMoveXY(Olaf, 44, 25);
End;

Every 0$1 trigger russianPatrol do
var i;
begin
enable;

for i in russianPatrol do
    if GetLives(i) < 1000 and not HasTask(i) then
       ComAgressiveMove(i, 42, 12);
End;

// attack Johnny
Every 0$1 trigger GetAttitude(1, 2) = att_enemy and IsOk(Johnny) do
Say(Johnny, 'DJ-fire-1');

// spotted east patrol
Every 0$1 trigger not mercenariesSpotted and UnitFilter(russianForces, [f_see, 2]) do
begin
CenterNowOnUnits(Heike);

DialogueOn;
Say(Heike, 'DH-see-enemy-patrol-1');
Say(UnitFilter(russianForces, [f_sex, sex_male])[1], 'DR-see-enemy-patrol-1');
DialogueOff;

ComAgressiveMove(russianForces, 66, 22);

wait(0$10);

AddComWait(russianForces, 0$05);
AddComMoveToArea(russianForces, russianEastPatrolArea);
AddComFree(russianForces);
End;

Every 0$1 trigger UnitFilter(russianForces, [f_not, [f_lives, 960]]) do
var i, tmp, tmp2, tmp3;
begin
enable;

tmp := FilterAllUnits([f_side, 2]);
tmp2 := UnitFilter(russianForces, [f_and, [f_not, [f_lives, 960], [f_lives, 500]]]);
tmp3 := UnitFilter(russianForces, [f_not, [f_lives, 500]]);

if not tmp then
   exit;

for i in tmp2 do
    if GetDistUnits(i, NearestUnitToUnit(tmp, i)) < 10 then
       ComAttackUnit(i, NearestUnitToUnit(tmp, i));

if tmp3 then
   for i in tmp do
       if GetDistUnitXY(i, 119, 21) > 10 then
          ComMoveXY(i, 119, 21);
End;

// see animals
// tiger
Every 0$2 trigger missionStarted and GetDistUnits(Heike, NearestUnitToUnit(FilterAllUnits([f_class, class_tiger]), Heike)) < 6 do
begin
animalCounter := animalCounter + 1;
Say(Heike, 'DH-tiger-1');
End;

// ape
Every 0$2 trigger missionStarted and GetDistUnits(Heike, NearestUnitToUnit(FilterAllUnits([f_class, class_apeman]), Heike)) < 6 do
begin
animalCounter := animalCounter + 1;
Say(Heike, 'DH-ape-1');
End;

// bird
Every 0$2 trigger missionStarted and GetDistUnits(Heike, NearestUnitToUnit(FilterAllUnits([f_class, class_phororhacos]), Heike)) < 6 do
begin
animalCounter := animalCounter + 1;
Say(Heike, 'DH-bird-1');
End;

Every 0$2 trigger animalCounter = 3 do
SetAchievement('ACH_ZOOLIGST');

// russian spotted
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_see, 3]]) do
russianSpotted := true;

// see enemy buildings
Every 0$1 trigger GetDistUnits(Heike, NearestUnitToUnit(FilterAllUnits([[f_side, 3], [f_type, unit_building]]), Heike)) < 8 do
Say(Heike, 'DH-enemy-base');

// DogTag event
Every 0$1 trigger not IsPlaced(Johnny) and not dogTagFound and dogTag and not lastObjectiveActive and not mercenariesQuestDone do
var i;
begin
repeat
 wait(0$1);

 for i in dogTag do
     if GetDistUnitXY(Heike, i[1], i[2]) < 3 then
        begin
        dogTagFound := i;
        break;
        end;

until dogTagFound;

CenterNowOnUnits(Heike);

ComMoveXY(Heike, dogTagFound[1], dogTagFound[2]);

SetAchievement('ACH_DOGTAG');

if mercenariesQuestReady then
   Say(Heike, 'DH-dogtag-found-quest')
else
   Say(Heike, 'DH-dogtag-found');
End;

// Olaf spotted
Every 0$1 trigger not olafSpotted and not mercenariesSpotted and See(5, Heike) and GetDistUnits(Heike, Olaf) < 8 do
begin
olafSpotted := true;

if HasTask(Olaf) then
   ComStop(Olaf);

InGameOn;

if GetDistUnits(Olaf, Heike) > 8 then
   ComMoveUnit(Olaf, Heike);

repeat
 wait(0$1);
until GetDistUnits(Olaf, Heike) <= 8;

ComTurnUnit(Heike, Olaf);
ComTurnUnit(Olaf, Heike);

Say(Olaf, 'DO-olaf-1');
Say(Heike, 'DH-olaf-1');
Say(Olaf, 'DO-olaf-2');
Say(Heike, 'DH-olaf-2');
Say(Olaf, 'DO-olaf-3');
Say(Olaf, 'DO-olaf-4');
Say(Heike, 'DH-olaf-4');
Say(Olaf, 'DO-olaf-5');
Say(Heike, 'DH-olaf-5');
Say(Olaf, 'DO-olaf-6');

if russianSpotted or johnnySaved then
   begin
   Say(Heike, 'DH-olaf-6a');
   Say(Olaf, 'DO-olaf-7a');
   end
else
   begin
   Say(Heike, 'DH-olaf-6b');
   Say(Olaf, 'DO-olaf-7b');
   end;

Say(Olaf, 'DO-olaf-8');

InGameOff;

ChangeMissionObjectives('O3_camp_cont');

ComMoveXY(Olaf, 52, 90);
AddComTurnXY(Olaf, 53, 89);
End;

// heike goes to camp without olaf
Every 0$1 trigger not olafSpotted and GetDistUnits(Heike, Givi) < 36 do
ComMoveXY(Olaf, 39, 71);

// spot camp
Every 0$1 trigger not mercenariesSpotted and (See(2, Mike) or See(2, Givi)) do
var i, driver, tank;
begin
if not johnnySaved then
   KillUnit(Johnny);

mercenariesSpotted := true;
ChangeMissionObjectives('O4_camp_cont');

InGameOn;

CenterNowOnXY(53, 89);
PlaceSeeing(53, 89, 2, -8);

if olafSpotted then
   begin
   AddComTurnUnit(Olaf, Heike);
   ComMoveXY(Heike, 51, 86);
   AddComTurnUnit(Heike, Olaf);

   Say(Olaf, 'DO-camp-1'); 

   ComTurnUnit([Givi, Mike], Heike);

   Say(Givi, 'DG-camp-1');
   Say(Heike, 'DH-camp-1');
   Say(Olaf, 'DO-camp-2');
   end
else
   begin
   ComMoveXY(Heike, 51, 86);
   AddComTurnUnit(Heike, Givi);

   ComTurnUnit([Givi, Mike], Heike);

   Say(Givi, 'DG-camp-1a');
   Say(Heike, 'DH-camp-1a');

   wait(0$0.03);

   ComTurnUnit(Mike, Olaf);

   Say(Mike, 'DM-camp-1a');

   ComMoveXY(Olaf, 52, 90);

   ComTurnUnit(Mike, Heike);

   Say(Givi, 'DG-camp-2a');
   Say(Heike, 'DH-camp-2a');
   Say(Mike, 'DM-camp-2a');

   repeat
    wait(0$1);
   until GetDistUnits(Olaf, Heike) < 10;

   AddComTurnUnit(Olaf, Heike);
   ComTurnUnit(Mike, Heike);

   Say(Olaf, 'DO-camp-2a');

   ComTurnUnit(Heike, Olaf);

   if russianSpotted then
      begin
      Say(Heike, 'DH-camp-2aa');
      Say(Givi, 'DG-camp-2aa');
      Say(Mike, 'DM-camp-2aa');
      end
   else
      begin
      Say(Heike, 'DH-camp-2ab');
      Say(Mike, 'DM-camp-2ab');
      end;

   Say(Heike, 'DH-camp-3a');
   Say(Olaf, 'DO-camp-3a');
   Say(Givi, 'DG-camp-3a');

   Say(Heike, 'DH-camp-4aa');
   Say(Mike, 'DM-camp-4aa');

   Say(Olaf, 'DO-camp-4a');   
   end;

Say(Olaf, 'DO-camp-3');
Say(Olaf, 'DO-camp-4');

ComTurnUnit(Givi, Olaf);

Say(Givi, 'DG-camp-4');

ComTurnUnit(Heike, Givi);

Say(Heike, 'DH-camp-4');

ComTurnUnit([Mike, Olaf, Givi], Heike);

Say(Givi, 'DG-camp-5');
Say(Givi, 'DG-camp-6');
Say(Heike, 'DH-camp-6');
Say(Givi, 'DG-camp-7');

if DogTagFound then
   begin
   Say(Heike, 'DH-quest-1a');
   Say(Givi, 'DG-quest-1a');
   mercenariesQuestDone := true; 
   InGameOff;
   end
else
   begin
   Say(Heike, 'DH-camp-7');

   if GetLives(Heike) < 1000 then
      begin
      Say(Olaf, 'DO-heal-1');

      ComMoveXY(Heike, 51, 88);

      repeat
       wait(0$1);
      until GetDistUnits(Heike, Olaf) < 4 or IsAt(Heike, 51, 88);

      SetLives(Heike, 1000);

      wait(0$2);
      end; 

   InGameOff;

   ComTurnXY([Olaf, Givi, Mike], 53, 89);

   ChangeMissionObjectives('O5_proof_cont');
   mercenariesQuestReady := true;
   end;

if not mercenariesQuestReady then
   exit;

wait(0$35);

driver := PreparePatrolTank;

if not driver then
   exit;

SetAttitude(2, 1, att_enemy, true);

tank := IsInUnit(driver);

SetLives(tank, 500);
SetLives(driver, 450);

ComMoveXY(driver, 59, 60);

repeat
 wait(0$1);
until IsAt(tank, 59, 60);

if not See(1, Heike) then
   begin
   if Difficulty < 3 then
      PlaceSeeing(59, 60, 2, 6);

   ComExitVehicle(driver);

   wait(0$1);

   AddComMoveXY(driver, 58, 54);

   wait(0$1);

   AddComTurnXY(driver, 57, 53);
   AddComWait(driver, 0$10);
   AddComEnterUnit(driver, tank);
   AddComMoveXY(driver, 107, 118);
   end
else
   begin
   ComMoveXY(driver, 107, 118);
   end;

repeat
 wait(0$1);
until IsAt(tank, 107, 118) or IsDead(driver);

if IsDead(driver[1]) then
   exit;

for i in FilterAllUnits([f_side, 1]) do
    RemoveUnit(i);
End;

// see tank in camp
Every 0$1 trigger FilterUnitsInArea(campArea, [[f_side, 2], [f_type, unit_vehicle]]) do
begin
InGameOn;

ComTurnUnit([Olaf, Mike, Givi], Heike);

Say(Mike, 'DM-quest-1');
Say(Givi, 'DG-quest-1');

InGameOff;

mercenariesQuestDone := true;
End;

// see dogtag in camp
Every 0$1 trigger IsInArea(Heike, campArea) and dogTagFound and mercenariesQuestReady and not FilterUnitsInArea(campArea, [[f_side, 2], [f_type, unit_vehicle]]) do
var p;
begin
InGameOn;

ComTurnUnit([Olaf, Mike, Givi], Heike);

ComMoveXY(Heike, 54, 86);

p := p + 1;

repeat
 wait(0$1);
 p := p + 1;
until GetDistUnits(Heike, Givi) < 4 or p > 5;

Say(Mike, 'DM-quest-1');
Say(Givi, 'DG-quest-1');

ComMoveXY(Heike, 55, 87);

InGameOff;

mercenariesQuestDone := true;
End;

// heike driver
Every 0$1 trigger Heike in FilterAllUnits([f_driving]) do
begin
if Difficulty < 3 then
   RemoveSeeing(59, 60, 2);

Say(Heike, 'DH-driver');
End;

// quest done
Every 0$1 trigger mercenariesQuestDone do
begin
if mercenariesQuestReady then
   ChangeMissionObjectives('O5_proof_cont_out');

InGameOn;

if IsInUnit(Heike) then
   ComExitVehicle(Heike);

wait(3);

AddComTurnUnit(Heike, Givi);
ComTurnUnit([Olaf, Mike, Givi], Heike);

Say(Heike, 'DH-quest-done-1');
Say(Mike, 'DM-quest-done-1');
Say(Givi, 'DG-quest-done-1');

ComTurnUnit(Heike, Olaf);

Say(Heike, 'DH-quest-done-2');
Say(Olaf, 'DO-quest-done-2');
Say(Olaf, 'DO-quest-done-3');

ComMoveXY(Givi, 51, 90);
AddComTurnXY(Givi, 50, 89);

ComTurnUnit(Heike, Givi);

Say(Givi, 'DG-quest-done-4');

ComTurnUnit(Givi, Heike);

if GetLives(Heike) < 1000 then
   begin
   Say(Olaf, 'DO-heal-1');

   ComMoveXY(Heike, 51, 88);

   repeat
    wait(0$1);
   until GetDistUnits(Heike, Olaf) < 4 or IsAt(Heike, 51, 88);

   SetLives(Heike, 1000);

   wait(0$2);
   end;

Say(Heike, 'DH-quest-done-4');
Say(Olaf, 'DO-quest-done-4');

ComTurnUnit(Heike, Olaf);

Say(Heike, 'DH-quest-done-5');

InGameOff;

ChangeMissionObjectives('O6_end_cont');

SetSide(Givi, 2);
SetSide(Mike, 2);

SetTech(tech_explos, 2, state_researched);

ComTurnXY(Olaf, 53, 89);

lastObjectiveActive := true;
SetAreaMapShow(escapeEast, 1);

PreparePrison;
End;

// heike in north base
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_dist, northDep, 4]]) do
var i, tmp;
begin
wait([0$7, 0$06, 0$06][Difficulty]);

tmp := FilterAllUnits([[f_side, 3], [f_class, 1], [f_inside]]);

if not tmp then
   exit;

for i in tmp do
    begin
    ComExitBuilding(i);
    AddComAgressiveMove(i, 121, 22);
    end;

ComAgressiveMove(russianForces, 121, 22);
ComAgressiveMove(russianTank, 121, 22);

AddComAttackUnit(russianForces, Heike);
AddComAttackUnit(russianTank, Heike);
End;

// heike in south base
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_dist, southDep, 4]]) do
var i, tmp;
begin
wait([0$8, 0$07, 0$06][Difficulty]);

tmp := FilterAllUnits([[f_side, 3], [f_class, 1], [f_inside]]);

if not tmp then
   exit;

for i in tmp do
    begin
    ComExitBuilding(i);
    AddComAgressiveMove(i, 142, 71);
    end;

ComAgressiveMove(russianForces, 142, 71);
ComAgressiveMove(russianTank, 142, 71);

AddComAttackUnit(russianForces, Heike);
AddComAttackUnit(russianTank, Heike);
End;

// olaf repair veh
Every 0$1 trigger FilterAllUnits([[f_side, 2], [f_type, unit_vehicle], [f_empty], [f_not, [f_lives, 1000]], [f_dist, Olaf, 8]]) and IsOk(Olaf) do
begin
enable;

ComRepairVehicle(Olaf, FilterAllUnits([[f_side, 2], [f_type, unit_vehicle], [f_empty], [f_not, [f_lives, 1000]], [f_dist, Olaf, 8]])[1]);
AddComMoveXY(Olaf, 52, 90);
AddComHold(Olaf);
End;

// north depot destroyed
Every 0$1 trigger lastObjectiveActive and not IsOk(northDep) and Difficulty = 1 and not prisonFound do
begin
Say(Heike, 'DH-prison-missing');
End;

// south depot destroyed
Every 0$1 trigger lastObjectiveActive and not IsOk(southDep) and Difficulty > 1 and not prisonFound do
begin
Say(Heike, 'DH-prison-missing');
End;

// workshop destroyed
Every 0$1 trigger lastObjectiveActive and not IsOk(workshop) and not prisonFound do
begin
Say(Heike, 'DH-prison-missing-a');
End;

// lab destroyed
Every 0$1 trigger lastObjectiveActive and not IsOk(lab) and not prisonFound do
begin
Say(Heike, 'DH-prison-missing-a');
End;

// prison found
Every 0$1 trigger not prisonFound and lastObjectiveActive and See(2, Kamil) do
begin
ComTurnUnit(Kamil, Heike);

DialogueOn;

CenterNowOnUnits(Kamil);

Say(Kamil, 'DK-prison-found');
Say(Heike, 'DH-prison-found');

DialogueOff;

SetSide(Kamil, 2);

prisonFound := true;

ChangeMissionObjectives('O6_prison_out');
End;

// heike get hurt
Every 0$1 trigger GetLives(Heike) < 500 do
heikeHurt := true;

// if player captured depot
Every 0$3 trigger FilterAllUnits([[f_side, 2], [f_type, unit_building]]) do
var i, tmp, un;
begin
uc_side := 3;
uc_nation := 3;

tmp := [];

for i := 1 to 8 do
    begin
    PrepareHuman(false, 1, 4);
    un := CreateHuman;
    SetDir(un, 4);
    PlaceUnitXY(un, 158, 80, false);
    ComMoveXY(un, 147, 75);
    wait(0$1);

    tmp := Replace(tmp, tmp + 1, un);
    end;

repeat
 wait(0$1);

 for i in tmp do
     begin
     un := NearestUnitToUnit(FilterAllUnits([f_side, 2]), i);

     if HexInfo(GetX(un[1]), GetY(un[1])) then
        ComAgressiveMove(i, GetX(un[1]), GetY(un[1]))
     else
        ComAttackUnit(i, Heike);
     end;

until IsDead(Heike) or not tmp;
End;

// end mission
Every 0$1 trigger lastObjectiveActive and IsOk(Heike) and FilterUnitsExceptArea(escapeEast, [[f_type, unit_human], [f_side, 2], [f_ok]]) = 0 do
var tmp;
begin
if not heikeHurt then
   SetAchievement('ACH_INVINCIBLE');

tmp := FilterAllUnits([[f_side, 2], [f_type, unit_human], [f_ok]]);

if prisonFound then
   AddMedal('med1', 1)
else
   AddMedal('med1', -1);

if (tmp < 3 and not prisonFound)
or (tmp < 4 and prisonFound) then
   AddMedal('med2', -1)
else
   AddMedal('med2', 1);

if prisonFound and johnnySaved and tmp >= 4 then
   AddMedal('med3', 1)
else
   AddMedal('med3', -1);

GiveMedals('MAIN');

RewardPeople(tmp);

SaveCharacters(Heike, '01_Heike');

if IsOk(Givi) and Givi in tmp then
   SaveCharacters(Givi, '01_Givi');

if IsOk(Mike) and Mike in tmp then
   SaveCharacters(Mike, '01_Mike');

if IsOk(Kamil) and Kamil in tmp then
   SaveCharacters(Kamil, '01_Kamil');

YouWin;
End;

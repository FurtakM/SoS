// Kód pro Amerièany.


// Èísla postav.
Var AmSci1, AmSci2, AmSci3, AmSci4;
Var AmMech1, AmMech2, AmMech3, AmMech4;
Var AmEng1, AmEng2;
Var AmEng_Oil, AmEng_Sib;
Var Obranci, Obranci_Oil, Obranci_Sib;
Var Patrola;
Var Konvoj, Doprovod;                  // Konvoj náklaïákù a jeho doprovod.
Var Tanky;                             // Tanky v základnì.
Var Max_Tanky, Max_Doprovod;           // Maximální poèet tankù.
Var Kradenik;                          // Auák, co pøípadnì ukradne artefat.
Var MamKontrolovatVyrobu;              // Mám provádìt kontrolu vıroby?
Var VyrobenoAut;                       // Kolik jsem toho vyrobil
Var ProbihaPrevoz;                     // 1 = jedu pro ropu, 2 = pro siberit, 3 = zpátky
Var PrevozNalozil, PrevozVylozil;      // Probíhá zrovna pøevoz pomocí konvoje?
Var VidelRusa;                         // Vidìl u Rusa?
Var ProbihaUtok;                       // Probíhá právì útok?
Var CisloUtoku;                        // Kolikátı útok je v poøadí.
Var PrvniUtok;                         // Èekání do prvního útoku.
Var ZacalyUtoky;                       // U jsem zaèal útoèit?
Var Utok_ZbyvaMinut;                   // Kolik minut zbıvá do prvního útoku.
Var CasyUtoku;                         // Poèty minut mezi útoky.
Var Utok_Auta;                         // Kdo útoèí.
Var KdoLeci;                           // Seznam, kdo léèí.
Var KdoOpravuje;                       // Seznam, kdo opravuje.
Var ZnicenOil, ZnicenSib;              // Byla znièena daná subzákladna?
Var KonvojBylRozebran;                 // Rozebral jsem koonvoj?
Var RozebratAuta;                      // Která auta mám rozebrat.
// Makra.
Var RegUtok, RegPatrola;
Var MakroObrana, MakroUtok, MakroUtokMove;
Var MakroPatrola;                      // Patrola.
Var MakroPrevoz;                       // Makro na pøevoz ropy a siberitu.


Export Function init_amici;
Var Base, Reg;
Begin
  Disable (21); Disable (22);
  RevealFogArea (side_Am, Americani);
  RevealFogArea (side_Am, Americani_Oil);
  RevealFogArea (side_Am, Americani_Sib);
  // Sloitost.
  PrvniUtok = dif_PrvniUtok [Difficulty];
  CasyUtoku = dif_CasovacUtoku [Difficulty];
  // Postavy.
  vytvor_postavy;
  // Promìnné.
  Kradenik = 0;
  Tanky = [];
  Konvoj = []; Doprovod = [];
  Max_Tanky = 7; Max_Doprovod = 2;
  ProbihaPrevoz = 0;
  VidelRusa = false; ZacalyUtoky = false;
  ProbihaUtok = false; Utok_ZbyvaMinut = -1; CisloUtoku = 1;
  Utok_Auta = [];
  KdoLeci = []; KdoOpravuje = [];
  ZnicenOil = false; ZnicenSib = false;
  KonvojBylRozebran = false;
  RozebratAuta = false;
  // Nastaví se na TRUE, a vyrobím všechna poèáteèní auta.
  MamKontrolovatVyrobu = false;
  VyrobenoAut = 0;
  // Nastavení poèáteèního stavu surovin.
  sur (AmDepot, 500, 280, 250);
  sur (AmDepot_Oil, 180 - Difficulty * 30, 50, 0);
  sur (AmDepot_Sib, 180 - Difficulty * 30, 30, 50);
  // Další inicializace.
  zacni_zkoumat;
  zacni_vyrabet;
  // Registrace.
  If Obranci_Oil then
  Begin
    Reg = McRegistry (side_Am, [[MC_REG_AREA_TO_PROTECT, Americani_Oil]]);
    McDefend (1, Reg, Obranci_Oil, []);
  End;
  If Obranci_Sib then
  Begin
    Reg = McRegistry (side_Am, [[MC_REG_AREA_TO_PROTECT, Americani_Sib]]);
    McDefend (1, Reg, Obranci_Sib, []);
  End;
  // Registrace.
  RegUtok = McRegistry (side_Am, [[MC_REG_AREA_TO_GUARD, AreaUtok], MC_REG_IGNORE_FOG]);
  RegPatrola = McRegistry (side_Am, [[MC_REG_AREA_TO_GUARD, AreaUtok], -MC_REG_ONLY_IMPORTANT]);
  // Obrana a útok.
  Reg = McRegistry (side_Am, [[MC_REG_AREA_TO_PROTECT, AmericaniObrana]]);
  MakroObrana = McDefend (2, Reg, Tanky union Obranci, [[MC_AREA_DONT_LEAVE, Americani]]);
  // Patrola.
  MakroPatrola = McPatrol (100, RegPatrola, Patrola, [MC_NO_STOP]);
End;


// Vytvoøí postavy a plácne je buï do budov, nebo na mapu.
Function vytvor_postavy;
Begin
  AmSci1 = vytvor_postavu_B (AmLabWS, CLASS_SCIENTISTIC);
  AmSci2 = vytvor_postavu_B (AmLabWS, CLASS_SCIENTISTIC);
  AmSci3 = vytvor_postavu_B (AmLabCO, CLASS_SCIENTISTIC);
  AmSci4 = vytvor_postavu_B (AmLabCO, CLASS_SCIENTISTIC);
  AmMech1 = vytvor_postavu_B (AmFactGun, CLASS_MECHANIC);
  AmMech2 = vytvor_postavu_B (AmFactGun, CLASS_MECHANIC);
  AmMech3 = vytvor_postavu_B (AmFactRock, CLASS_MECHANIC);
  AmMech4 = vytvor_postavu_B (AmFactRock, CLASS_MECHANIC);
  AmEng1 = vytvor_postavu_A (Americani, CLASS_ENGINEER);
  AmEng2 = vytvor_postavu_A (Americani, CLASS_ENGINEER);
  AmEng_Oil = vytvor_postavu_B (AmDepot_Oil, CLASS_ENGINEER);
  AmEng_Sib = vytvor_postavu_B (AmDepot_Sib, CLASS_ENGINEER);
  Obranci = vytvor_postavy_A (8, Americani, CLASS_SOLDIER) union vytvor_postavy_A (2, Americani, CLASS_SNIPER);
  Patrola = vytvor_postavy_A (Difficulty + 2, Americani, CLASS_SOLDIER);
  Obranci_Oil = vytvor_postavy_A (Difficulty - 1, Americani_Oil, CLASS_SOLDIER);
  Obranci_Sib = vytvor_postavy_A (Difficulty - 1, Americani_Sib, CLASS_SOLDIER);
End;


Function zpet_domu (Kdo);
Begin
  If Kdo = AmSci1 then AddComEnterUnit (Kdo, AmLabWS)
  else If Kdo = AmSci2 then AddComEnterUnit (Kdo, AmLabWS)
  else If Kdo = AmSci3 then AddComEnterUnit (Kdo, AmLabCO)
  else If Kdo = AmSci4 then AddComEnterUnit (Kdo, AmLabCO)
  else If Kdo = AmMech1 then AddComEnterUnit (Kdo, AmFactGun)
  else If Kdo = AmMech2 then AddComEnterUnit (Kdo, AmFactGun)
  else If Kdo = AmMech3 then AddComEnterUnit (Kdo, AmFactRock)
  else If Kdo = AmMech4 then AddComEnterUnit (Kdo, AmFactRock);
End;


Function vytvor_postavu (Cls);
Var X, Y;
Begin
  // Vytvoøit postavu.
  UC_Side = side_Am; UC_Nation = NATION_AMERICAN;
  HC_Sex = Rand (SEX_MALE, SEX_FEMALE);
  HC_Class = Cls;
  PrepareClassSkills (Cls, 7);
  Result = CreateHuman;
End;


Function vytvor_postavu_B (Budova, Cls);
Begin
  Result = vytvor_postavu (Cls);
  PlaceHumanInUnit (Result, Budova);
End;


Function vytvor_postavu_A (Area, Cls);
Begin
  Result = vytvor_postavu (Cls);
  PlaceUnitArea (Result, Area, false);
End;


Function vytvor_postavy_A (Pocet, Area, Cls);
Var I;
Begin
  Result = [];
  For I = 1 to Pocet do Result = Result union [vytvor_postavu_A (Area, Cls)];
End;


// Zadá vızkum.
Function zacni_zkoumat;
Begin
  AddComResearch (AmLabWS, TECH_TECH1);
  AddComResearch (AmLabWS, TECH_WEAP1);
  AddComResearch (AmLabWS, TECH_SIB1);
  AddComResearch (AmLabWS, TECH_TECH2);
  AddComResearch (AmLabWS, TECH_WEAP2);
  AddComResearch (AmLabWS, TECH_SIB2);
  AddComResearch (AmLabWS, TECH_TECH3);
  AddComResearch (AmLabWS, TECH_WEAP3);
  AddComResearch (AmLabWS, TECH_SIB3);
  AddComResearch (AmLabCO, TECH_MATDET);
  AddComResearch (AmLabCO, TECH_VIRUS);
  AddComResearch (AmLabCO, TECH_COMP1);
  AddComResearch (AmLabCO, TECH_OPTO1);
  AddComResearch (AmLabCO, TECH_COMP2);
  AddComResearch (AmLabCO, TECH_OPTO2);
  AddComResearch (AmLabCO, TECH_COMP3);
  AddComResearch (AmLabCO, TECH_OPTO3);
End;


// Vyrobí poèáteèní vozidla.
Function zacni_vyrabet;
Var I;
Begin
  // Vyrobit sbìraèe, kradeníka a jeden do konvoje.
  For I = 1 to 2 do vyrob_nakladak;
  // Vyrobit doprovod konvoje.
  For I = 1 to Max_Doprovod do vyrob_tank (false);
End;


// Nastaví suroviny do základny.
Function sur (Budova, Cans, Oil, Sib);
Var Base;
Begin
  Base = GetBase (Budova);
  SetResourceType (Base, MAT_CANS, Cans);
  SetResourceType (Base, MAT_OIL, Oil);
  SetResourceType (Base, MAT_SIBERIT, Sib);
End;


// Postavenı auák.
Export Function amici_VehicleConstructed (Veh, Fact);
Var Weapon;
Begin
  If GetSide (Veh) = side_Am then
  Begin
    Weapon = GetWeapon (Veh);
    // Mám zaèít s kontrolou vıroby dalších aut?
    VyrobenoAut = VyrobenoAut + 1;
    If VyrobenoAut >= 2 + Max_Doprovod then MamKontrolovatVyrobu = true;
    // Je to náklaïák?
    If (Weapon = US_CARGO_BAY) then
    Begin
      If not Kradenik then Kradenik = Veh
      else Konvoj = Konvoj union [Veh];
      AddComHold (Veh);
    End else Begin
      If not KonvojBylRozebran and (Doprovod < Max_Doprovod) and (Weapon <> US_DOUBLE_LASER) then
      Begin
        Doprovod = Doprovod union [Veh];
      End else Begin
        Tanky = Tanky union [Veh];
        ComMoveToArea (Veh, Parkoviste);
        AddMcUnits (MakroObrana, [Veh]);
      End;
    End;
  End;
End;


Function jednotka_pryc (Un);
Begin
  // Vyøadit jednotku ze seznamù.
  If Un = Kradenik then Kradenik = 0;
  If Un in Tanky then Tanky = Tanky diff [Un];
  If Un in Konvoj then Konvoj = Konvoj diff [Un];
  If Un in Doprovod then Doprovod = Doprovod diff [Un];
  If Un in Obranci then Obranci = Obranci diff [Un];
  If Un in Patrola then Patrola = Patrola diff [Un];
  If Un in Utok_Auta then Utok_Auta = Utok_Auta diff [Un];
  If Un in RozebratAuta then RozebratAuta = RozebratAuta diff [Un];
  // Léèení?
  If Un in KdoLeci then KdoLeci = KdoLeci diff [Un];
  If Un in KdoOpravuje then KdoOpravuje = KdoOpravuje diff [Un];
  // Je-li znièen celı konvoj, zrušit pøevoz. Toté pro patrolu.
  If not Konvoj then ProbihaPrevoz = 0;
  If not Patrola then KillMc (MakroPatrola);
End;


// Znièená jednotka.
Export Function amici_UnitDestroyed (Un);
Begin
  jednotka_pryc (Un);
End;


Export Function amici_VehicleCaptured (VehNew, VehOld, OrigSide, Hum);
Begin
  If (OrigSide = side_Am) then jednotka_pryc (VehOld);
End;


Export Function amici_BuildingCaptured (Build, OrigSide, Eng);
Begin
  If (OrigSide = side_Am) then jednotka_pryc (Build);
End;


// Vyrobíme jeden náklaïák.
Function vyrob_nakladak;
Begin
  AddComConstruct (AmFactRock, US_MEDIUM_WHEELED, ENGINE_SIBERITE, CONTROL_COMPUTER, US_CARGO_BAY);
End;


// Vyrobíme jeden tank.
Function vyrob_tank (PovolitDblAPasy);
Var P, Chassis, Weapon, Tovarna;
Begin
  // Zbraò a podvozek.
  If PovolitDblAPasy then P = Rand (1, 100)
  else Begin
    P = Rand (1, 79);
    While (P >= 30) and (P < 45) do P = Rand (1, 79);
  End;
  If (Difficulty = 1) and (P >= 80) then P = Rand (1, 79);
  If P < 15 then Begin Weapon = US_LASER; Chassis = US_MEDIUM_WHEELED; End
  else If P < 30 then Begin Weapon = US_GATLING_GUN; Chassis = US_MEDIUM_WHEELED; End
  else If P < 45 then Begin Weapon = US_HEAVY_GUN; Chassis = US_HEAVY_TRACKED; End
  else If P < 60 then Begin Weapon = US_DOUBLE_GUN; Chassis = US_MEDIUM_WHEELED; End
  else If P < 80 then Begin Weapon = US_ROCKET_LAUNCHER; Chassis = US_MEDIUM_WHEELED; End
  else Begin Weapon = US_DOUBLE_LASER; Chassis = US_HEAVY_TRACKED; End;
  // Dát náhodnı podvozek, ale aspoò tak dobrı jako je potøeba
  If PovolitDblAPasy then Chassis = Rand (Chassis, US_HEAVY_TRACKED);
  // Zadat vırobu.
  If Weapon = US_ROCKET_LAUNCHER then Tovarna = AmFactRock
  else Tovarna = AmFactGun;
  AddComConstruct (Tovarna, Chassis, ENGINE_SIBERITE, CONTROL_COMPUTER, Weapon);
End;


// Kontrola, zda existují všechny potøebné auáky.
Function kontrola_jednotek;
Var Base, Cans, Oil, Sib;
Begin
  Base = GetBase (AmDepot);
  // Jestlie nemám náklaïáky, musím nìjaké vyrobit.
  If not Kradenik then vyrob_nakladak;
  If not Konvoj and not KonvojBylRozebran then vyrob_nakladak;
  // Jestlie mám dost surovin, vyrobím i nìjaké tanky.
  // Pokud ale u mám tankù moc, tak na to kašlu.
  If (Tanky < Max_Tanky) or (not KonvojBylRozebran and (Doprovod < Max_Doprovod)) then
  Begin
    Cans = GetResourceType (Base, MAT_CANS);
    Oil = GetResourceType (Base, MAT_OIL);
    Sib = GetResourceType (Base, MAT_SIBERIT);
    If (Cans >= 200) and (Oil >= 30) and (Sib >= 20) then vyrob_tank (true);
  End;
End;


// Vıroba jednotek.
Every 0$51+0$3 Trigger MamKontrolovatVyrobu do
Begin
  kontrola_jednotek;
  Enable;
End;


// Sbírání beden a oprava budov.
Every 1$7 do
Var Inz;
Begin
  Inz = FilterAllUnits ([[F_SIDE, side_Am], [F_TYPE, UNIT_HUMAN], [F_CLASS, CLASS_ENGINEER], [F_OK]]) diff [AmEng_Oil, AmEng_Sib];
  sbirej_bedny (Inz, Americani);
  opravuj_budovy (Inz, Americani, side_Am);
  Enable;
End;


// Zjistí, zda je potøeba pøevézt suroviny do základny.
Function kontrola_prevoz;
Var Base, Base_Oil, Base_Sib;
Var Ropa, Sibe, KolikRopy, KolikSibe;
Begin
  If ProbihaPrevoz then Exit;
  // Je základna OK?
  If not IsOK (AmDepot) or (GetSide (AmDepot) <> side_Am) then Exit;
  // Mám konvoj a doprovod?
  If not Konvoj then Exit;
  If Doprovod < Max_Doprovod then Exit;
  // Zjistím základny.
  Base = GetBase (AmDepot);
  Base_Oil = GetBase (AmDepot_Oil);
  Base_Sib = GetBase (AmDepot_Sib);
  KolikRopy = GetResourceType (Base, MAT_OIL);
  KolikSibe = GetResourceType (Base, MAT_SIBERIT);
  // Kontrola, co mám pøivézt.
  Ropa = IsOk (AmDepot_Oil) and (GetSide (AmDepot_Oil) = side_Am) and
         GetResourceType (Base_Oil, MAT_OIL) > 100;
  Sibe = IsOk (AmDepot_Sib) and (GetSide (AmDepot_Sib) = side_Am) and
         GetResourceType (Base_Sib, MAT_SIBERIT) > 20;
  // Zaøídím pøevoz.
  If Ropa or Sibe then
  Begin
    // Pokud mùu pøeváet obojí, vyberu jedno.
    If Ropa and Sibe then
      If KolikSibe < 30 then Ropa = false
      else If KolikRopy > 10 * KolikSibe then Ropa = false
      else If KolikRopy < 5 * KolikSibe then Sibe = false
      else If Prob (50) then Ropa = false
      else Sibe = false;
    // A pøevezu to.
    If Sibe then prevoz_sibe
    else prevoz_ropa;
  End;
End;


Function prevoz_init (Co);
Begin
  ProbihaPrevoz = Co;
  PrevozNalozil = false;
  PrevozVylozil = false;
End;


Export Function amici_McDone (McId, State);
Begin
  // Skonèil pøevoz?
  If MakroPrevoz and (McId = MakroPrevoz) then
  Begin
    Case ProbihaPrevoz of
    1: prevoz_ropa_2;
    2: prevoz_sibe_2;
    3: prevoz_konec;
    End;
  End;
  // Skonèila pøíprava na útok?
  If MakroUtokMove and (McId = MakroUtokMove) then
  Begin
    MakroUtok = McAttack (10, RegUtok, Utok_Auta, [[MC_RETREAT_AREA_PEOPLE, Parkoviste], [MC_RETREAT_AREA_VEHICLES, Parkoviste], [MC_RETREAT_LIVES_PEOPLE, 10], [MC_RETREAT_LIVES_VEHICLES, 10]]);
  End;
End;


// Zaøídí pøevoz ropy.
Function prevoz_ropa;
Var Way;
Begin
  // Poznamenám, e pøevoz probíhá.
  prevoz_init (1);
  // Nejprve musíme dojet do základny.
  Way = cesta_B_X ^ cesta_X_O;
  MakroPrevoz = McMove (50, Konvoj union Doprovod, Way, []);
End;


// Dokonèí pøevoz ropy.
Function prevoz_ropa_2;
Var I, Way;
Begin
  If not ProbihaPrevoz then Exit;
  If PrevozNalozil then Exit;
  ProbihaPrevoz = 3; PrevozNalozil = true;
  // Naloíme ropu.
  For I in Konvoj do naloz (I, AmDepot_Oil, MAT_OIL, 100);
  // Jedeme zpátky.
  Way = cesta_O_X ^ cesta_X_B;
  MakroPrevoz = McMove (50, Konvoj union Doprovod, Way, []);
End;


// Zaøídí pøevoz sibertu.
Function prevoz_sibe;
Var Way;
Begin
  // Poznamenám, e pøevoz probíhá.
  prevoz_init (2);
  // Nejprve musíme dojet do základny.
  Way = cesta_B_X ^ cesta_X_S;
  MakroPrevoz = McMove (50, Konvoj union Doprovod, Way, []);
End;


// Dokonèí pøevoz ropy.
Function prevoz_sibe_2;
Var I, Way;
Begin
  If not ProbihaPrevoz then Exit;
  If PrevozNalozil then Exit;
  ProbihaPrevoz = 3; PrevozNalozil = true;
  // Naloíme ropu.
  For I in Konvoj do naloz (I, AmDepot_Sib, MAT_SIBERIT, 100);
  // Jedeme zpátky.
  Way = cesta_S_X ^ cesta_X_B;
  MakroPrevoz = McMove (50, Konvoj union Doprovod, Way, []);
End;


// Ukonèí pøevoz.
Function prevoz_konec;
Var Nakladaky;
Begin
  If not ProbihaPrevoz then Exit;
  ProbihaPrevoz = 0;
  If PrevozVylozil then Exit;
  PrevozVylozil = true;
  // Øíct náklaïákùm, aby vyloily náklad.
  Nakladaky = UnitFilter (Konvoj, [[F_WEAPON, US_CARGO_BAY]]);
  AddComMoveUnit (Nakladaky, AmDepot);
  // Øíct jednotkám, aby dojely na parkovištì.
  AddComMoveToArea (Konvoj union Doprovod, Parkoviste);
  AddComHold (Konvoj);
  // Ukonèit pøevoz.
  ProbihaPrevoz = false;
End;


// Pøevoz surovin do základny.
Every 1$5 do
Begin
  kontrola_prevoz;
  If not KonvojBylRozebran then Enable;
End;


// Pošle jednotky ze základny na køiovatku.
Function cesta_B_X;
Begin
  Result = [[148,44],[157,57],[148,73],[136,75],[119,78]];
End;


// Pošle jednotky od køiovatky do základny.
Function cesta_X_B;
Begin
  Result = [[121,78],[135,75],[151,71],[156,58],[142,37],[139,14]];
End;


// Pošle jednotky z køiovatky k ropì.
Function cesta_X_O;
Begin
  Result = [[108,78],[96,77],[86,72],[79,74],[67,72]];
End;


// Pošle jednotky od ropy na køiovatku.
Function cesta_O_X;
Begin
  Result = [[74,72],[88,73],[103,79],[109,78]];
End;


// Pošle jednotky z køiovatky k siberitu.
Function cesta_X_S;
Begin
  Result = [[121,90],[126,98],[134,113],[135,124]];
End;


// Pošle jednotky od siberitu na køiovatku.
Function cesta_S_X;
Begin
  Result = [[135,122],[132,110],[128,104],[122,92],[118,86]];
End;


// U jsem vidìl Rusy?
Every 0$1.1 Trigger vidi_strana_stranu (side_Am, side_Ru) do
Begin
  VidelRusa = true;
End;


// Kontrola, zda mám ukrást artefakt.
Function zkontroluj_artefakt;
Begin
  // Pokud je v autì nebo ho mám já, nedìlám nic.
  If ArtefaktMajAmici then Exit;
  If AutoSArtefaktem then Exit;
  If not KdeJeArtefakt then Exit;
  // Pokud nemám auák, kterej by ho mohl ukrást, nedìlám nic.
  If not Kradenik then Exit;
  // Pokud je v blízkosti nìjakej Rusák, tak na to peèu.
  // Ukradnu artefakt.
  ComMoveUnit (Kradenik, AmDepot);
  AddComMoveXY (Kradenik, KdeJeArtefakt [1], KdeJeArtefakt [2]);
  AddComSailEvent (Kradenik, 111);
  AddComWait (Kradenik, 0$1);
  AddComCollect (Kradenik, KdeJeArtefakt [1], KdeJeArtefakt [2]);
  AddComMoveToArea (Kradenik, Americani);
  AddComMoveUnit (Kradenik, AmDepot);
  AddComHold (Kradenik);
End;


Export Function event_KradenikPredKradenim;
Begin
  If not IsOk (Kradenik) then Exit;
  uvolni_auto (Kradenik, 10 * VelikostArtefaktu);
End;


// Kontrola, zda mám ukrást artefakt.
Every 2$1 do
Begin
  If Difficulty = 1 then Exit;
  zkontroluj_artefakt;
  Enable;
End;


// Sebere nìjaká auta na doprovod JMM.
Export Function amici_vezmi_doprovod;
Var KolikChci, Volne, Vsechny, Dbl, I;
Begin
  Result = [];
  KolikChci = dif_JMMDoprovodPocet [Difficulty];
  // Zjistit, které auáky jsou k dispozici.
  Vsechny = Tanky diff Utok_Auta;
  Volne = UnitFilter (Vsechny, [[F_CONTROL, CONTROL_COMPUTER]]);
  Dbl = UnitFilter (Volne, [[F_WEAPON, US_DOUBLE_LASER]]);
  Volne = Volne diff Dbl;
  // Zjistím, zda je jich dostateènı poèet.
  If Volne < KolikChci then Exit;
  // Vzít je.
  For I = 1 to KolikChci do Result = Result union [Volne [I]];
  Tanky = Tanky diff Result;
End;


// Americkı útok.
Function zjisti_auta_na_utok;
Var KolikChci, I, Riditelne, Dbl;
Begin
  // Zjistím, kolik auákù chci.
  KolikChci = dif_UtokSkupina [Difficulty];
  I = Rand (1, 100);
  If (I < 25) then KolikChci = KolikChci - 1
  else If (I > 75) and (CisloUtoku > 1) then KolikChci = KolikChci + 1;
  // Zjistím, které auáky jsou k dispozici.
  Riditelne = [];
  For I in Tanky do
  Begin
    If (GetControl (I) = CONTROL_MANUAL) and not IsDrivenBy (I) then Continue;
    If (GetWeapon (I) = US_CARGO_BAY) then Continue;
    If (GetLives (I) < HRANICE_ZDRAVI) then Continue;
    Riditelne = Riditelne union [I];
  End;
  // Jestli nemám aspoò dva double lasery, vyhodím dva double lasery.
  Dbl = UnitFilter (Riditelne, [[F_TYPE, UNIT_VEHICLE], [F_WEAPON, US_DOUBLE_LASER]]);
  Riditelne = Riditelne diff Dbl;
  If (Dbl >= 2) then
  Begin
    Riditelne = [Dbl [1], Dbl [2]] ^ Riditelne;
  End;
  // Mám dost auákù?
  If (CisloUtoku = 1) and (KolikChci > Riditelne) then Result = Riditelne
  else If Riditelne >= KolikChci then
  Begin
    // Vezmu první auáky.
    Result = [];
    For I = 1 to KolikChci do
      Result = Result union Riditelne [I];
  End else Result = [];
  // Pøidám i nìjaké vojáky.
  If Prob (50) and (Obranci > 4) then
    For I = 1 to Rand (1, 2) do
      Result = Result union [Obranci [I]];
End;


// Zaène útok s vozidly z Utok_Auta.
Function zacni_utok;
Var Area, I, Prio;
Begin
  // Zaèátek útoku.
  ProbihaUtok = true;
  // Pøidám pøíkaz na útoèení.
  MakroUtokMove = McMove (11, Utok_Auta, [[135,15],[142,40],[156,56]], [MC_MOVE_DONTCAPTURE]);
  // Makro Attack udìlám v McDone.
End;


// Tato funkce znièí konvoj.
Function rozeber_konvoj;
Var I, F;
Begin
  If KonvojBylRozebran then Exit;
  KonvojBylRozebran = true;
  // Pøidat tanky do tankù.
  Tanky = Tanky union Doprovod;
  Doprovod = [];
  // Rozebrat náklaïáky.
  RozebratAuta = RozebratAuta union Konvoj;
  Konvoj = [];
End;


Every 0$5.3 Trigger ProbihaPrevoz = 0 Marked 22 do
Begin
  rozeber_konvoj;
End;


// Zruší útok.
Function vrat_utok;
Var I;
Begin
  ProbihaUtok = false; CisloUtoku = CisloUtoku + 1;
  // Okamitì zastaví útok a pošle vozidla zpìt.
  MakroUtok = 0; MakroUtokMove = 0;
  AddComMoveToArea (Utok_Auta, Parkoviste);
End;


Export Function amici_McAttackDone (McId, Un);
Begin
  vrat_utok;
End;


// Vrátí první èlen v èasech útokù.
Function zjisti_cas_utoku;
Begin
  If CasyUtoku then
  Begin
    Result = CasyUtoku [1];
    If CasyUtoku > 1 then CasyUtoku = Delete (CasyUtoku, 1);
  End else Result = 5;
End;


// Zjistí, zda Amíci dosáhli bodu, od kterého je moné útoèit.
Function utoceni_splneny_predpoklady;
Begin
  // Útoèit mùu tehdy, pokud jsem buï vidìl Rusa nebo pokud uplynula
  // dostateènì dlouhá doba.
  Result = VidelRusa or (Tick > 12$0);
  If not ZnicenSib and not ZnicenOil and (Tick < 15$0) then Result = false;
  // Aby to nebylo úplnì deterministické, budu nìkteré útoky odkládat.
  If Prob (25) then Result = false;
End;


// Kadou cca minutu je volána tato funkce.
Function casovac_utok;
Begin
  // Pokud probíhá útok, nedìlám nic.
  If ProbihaUtok then Exit;
  // Zkontrolujeme, zda máme nastavené poèítadlo minut.
  If Utok_ZbyvaMinut = -1 then Utok_ZbyvaMinut = zjisti_cas_utoku;
  // Odeèteme jednu minutu.
  If Utok_ZbyvaMinut > 0 then Utok_ZbyvaMinut = Utok_ZbyvaMinut - 1;
  // Zjistíme, zda je moné vùbec zaèít s útokem.
  // Pokud ne, neútoèíme - ale poèítadlo odeètáme.
  If not utoceni_splneny_predpoklady then Exit;
  // Pokud nedošel èasovaè na nulu, nedìláme nic.
  If Utok_ZbyvaMinut > 0 then Exit;
  // Neprobíhá náhodou pøevoz? Pokud ano, poèkám, a
  // pøevoz skonèí. Kadopádnì taky rozeberu konvoj.
  If not ZacalyUtoky then
  Begin
    Enable (22);
    ZacalyUtoky = true;
  End;
  If ProbihaPrevoz then Exit;
  // Mám vùbec dostatek aut na útok?
  Utok_Auta = zjisti_auta_na_utok;
  If not Utok_Auta then Exit;
  // Útok mùe zaèít!
  zacni_utok;
  // Nastavíme èasovaè na další útok.
  Utok_ZbyvaMinut = zjisti_cas_utoku;
End;


// Tento trigger bude kontrolovat, zda má zaèít útok.
Every 0$45 Marked 21 do
Begin
  Wait (Rand (0$0, 0$30));
  casovac_utok;
  Enable;
End;


Every 0$30 do
Begin
  Wait (1$0 * Rand (PrvniUtok [1], PrvniUtok [2]));
  Enable (21);
End;


Function opravuj;
Begin
  If KdoOpravuje then Exit;
  KdoOpravuje = opravuj_auta ([AmMech1, AmMech2, AmMech3, AmMech4], 114, Americani, side_Am, Utok_Auta);
End;


Function kuryruj;
Begin
  If KdoLeci then Exit;
  KdoLeci = kuryruj_lidi ([AmSci1, AmSci2, AmSci3, AmSci4], 113, Americani, side_Am);
End;


Export Function event_AmiciOprava (Event);
Var Kdo;
Begin
  Case Event of
  113: Begin
         For Kdo in KdoLeci do zpet_domu (Kdo);
         KdoLeci = [];
       End;
  114: Begin
         For Kdo in KdoOpravuje do zpet_domu (Kdo);
         KdoOpravuje = [];
       End;
  End;
End;


// Oprava aut a lidí.
Every 0$57 do
Begin
  opravuj;
  kuryruj;
  Enable;
End;


Every 0$7.1 do
Begin
  If zije_zakladna (side_Am, AmDepot_Oil, Americani_Oil, Obranci_Oil) then Enable
  else ZnicenOil = true;
End;


Every 0$7.9 do
Begin
  If zije_zakladna (side_Am, AmDepot_Sib, Americani_Sib, Obranci_Sib) then Enable
  else ZnicenSib = true;
End;


// Zajistí pøísun surovin, pokud budou Amerièanùm docházet.
Function zkontroluj_suroviny (Mat, Amount);
Var Base, Inz;
Begin
  If not IsOk (AmDepot) then Exit;
  // Zjistit, zda je v základnì dostateèné mnoství materiálu.
  Base = GetBase (AmDepot);
  If GetResourceType (Base, Mat) >= Amount then Exit;
  // Není tam dost materiálu. Tak si vytvoøíme další.
  // Zjistíme, zda existují nìjací inenıøi.
  Inz = FilterUnitsInArea (Americani, [[F_SIDE, side_Am], [F_TYPE, UNIT_HUMAN], [F_CLASS, CLASS_ENGINEER]]);
  If Inz and (GetListOfCratesInArea (Americani) < 15) then
  Begin
    // Mám inenıry, tak vytvoøím materiál venku.
    CreateResourcesArea (Mat, 5, BednyAmericani, true);
    sbirej_bedny (Inz, BednyAmericani);
  End else Begin
    // Inenıøi jsou v háji, a tak pøidám materiál podvodnì pøímo do základny.
    AddResourceType (Base, Mat, 50);
  End;
End;


Every 2$12 do
Begin
  zkontroluj_suroviny (MAT_CANS, 250);
  zkontroluj_suroviny (MAT_SIBERIT, 50);
  zkontroluj_suroviny (MAT_OIL, 150);
  Enable;
End;


Every 0$17 Trigger RozebratAuta do
Var F, Hotovo, I;
Begin
  // Zjistit dostupné továrny.
  F = UnitFilter ([AmFactGun, AmFactRock], [[F_SIDE, side_Am], [F_OK]]);
  If F then
  Begin
    Hotovo = [];
    // Rozebrat auta.
    For I in RozebratAuta do
    Begin
      If IsDead (I) then Hotovo = Hotovo union [I]
      else If not HasTask (I) then ComRecycle (I, nahoda_seznam (F));
    End;
    // Poznaèit si jejich rozebrání.
    RozebratAuta = RozebratAuta diff Hotovo;
  End;
  // Znovu.
  Enable;
End;



// Prepare Hero for use
// ident - ident from start.txt - 'JMM' for example
// exist_mode
//   - false if unit not exist in save
//   - true if unit already exist in save
// example: JMM := PrepareUnit('JMM', false);
Export Function PrepareUnit(ident, exist_mode, prefix);
var unit;
begin

if exist_mode then
   unit := CreateCharacter(prefix & ident)
  else
   unit := NewCharacter(ident);

result := unit;
End;

// example: PrepareTank(1, 1, us_medium_wheeled, engine_combustion, control_manual, us_machine_gun, 80);
Export Function PrepareTank(side, nation, chassis, engine, control, weapon, fuel);
begin
uc_side := side;
uc_nation := nation;

vc_chassis := chassis;
vc_engine := engine;
vc_control := control;
vc_weapon := weapon;
vc_fuel_battery := fuel;

result := CreateVehicle;
End;


Export Function SayX(units, ident);
var i;
begin
result := false;

if not units then
   exit;

for i in units do
    if IsOk(i) then
       begin
       Say(i, ident);
       result := i;
       break;
       end;
End;

Export Function SpawnApeman(area, amount);
var i;
begin
InitUc;
InitHc;

uc_side := 0;
uc_nation := 0;

for i = 1 to amount do
    begin
    hc_attr := [rand(9, 12), rand(9, 12)];
    hc_agressivity := rand(-1, 1);
    PrepareHuman(false, class_apeman, 1);
    PlaceUnitArea(CreateHuman, area, false);
    end;

InitHc;
End;

Export Function PrepareNature;
var i;
begin
SpawnApeman(forest, [4, 4, 3][Difficulty]);

for i := 1 to 3 do
    begin
    hc_class := 21;
    hc_gallery := '';
    PlaceUnitArea(CreateHuman, forest, false);
    end;

for i := 1 to 2 do
    begin
    hc_class := 18;
    hc_gallery := '';
    PlaceUnitArea(CreateHuman, forest, false);
    end;

for i := 1 to 2 do
    begin
    hc_class := 13;
    hc_gallery := '';
    PlaceUnitArea(CreateHuman, forest, false);
    end;

End;

Export Function SeeUnit(observer, unit);
var i, tmp;
begin
if not observer or not unit then
   exit;

if not See(GetSide(observer), unit) then
   exit;


result := GetDistUnits(observer, unit) < 12;
End;

Export Function PlaceUnitHex(un, hex, mode);
begin
if ValidHex(hex[1], hex[2]) and HexInfo(hex[1], hex[2]) = 0 then
   PlaceUnitXY(un, hex[1], hex[2], mode);
End;

Export function Video(mode);
begin
ingame_video = mode;
interface_hidden = mode;
End;

Export Function ReverseArray(array);
var i;
begin
// [1, 2, 3, 4] -> [4, 3, 2, 1]
if not array then
   exit;

result := [];

for i := 1 to array do
    result := Insert(result, 1, array[i]);
End;

Export Function ComExit(unit);
begin
result := IsInUnit(unit);

if not result then
   exit;

if GetType(result) = unit_vehicle then
   ComExitVehicle(unit)
else
   ComExitBuilding(unit);
End;

// Return's tech list by nation
// side
// nation
// state:
//       state_enabled 1
//       state_researched 2
//       state_disabled 0
//       all -1
Export Function GetTechNation(side, nation, state);
var i;
begin
if not side or not nation then
   exit;

case nation of
     nation_american: result := [35, 45, 46, 47, 82, 83, 84, 85, 86, 1, 2, 6, 15, 16, 7, 12, 13, 10, 14, 20, 21, 22, 25, 32, 27, 36, 69, 39, 34, 40, 48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 60, 61, 62, 80, 82, 83, 84, 85, 86];
     nation_arabian: result := [35, 45, 46, 47, 82, 83, 84, 85, 87, 70, 1, 11, 3, 4, 5, 6, 15, 18, 7, 17, 8, 20, 21, 22, 72, 26, 69, 39, 40, 41, 42, 43, 48, 49, 50, 51, 52, 53, 54, 55, 56, 60, 61, 62, 66, 67, 68, 81, 82, 83, 84, 85, 87];
     nation_russian: result := [46, 47, 1, 2, 82, 83, 84, 85, 86, 11, 9, 20, 19, 21, 24, 22, 25, 28, 29, 30, 31, 37, 38, 32, 27, 33, 69, 39, 34, 40, 71, 23, 44, 48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 63, 64, 65, 82, 83, 84, 85, 86];
end;

if state > -1 and state < 3 then
   for i in result do
       if GetTech(i, side) <> state then
          result := result diff i;
End;


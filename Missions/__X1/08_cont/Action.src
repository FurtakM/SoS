Export function Action;
var speaker, i, tmp;
begin  
Video(true);

hasNormalScene := Kaia or Givi;

if hasNormalScene then
   begin
   for i in americanSquad do
       begin
       SetClass(i, 33);
       PlaceUnitXYR(i, 78, 16, 5, false);
       ComHold(i);

       //if Prob(60) then
       //   ForceSleep(i, true);
       end;

   tmp := [ Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf,
            Kurt, Givi ] union heikeSecondSquad;

   SetClass(Heike, 33);
   PlaceUnitXYD(Heike, 117, 24, 3, false);
   ComTurnXY(Heike, 123, 39);

   for i in tmp do
       begin
       SetClass(i, 33);
       PlaceUnitArea(i, arSpawnArea1, false);
       ComTurnXY(i, 113, 16);
       end;
   end;

CenterNowOnXY(124, 39);

PrepareDeadBodies;
PrepareFirstScene;

PlaceSeeing(152, 65, 2, -70);
RemoveSeeing(152, 65, 2);

PlaceSeeing(122, 40, 2, -8);

CenterNowOnXY(124, 37);

speaker := HexInfo(124, 37);

Say(speaker, 'DRum-Execution-1');
Say(speaker, 'DRum-Execution-2');

wait(0$01);

Say(speaker, 'DRum-Execution-3');

wait(0$1);

Say(speaker, 'DRum-Execution-4');

ComAttackUnit(russianKillers[1], HexInfo(122, 39));
ComAttackUnit(russianKillers[2], HexInfo(123, 41));

AddComHold(russianKillers);

wait(0$1);

RemoveSeeing(122, 40, 2);

if hasNormalScene then
   ActionNormal
else
   ActionAdditional;
End;

Function ActionNormal;
var i, tmp;
begin
CenterNowOnUnits(Heike);

wait(0$1);

ComMoveXY(Heike, 115, 19);
AddComTurnXY(Heike, 113, 16);

wait(0$2);

Say(Heike, 'DH-Conspiracy-1');
Say(Kaia, 'DK-Conspiracy-2');
Say(Givi, 'DG-Conspiracy-3');
Say(Givi, 'DG-Conspiracy-4');
Say(Heike, 'DH-Conspiracy-5');

if IsOk(Kamil) then
   begin
   hasVodka := true;

   Say(Kamil, 'DKam-Conspiracy-6');
   Say(Kamil, 'DKam-Conspiracy-7');
   Say(Kamil, 'DKam-Conspiracy-8');
   Say(Kamil, 'DKam-Conspiracy-9');
   end;

if IsOk(Sophia) then
   begin
   americanContact := true;

   Say(Sophia, 'DS-Conspiracy-10');
   Say(Markov, 'DM-Conspiracy-11');
   Say(Sophia, 'DS-Conspiracy-12');

   ShowPlace(84, 19, 0$3);

   if johnnySaved then
      begin
      Say(Sophia, 'DS-Conspiracy-13');
      Say(Heike, 'DH-Conspiracy-14');
      Say(Sophia, 'DH-Conspiracy-15');
      end
   else
      begin
      Say(Sophia, 'DS-Conspiracy-a-13');
      Say(Heike, 'DH-Conspiracy-a-14');
      Say(Sophia, 'DH-Conspiracy-a-15');
      end;

   if IsOk(Givi) then
      begin
      Say(Givi, 'DG-Conspiracy-a-16');
      Say(Heike, 'DH-Conspiracy-a-17');
      end;
   end;

Say(Heike, 'DH-Conspiracy-16');

if IsOk(Givi) then
   begin
   hasStimDrugs := true;

   Say(Givi, 'DG-Conspiracy-17');

   SetSide(drugsLab, 2);
   ShowPlace(184, 95, 0$3);
   SetSide(drugsLab, 3);

   Say(Givi, 'DG-Conspiracy-18');
   Say(Givi, 'DG-Conspiracy-19');
   Say(Givi, 'DG-Conspiracy-20');
   Say(Heike, 'DH-Conspiracy-21');

   if IsOk(Kaia) then
      begin
      hasExplosions := true;

      Say(Kaia, 'DK-Conspiracy-22');

      SetSide(barExplosion, 2);
      ShowPlace(166, 104, 0$3);
      SetSide(barExplosion, 3);

      Say(Kaia, 'DK-Conspiracy-23');
      end;

   if IsOk(Olaf) then
      begin
      hasExplosionsInFactory := true;

      Say(Olaf, 'DO-Conspiracy-24');
      ShowPlace(175, 54, 0$2);

      if IsOk(Markov) then
         begin
         Say(Markov, 'DMar-Conspiracy-25');
         Say(Markov, 'DMar-Conspiracy-26');
         end;
      end;
   end;

if IsOk(Kurt) and IsOk(Kaia) then
   begin
   hasSniperRifle := true;

   Say(Kurt, 'DKs-Conspiracy-27');

   if IsOk(Givi) then
      Say(Givi, 'DG-Conspiracy-28')
   else if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-Conspiracy-a-28') then
           Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-Conspiracy-a-28');

   Say(Kurt, 'DKs-Conspiracy-29');
   Say(Kurt, 'DKs-Conspiracy-30');
   Say(Kaia, 'DK-Conspiracy-31');

   if not Say(Markov, 'DMar-Conspiracy-33') and heikeSecondSquad then
      if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-Conspiracy-a-34') then
         Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-Conspiracy-a-34');

   Say(Kaia, 'DK-Conspiracy-34');
      
   if IsOk(Markov) then
      begin
      Say(Markov, 'DMar-Conspiracy-35');
      Say(Kaia, 'DK-Conspiracy-36');
      Say(Markov, 'DMar-Conspiracy-37');
      Say(Markov, 'DMar-Conspiracy-38');

      ShowPlace(137, 98, 0$3);

      Say(Markov, 'DMar-Conspiracy-39');
      end
   else if heikeSecondSquad then
      begin
      ShowPlace(137, 98, 0$3);

      if not Say(UnitFilter(heikeSecondSquad, [f_sex, sex_male])[1], 'DArm-Conspiracy-a-35') then
         Say(UnitFilter(heikeSecondSquad, [f_sex, sex_female])[1], 'DArf-Conspiracy-a-35');
      end;
   end;

Say(Heike, 'DH-Conspiracy-40');

if IsOk(Mike) then
   begin
   hasScout := true;

   Say(Mike, 'DM-Conspiracy-41');
   Say(Mike, 'DM-Conspiracy-42');

   ShowPlace(73, 102, 0$3);

   Say(Mike, 'DM-Conspiracy-43');
   end;

if hasVodka and IsOk(Kaia) and IsOk(Kurt) then
   begin
   hasMethanol := true;

   Say(Kaia, 'DK-Conspiracy-44');
   Say(Kaia, 'DK-Conspiracy-45');
   Say(Kamil, 'DKam-Conspiracy-46');
   end;

Say(Heike, 'DH-Conspiracy-47');

wait(0$1);

your_side := 8;

CenterNowOnXY(1, 1);

for i in FilterAllUnits([[f_side, 2], [f_type, unit_human]]) do
    RemoveUnit(i);

wait(0$0.3);

Query('StartInfo1');

wait(0$1);

LoadColorsTXT('colors_night.txt');

Query('StartInfo2');

PrepareGame;

PlaceSeeing(152, 65, 2, -70);
RemoveSeeing(152, 65, 2);

PlaceSeeing(133, 76, 2, -6);

PlaceSeeing(152, 65, 8, -70);
RemoveSeeing(152, 65, 8);

PlaceSeeing(133, 76, 8, -6); 

CenterNowOnUnits(ruOutpost2);

dialogue_skipped := false;

Say(Heike, 'DH-Escape-1');
Say(Heike, 'DH-Escape-2');

StartLockerMinigame;

Say(Heike, 'DH-Escape-4');

wait(0$3);

your_side := 2;

ComExitBuilding(Heike);
AddComMoveXY(Heike, 128, 64);

repeat
 wait(0$1);
until IsAt(Heike, 128, 64);

ComHold(Heike);

Say(Heike, 'DH-Start-1');

if not americanContact then
   Say(Heike, 'DH-Start-c-2')
else if johnnySaved then
   Say(Heike, 'DH-Start-a-2')
else
   Say(Heike, 'DH-Start-b-2');

Video(false); // gl hf
tick := 0;

if americanContact then
   ChangeMissionObjectives('M1a')
else
   ChangeMissionObjectives('M1');

SaveForQuickRestart;
End;

// Scene where Heike is going to die
Function ActionAdditional;
var i, un, tmp;
begin
PlaceSeeing(115, 27, 2, -6);

PlaceHumanInUnit(Heike, HexInfo(115, 27));
SetClass(Heike, 33);

CenterNowOnXY(124, 39);

Say(Heike, 'DH-Fail-1');

RaiseSailEvent(eventCenterCamOnHeike);

ComExit(Heike);
AddComMoveXY(Heike, 109, 33);
AddComHold(Heike);

RemoveSeeing(115, 27, 2);

wait(0$2);

repeat
 wait(0$1);
until IsAt(Heike, 109, 33);

Say(Heike, 'DH-Fail-2');

ComMoveXY(Heike, 111, 48);

repeat
 wait(0$1);
until GetDistUnitXY(Heike, 111, 48) < 4;

AddComMoveXY(Heike, 106, 72);
AddComMoveXY(Heike, 103, 80);

uc_side := 3;
uc_nation := 3;

PrepareHuman(sex_male, class_soldier, 6);
hc_attr := [12, 12];
un := CreateHuman;

PlaceUnitXYD(un, 134, 52, 4, false);

ComTurnUnit(un, Heike);
ComMoveUnit(un, Heike);

Say(un, 'DRum-Fail-3');

for i := 1 to 3 do
    begin
    PrepareSoldier(false, 10);
    un := CreateHuman;
    tmp := Join(tmp, un);
    PlaceUnitArea(un, failSceneGuardArea, false);
    ComTurnUnit(un, Heike);
    end;

repeat
 wait(0$1);
until UnitFilter(tmp, [f_see, 2]) > 1;

wait(0$1);

DialogueOn;

Say(Heike, 'DH-Fail-4');
Say(Heike, 'DH-Fail-5');
Say(Heike, 'DH-Fail-6');

DialogueOff;

ComAttackUnit(tmp, Heike);

repeat
 wait(0$1);
until IsDying(Heike);

KillUnit(Heike);

wait(0$4);

ResetFog;

DialogueOn;

dwait(0$1);

Heike := PrepareUnit('Heike', (not debug), prefix);
SayNoFace(Heike, 'DH-Fail-7');

DialogueOff;

YouLost('Fail');
End;

// ACTION EVENTS
// east road
Every 0$1 trigger SeeXY(2, 173, 29) and GetDistUnitXY(Heike, 173, 29) < 6 do
Say(Heike, 'DH-EastRoad');

// see eastern part of the base
Every 0$1 trigger SeeXY(2, 217, 120) and GetDistUnitXY(Heike, 217, 120) < 6 do
Say(Heike, 'DH-EastEnd');

// heike on the hill
Every 0$1 trigger IsInArea(Heike, hillArea) do
begin
if not heikeSquad then
   Say(Heike, 'DH-Hill')
else
   Say(Heike, 'DH-Hill-a');
End;

// heike on the border of base
Every 0$1 trigger IsInArea(Heike, baseBorderArea) do
var x, y, v;
begin
DialogueOn;

CenterNowOnUnits(Heike);

x := GetX(Heike);
y := GetY(Heike);

PlaceSeeing(x, y, 2, -12);
RemoveSeeing(x, y, 2);

if FilterAllUnits([f_or, [f_btype, b_bunker], [f_btype, b_turret]]) then
   v := 3
else
   v := 2;

disable;

Say(Heike, 'DH-Outpost-' & rand(1, v));

DialogueOff;

wait(0$10);

enable;
End;   

// spot givi
Every 0$1 trigger See(2, Givi) and GetDistUnits(Heike, Givi) < 6 or AreInTheSameBuilding(Givi, Heike) do
begin
DialogueOn;
CenterNowOnUnits(Givi);

Say(Heike, 'DH-Givi-1');
Say(Givi, 'DG-Givi-2');

DialogueOff;

SetSide(Givi, 2);
heikeSquad := Join(heikeSquad, Givi);
End;

Function SayHEmpty();
begin
case rand(1, 3) of
     1: Say(Heike, 'DH-Empty-1');
     2: Say(Heike, 'DH-Empty-2');
     3: Say(Heike, 'DH-Empty-3');
     end;
End;

Function SayHVisited();
begin
case rand(1, 3) of
     1: Say(Heike, 'DH-Visited-1');
     2: Say(Heike, 'DH-Visited-2');
     3: Say(Heike, 'DH-Visited-3');
     end;
End;

Every 0$2 trigger not isAlarm and entranceAllowedList and IsOk(Heike) do
var i;
begin
enable;

for i in entranceAllowedList do
    if InEntrance(Heike, i) then
       begin
       HeikeEnterBuilding(i);
       break;
       end;
End;

Function HeikeEnterBuilding(building);
begin
if not building then
   exit;

if building in entranceVisitedList then
   begin
   SayHVisited();
   exit;
   end;

case Query('EnterBuilding') of
     1: ;
     2: exit;
     end;

entranceVisitedList := Join(entranceVisitedList, building);

InGameOn;

SetSide(Heike, 5);
SetSide(building, 5);

ComEnterUnit(Heike, building);

wait(0$3);

case building of
     ruFac: begin
            if IsOk(Markov) then
               begin
               Say(Heike, 'DH-Krastyo-1');
               Say(Markov, 'DMar-Krastyo-2');
               Say(Markov, 'DMar-Krastyo-3');

               SetSide(Markov, 2);
               heikeSquad := Join(heikeSquad, Markov);
               ComExitBuilding(Markov); 
               end
            else
               SayHEmpty();
            end;
     ruBarKam: begin
               if IsOk(Kamil) then
                  begin
                  Say(Heike, 'DH-Kamil-1');
                  Say(Kamil, 'DKam-Kamil-2');

                  if IsOk(Sophia) then
                     if Sophia in heikeSquad then
                        Say(Kamil, 'DKam-Kamil-a-3')
                     else
                        Say(Kamil, 'DKam-Kamil-3');
                  end
               else
                  SayHEmpty();
               end;
     ruOutpost1: begin
                 if IsOk(Kurt) then
                    begin

                    end
                 else
                    SayHEmpty();
                 end;
     ruOutpostBar: begin
                   if not hasGuardPatrolPlan then
                      begin
                      Say(Heike, 'DH-PatrolPlan');
                      hasGuardPatrolPlan := true;
                      ChangeSideFog(6, 2);
                      end
                   else
                      SayHEmpty();
                   end;
     end;

ComExitBuilding(Heike);

SetSide(Heike, 2);
SetSide(building, 3);

InGameOff;
End;


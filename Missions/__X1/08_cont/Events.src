On SailEvent(event) do
begin
case event of
     centerCamEvent:
        begin
        repeat
         wait(0$1);
         CenterOnUnits(Heike);
        until IsDying(Heike);
        end;
     end;
End;

On SailEventCustom(p1, x, y, time, p5, p6, p7) do
begin
if dialogue_skipped then
   exit;

if showPlaceEvent then
   begin
   PlaceSeeing(x, y, 2, -6);
   CenterOnXY(x, y);

   wait(time);

   CenterNowOnUnits(Heike);
   RemoveSeeing(x, y, 2);
   end;
End;

On UnitDestroyed(un) do
var x, y;
begin
if un = Heike and hasNormalScene then
   if loseCounter then
      YouLost('HeikeNoSurrender')
   else
      YouLost('Heike');

if GetSide(un) = 2 and GetType(un) = unit_human then
   loseCounter := Inc(loseCounter);

if GetSide(un) = 6 and not isAlarm and un in ruPatrol then
   begin
   x := GetX(un);
   y := GetY(un);

   ruKilledHex := Join(ruKilledHex, [x, y]);
   ruPatrol := ruPatrol diff un;

   if SeeXY(3, x, y) then
      isAlarm := true;
   end;
End;

On Contact(s1, s2) do
begin
if meetWithMuchin then
   exit;

if (s1 = 2 and s2 = 3) or (s1 = 3 and s2 = 2) then
   isAlarm := true;
End;
Every 0$2 trigger isAlarm do
var i, points, tmp;
begin
DialogueOn;
Say(Heike, 'DH-Alarm-1');
DialogueOff;

PlaySoundXY(-1, -1, 'alarm');

SetAttitude(3, 2, att_enemy, true);
SetAttitude(6, 2, att_enemy, true);

points := [ [107, 39], [143, 33], [180, 115], [96, 90], [59, 52], [16, 15], [90, 120] ];
tmp := FilterAllUnits([[f_side, 6], [f_class, class_soldier]]);

for i in tmp do
    begin
    ComExit(i);                
    AddComAgressiveMove(i, points[i mod 7 + 1][1], points[i mod 7 + 1][2]);
    end;

Explode;

wait(0$45);

AddComAttackUnit(tmp, Heike);
End;

Export Function Explode;
var i, x, y, j;
begin
if hasExplosionsInFactory then
   explosivePlantedList := Join(explosivePlantedList, ruFac);

if explosivePlantedList then
   begin
   for i in explosivePlantedList do
       begin
       x := GetX(i);
       y := GetY(i);

       for j in [1, 3, 5] do
           MineExplosion(ShiftX(x, j, 2), ShiftY(y, j, 2), true);
       end;

   Say(Heike, 'DH-End-2');

   wait(0$5 * Count(explosivePlantedList));
   end;

if not isAlarm then
   isAlarm := true;
End;

// see arabian vehicle
Every 0$3 trigger FilterAllUnits([[f_side, 2], [f_type, unit_vehicle], [f_or, [f_see, 3], [f_see, 6]]]) do
isAlarm := true;
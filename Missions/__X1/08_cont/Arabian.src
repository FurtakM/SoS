Export Heike, Givi, Mike, Ibrahim, Kamil, Kaia, Sophia, Markov, Olaf, Kurt, Aviradze,
       heikeSecondSquad, americanSquad, Harris, Doug, Jerry;
Export HeikeTmp, GiviTmp;
Export Function PrepareArabian;
var i;
begin
uc_side := 4;
uc_nation := 1;
americanSquad := CreateCharacterSet('07_Americans');

if americanSquad < 5 then
   begin
   if not americanSquad then
      americanSquad := [];

   for i := americanSquad to [2, 3, 4, 4][Difficulty] do
       begin
       PrepareHuman(false, 2, 6);
       americanSquad := Join(americanSquad, CreateHuman);
       end;
   end
else
   for i := Count(americanSquad) downto 5 do
       americanSquad := Delete(americanSquad, i);

Doug := PrepareUnit('UsCommander', (not debug), '07_');

PrepareSoldier(sex_male, 6);
Jerry := CreateHuman;
SetClass(Jerry, class_prison);

if not Doug then
   begin
   PrepareSoldier(sex_male, 6);
   Doug := CreateHuman;
   end;

SetClass(Doug, class_prison);

if true or johnnySaved or debug then
   begin
   Harris := NewCharacter('Harris');
   SetClass(Harris, class_prison);
   end;

uc_side := 2;
uc_nation := 2;

Heike := PrepareUnit('Heike', (not debug), prefix);
Ibrahim := PrepareUnit('Ibrahim', (not debug), prefix);
Givi := PrepareUnit('Givi', (not debug), prefix);
Kamil := PrepareUnit('Kamil', (not debug), prefix);
Kaia := PrepareUnit('Kaia', (not debug), prefix);
Sophia := PrepareUnit('Sophia', (not debug), prefix);
Markov := PrepareUnit('Markov', (not debug), prefix);
Aviradze := PrepareUnit('Aviradze', (not debug), prefix);
Kurt := PrepareUnit('Kurt', (not debug), prefix);
Mike := PrepareUnit('Mike', (not debug), prefix);
//Olaf := PrepareUnit('Olaf', (not debug), prefix);
Olaf := NewCharacter('Olaf');

heikeSecondSquad := CreateCharacterSet('07_others');
End;

// Place Heike and the others in different places
Export Function PrepareGame;
var i;
begin
// heike in west outpost
PlaceHumanInUnit(Heike, ruOutpost2);

// givi nearby barracks
if Givi then
   begin
   SetSide(Givi, 5);
   PlaceUnitArea(Givi, giviArea, false);
   ComHold(Givi);
   end;

// markov in factory
if Markov then
   begin
   SetSide(Markov, 5);
   SetClass(Markov, class_mechanic);
   PlaceHumanInUnit(Markov, ruFac);
   end;

if Mike then
   begin
   SetSide(Mike, 5);
   PlaceUnitArea(Mike, mikeArea, false);
   ComHold(Mike);
   end;

// olaf nearby factory
if Olaf then
   begin
   SetSide(Olaf, 5);
   PlaceUnitArea(Olaf, olafArea, false);
   ComLookAtDir(Olaf, 3);
   end;

// kamil in barracks
if Kamil then
   begin
   SetSide(Kamil, 5);
   PlaceHumanInUnit(Kamil, ruBarKam);
   end;

// kurt in outpost
if Kurt then
   begin
   SetSide(Kurt, 5);
   PlaceHumanInUnit(Kurt, ruOutpost1);
   end;

// kaia in barracks
if Kaia then
   begin
   SetSide(Kaia, 5);
   PlaceHumanInUnit(Kaia, ruKaiaBar);
   end;

// sopiha nearby us lab
if Sophia then
   begin
   SetSide(Sophia, 5);
   PlaceUnitXY(Sophia, 142, 96, false);

   ComHold(Sophia);
   end;

// harris nearby factory
if Harris then
   begin
   SetSide(Harris, 4);
   PlaceUnitArea(Harris, harrisArea, false);
   ComHold(i);
   end;

// doug in barracks
if Doug then
   begin
   SetSide(Doug, 4);
   PlaceHumanInUnit(Doug, dougBar);
   end;

if Aviradze then
   begin
   SetSide(Aviradze, 5);
   PlaceHumanInUnit(Aviradze, ruLab);
   end;

if Ibrahim then
   begin
   SetSide(Ibrahim, 5);
   PlaceHumanInUnit(Ibrahim, ruLab);
   end;

if Jerry then
   begin
   SetSide(Jerry, 4);
   PlaceUnitXY(Jerry, 119, 59, false);
   ComHold(Jerry);
   end;

PlaceHumanInUnit(ruCommisar, ruLab);

for i in americanSquad do
    begin
    PlaceUnitArea(i, americansRandomArea, false);
    ComHold(i);
    end;

for i in heikeSecondSquad do
    begin
    SetSide(i, 5);
    PlaceUnitArea(i, arabiansRandomArea, false);
    ComHold(i);
    end;

if not hasVodka then
   for i in FilterAllUnits([f_or, [f_side, 2], [f_side, 5]]) do
       SetLives(i, 500);

for i in FilterAllUnits([f_side, 4]) do
    SetLives(i, 500);
End;

Export function SetClassKaiaRevolver;
begin
if IsOk(Kaia) then
   SetClass(Kaia, class_revolver); // revolver
End;

Export function SpawnHeikeAsRussian;
begin
RemoveUnit(Heike);

heikeTmp := Heike;

uc_side := 2;

Heike := NewCharacter('HeikeRu');
CopySkills(heikeTmp, Heike);

PlaceHumanInUnit(Heike, laundry);
SetClass(Heike, class_soldier);

heikeAsRussian := true;

ChangeMissionObjectives_Silent('M1-laundry-out');
End;

Export function SpawnGiviAsRussian;
begin
RemoveUnit(Givi);

giviTmp := Givi;

uc_side := 2;

Givi := NewCharacter('GiviRu');
CopySkills(giviTmp, Givi);

PlaceHumanInUnit(Givi, laundry);
SetClass(Givi, class_soldier);

giviAsRussian := true;

ChangeMissionObjectives_Silent('M1-laundry-out');
End;
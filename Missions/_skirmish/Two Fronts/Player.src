Export Commanders, Player, Gladkov, Davidov, Gorki, Stolypin, playerSquad, playerCom;
Export Function InitCommanders(commander);
var i, skill, tmp, team, veh;
begin
uc_side := your_side;
uc_nation := nation_russian;

// Gladkov
// dostepne bazooki, lepsi zolnierze
Gladkov := NewCharacter('Gladkov');

// Dawidow
// dostepne pojazdy komputerowe, pole tau, lepsi naukowce
Davidov := NewCharacter('Davidov');

// Burlak
// dostepne hg, rakiety, lepsi mechanicy
Gorki := NewCharacter('Burlak');

// Stolypin
// lepsi inzynierowie, dzwig na start, komora z surowcami na start
Stolypin := NewCharacter('Stolypin');

commanders := [Gladkov, Davidov, Gorki, Stolypin];

//player := CharacterSelection('text', 1, 1, [sel_not_hired, sel_changeable]^commanders, []);
playerCom := commanders[commander];

team := [];
skill := [4, 3, 3][Difficulty];

uc_side := 3;
uc_nation := 3;

hc_gallery := '';
hc_name := '';
hc_importance := 0;

case playerCom of
     Gladkov:
         begin
         for i := 1 to 6 do
             begin
             PrepareHuman(false, 1, skill + 1);
             team := Join(team, CreateHuman);
             end;

         for i := 1 to 8 do
             begin
             PrepareHuman(false, (i mod 3) + 2, skill);
             team := Join(team, CreateHuman);
             end;

         SetTech(tech_bazooka, 3, state_enabled);
         SetTech(tech_advmet, 3, state_enabled);
         end;

     Davidov:
         begin
         for i := 1 to 6 do
             begin
             PrepareHuman(false, 4, skill + 1);
             team := Join(team, CreateHuman);
             end;

         for i := 1 to 8 do
             begin
             PrepareHuman(false, (i mod 3) + 1, skill);
             team := Join(team, CreateHuman);
             end;

         SetTech(tech_advmet, 3, state_enabled);
         SetTech(tech_ai, 3, state_enabled);
         SetTech(tech_advai, 3, state_enabled);
         SetTech(30, 3, state_enabled);
         SetTech(63, 3, state_enabled);
         SetTech(tech_comp1, 3, state_enabled);
         SetTech(tech_comp2, 3, state_enabled);

         SetRestrict(b_lab_full, 3, state_enabled);
         SetRestrict(b_lab_computer, 3, state_enabled);
         SetRestrict(b_lab_spacetime, 3, state_enabled);
         SetRestrict(b_ext_computer, 3, state_enabled);
         SetRestrict(b_turret, 3, state_enabled);

         SetResourceType(GetBase(playerDepot), 3, 20);
         end;

     Gorki:
         begin
         for i := 1 to 6 do
             begin
             PrepareHuman(false, 3, skill + 1);
             team := Join(team, CreateHuman);
             end;

         for i := 1 to 8 do
             begin
             PrepareHuman(false, [1, 2, 4][Rand(1, 3)], skill);
             team := Join(team, CreateHuman);
             end;

         SetTech(tech_rocket, 3, state_enabled);
         SetTech(tech_advmet, 3, state_enabled);
         SetRestrict(b_ext_rocket, 3, state_enabled);

         vc_chassis := ru_medium_tracked;
         vc_engine := engine_combustion;
         vc_control := control_manual;
         vc_weapon := ru_rocket_launcher;
         vc_fuel_battery := 50;

         veh := CreateVehicle;
         SetMark(veh, 1);
         PlaceUnitXY(veh, 107, 83, false);
         end;

     Stolypin:
         begin
         for i := 1 to 6 do
             begin
             PrepareHuman(false, 2, skill+1);
             team := Join(team, CreateHuman);
             end;

         for i := 1 to 8 do
             begin
             PrepareHuman(false, [1, 3, 4][Rand(1, 3)], skill);
             team := Join(team, CreateHuman);
             end;

         SetTech(tech_advmet, 3, state_enabled);

         vc_chassis := ru_medium_tracked;
         vc_engine := engine_combustion;
         vc_control := control_manual;
         vc_weapon := ru_cargo_bay;
         vc_fuel_battery := 30;

         veh := CreateVehicle;
         SetCargo(veh, 1, 100);
         PlaceUnitXY(veh, 107, 83, false);

         vc_chassis := ru_medium_tracked;
         vc_engine := engine_combustion;
         vc_control := control_manual;
         vc_weapon := ru_crane;
         vc_fuel_battery := 30;

         veh := CreateVehicle;
         PlaceUnitXY(veh, 115, 96, false);
         end;
     end;

if isTest then
   tmp := ShrinkArray(team, 7)
else
   tmp := CharacterSelection('text', [9, 8, 7][Difficulty], [9, 8, 7][Difficulty], [sel_not_hired, sel_change_class, sel_changeable] ^ team, [class_soldier, class_engineer, class_mechanic, class_scientistic]);

playerSquad := tmp;

SetDir(playerCom, 4);
PlaceUnitArea(playerCom, eastArr, false);
ComHold(playerCom);

for i := 1 to [5, 4, 4][Difficulty] do
    begin
    uc_side := 6;
    uc_nation := nation_russian;

    PrepareHuman(false, i mod 4 + 1, skill);
    PlaceUnitXYR(CreateHuman, 104, 88, 3, false);
    end;    
End;


Export Popov;
Export Function InitAction;
var commander;
begin
ChangeSideFog(6, 3);

CenterNowOnXY(1, 1);

uc_side := 6;
Popov := NewCharacter('Popov');   

Difficulty := GetMultiplayerSetting(0);

if not Difficulty then
   Difficulty := 2;

commander := GetMultiplayerSetting(1);

if not commander then
   commander := 1;
   
InitCommanders(commander);
End;       

Every 0$03 do
var i;
begin
repeat
 wait(rand(1$25, 3$00));

 for i := 1 to 2 do
    begin
    repeat
     wait(0$3);
    until PlaceUnitArea(playerSquad[1], eastArr, false);

    ComMoveXY(playerSquad[1], 107, 88);
    SetSide(playerSquad[1], 3);

    playerSquad := Delete(playerSquad, 1);

    if not playerSquad then
       exit;
    end;
until not playerSquad;
End;      




Export Function PrepareAttack(area, n);
var i, un;
begin

for i := 1 to n do
    begin
    uc_side := 6;
    uc_nation := 3;

    vc_chassis := [ru_heavy_wheeled, ru_medium_tracked, ru_heavy_wheeled][rand(1,3)];
    vc_control := control_computer;
    vc_engine := engine_combustion;
    vc_weapon := [ru_gatling_gun, ru_gun, ru_gun][rand(1,3)];
    vc_fuel_battery := 80;

    un := CreateVehicle;

    case area of
         eastArr:
              begin
              SetDir(un, 4);

              repeat
               wait(0$1);
              until PlaceUnitArea(un, area, false);

              ComMoveXY(un, 79, 33);
              end;

         southArr:
              begin
              repeat
               wait(0$1);
              until PlaceUnitArea(un, area, false);

              ComMoveXY(un, 131, 148);
              end;
         end;

    wait(0$02);
    end;

End;

Export Function PrepareConvoy(area, n);
var i, un;
begin

for i := 1 to n do
    begin
    uc_side := 6;
    uc_nation := 3;

    PrepareHuman(false, 3, 4);

    vc_chassis := ru_medium_tracked;
    vc_control := control_manual;
    vc_engine := engine_combustion;
    vc_weapon := ru_cargo_bay;
    vc_fuel_battery := 50;

    un := CreateVehicle;

    case area of
         eastArr: SetDir(un, 4);
         southArr: SetDir(un, 5);
         end;

    PlaceUnitArea(un, area, false);
    PlaceHumanInUnit(CreateHuman, un);

    SetCargo(un, mat_cans, 100);

    ComMoveXY(un, 106, 88);
    AddComUnload(un);
    AddComMoveToArea(un, area);

    wait(0$02);
    end;

wait(0$05);

while (FilterAllUnits([[f_side, 6], [f_weapon, ru_cargo_bay]])) do
    begin
    wait(0$01);

    for i in FilterAllUnits([[f_side, 6], [f_weapon, ru_cargo_bay]]) do
        begin
        if IsInArea(i, playerBaseArea) and GetSex(IsDrivenBy(i)) = sex_male and dialogTrans then
           begin
           Say(IsDrivenBy(i), 'Dtran');
           dialogTrans := false;
           end;

        if IsIdle(i) then
           ComMoveToArea(i, area);

        if IsInArea(i, area) and GetCargo(i, mat_cans) = 0 then
           begin
           RemoveUnit(IsDrivenBy(i));
           RemoveUnit(i);
           end;
        end;
    end;
End;

// pink attack
Every 0$02 trigger FilterAllUnits([[f_side, 6], [f_type, unit_vehicle], [f_not, [f_weapon, ru_cargo_bay]]]) do
var i, tmp;
begin     
repeat
 wait(0$3);
 tmp := FilterAllUnits([[f_side, 6], [f_type, unit_vehicle], [f_ok], [f_not, [f_weapon, ru_cargo_bay]]]);

 for i in tmp do
     if not HasTask(i) and GetLives(i) > 250 then
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 6]), i));
until not tmp;

enable;
End;



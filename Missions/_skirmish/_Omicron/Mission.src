// Gra startuje
Export Function Start_Game;
begin

       global_diff := GetMultiplayerSetting(0);

       if not global_diff then
          global_diff := 1;

        {Case Query('textdiff') of
        1: global_diff := 1;
        2: global_diff := 2;
        3: global_diff := 3;
        end;}


     // start misji
        CenterOnXY(70, 130);

        Case Query('text1') of
        1:; end;

        ChangeMissionObjectives('target1');

        game_started := true;
        crates_allow := true;

        // test audio | Wait(0$01); Say(sikorski, 'DS-1');
        

     // trudnosc
        if global_diff > 2 then
           RemoveUnit(us_spec_b);

End;

Every 0$0.3 trigger SeeSide(sikorski_veh, 6) or SeeSide(sikorski_veh, 3) or SeeSide(sikorski, 3) or SeeSide(sikorski, 6) do
Say(sikorski, 'DRus');


// Sikorski dociera do Omikron
Every 0$03 trigger See(4, sikorski) or See(4, sikorski_veh) do
var i, per;
begin

     Wait(0$05);

     for i in FilterAllUnits([f_side, 4]) do
         SetSide(i, 1);

     per := FilterAllUnits([[f_side, 1], [f_type, unit_human], [f_sex, sex_male]]) diff sikorski;

     InGameOn;
      CenterNowOnXY(19, 13);

      if IsInUnit(sikorski) then
         begin
         ComExitVehicle(sikorski);
         Wait(0$0.3);
         end;

       ComTurnUnit(sikorski, per[1]);
       ComTurnUnit(per[1], sikorski);

       Wait(0$0.3);

       Say(sikorski, 'DS-1');
       Say(per[1], 'DP-1');
       Say(sikorski, 'DS-2');
       Say(per[1], 'DP-2');
       Say(sikorski, 'DS-3');
       Say(per[1], 'DP-3');
       Say(sikorski, 'DS-4');

       Wait(0$0.6);

       SayRadio(ron, 'DR-1');
       Say(sikorski, 'DS-5');
       SayRadio(ron, 'DR-2');
       Say(sikorski, 'DS-6');
        Wait(0$0.3);
       SayRadio(ron, 'DR-3');

      InGameOff;

      ChangeMissionObjectives('target2');

      Wait(0$03);

      Say(sikorski, 'DS-7');

     Wait([6$00, 5$00, 4$00][global_diff]);

     russian_produce := true;

End;



// spawn losowej postaci
Export us_unit;
Every 0$11 trigger timer >= [25$00, 27$30, 29$20][global_diff] do
begin

     uc_side := 8;
     uc_nation := 1;

     // us_unit := CreateHumanWithClass(Rand(1,4), am_def);
     hc_sex := sex_male; // voice act!
     hc_gallery := '';
     hc_name := '';
     hc_class := Rand(1, 4);
     hc_skills := [Rand(2, am_def), Rand(2, am_def), Rand(2, am_def), Rand(2, am_def)];

     us_unit := CreateHuman;

     PlaceUnitArea(us_unit, swamp_spawn, true);

       Wait(0$0.3);

     SayRadio(us_unit, 'Dun1');

End;


Every 0$01 trigger See(1, us_unit) do
SetSide(us_unit, 1);



// Posi³ki
Every 1$35 trigger timer >= [8$00, 9$00, 10$00][global_diff] do
var un, veh, i;
begin

     uc_side := 4;
     uc_nation := 1;

     vc_chassis := [us_medium_tracked, us_medium_wheeled, us_medium_wheeled][global_diff];
     vc_engine := engine_solar;
     vc_control := control_manual;
     vc_weapon := us_cargo_bay;

     veh := CreateVehicle;
     PlaceUnitArea(veh, am_car, false);

     // un := CreateHumanWithClass(1, am_def);
     hc_sex := sex_male; // voice act!
     hc_gallery := '';
     hc_name := '';
     hc_class := 3;
     hc_skills := [Rand(2, am_def), Rand(2, am_def), Rand(2, am_def), Rand(2, am_def)];

     un := CreateHuman;

     PlaceUnitArea(veh, am_car, true);
     PlaceHumanInUnit(un, veh);

     SetCargo(veh, mat_cans, 60);
     SetFuel(veh, 98);

     SetLives(un, 500);
     SetLives(veh, Rand(900, 999));

     ComMoveXY(un, 14, 9);

     Repeat
      Wait(0$01);
     Until See(1, veh) or See(1, un);

     Say(un, 'Dcargo');
     SetSide(un, 1);
     SetSide(veh, 1);

End;

Every 0$01 trigger timer >= [18$00, 19$00, 21$00][global_diff] and FilterAllUnits([[f_side, 6], [f_type, unit_vehicle]]) < 5 do
var un, speaker, veh, i, area;
begin

      area := [us_north, us_south, us_north][global_diff];
      speaker := [];

      uc_side := 1;
      uc_nation := 1;

      For i = 1 to 7 - [global_diff] do
          begin
           un := CreateHumanWithClass(Rand(1,4), am_def);
           PlaceUnitArea(un, area, false);
           speaker := speaker ^ un;
          End;

           vc_chassis := [us_medium_tracked, us_medium_wheeled, us_light_wheeled][global_diff];
           vc_engine := engine_solar;
           vc_control := control_manual;
           vc_weapon := [us_double_gun, us_gatling_gun, us_machine_gun][global_diff];

           veh := CreateVehicle;
           PlaceUnitArea(veh, area, false);
           un := CreateHumanWithClass(3, am_def);
           PlaceHumanInUnit(un, veh);

           Wait(0$0.3);

          SayRadio(UnitFilter(speaker, [f_sex, sex_male])[1], 'Dhelp');

          CenterNowOnXY(GetX(un), GetY(un));

End;

Every 0$01 trigger FilterUnitsInArea(kurin_a, [f_or, [f_side, 3], [f_side, 6]]) and IsOk(Kurin) do
SayRadio(Kurin, 'Dkurin');


Every 0$01 trigger timer > 27$00 do
SayRadio(jack, 'ar_come');


Every 0$01 trigger FilterAllUnits([f_side, 2]) > 0 do
begin
FHeike;
Wait(0$01);
SayRadio(heike, 'heike1');
End;


Every 15$00 trigger timer > 22$00 do
begin
enable;
ArabianAttack(2, ar_force, Rand(1,2), [ar_area1, ar_area2][Rand(1,2)]);
End;


// Wygrana
Every 0$01 trigger timer >= Rand(65$00, 67$30) or (FilterAllUnits([f_side, 3]) + FilterAllUnits([f_side, 6]) < 3) do
var i;
begin

     InGameOn;
     DialogueOn;
      SayRadio(ron, 'you_win');
     DialogueOff;
     InGameOff;

     AddMedal('med', 1);

     //GiveMedals('MAIN');

     //YouWin;
     End_The_Mission_Allowed := true;

     Case Query('MEnd') of
     1: ;
     end;

End;


On EndTheMissionRaised(exitt) do
begin
End_The_Mission_Allowed := false;

   if FilterAllUnits([f_side, 3]) + FilterAllUnits([f_side, 6]) < 3 then
      AddMedal('ru', 1)
       else
        AddMedal('ru', -1);

   if FilterAllUnits([[f_side, 1], [f_type, unit_human]]) > 6 then
      AddMedal('pep', 1)
       else
        AddMedal('pep', -1);

   GiveMedals('MAIN');
   YouWin;

End;

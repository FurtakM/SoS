// EVENTY
// Skrypty

On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

     if un = sikorski then
        YouLost('sik');

     if un = omikron then
        YouLost('omi');

     if un in ru_list_attackers then
        ru_list_attackers := ru_list_attackers diff un;

     // RU 6
     if GetSide(un) = 6 and un in FilterAllUnits([f_type, unit_building]) then
        begin
        if GetBType(un) = b_factory then
           ru_bul := ru_bul ^ b_workshop;
        if GetBType(un) = b_lab_full or GetBType(un) = b_lab_half then
           ru_bul := ru_bul ^ b_lab;
        if GetBType(un) = b_barracks then
           ru_bul := ru_bul ^ b_armoury;

        if not GetBType(un) in [b_factory, b_lab_full, b_lab_half, b_barracks] then
           ru_bul := ru_bul ^ GetBType(un);

        ru_bul := ru_bul ^ GetX(un);
        ru_bul := ru_bul ^ GetY(un);
        ru_bul := ru_bul ^ GetDir(un);
        end;

     if GetSide(un) = 6 and un in FilterAllUnits([f_type, unit_vehicle]) diff FilterAllUnits([f_weapon, ru_rocket_launcher]) then
        begin
        ru_veh := ru_veh ^ GetChassis(un);
        ru_veh := ru_veh ^ GetEngine(un);
        ru_veh := ru_veh ^ GetControl(un);
        ru_veh := ru_veh ^ GetWeapon(un);
        end;

     if un in FilterAllUnits([[f_side, 6], [f_class, 3]]) and FilterAllUnits([[f_side, 6], [f_btype, b_factory], [f_lives, 251]]) > 0 then
        begin
        uc_side := 6;
        uc_nation := 3;
        un := CreateHumanWithClass(3, ru_def);
        PlaceHumanInUnit(un, ru_fac);
        end;


     // RU 3
     if GetSide(un) = 3 and un in FilterAllUnits([f_type, unit_building]) then
        begin
        if GetBType(un) = b_factory then
           ru2_bul := ru2_bul ^ b_workshop;
        if GetBType(un) = b_lab_full or GetBType(un) = b_lab_half then
           ru2_bul := ru2_bul ^ b_lab;
        if GetBType(un) = b_barracks then
           ru2_bul := ru2_bul ^ b_armoury;

        if not GetBType(un) in [b_factory, b_lab_full, b_lab_half, b_barracks, b_ext_gun, b_ext_noncombat, b_ext_track] then
           ru2_bul := ru2_bul ^ GetBType(un);

        ru2_bul := ru2_bul ^ GetX(un);
        ru2_bul := ru2_bul ^ GetY(un);
        ru2_bul := ru2_bul ^ GetDir(un);
        end;

     if GetSide(un) = 3 and un in FilterAllUnits([f_type, unit_vehicle])  diff FilterAllUnits([f_weapon, ru_rocket_launcher]) then
        begin
        ru2_veh := ru2_veh ^ GetChassis(un);
        ru2_veh := ru2_veh ^ GetEngine(un);
        ru2_veh := ru2_veh ^ GetControl(un);
        ru2_veh := ru2_veh ^ GetWeapon(un);
        end;

     if un in FilterAllUnits([[f_side, 3], [f_class, 3]]) and FilterAllUnits([[f_side, 3], [f_btype, b_factory], [f_lives, 251]]) > 0 then
        begin
        uc_side := 3;
        uc_nation := 3;
        un := CreateHumanWithClass(3, ru_def);
        PlaceHumanInUnit(un, FilterAllUnits([[f_side, 3], [f_btype, b_factory]])[1]);
        end;

End;

On VehicleConstructed(veh, fac) do
begin
if veh in FilterAllUnits([[f_or, [f_side, 6], [f_side, 3]], [f_type, unit_vehicle], [f_not, [f_or, [f_weapon, ru_crane], [f_weapon, ru_cargo_bay], [f_weapon, ru_bulldozer]]]]) then
   ru_list_attackers := Insert(ru_list_attackers, 1, veh);
End;



On BuildingComplete(b) do
var i;
begin

     if b in FilterAllUnits([f_side, 6]) then
        for i = 1 to 4 do
        ru_bul := Delete(ru_bul, 1);

     if b in FilterAllUnits([f_side, 3]) then
        for i = 1 to 4 do
        ru2_bul := Delete(ru2_bul, 1);

End;

On UnitDestroyed_Extra(un, killerSide, killerUnit) do
begin
SOS_UnitExtraDestroyed(un, killerSide, killerUnit);
End;

On Command(cmd) do
begin
SOS_Command(cmd);
End;




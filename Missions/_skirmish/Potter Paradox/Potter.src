Export Function Game;
var i, val, areas, wave;
begin

areas := [north_west_1, west_2, south_west_3, north_4, east_5, south_east_6];
val := [5, 6, 7][Difficulty];
wave := 1;

while not game_over do
 begin

 repeat
  begin
  Wait(0$01);
  wave_time := wave_time + 0$01;
  end
 until FilterAllUnits([f_side, 7]) < 10 or wave_time >= 10$00;

 wave_time := 0$00;
 Wait(2$30);

 if not ignore_wave then
    begin

    for i = 1 to areas do
        begin
        SpawnPotter(wave + val, areas[i]);
        Wait(0$10);
        end;

    wave := wave + 1;
    end
   else
    ignore_wave := false;
 end;

End;

Export Function SpawnPotter(val, area);
var i, v, un, class;
begin
uc_side := 7;
uc_nation := 2;

v := 10 + (tick div 5$00);

if v > 25 then
   v := 25;

InitHc();

hc_gallery := '';
//hc_gallery := 'potter';
//hc_face_number := 2;
hc_importance := 0;
//hc_name := 'George Potter';
hc_name := '';
hc_attr := [v, v];

if val > 35 then
   val := 35;

for i = 1 to val do
    begin

    if i mod 9 = 0 then
       class := class_apeman_kamikaze
    else
     if i mod 4 = 0 then
        class := class_apeman_soldier
       else
        class := class_apeman;

    if i mod 6 = 0 then
       begin
       vc_chassis := ar_half_tracked;
       vc_control := control_apeman;
       vc_engine := engine_siberite;
       vc_weapon := [ar_rocket_launcher, ar_gun, ar_selfpropelled_bomb, ar_bio_bomb][rand(1,4)];
       un := CreateVehicle;
       end
      else
       begin
       PrepareHuman(sex_male, class, 10);
       un := CreateHuman;
       end;

    PlaceUnitArea(un, area, false);
    EffectTeleport(GetX(un), GetY(un));
    end;
End;

// Attack script
Every 0$3 trigger alert and not game_over do
var i, list, enemy;
begin
enable;

list := FilterAllUnits([f_side, 7]);
enemy := FilterAllUnits([f_enemy, 7]);

if not list then
   exit;

for i in list do
    begin     
    if enemy then
       if not HasTask(i) then
          ComAttackUnit(i, NearestUnitToUnit(enemy, i));
    end;
End;
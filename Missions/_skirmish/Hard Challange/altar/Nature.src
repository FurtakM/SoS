// Zdroják, který pøipraví pøírodní objekty - tygry apod.


Var Tygri;


// Tato funkce vytvoøí Cnt jednotek typu Cls a umístí je do Area.
// Vrátí seznam jejich ID.
Function create_nature (Cls, Cnt, Area);
Var i, h;
Begin
  Result = [];
  UC_nation = NATION_NATURE;
  UC_side = side_neutral;
  HC_Class = Cls;
  HC_Name = '';
  For i = 1 to Cnt do Begin
    h = CreateHuman; Result = Result union [h];
    PlaceUnitArea (h, Area, false);
  End;
End;


// Tento trigger testuje, zda jsou tygøi naživu. Pokud jsou všichni mrtví,
// spustí trigger 11, který vytvoøí nové tygry v lese. Pouští se každou
// pùlminutu, a tak je vlastnì objevení tygrù znáhodnìno do pùlminuty
// od likvidace prvních tygrù.
Every 0$30 do
Var ZiviTygri;
Begin
  If Debug then Exit;
  ZiviTygri = UnitFilter (Tygri, [[F_ALIVE]]);
  if ZiviTygri then Enable
  else Enable (11);
End;


// Tento trigger poèká, až bude za øekou Amerièan, a pak vytvoøí pár tygrù v lese
// a pošle je zaútoèit na pøítomné Amerièany.
Every 0$5 Marked 11 do
Var NoviTygri, Americani, I;
Begin
  If Debug then Exit;
  Americani = FilterUnitsInArea (ZaRekou, [[F_SIDE, Side_Am], [F_TYPE, UNIT_HUMAN]]);
  if Americani then begin
    NoviTygri = create_nature (class_tiger, 4, LesZaRekou);
    SetSide (NoviTygri, side_Enemy);
    For I = 1 to Americani do
      AddComAttackUnit (NoviTygri, Americani [I]);
  End else Enable;
End;


Export Function prepare_nature;
Begin
  If Debug then Exit;
  Disable (11);
  Tygri = create_nature (CLASS_TIGER, 4, ZaRekou);    // Tygøi za øekou.
  create_nature (CLASS_FISH, 10, Reka);               // Ryby v øece.
  create_nature (CLASS_FROG, 2, Brod);                // Žáby na brodu.
  create_nature (CLASS_FROG, 2, Jezirko);             // Žáby na prameni.
  create_nature (CLASS_FISH, 2, Jezirko);             // Ryby na prameni.
  create_nature (CLASS_PHORORHACOS, 4, LesPodCestou); // Ptáci v lese pod cestou.
  create_nature (CLASS_PHORORHACOS, 3, LesPredRekou); // Ptáci v lese pøed øekou.
  create_nature (CLASS_BAGGIE, 5, Louka);
End;



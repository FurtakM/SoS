Export Powell, powell_side, powellAttackGroup;
Export Function PreparePowell;
var i, tmp;
begin
powell_side := 4;

uc_side := powell_side;
uc_nation := 1;

Powell := NewCharacter('Powell');

SetSkill(Powell, 1, 7);
SetSkill(Powell, 2, 5);
SetSkill(Powell, 3, 6);
SetSkill(Powell, 4, 4);

PlaceHumanInUnit(Powell, us_depot);

tmp := PrepareBase(us_depot, powell_base, '', [8, 7, 6][Difficulty], [2500, 300, 0], [4, 4, 4, 4]);
mc_bases := Replace(mc_bases, 1, tmp);

powellAttackGroup := [];
End;


Export Function InitMC_Powell();
begin
MC_SetScanArea(1, powell_base);
MC_SetParkingArea(1, powell_parking);

MC_SetDepositsXY(1, [
   [167, 102, 0],
   [173,  83, 1],
   [171, 141, 0],
   [178, 148, 1]
]);

MC_SetAllowedTurretWeapons(1, [us_radar, us_heavy_gun, us_rocket_launcher]);
MC_SetCratesArea(1, powell_crates);
MC_SetTame(1, powell_ape);
MC_SetDefenderLimit(1, 4);

MC_SetProduceList(1, [
   [us_medium_tracked, engine_combustion, control_remote, us_double_gun],
   [us_heavy_tracked, engine_combustion, control_remote, us_heavy_gun],
   [us_heavy_tracked, engine_combustion, control_remote, us_rocket_launcher],
   [us_heavy_tracked, engine_combustion, control_remote, us_heavy_gun],
   [us_heavy_tracked, engine_combustion, control_remote, us_heavy_gun]
]);

MC_SetBuildingList(1, [
   [b_lab, 200, 111, 4],
   [b_oil_mine, 167,102, 0],
   [b_armoury, 178, 117, 2],
   [b_workshop, 201, 140, 4],
   [b_solar_power, 206, 124, 3],
   [b_solar_power, 209, 128, 3],
   [b_solar_power, 211, 132, 3],
   [b_siberite_mine, 173, 83, 3],
   [b_lab, 186, 103, 2],
   [b_turret, 173, 99, 2],
   [b_bunker, 174, 102, 2],
   [b_bunker, 178, 100, 2],
   [b_turret, 174, 108, 1],
   [b_turret, 182, 122, 1],
   [b_control_tower, 194, 124, 5],
   [b_siberite_power, 204, 133, 3],
   [b_solar_power, 201, 133, 3],
   [b_siberite_power, 199, 119, 5],
   [b_turret, 184, 141, 1],
   [b_turret, 183, 131, 2],
   [b_turret, 194, 146, 4],
   [b_turret, 190, 142, 4]
]);

MC_SetLabKind(1, [b_lab_weapon, b_lab_opto, b_lab_computer, b_lab_siberium]);
MC_SetFactoryExtension(1, 201, 140, 4, [b_ext_noncombat, b_ext_track, b_ext_gun, b_ext_rocket, b_ext_radio]);
MC_SetUpgradeBuilding(1, b_depot);
End;

Every 8$00 + 2$00 trigger IsOk(us_depot) do
var i, base, tmp, attackers, amount;
begin
enable;

base := 1;
tmp := [
         [us_heavy_tracked, engine_combustion, control_remote, us_heavy_gun],
         [us_heavy_tracked, engine_combustion, control_remote, us_heavy_gun],
         [us_medium_tracked, engine_combustion, control_remote, us_rocket_launcher],
         [us_medium_tracked, engine_combustion, control_remote, us_rocket_launcher]
       ];

amount := amount + tick div 10$00;

if amount > 8 then
   amount := 8;

for i = 1 to amount do
    tmp := Insert(tmp, tmp + 1, [[us_medium_tracked, us_heavy_tracked][rand(1,2)], engine_combustion, control_remote, [us_rocket_launcher, us_double_gun][rand(1,2)]]);

MC_InsertProduceList(base, tmp);

repeat
 wait(0$1);
until MC_GetVehicles(base, true) >= amount;

wait(0$30);

attackers := mc_vehicles[base] diff UnitFilter(mc_vehicles[base], [f_or, [f_weapon, us_cargo_bay], [f_weapon, us_crane], [f_weapon, us_bulldozer]]);

if not attackers then
   exit;

DialogPowellsAttack;

powellAttackGroup := attackers;
mc_vehicles := Replace(mc_vehicles, base, mc_vehicles[base] diff attackers);

if sikorskiInTrouble and FilterUnitsInArea(sikorski_base, [f_enemy, 4]) then
   ComAgressiveMove(attackers, 135, 21)
else
   ComAgressiveMove(attackers, 146, 103);

wait(0$5);

repeat
 wait(0$1);
until not UnitFilter(attackers, [f_hastask]);

if FilterAllUnits([f_side, 3]) then
   ComAgressiveMove(attackers, 102, 116)
else
   ComAgressiveMove(attackers, 66, 41);

wait(0$10);

repeat
 wait(0$1);

 for i in attackers do
     if not HasTask(i) then
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 4]), i));

until not UnitFilter(attackers, [f_ok]);

DialogPowellsAttackFailed;
End;
// War for Territory
// 29-01-2016
// by Serpent

Starting
begin
ResetFog;

AnimateTrees(true);
InitVariables;

if debug then
   begin
   FogOff(1);
   Difficulty := 2;
   end
else
   Difficulty := GetMultiplayerSetting(0);

ChangeSideFog(4, 1);

PrepareNature(0, 0, 5, 0, 0, 0, 0, gensher_ape, 0);
PrepareNature(0, 0, 3, 0, 0, 0, 0, powell_ape, 0);
PrepareNature(0, 0, 5, 0, 0, 0, 0, popov_ape, 0);
PrepareNature(0, 0, 5, 0, 0, 0, 0, sikorski_ape, 0);

PrepareGensher;
PreparePopov;
PreparePowell;
PrepareSikorski;

MC_Start();

Action;
End;

Export debug, game, staticMines, arDestroyed, ruDestroyed, loseCounter,
       powellLoseCounter, powellInTrouble, sikorskiInTrouble;
Function InitVariables;
begin
debug := false;
game := true;
staticMines := [];
arDestroyed := false;
ruDestroyed := false;
powellInTrouble := false;
sikorskiInTrouble := false;
End;


Export Function CustomInitMacro;
begin
InitMC_Gensher();
InitMC_Powell();
InitMC_Popov();
End;


// Crates
Every 0$10 do
var cr, time, i;
begin
time := 0$20;

repeat
 wait(time);
 cr := rand(1, 5);

 for i in [powell_crates, gensher_crates, popov_crates, sikorski_crates] do
     begin
     if GetResourceArea(i, mat_cans) < 40 then
        CreateCratesArea(cr, i, true);

     wait(rand(0$5, 0$15));
     end;

 time := time + 0$2;

 if time > 0$35 then
    time := 0$25;

until not game;
End;

Every 0$1 trigger FilterAllUnits([[f_not, [f_side, 1]], [f_engine, engine_combustion]]) do
var i, tmp;
begin
enable;
tmp := FilterAllUnits([[f_not, [f_side, 1]], [f_engine, engine_combustion]]);

for i in tmp do
    if GetFuel(i) < 5 then
       SetFuel(i, 5);
End;



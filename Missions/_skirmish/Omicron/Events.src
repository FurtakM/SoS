On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

if un = Sikorski then
   YouLost('sik');

if un in russianOmicronAttackers then
   russianOmicronAttackers := russianOmicronAttackers diff un;

if un in russianCargo then
   russianCargo := russianCargo diff un;

if un in FilterAllUnits([[f_side, 3], [f_weapon, ru_cargo_bay]]) then
   ProduceNew(1);

if GetSide(un) in [1, 4] and GetType(un) = unit_human and GetNation(un) = nation_american then
   usLoseCounter := usLoseCounter + 1;
End;

On VehicleConstructed(veh, factory) do
begin
SOS_VehicleConstructed(veh, factory);

if GetSide(factory) <> 3 then
   exit;

if GetWeapon(veh) = ru_cargo_bay then
   exit;

russianOmicronAttackers := russianOmicronAttackers union [veh, IsInUnit(veh)];

AddComAgressiveMove(veh, 71, 37);
End;

On VehicleCaptured(newId, oldId, oldSide, capturingUnit) do
begin
if oldId in russianOmicronAttackers then
   russianOmicronAttackers := russianOmicronAttackers diff oldId;

if oldId in russianCargo then
   russianCargo := russianCargo diff oldId;

if newId in russianOmicronAttackers then
   russianOmicronAttackers := russianOmicronAttackers diff newId;

if newId in russianCargo then
   russianCargo := russianCargo diff newId;
End;

On UnitDestroyed_Extra(un, killerSide, killerUnit) do
begin
SOS_UnitExtraDestroyed(un, killerSide, killerUnit);

if killerSide in [1, 4] and GetSide(un) in [3, 6] then
   ruLoseCounter := ruLoseCounter + 1;

if killerSide in [1, 4] and GetType(un) = unit_vehicle and GetSide(un) in [3, 6] then
   tanksCounter := tanksCounter + 1;
End;

On EvacuateVehicle(driver, veh, p1, p2) do
begin
if GetSide(driver) in [3, 6] then
   begin
   ComRepairVehicle(driver, veh);

   repeat
    wait(0$1);
   until IsDead(veh) or IsDead(driver) or GetLives(veh) > 250;

   if GetLives(veh) > 250 then
      begin
      ComEnterUnit(driver, veh);
      AddComMoveXY(driver, 71, 29);
     
      SetTag(veh, 1);

      if not veh in russianOmicronAttackers then
         russianOmicronAttackers := Join(russianOmicronAttackers, veh);
      end;
   end;
End;

On Command(cmd) do
begin
SOS_Command(cmd);
End;
// Kód pro Heike.


// Seznam únikových oblastí.
Var Uniky;
Var HeikeVytvorena;
Var HeikeUtika;             // Už utíká?
Var HeikeJdeKam;            // Kam jde Heike? (prvek z Uniky)
// Kolik jednotek má zvýt Arabùm, aby se objevila Heike.
// Auta a budovy se testují pomocí AND, lidi OR.
Var Start_Budovy, Start_Auta, Start_Lidi;
// Obrana.
Var Registry, Makro;
// Zajmutí Heike.
Var HeikeVCervenem;
Var Zajmuta;


Export Function init_heike;
Begin
  Disable (31); Disable (32);
  Uniky = [[Unik1, HeikeCil1], [Unik2, HeikeCil2],
           [Unik3, HeikeCil3], [Unik4, HeikeCil4],
           [Unik5, HeikeCil5], [Unik6, HeikeCil6],
           [Unik7, HeikeCil7]];
  HeikeVytvorena = false;
  HeikeUtika = false;
  HeikeVCervenem = false;
  Zajmuta = false;
  // Obtížnost.
  Start_Budovy = dif_HeikeBudovy [Difficulty];
  Start_Auta = dif_HeikeAuta [Difficulty];
  Start_Lidi = dif_HeikeLidi [Difficulty];
End;


Function vytvor_heike;
Var I;
Begin
  // Heike.
  Heike = NewCharacter ('Heike');
  SetSide (Heike, side_Heike);
  // Kompanyje.
  Heikeovci = [];
  InitHC; InitUC;
  UC_Side = side_Strazci; UC_Nation = NATION_ARABIAN;
  HC_Name = ''; HC_Class = CLASS_SOLDIER; UC_Placed = false;
  PrepareSoldierSkills (5);
  For I = 1 to dif_HeikeovciPocet [Difficulty] do
  Begin
    HC_Sex = Rand (1, 2);
    Heikeovci = Heikeovci union [CreateHuman];
  End;
  Heikeovci = Heikeovci diff [0];
  SetLives (Heikeovci, dif_HeikeovciLives [Difficulty]);
End;


// Øekne, aby Heike šla do základny.
Function heike_do_zakladny;
Begin
  AddComMoveXY (Heike, 87, 120);
  AddComMoveXY (Heike, 83, 114);
  AddComMoveXY (Heike, 74, 97);
  AddComMoveXY (Heike, 66, 78);
  If VsevSaved then
  Begin
    AddComMoveXY (Heike, 61, 70);
    AddComMoveXY (Heike, 56, 59);
    AddComMoveXY (Heike, 44, 44);
  End else Begin
    AddComMoveXY (Heike, 44, 73);
    AddComMoveXY (Heike, 36, 65);
    AddComMoveXY (Heike, 47, 59);
    AddComMoveXY (Heike, 28, 51);
    AddComMoveXY (Heike, 29, 42);
  End;
  AddComMoveXY (Heike, 40, 37);
  AddComMoveXY (Heike, 33, 22);
  AddComMoveXY (Heike, 33, 16);
  AddComMoveXY (Heike, 38, 13);
  AddComMoveXY (Heike, 46, 13);
  AddComMoveXY (Heike, 54, 16);
  AddComMoveXY (Heike, 62, 15);
  AddComMoveXY (Heike, 73, 14);
  AddComMoveXY (Heike, 91, 13);
  AddComMoveToArea (Heike, HeikeCil);
End;


// Objeví se Heike.
Function heike_start;
Var Auto;
Begin
  // Pøidat Xaviera na stranu Rusa, pokud se tak ještì nestalo.
  If GetSide (Xavier) <> side_Ru then xavier_prisel;
  // Vytvoøit Heike a kompanyji.
  vytvor_heike;
  PlaceUnitArea (Heike, HeikeStart, false);
  place_list (Heikeovci, HeikeStart);
  Wait (Rand (0$1, 0$5));
  // Makra.
  Registry = McRegistry (side_Heike, [[MC_REG_UNITS_TO_PROTECT, [Heike]]]);
  Makro = McDefend (0, Registry, Heikeovci, []);
  // Kecání.
  DialogueOn;
  dialog_HeikeObj;
  ChangeMissionObjectives ('MHeike');
  Query ('QHeike');
  DialogueOff;
  RevealFogArea (side_Ru, HeikeStart);
  CenterOnUnits (Heike);
  // Heike jde do základny.
  heike_do_zakladny;
  ComMoveUnit (Heikeovci, Heike);
  // Triggery.
  Enable (32);
End;


// Kontrola, zda se nemá objevit Heike.
Function heike_kontrola_start;
Var Budovy, Auta, Lidi;
Begin
  If HeikeVytvorena then Exit;
  // Jednotky, které zbývají.
  Budovy = FilterAllUnits ([[F_SIDE, side_Ar], [F_TYPE, UNIT_BUILDING], [F_OK], [F_PLACED]]);
  Auta = FilterAllUnits ([[F_SIDE, side_Ar], [F_TYPE, UNIT_VEHICLE], [F_OK], [F_PLACED]]);
  Lidi = FilterAllUnits ([[F_SIDE, side_Ar], [F_TYPE, UNIT_HUMAN], [F_OK], [F_PLACED]]);
  // Zbývá málo budov?
  If ((Budovy <= Start_Budovy) and (Auta <= Start_Auta)) or (Lidi <= Start_Lidi) then
  Begin
    HeikeVytvorena = true;
    Wait (Rand (0$3, 0$20));
    heike_start;
  End;
End;


// Zjistí, zda je oblast volná pro únik Heike.
Function je_volny_unik (Area);
Var KdoTam;
Begin
  KdoTam = FilterUnitsInArea (Area, [[F_SIDE, side_Ru], [F_OK]]);
  If KdoTam then Result = false
  else Result = true;
End;


// Zjistí, které únikové plochy z Uniky jsou volné.
Function zjisti_volne;
Var Akt, Jednotky;
Begin
  Result = [];
  For Akt in Uniky do
  Begin
    If je_volny_unik (Akt [1]) then Result = Result union [Akt];
  End;
End;


// Zjistí, zda Heike utekla.
Function zjisti_heike_utekla;
Var Akt;
Begin
  Result = false;
  If not IsOk (Heike) then Exit;
  Result = true;
  If IsInArea (Heike, HeikeCil) then Exit;
  If HeikeUtika then
  Begin
    For Akt in Uniky do
      If IsInArea (Heike, Akt [2]) then Exit;
  End;
  Result = false;
End;


// Švindlování - dokud nebude implementováno natvrdo.
Function GetDistUnitArea2 (Un, Area);
Var Hex;
Begin
  Hex = RandHexArea (Area, false);
  Result = GetDistUnitXY (Un, Hex [1], Hex [2]);
End;


// Zjistí, kam je nejlepší utíkat.
Function zjisti_nejlepsi_unik (Seznam);
Var Min, Akt, AktDal;
Begin
  Result = [];
  Min = 99999;
  For Akt in Seznam do
  Begin
    AktDal = GetDistUnitArea2 (Heike, Akt [1]);
    If AktDal < Min then
    Begin
      Result = Akt;
      Min = AktDal;
    End;
  End;
End;


// Heike utíká.
Function heike_utika;
Var Volno, Hex;
Begin
  If zjisti_heike_utekla then Begin heike_utekla; Exit; End;
  // Musíme zjistit, které únikové plochy jsou volné.
  // Heike se pøesunuje pryè.
  Volno = zjisti_volne;
  // Pokud už se Heike pøesunuje, tak zkontrolujeme, zda její cíl
  // je poøád volný.
  If HeikeUtika then
  Begin
    // Zjistíme, zda je cíl poøád volný. Pokud ano, necháme Heike
    // provádìt pøíkazy, které dostala døíve.
    If je_volny_unik (HeikeJdeKam [1]) then Exit;
  End;
  // Pokud neutíká nebo není její cíl volný, tak musíme zjistit jiný cíl.
  HeikeJdeKam = zjisti_nejlepsi_unik (Volno);
  // A øekneme Heike, aby utíkala pryè.
  ComMoveToArea (Heike, HeikeJdeKam [1]);
  AddComMoveToArea (Heike, HeikeJdeKam [2]);
  HeikeUtika = true;
End;


// Až budou mrtví všichni strážci, zaène Heike utíkat.
// Disabluju rovnìž øízení strážcù.
Function strazci_mrtvi;
Begin
  heike_utec;
End;


// Trigger, který je volán pøi pobytu Heike na mapì.
// Pokud se dostane do své základny, hráè prohrál.
Every 0$1.3 Marked 32 do
Begin
  // Zjistit, zda Heike utekla.
  If zjisti_heike_utekla then Begin heike_utekla; Exit; End;
End;


// Trigger, který je volán pøi úniku Heike. Øídí pohyb Heike.
Every 0$1.1 Marked 31 do
Begin
  heike_utika;
  Enable;
End;


// Heike zaène svùj útìk.
Function heike_utec;
Begin
  If IsInArea (Heike, Obkliceni) then
  Begin
    // Pokud je Heike poøád uvnitø oblasti, kde mùže být obklíèena,
    // zaène utíkat jedním z východù.
    Enable (31);
    heike_utika;
  End else Begin
    // Jinak ji necháme dojít do základny a tam zmizí.
    Enable (32);
    ComMoveToArea (Heike, HeikeCil);
  End;
End;


// Heike utekla.
Function heike_utekla;
Begin
  InGameOn;
  CenterOnUnits (Heike);
  Wait (0$2);
  // Není do té doby Heike mrtvá?
  If not IsOk (Heike) then
  Begin
    InGameOff;
    heike_mrtva;
    Exit;
  End;
  RemoveUnit (Heike);
  Wait (0$2);
  YouLost ('Heike');
  InGameOff;
End;


// Heike se vzdala.
Function heike_zajata;
Begin
  If Zajmuta then Exit;
  Zajmuta = true;
  SetAttitude (side_Ru, side_Heike, ATT_FRIEND, true);
  ComHold (Heike);
  If (GetLives (Heike) <= HRANICE_UMIRANI) then SetLives (Heike, HRANICE_UMIRANI + 1);
  ComCancel (Heike); ComHold (Heike);
  DialogueOn;
  dialog_HeikeCapt;
  DialogueOff;
  Wait (0$2);
  vyhral ('Main2');
End;


// Heike je mrtvá.
Function heike_mrtva;
Begin
  DialogueOn;
  Wait (0$3);
  vyhral ('Main1');
  DialogueOff;
End;


// Když zemøe strážce Heike.
Function straz_umrela (Un);
Begin
  If Un in Heikeovci then
  Begin
    Heikeovci = Heikeovci diff [Un];
    If not Heikeovci then strazci_mrtvi;
  End;
End;


Export Function heike_UnitDestroyed (Un);
Begin
  // Heike je mrtvá?
  If Un = Heike then Begin heike_mrtva; Exit; End;
  // Nìkdo z kompanyje Heike? Pokud už nezbyl nikdo, tak zaène Heike utíkat.
  straz_umrela (Un);
  // Zkontrolovat, zda zbývá málo arabských budov.
  heike_kontrola_start;
End;


Export Function heike_VehicleCaptured (VehNew, VehOld, OrigSide, Hum);
Begin
  // Zkontrolovat, zda zbývá málo arabských budov.
  heike_kontrola_start;
End;


Export Function heike_BuildingCaptured (Build, OrigSide, Eng);
Begin
  // Zkontrolovat, zda zbývá málo arabských budov.
  heike_kontrola_start;
End;


Export Function heike_UnitGoesToRed (Un);
Begin
  // Poznamenat si, že Heike je v èerveném.
  If Un = Heike then HeikeVCervenem = true;
  // Nìkdo z kompanyje Heike? Pokud už nezbyl nikdo, tak zaène Heike utíkat.
  straz_umrela (Un);
  // Zkontrolovat, zda zbývá málo arabských budov.
  heike_kontrola_start;
End;


Export Function heike_StartHeal (Un, Medic);
Begin
  // Odfiltrovat jiné možnosti než léèení Heike ruským vìdcem
  // po zabití její kompanyje a poté, co je v èerveném.
  If Un <> Heike then Exit;
  If not HeikeVCervenem then Exit;
  If Heikeovci then Exit;
  If GetSide (Medic) <> side_Ru then Exit;
  // Je zajmutá.
  heike_zajata;
End;



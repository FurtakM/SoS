// Triggery:
//  1: test, zda je Vsevolod naživu.
//  2: test, zda je Vsevolod zpìt ve žluté.
//  3: èekání na Xaviera - VS
//  4: èekání na Xaviera - NV
//  5: pøidávání životù po minì.
// 21: Xavier má dojít do Arabské báze.
// 22: Xavier je v Arabské bázi a prochází se tam.
// 23: Xavier má dojít k dropboxu.
// 24: Nìkdo z Rusù má dojít k dropboxu.
// 26: Xavier krade auák.
// 31: Heike utíká.
// 32: Test, zda Heike došla do základny.
// 43: Èasovaè pro arabské útoky.
// 44: Testování upgradu zbraní na bunkrech.
// 45: Kontrola, zda patrole nedošlo palivo.
// 46: Èasovaè pro stažení jednotek do základny.
// 47: Kontrola na vyrobení dálkového ovládadla.
// 51: Èekání na pøíchod Yashina.
// 52: Yashin odjíždí.
// 53: Kontrola, zda nìkdo neútoèí na Yashina.
// 54: Yashin chce auák.
// 55: Yashin chce auák, ale dlouho žádnej nemá, tak si øekne znova.
// 56: Èekání, až bude znièen bunkr.
// 57: Pøidávání životù Yashinovi.

// Události.
// 101: Dokonèeno koleèko patroly.
// 102: Dokonèena oprava.
// 103: Dokonèeno léèení 1.
// 104: Dokonèeno léèení 2.
// 111: Yashin zaèíná útoèit na bunkry.
// 121: Xavier pøišel.


// Strany.
Export side_Neutral, side_Ru, side_Ru2, side_Ar, side_Xavier, side_Heike, side_Strazci;
// Promìnné.
Export VsevSaved, HeikeCaptured;
// Postavy Rusa.
Export Burlak, Vsevolod, Popov, Yelena, Gleb, Petrosyan, Karamazov,
       Petrovova, Dolgov, Titov, Platonov, Lipshchin;
Export Rusaci, WithVsevolod;
Export Konvoj;
// Ostatní postavy - netvoøí se tady, ale v unitech.
Export Xavier, Heike, Heikeovci;
// Medaile.
Export ContactIn5Min, PropaslQuickly;
// Poèet minut, co už uplynulo.
Export Minuty;
// Yelena není Kirilenková, ale jiná. Nesejvuje se jako Kirilenkova, ale do Survivors2.
Var FalesnaYelena;


Function load_char (Ident);
Begin
  If TestCharacters (Ident) then
  Begin
    Result = CreateCharacter (Ident);
    If IsDead (Result) then
    Begin
      DestroyUnit (Result);
      Result = 0;
    End;
  End else Result = NewCharacter (Ident);
End;


Function load_set (Ident);
Begin
  If TestCharacters (Ident) then Result = CreateCharacterSet (Ident)
  else Result = [];
End;


Function save_char (Handle, Ident);
Begin
  If Handle then SaveCharacters ([Handle], Ident);
End;


Function vytvor_yelenu;
Begin
  UC_Nation = NATION_RUSSIAN; UC_Side = side_Ru2;
  HC_name = 'Yelena Trajkov'; HC_sex = SEX_FEMALE;
  HC_class = CLASS_SOLDIER; PrepareSoldierSkills (5);
  Result = CreateHuman;
End;


Function load_konvoj;
Var Convoy_06, Spec, Tmp;
Var MaxCnt, MaxCans, MaxOil, MaxSib, Cans, Oil, Sib;
Var TedCans, TedOil, TedSib;
Begin
  // Nahrát promìnnou.
  Convoy_06 = LoadVariable ('Convoy_06', []); DeleteVariable ('Convoy_06');
  If not Convoy_06 then Convoy_06 = [[NATION_RUSSIAN, RU_HEAVY_MACHINE_GUN, CONTROL_MANUAL, ENGINE_COMBUSTION, RU_MEDIUM_WHEELED, 0, 0, 0], [NATION_RUSSIAN, RU_CARGO_BAY, CONTROL_MANUAL, ENGINE_COMBUSTION, RU_MEDIUM_WHEELED, 30, 50, 20], [NATION_RUSSIAN, RU_CARGO_BAY, CONTROL_MANUAL, ENGINE_COMBUSTION, RU_MEDIUM_WHEELED, 40, 50, 10]];
  UC_Side = side_Ru;
  Konvoj = [];
  // Zkrouhnout maximální naložení konvoje.
  Tmp = dif_MaxKonvojCargo [Difficulty];
  MaxCnt = Tmp [1]; MaxCans = Tmp [2];
  MaxOil = Tmp [3]; MaxSib = Tmp [4];
  // Vytvoøit jednotlivé náklaïáky.
  For Spec in Convoy_06 do
  Begin
    UC_Nation = Spec [1];
    VC_Weapon = Spec [2]; VC_Control = Spec [3];
    VC_Engine = Spec [4]; VC_Chassis = Spec [5];
    Tmp = CreateVehicle;
    AddCargo (Tmp, MAT_CANS, Spec [6]);
    AddCargo (Tmp, MAT_OIL, Spec [7]);
    AddCargo (Tmp, MAT_SIBERIT, Spec [8]);
    Konvoj = Konvoj union [Tmp];
    If Konvoj >= MaxCnt then Break;
  End;
  // Spoèítáme naložení a budeme likvidovat to, co je navíc nad maximum.
  Cans = 0; Oil = 0; Sib = 0;
  For Spec in Konvoj do
  Begin
    // Aktuální náklad.
    TedCans = GetCargo (Spec, MAT_CANS);
    TedOil = GetCargo (Spec, MAT_OIL);
    TedSib = GetCargo (Spec, MAT_SIBERIT);
    // Pøesahuje maximum?
    If Cans + TedCans > MaxCans then TedCans = MaxCans - Cans;
    If Oil + TedOil > MaxOil then TedOil = MaxOil - Oil;
    If Sib + TedSib > MaxSib then TedSib = MaxSib - Sib;
    // Nastavit náklad.
    SetCargo (Spec, MAT_CANS, 0); SetCargo (Spec, MAT_OIL, 0); SetCargo (Spec, MAT_SIBERIT, 0);
    AddCargo (Spec, MAT_CANS, TedCans);
    AddCargo (Spec, MAT_SIBERIT, TedSib);
    AddCargo (Spec, MAT_OIL, TedOil);
    // Pøièíst.
    Cans = Cans + TedCans; Oil = Oil + TedOil; Sib = Sib + TedSib;
  End;
End;


Function load;
Begin
  UC_Nation = NATION_RUSSIAN; UC_Side = side_Ru;
  // Promìnné.
  VsevSaved = LoadVariable ('VsevSaved', false);
  // Základní charaktery.
  Burlak = load_char ('Burlak');
  Vsevolod = load_char ('Vsevolod');
  If IsDead (Vsevolod) then VsevSaved = false;
  Yelena = load_char ('Kirilenkova');
  If not Yelena or IsDead (Yelena) then
  Begin
    Yelena = vytvor_yelenu;
    FalesnaYelena = true;
  End else FalesnaYelena = false;
  // Dál pokraèuju podle toho, jestli jsem zachránil Vsevoloda.
  If VsevSaved then load_vs else load_nv;
End;


Function load_nv;
Var Ostatni;
Begin
  // Charaktery.
  Karamazov = load_char ('Karamazov');
  Petrovova = load_char ('Petrovova');
  Gleb = load_char ('Gleb');
  Petrosyan = load_char ('Petrosyan');
  Lipshchin = load_char ('Lipshchin');
  Titov = load_char ('Titov');
  Dolgov = load_char ('Dolgov');
  // Jen pøes rádio.
  Popov = NewCharacter ('Popov');
  Platonov = NewCharacter ('Platonov');
  // Ostatní.
  Ostatni = load_set ('Survivors2');
  Rusaci = Ostatni union [Karamazov, Petrovova, Gleb, Petrosyan, Lipshchin, Titov, Dolgov] diff [0];
  // Strana.
  SetSide (Rusaci, side_Ru);
End;


Function load_vs;
Var Ostatni;
Var KaramazovLoc, PetrovovaLoc, GlebLoc, PetrosyanLoc;
Var LipshchinLoc, TitovLoc, DolgovLoc;
Begin
  KaramazovLoc = LoadVariable ('KaramazovLoc', 0);
  PetrovovaLoc = LoadVariable ('PetrovovaLoc', 0);
  GlebLoc = LoadVariable ('GlebLoc', 0);
  PetrosyanLoc = LoadVariable ('PetrosyanLoc', 0);
  LipshchinLoc = LoadVariable ('LipshchinLoc', 0);
  TitovLoc = LoadVariable ('TitovLoc', 0);
  DolgovLoc = LoadVariable ('DolgovLoc', 0);
  If KaramazovLoc in [7, -7] then Karamazov = load_char ('Karamazov');
  If PetrovovaLoc in [7, -7] then Petrovova = load_char ('Petrovova');
  If GlebLoc in [7, -7] then Gleb = load_char ('Gleb');
  If PetrosyanLoc in [7, -7] then Petrosyan = load_char ('Petrosyan');
  If LipshchinLoc in [7, -7] then Lipshchin = load_char ('Lipshchin');
  If TitovLoc in [7, -7] then Titov = load_char ('Titov');
  If DolgovLoc in [7, -7] then Dolgov = load_char ('Dolgov');
  Ostatni = load_set ('Survivors2');
  // Jen pøes rádio.
  Popov = NewCharacter ('Popov');
  Platonov = NewCharacter ('Platonov');
  // Skupiny.
  WithVsevolod = [Yelena] union load_set ('WithVsevolod');
  Rusaci = Ostatni;
  If KaramazovLoc = 7 then Rusaci = Rusaci union [Karamazov]
  else If KaramazovLoc = -7 then WithVsevolod = WithVsevolod union [Karamazov];
  If PetrovovaLoc = 7 then Rusaci = Rusaci union [Petrovova]
  else If PetrovovaLoc = -7 then WithVsevolod = WithVsevolod union [Petrovova];
  If GlebLoc = 7 then Rusaci = Rusaci union [Gleb]
  else If GlebLoc = -7 then WithVsevolod = WithVsevolod union [Gleb];
  If PetrosyanLoc = 7 then Rusaci = Rusaci union [Petrosyan]
  else If PetrosyanLoc = -7 then WithVsevolod = WithVsevolod union [Petrosyan];
  If LipshchinLoc = 7 then Rusaci = Rusaci union [Lipshchin]
  else If LipshchinLoc = -7 then WithVsevolod = WithVsevolod union [Lipshchin];
  If TitovLoc = 7 then Rusaci = Rusaci union [Titov]
  else If TitovLoc = -7 then WithVsevolod = WithVsevolod union [Titov];
  If DolgovLoc = 7 then Rusaci = Rusaci union [Dolgov]
  else If DolgovLoc = -7 then WithVsevolod = WithVsevolod union [Dolgov];
  // Dotvoøit další.
  While (Rusaci union WithVsevolod) < 6 do
    WithVsevolod = WithVsevolod union [vytvor_rusaka];
  // Konvoj.
  load_konvoj;
  // Strana.
  SetSide (Rusaci, side_Ru); SetSide (Konvoj, side_Ru);
  SetSide (Vsevolod, side_Ru2); SetSide (WithVsevolod, side_Ru2);
End;


Function save;
Var Lidi, Ostatni, Opice;
Begin
  // Smazat staré sety.
  DeleteVariable ('Convoy_06');
  // Promìnné.
  HeikeCaptured = Heike and IsOK (Heike);
  SaveVariable (HeikeCaptured, 'HeikeCaptured');
  SaveVariable (not HeikeCaptured, 'HeikeKilled');
  // Hlavní charaktery.
  save_char (Burlak, 'Burlak');
  If not FalesnaYelena then save_char (Yelena, 'Kirilenkova');
  save_char (Karamazov, 'Karamazov');
  save_char (Petrovova, 'Petrovova');
  save_char (Gleb, 'Gleb');
  save_char (Petrosyan, 'Petrosyan');
  save_char (Lipshchin, 'Lipshchin');
  save_char (Titov, 'Titov');
  save_char (Dolgov, 'Dolgov');
  save_char (Vsevolod, 'Vsevolod');
  SaveCharacters ([Xavier], 'Xavier');
  SaveCharacters ([Heike], 'Heike');
  // Ostatní charaktery, vèetnì tech nepoužitých (ty jsou not F_PLACED).
  Lidi = FilterAllUnits ([[F_SIDE, side_Ru], [F_ALIVE], [F_TYPE, UNIT_HUMAN]]);
  Opice = UnitFilter (Lidi, [[F_OR, [F_CLASS, CLASS_APEMAN], [F_CLASS, CLASS_APEMAN_SOLDIER], [F_CLASS, CLASS_APEMAN_ENGINEER], [F_CLASS, CLASS_APEMAN_KAMIKAZE]]]);
  Ostatni = Lidi diff [0, Burlak, Vsevolod, Heike, Yelena, Popov, Gleb, Petrosyan, Karamazov, Petrovova, Dolgov, Titov, Platonov, Lipshchin, Yashin, Xavier];
  Ostatni = Ostatni diff Opice;
  If FalesnaYelena and IsOk (Yelena) then Ostatni = Ostatni union [Yelena];
  SaveCharacters (Ostatni, 'Survivors2');
End;


Function dej_medaile;
Begin
  // Xavier.
  If Xavier and IsOK (Xavier) then AddMedal ('Xavier', 1)
  else AddMedal ('Xavier', -1);
  // Time.
  If VsevSaved then
    If PropaslQuickly then AddMedal ('TimeVS', -1)
    else AddMedal ('TimeVS', 1)
  else
    If ContactIn5Min then AddMedal ('TimeNV', 1)
    else AddMedal ('TimeNV', -1);
  // Heike.
  If IsOK (Heike) then AddMedal ('Heike', 1)
  else AddMedal ('Heike', -1);
End;


Export Function vyhral (Ident);
Var Lidi, Opice;
Begin
  // Medaile.
  dej_medaile;
  GiveMedals (Ident);
  // Seznam lidí.
  Lidi = FilterAllUnits ([[F_SIDE, side_Ru], [F_TYPE, UNIT_HUMAN], [F_ALIVE]]);
  Opice = UnitFilter (Lidi, [[F_OR, [F_CLASS, CLASS_APEMAN], [F_CLASS, CLASS_APEMAN_SOLDIER], [F_CLASS, CLASS_APEMAN_ENGINEER], [F_CLASS, CLASS_APEMAN_KAMIKAZE]]]);
  RewardPeople (Lidi diff Opice);
  // Uložit.
  save;
  // Konec mise.
  YouWin;
End;


Function set_attitudes;
Begin
  // Burlak a Xavier sdílejí mlhu a jsou kámoši.
  ChangeSideFog (side_Xavier, GetSideFog (side_Ru));
  ChangeSideFog (side_Ru2, GetSideFog (side_Ru));
  // Yashin je kamarád Rusù a neutrál Arabù.
  SetAttitude (side_Ru2, side_Ru, ATT_FRIEND, true);
  SetAttitude (side_Ru2, side_Ar, ATT_ENEMY, false);
  SetAttitude (side_Ar, side_Ru2, ATT_FRIEND, false);
  SetAttitude (side_Ru2, side_Strazci, ATT_ENEMY, false);
  SetAttitude (side_Ru2, side_Heike, ATT_NEUTRAL, false);
  SetAttitude (side_Strazci, side_Ru2, ATT_NEUTRAL, false);
  SetAttitude (side_Heike, side_Ru2, ATT_NEUTRAL, false);
  // Xavier je kámoš všech (hraje to na všech pìt stran).
  SetAttitude (side_Ru, side_Xavier, ATT_FRIEND, true);
  SetAttitude (side_Ru2, side_Xavier, ATT_FRIEND, true);
  SetAttitude (side_Ar, side_Xavier, ATT_FRIEND, true);
  SetAttitude (side_Heike, side_Xavier, ATT_NEUTRAL, true);
  SetAttitude (side_Strazci, side_Xavier, ATT_NEUTRAL, true);
  // Rusák na Heike nestøíli, ale ona na nìj jo.
  SetAttitude (side_Ru, side_Heike, ATT_NEUTRAL, false);
  SetAttitude (side_Heike, side_Ru, ATT_ENEMY, false);
  SetAttitude (side_Heike, side_Ar, ATT_FRIEND, true);
  SetAttitude (side_Heike, side_Strazci, ATT_FRIEND, true);
  SetAttitude (side_Ar, side_Strazci, ATT_FRIEND, true);
End;


Starting
Begin
  AutoEnemy = false;
  // Základní promìnné.
  side_Neutral = 0;           // Pøíroda.
  side_Ar = 2;                // Amík.
  side_Ru = 3;                // Rusáci.
  side_Ru2 = 6;               // Vsevolod.
  side_Xavier = 5;            // Xavier.
  side_Heike = 7;             // Heike.
  side_Strazci = 8;           // Strážci Heike.
  // Nastavit kamarádství.
  set_attitudes;
  // Ostatní promìnné.
  ContactIn5Min = 0; PropaslQuickly = 0;
  Minuty = 0;
  // Základní inicializace.
  InitUc; InitBc; InitHc; InitVc;
  RandomizeAll;
  // Složitost.
  init_difficulty;
  // Naètení z pøedchozí mise.
  load;
  // Inicializace zbytku.
  init_nature;
  init_suroviny;
  init_arabi;
  init_rusove;
  init_xavier;
  init_heike;
  init_yashin;
  // Úvodní dialog.
  uvod;
End;


Every 1$0 do
Begin
  Minuty = Minuty + 1;
  Enable;
End;



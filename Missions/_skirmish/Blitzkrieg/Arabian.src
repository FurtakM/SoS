Export Function PrepareHeike;
begin

     uc_side := 2;
     uc_nation := 2;

     hc_gallery := 'sandar';
     hc_face_number := 2;
     hc_name := 'Heike Steyer';
     hc_sex := sex_female;
     hc_skills := [7, 3, 4, 2];
     hc_class := 1;
     hc_importance := 100;

     heike := CreateHuman;

     hc_gallery := 'sandar';
     hc_face_number := 25;
     hc_name := 'Olaf Larsson';
     hc_sex := sex_male;
     hc_skills := [2, 4, 6, 3];
     hc_class := 3;
     hc_importance := 0;

     olaf := CreateHuman;

     hc_gallery := 'us';
     hc_face_number := 18;
     hc_name := 'Kurt Schmidt';
     hc_sex := sex_male;
     hc_skills := [5, 1, 2, 0];
     hc_class := 1;

     kurt := CreateHuman;

End;


Export ar_vehs;
Export Function PrepareArabian;
var i, un;
begin
ar_force := [];
ar_vehs  := [];

uc_side := 2;
uc_nation := nation_arabian;

for i = 1 to 2 do
    SetCargo(FilterAllUnits([[f_side, 2], [f_weapon, ru_cargo_bay]])[i], mat_oil, 100);

for i = 1 to [4,4,3][global_diff] do
    begin
    un := CreateHumanWithClass([class_soldier, class_mortar, class_mortar][Rand(1,3)], ar_def);
    ar_force := ar_force ^ un;
    end;  

for i = 1 to 2 do
    begin
    un := CreateHumanWithClass(4, ar_def);
    ar_force := ar_force ^ un;
    end;

for i = 1 to 4 do
    begin
    un := CreateHumanWithClass(3, ar_def);
    ar_vehs := ar_vehs ^ un;

    vc_chassis := [ar_medium_trike, ar_half_tracked][Rand(1,2)];
    vc_engine := engine_combustion;
    vc_control := control_manual;
    vc_weapon := [ar_gun, ar_flame_thrower][Rand(1,2)];

    un := CreateVehicle;
    ar_force := ar_force ^ un;
    end;


End;


Export omar_force;
Export Function PrepareOmarForce;
var i, un, v;
begin
omar_force := [];

uc_side := 5;
uc_nation := nation_arabian;

for i = 1 to 2 do
    begin
    Wait(0$02);

    un := CreateHumanWithClass([class_soldier, class_mortar, class_mortar][Rand(1,3)], ar_def);
    omar_force := omar_force ^ un;
    PlaceUnitArea(un, south_spawn, false);
    ComMoveXY(un, 186, 126);
    end;

for i = 1 to 3 do
    begin
    Wait(0$03);
    un := CreateHumanWithClass(3, ar_def);

    vc_chassis := [ar_medium_trike, ar_half_tracked][Rand(1,2)];
    vc_engine := engine_combustion;
    vc_control := control_manual;
    vc_weapon := [ar_rocket_launcher, ar_flame_thrower][Rand(1,2)];

    v := CreateVehicle;
    PlaceUnitArea(v, south_spawn, false);
    PlaceHumanInUnit(un, v);
    ComMoveXY(v, 186, 126);
    end;

Wait(0$30);
omar_attack := true;
End;


Every 0$01 trigger omar_attack do
var i;
begin
enable;

for i in FilterAllUnits([f_side, 5]) do
    if not HasTask(i) then
       ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_enemy, 5]), i));
End;





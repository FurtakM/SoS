Export unreachableList;
On DestinationUnreachable(unit) do
begin
if not unit in unreachableList then
   unreachableList := Insert(unreachableList, unreachableList+1, unit);
End;

On UnitDestroyed(un) do
begin
SOS_UnitDestroyed(un);

if GetSide(un) = your_side and GetType(un) = unit_human then
   begin
   graveyard := Insert(graveyard, graveyard+1, un);
   end;

if GetSide(un) = 7 then
   begin

   if un in FilterAllUnits([f_weapon, 91]) then
      begin
      if Rand(1, 100) <= 70 then
         ArtContamination(GetX(un), GetY(un), 10);
      end;

   case GetType(un) of
   unit_human: begin

               case GetClass(un) of
               class_apeman:                  AddPoints(2);
               class_apeman_soldier:          AddPoints(5);
               class_apeman_kamikaze:         AddPoints(30);
               class_soldier:                 AddPoints(50);
               class_bazooker, class_sniper,
               class_mortar:                  AddPoints(75);
               class_mechanic:                AddPoints(60);
               end;

               end;

   unit_vehicle: begin

                 case GetChassis(un) of
                 ar_light_trike, us_light_wheeled, ar_hovercraft:         AddPoints(100);

                 ar_medium_trike, ar_half_tracked, us_medium_wheeled,
                 us_medium_tracked, ru_medium_wheeled, ru_medium_tracked: AddPoints(250);

                 us_heavy_tracked, us_morphling, ru_heavy_wheeled,
                 ru_heavy_tracked:                                        AddPoints(500);

                 25:                                                      AddPoints(1250);
                 end;

                 end;
   end;

   end;
End;

On BuildingComplete(b) do
begin
if GetBType(b) = b_depot then
   begin
   base_x := GetX(b);
   base_y := GetY(b);
   base := b;
   game_started := true;
   end;
End;

On UnitGoesToRed(un) do
begin
if GetSide(un) = your_side then
   exit;

if GetType(un) = unit_vehicle then
   begin
   KillUnit(un);

   if GetNation(un) = nation_arabian or GetWeapon(un) in [ru_time_lapser] then
      ExplodeVehicle(un);
   end;

if GetType(un) = unit_human then
   begin
   if (GetNation(un) = 2 and GetTech(5, 7) <> state_researched) or GetNation(un) in [0,1,3] then
      KillUnit(un);
   end;
End;

// Border
Every 0$1 do
begin
enable;
CheckBuildUpArea(zoneOut, zoneIn, [f_side, your_side]);
End;

Every 0$01 trigger game_started and FilterAllUnits([[f_side, your_side], [f_type, unit_human]]) = 0 do
YouLost('die');


Every 0$21 trigger tick <= 15$00 do
begin
enable;
CreateCratesArea(Rand(1,5), cratesSpawn, true);
End;

Every 0$51 trigger tick > 15$00 do
begin
enable;

if FilterAllUnits([f_side, 7]) = 0 then
   CreateCratesArea(Rand(1,5), cratesSpawn, true);
End;

On BuildingStarted(building, builder) do
begin
if IsInArea(building, cratesSpawn) then
   KillUnit(building);
End;

On UnitDestroyed_Extra(un, killerSide, killerUnit) do
begin
SOS_UnitExtraDestroyed(un, killerSide, killerUnit);
End;

On Command(cmd) do
begin
SOS_Command(cmd);
End;
Export Collins;
Export us_alert, us_retreat, us_patrol, us_count;
Export Function PrepareAmerican;
var i, un, skill, towers;
begin
uc_side := 1;
uc_nation := 1;

us_alert := false;
us_retreat := false;
us_patrol := false;
us_count := 0;

skill := [5, 6, 7][Difficulty];


// commander
hc_class := 1;
hc_sex := sex_male;
hc_skills := [6, 3, 4, 2];
hc_name := 'Jack Collins';
hc_attr := [11, 11];
hc_gallery := '';
Collins := CreateHuman;
PlaceHumanInUnit(Collins, us_depot);

hc_name := '';

if Difficulty > 1 then
   begin
   bc_type := b_bunker;
   bc_level := 5;
   CreateAndPlaceBuildingXYD(40, 33, 2);

   if Difficulty > 2 then
      CreateAndPlaceBuildingXYD(41, 42, 0);
   end;

// depot
PrepareHuman(false, 2, skill);
PlaceHumanInUnit(CreateHuman, us_depot);

// factory
PrepareHuman(false, 3, skill);
PlaceHumanInUnit(CreateHuman, us_fac);
PlaceHumanInUnit(CreateHuman, us_fac);

// lab
PrepareHuman(false, 4, skill);
PlaceHumanInUnit(CreateHuman, us_lab);

// bunker
PrepareHuman(false, 1, skill);
un := CreateHuman;
SetDir(un, 4);
PlaceHumanInUnit(un, FilterAllUnits([[f_side, 1], [f_btype, b_breastwork]])[1]);

// weapon
towers := FilterAllUnits([[f_side, 1], [f_btype, b_bunker]]);

for i in towers do
    begin
    PrepareHuman(false, 1, skill);
    PlaceHumanInUnit(CreateHuman, i);
    PlaceWeaponTurret(i, [us_gatling_gun, us_light_gun, us_double_gun][Rand(1,3)]);
    end;


// patrol
   PrepareHuman(false, 1, skill);
   us_patrol := CreateHuman;
   PlaceUnitXY(us_patrol, 12, 5, false);

End;



// EVENT
Every 0$01 trigger us_count >= [3, 4, 5][Difficulty] do
begin
Say(Collins, 'DPM_C2');
us_retreat := true;
End;

Every 10$00 trigger not us_alert do
var R;
begin
uc_side := 1;
uc_nation := 1;

PrepareHuman(false, 1, 0);
R := CreateHuman;

SayRadio(R, 'DPM_R1');

Wait(0$03);

us_retreat := true;
End;

Every 0$01 trigger us_alert do
var i;
begin
Say(Collins, 'DPM_C1');

if UnitsInside(us_fac) then
   for i in UnitsInside(us_fac) do
       begin
       ComExitBuilding(i);
       AddComEnterUnit(i, us_arm);
       AddComChangeProfession(i, 1);
       end;

ComExitBuilding(Collins);
AddComEnterUnit(Collins, us_arm);

ComExitBuilding(FilterAllUnits([[f_side, 1], [f_class, 2]])[1]);
AddComMoveXY(FilterAllUnits([[f_side, 1], [f_class, 2]])[1], 67, 52);

ComExitBuilding(FilterAllUnits([[f_side, 1], [f_class, 4]])[1]);
AddComEnterUnit(FilterAllUnits([[f_side, 1], [f_class, 4]])[1], us_arm);
End;


Every 3$00 trigger not us_alert and GetSide(us_fac) = 1 do
begin
AddComConstruct(us_fac, us_medium_wheeled, engine_combustion, control_manual, us_gatling_gun);
AddComConstruct(us_fac, us_medium_tracked, engine_solar, control_manual, us_double_gun);
End;

Every 0$01 trigger us_patrol do
SetPatrol(us_patrol, [12, 5, 56, 4, 94, 34, 92, 108]);


Export Function SetPatrol(un, points);
var s, i, turn;
begin
s = 1;
turn := true; // false - left, true - right

while(un and not us_alert) do
 begin
 wait(35);

 if not HasTask(un) then
    begin
    ComMoveXY(un, points[s], points[s+1]);

    if s + 2 > points then
       turn := false;
    if s - 2 < 1 then
       turn := true;

    if turn then
       s := s + 2
        else
         s := s - 2;
    end;

 if GetLives(un) < 1000 or See(3, un) then
    begin
    us_alert := true;
    break;
    end;
 end;

ComEnterUnit(un, us_arm);
End;


// retreat
Every 0$01 trigger us_retreat do
var i, point;
begin
point := false;

if FilterUnitsInArea(zone1, [f_side, 3]) = 0 then
   point := us_ret1
    else
     if FilterUnitsInArea(zone1, [f_side, 3]) = 0 then
        point := us_ret2
         else
          point := us_ret3;

     for i in FilterAllUnits([[f_side, 1], [f_type, unit_human]]) do
         begin
         if IsInUnit(i) then
            begin
            ComExitBuilding(i);
            ComExitVehicle(i);
            end;
         AddComMoveToArea(i, point);
         end;
End;

Every 10 trigger us_retreat and FilterUnitsInArea(us_ret1, [f_side, 1]) do
begin
enable;
RemoveUnit(FilterUnitsInArea(us_ret1, [f_side, 1])[1]);
if medal1 then
   medal1 := false;
End;

Every 10 trigger us_retreat and FilterUnitsInArea(us_ret2, [f_side, 1]) do
begin
enable;
RemoveUnit(FilterUnitsInArea(us_ret2, [f_side, 1])[1]);
if medal1 then
   medal1 := false;
End;

Every 10 trigger us_retreat and FilterUnitsInArea(us_ret3, [f_side, 1]) do
begin
enable;
RemoveUnit(FilterUnitsInArea(us_ret3, [f_side, 1])[1]);
if medal1 then
   medal1 := false;
End;


Every 0$03 trigger FilterAllUnits([f_side, 1]) = 0 do
us_retreat := false;


Export Function CounterAttack;
var i, j, un, skill;
begin
uc_side := 1;
uc_nation := 1;

skill := [4, 5, 5][Difficulty];

for i = 1 to [4, 5, 6][Difficulty] do
    begin
    vc_chassis := [us_medium_wheeled, us_medium_tracked, us_heavy_tracked][Rand(1, 3)];
    vc_engine := engine_combustion;
    vc_control := control_manual;
    vc_weapon := [us_gatling_gun, us_double_gun, us_light_gun][Rand(1, 3)];
    vc_fuel_battery := Rand(88, 99);
    un := CreateVehicle;
    PlaceUnitXY(un, 64, 117, false);

    PrepareHuman(false, 3, skill);
    PlaceHumanInUnit(CreateHuman, un);

    us_force := Insert(us_force, 1, un);
    ComMoveXY(un, 60, 109);

    Wait(0$02);
    end;

RemoveSeeing(62, 113, 3);

while(FilterAllUnits([f_side, 1])) do
begin
Wait(0$01);

for i = 1 to us_force do
    ComAttackUnit(us_force[i], FindTarget(us_force[i]));

if FilterAllUnits([[f_side, 1], [f_class, 3], [f_not, [f_driving]]]) then
   for j in FilterAllUnits([[f_side, 1], [f_class, 3], [f_not, [f_driving]]]) do
       if not IsInArea(j, conv_end) then
          ComMoveToArea(j, conv_end)
           else
            RemoveUnit(j);
end;

End;
// Action
Export Function Action;
begin
Difficulty := GetMultiplayerSetting(0);

// okno dialogowe
case Query('info') of
1: ;
end;

ChangeMissionObjectives('PM_1');
SaveForQuickRestart;
End;


Every 0$01 trigger GetSide(us_depot) = 3 do
begin
Say(Stolypin, 'DPM_S1');
SayRadio(Popov, 'DPM_P1');
Say(Stolypin, 'DPM_S2');

SaveForQuickRestart;
ChangeMissionObjectives('PM_2');

Wait(2$30);

CenterOnXY(117, 4);

Convoy(1);

Wait(0$30);
Convoy(2);

Wait(1$30);
Convoy(3);

Wait(5$00);

convoy_start := true;
PrepareArabian;
PrepareAttack(1);

while(convoy_start) do
 begin
 Wait(Rand(2$00, 4$00));

 if FilterAllUnits([f_side, 2]) < 20 then
    PrepareAttack(Rand(1,2));

 Convoy(Rand(1,5));
 conv_counter := conv_counter + 1;

 if conv_counter mod [5, 4, 3][Difficulty] = 0 then
    begin
    Wait(1$20);
    SayRadio(Farmer, 'DPM_F2');
    PrepareAttack(3);          
    Wait(4$00);
    end;
 end;
End;


Every 1$30 trigger conv_counter mod 3 = 0 and FilterAllUnits([f_side, 2]) = 0 do
SayRadio(Farmer, 'DPM_F3');

Every 1$30 trigger conv_counter mod 5 = 0 and FilterAllUnits([f_side, 2]) = 0 do
SayRadio(Farmer, 'DPM_F4');


Every 0$10 trigger conv_counter >= 8 and FilterAllUnits([f_side, 6]) = 0 do
begin
SayRadio(Popov, 'DPM_PEnd');

AddMedal('med1', medal1);
AddMedal('med2', medal2);

if dest_counter >= [15, 10, 7][Difficulty] then
   medal3 := false;

AddMedal('med3', medal3);

GiveMedals('MAIN');
YouWin;
End;


// Help
Every 4$00 trigger convoy_start do
var i;
begin
SayRadio(Popov, 'DPM_PHelp');
Wait(0$30);

Randomize;

uc_side := 3;
uc_nation := 3;

for i = 1 to [5, 4, 3][Difficulty] do
    begin
    PrepareHuman(false, Rand(1,3), 5);
    PlaceUnitArea(CreateHuman, rus_start, false);
    end;

vc_chassis := ru_heavy_wheeled;
vc_engine := engine_combustion;
vc_control := control_manual;
vc_weapon := 89;
vc_fuel_battery := 88;

i := CreateVehicle;
SetDir(i, 5);
PlaceUnitXY(i, 63, 115, false);

SetCargo(i, mat_cans, 200);

PrepareHuman(false, 3, 5);
PlaceHumanInUnit(CreateHuman, i);

DialogueOn;

CenterNowOnXY(169, 115);
DWait(0$01);
CenterNowOnXY(63, 115);
DWait(0$01);

DialogueOff;

ComMoveXY(i, 57, 53);

// Saseda
Wait(Rand(3$00, 7$00));
PlaceUnitXY(Saseda, 86, 112, false);
Wait(0$0.3);
SayRadio(Saseda, 'DPM_Sa1');

ChangeMissionObjectives('PM_3');
End;

Every 0$01 trigger See(3, Saseda) do
begin
Say(Saseda, 'DPM_Sa2');
ChangeMissionObjectives('PM_4');
SetTech(tech_rocket, 3, state_enabled);
SetSide(Saseda, 3);
End;

// US Counter Attack
Every 16$00 trigger not medal1 and FilterAllUnits([f_side, 6]) = 0 do
begin
Say(Stolypin, 'DPM_S3');

PlaceSeeing(62, 113, 3, 10);
CenterOnXY(62, 113);
CounterAttack;
End;


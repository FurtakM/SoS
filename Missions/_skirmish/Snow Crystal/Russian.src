// Rosjanie
Export ru_dow, gorki;
Export Function PrepareRussian;
var un, i, filter, team;
begin

     // Gracz wybiera Ruskich (nie ma tu nic do game_type = 1)
     // GAME_TYPE = 2!

     // Zmiana strony gracza
        Your_Side := 3;
        ChangeSideFog(3, 3);
        ResetFog; // restart mg³y

     // Gorki
        uc_side := 6;
        uc_nation := 3;

        hc_gallery := 'ru';
        hc_face_number := 1;

        hc_name := 'J. I. Gorki';
        hc_class := 3;

        hc_sex := sex_male;
        gorki := CreateHuman;


     // Przygotowanie sk³adu do wyboru
        uc_side := 3;
        uc_nation := 3;

     // dowódca
        hc_importance := 100;
        hc_name := '';
        hc_gallery := '';
        //ru_dow := CreateHumanWithClass(1, game_skill);
        PrepareHuman(sex_male, 1, game_skill+3);
        ru_dow := CreateHuman;

        hc_importance := 0;

        for i = 1 to 20 do // 16 losowych postaci
        begin
        uc_side := 3;
        uc_nation := 3;

        un := CreateHumanWithClass(Rand(1,4), game_skill);
        filter := filter ^ un;
        end;

     // okno wyboru
        team := CharacterSelection('text', [9,8,7][game_diff], [9,8,7][game_diff],[sel_change_class, ru_dow, sel_changeable, sel_not_hired]^ filter, [class_soldier, class_mechanic, [class_engineer, 1], [class_scientistic, 1], [class_bazooker, 0, [6,4,3][game_diff]]]);
        team := team ^ ru_dow;

     // pojawienie sie wybranych ludzi na mapie
        for i = 1 to team do
            PlaceUnitArea(team[i], south_spawn, false);



End;


// GAME_TYPE 2
// Gracz przejmuje magazyn
Every 0$01 trigger GetSide(jota_base) = 3 do
var area;
begin

     depot_captured := true;

     ChangeMissionObjectives('GType2b');

     Say(ru_dow, 'D-rudow2');
     SayRadio(gorki, 'D-gorky2');

     While(true) do // zapêtlane ataki
     begin
     Wait(Rand(6$20, 9$31));
     area := [north_spawn, south_spawn];
     AmericanAttack(1, enemy_count, enemy_typ, [north_spawn, south_spawn]);
     end;

End;


// WYDARZENIA DLA GAME_TYPE 2!!!
Every 1$30 trigger game_type = 2 do
begin
enable;

     if timer <= 45$00 then
     begin
     enemy_count := [5, 6, 7][game_diff];
     enemy_typ  := 1;
     end;

     if timer > 60$00 and timer <= 80$00 then
     begin
     enemy_count := [6, 8, 9][game_diff];
     enemy_typ  := 2;
     end;

     if timer > 80$00 then
     begin
     enemy_count := [8, 9, 10][game_diff];
     enemy_typ  := 3;
     end;


End;



// Legion
Export farmer;
Every 20$00 trigger game_type = 2 do
begin
LegionAttack(8, enemy_count, 1, north_spawn);
 Wait(20$00);
LegionAttack(8, enemy_count, 2, north_spawn);
 Wait(4$00);

uc_side := 8;
uc_nation := 2;

hc_class := 1;
hc_gallery := '';
hc_sex := sex_male;

farmer := CreateHuman;

SayRadio(farmer, 'D-farmer');
End;




// GAME_TYPE 1
Every 0$01 trigger game_type = 1 do
var filter, enemy, i;
begin
enable;


     filter := FilterAllUnits([f_or, [f_side, 3], [f_side, 6]]);
     enemy  := FilterAllUnits([f_or, [f_side, 1], [f_side, 2]]);

     if filter > 0 then
        for i = 1 to filter do
            ComAttackUnit(filter[i], NearestUnitToUnit(enemy, filter[i]));

End;

Export Function RussianAttack(side, num, typ, list);
var i, un, filter, veh, chassis, weapon, f;
begin

     Randomize;

     uc_side := side;
     uc_nation := nation_russian;

     chassis := [ru_medium_tracked, ru_heavy_tracked];
     weapon  := [ru_heavy_machine_gun, ru_gatling_gun, ru_gun, ru_heavy_gun, ru_rocket_launcher, ru_rocket];


     // select type of attack
        Case typ of
        1: begin  // easy

           for i = 1 to num do
               begin
               vc_chassis := chassis[1];
               vc_engine  := engine_combustion;
               vc_control := control_manual;
               vc_weapon  := weapon[Rand(1,3)];

               veh := CreateVehicle;

               f := Rand(1,2);

               if f = 1 then
               SetDir(veh, 3)
                else
               SetDir(veh, 1);
               
               PlaceUnitArea(veh, list[f], false);

               un := CreateHumanWithClass(1, game_eskill);
               PlaceHumanInUnit(un, veh);
               end;


           end;


        2: begin  // medium

           for i = 1 to num do
               begin
               vc_chassis := chassis[Rand(1,2)];
               vc_engine  := engine_combustion;
               vc_control := [control_manual, control_computer][Rand(1,2)];
               vc_weapon  := weapon[Rand(1,3)];

               veh := CreateVehicle;

               f := Rand(1,2);

               if f = 1 then
               SetDir(veh, 3)
                else
               SetDir(veh, 1);
               
               PlaceUnitArea(veh, list[f], false); 

               if GetControl(veh) = control_manual then
                  begin
                  un := CreateHumanWithClass(1, game_eskill);
                  PlaceHumanInUnit(un, veh);
                  end;

               end;
           

           for i = 1 to 2 do
               begin
               un := CreateHumanWithClass(class_bazooker, game_eskill);
               PlaceUnitArea(un, list[f], false);
               end;


           End;


        3: begin  // hard

           for i = 1 to num do
               begin
               vc_chassis := chassis[2];
               vc_engine  := engine_combustion;
               vc_control := [control_computer, control_manual][Rand(1,2)];
               vc_weapon  := weapon[Rand(3,6)];

               veh := CreateVehicle;

               f := Rand(1,2);

               if f = 1 then
               SetDir(veh, 3)
                else
               SetDir(veh, 1);
               
               PlaceUnitArea(veh, list[f], false);

               if GetControl(veh) = control_manual then
                  begin
                  un := CreateHumanWithClass(1, game_eskill);
                  PlaceHumanInUnit(un, veh);
                  end;

               end;

          end;
     end;
End;


Export Function PrepareLegion;
var i, un, skill, dep, fac;
begin
uc_side := 8;
uc_nation := 2;
skill := [7, 8, 9][Difficulty];

dep := ar_base;
fac := FilterAllUnits([[f_side, 8], [f_btype, b_factory]])[1];

SetResourceType(GetBase(dep), mat_cans, 999999);
SetResourceType(GetBase(dep), mat_oil, 10000);
SetResourceType(GetBase(dep), mat_siberit, 10000);

// teleport
TeleportExit(legion_telep, 224, 5);


PrepareHuman(sex_male, 1, 6);
hc_name := 'Nicolas Vervecken';
hc_gallery := 'SecondCharsGal';
hc_face_number := 3;
hc_attr := [11, 10];

Vervecken := CreateHuman;
PlaceHumanInUnit(Vervecken, dep);

InitHc;

// bazooka
for i = 1 to 4 do
    begin
    PrepareHuman(false, class_bazooker, skill);
    PlaceHumanInUnit(CreateHuman, FilterAllUnits([[f_side, 8], [f_btype, b_barracks]])[1]);
    end;

// lab
for i = 1 to 2 do
    begin
    PrepareHuman(false, 4, skill);
    PlaceHumanInUnit(CreateHuman, FilterAllUnits([[f_side, 8], [f_btype, b_lab_full]])[1]);
    end;

// fab
for i = 1 to 6 do
    begin
    PrepareHuman(false, 3, skill+1);
    PlaceHumanInUnit(CreateHuman, fac);
    end;

// towers
uc_nation := 0;

for i in FilterAllUnits([[f_side, 8], [f_btype, b_bunker]]) do
    begin
    PrepareHuman(false, class_apeman_soldier, skill+1);
    PlaceHumanInUnit(CreateHuman, i);
    end;

PrepareHuman(false, class_sniper, 10);
un := CreateHuman;
PlaceUnitXY(un, 175, 128, false);
ComEnterUnit(un, ar_ibun);

PrepareHuman(false, class_soldier, 10);
un := CreateHuman;
PlaceHumanInUnit(un, HexInfo(240, 128));

un := CreateHuman;
SetDir(un, 1);
PlaceHumanInUnit(un, HexInfo(260, 147));

for i = 1 to 2 do
    AddComConstruct(fac, ar_half_tracked, engine_solar, control_manual, ar_control_tower);
End;


Export Function PrepareHovercraft(num);
var i;
begin
for i = 1 to num do
    AddComConstruct(FilterAllUnits([[f_side, 8], [f_btype, b_factory]])[1], ar_hovercraft, engine_combustion, control_remote, [ar_light_gun, ar_double_machine_gun][rand(1,2)]);
End;

Export Function AddKamikaze();
var i, un;
begin
uc_side := 8;
uc_nation := 0;

hc_class := 17;
hc_gallery := '';
hc_name := '';
hc_importance := 0;
hc_skills := [10, 10, 10, 10];

if ar_base then
   begin
   un := CreateHuman;
   PlaceHumanInUnit(un, ar_base);
   ComExitBuilding(un);
   AddComEnterUnit(un, legion_telep);
   end;

End;

// Attacks
Export legion_force, l_allow_attack;
Export Function PrepareAttack(n1, n2);
var i;
begin
legion_force := [];
l_allow_attack := false;

PrepareHovercraft(n1);

repeat
 wait(0$01);
until legion_force >= n1;

l_allow_attack := true;

for i = 1 to n2 do
    begin
    AddKamikaze();
    end;
End;

// ct control
Every 0$02 trigger FilterAllUnits([[f_side, 8], [f_ok], [f_control, control_remote], [f_not, [f_linked]]]) do
var i;
begin
enable;

for i in FilterAllUnits([[f_side, 8], [f_ok], [f_control, control_remote], [f_not, [f_linked]]]) do
    Connect(i);
End;


// legion attacks
Every 5$00 trigger legion_attacks_active do
begin
enable;
Wait([2$30, 1$00, 0$30][Difficulty]);
PrepareAttack((legion_attack_strenght - 2 + Difficulty), 2);
End;

Every 0$1 trigger l_allow_attack and UnitFilter(legion_force, [[f_not, [f_hastask]], [f_linked], [f_ok]]) do
var path, units;
begin
enable;

units := UnitFilter(legion_force, [[f_not, [f_hastask]], [f_linked], [f_ok]]);
path := [ [150,93], [232,88], [209,50], [208,29], [191,17] ];
AgressiveMove(units, path);
End;

Every 0$01 trigger FilterUnitsInArea(islandArea, [[f_side, 8], [f_class, 17]]) do
var i;
begin
enable;

for i in FilterUnitsInArea(islandArea, [[f_side, 8], [f_class, 17]]) do
    ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 6]), i));
End;


Every 6$00 trigger legion_telep do
var k, xy, i;
begin
enable;

xy := [[224, 5], [160,4], [228,56], [271,87]];

for i = 1 to 3 do
    EraseResourceArea(telArea, i);

for i = xy downto 1 do
    begin
    k := rand(1, xy);

    if HexInfo(xy[k][1], xy[k][2]) then
       xy := Delete(xy, k)
      else
       break;
    end;

TeleportExit(legion_telep, xy[k][1], xy[k][2]);
End;
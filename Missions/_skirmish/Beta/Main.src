// Sand of Siberia - Beta
// map by ALTAR, expanded by Serpent
// SAIL by Prazanin
// Idea, scenario and dialogs by Prazanin
// Edit: Nefarem

export MissionStart;
export southAreaBlocker;

starting
begin

  southAreaBlocker = true;

  ResetFog;
  AnimateTrees(true);
  RevealFogArea(1, BetaArea);

  GameType;

  SetBName(BetaA, 'beta');
  SetBName(BetaB, 'beta');

  SetResourceType(GetBase(BetaA), mat_cans, startCrates[1]);
  SetResourceType(GetBase(BetaB), mat_cans, startCrates[2]);


  PrepareHeroes;
  PrepareBeta;

  HideArea(1, SouthArea);

  InGameOn;
  ComHold(Brighton);
  CenterNowOnUnits(Brighton);

  ComMoveXY(Megan, 50, 42);

  repeat
   wait(0$1)
  until GetDistUnits(Megan, Brighton) < 3;

  ComTurnUnit(Megan, Brighton);

  Say(Megan, 'DMegan-1a');

  wait(0$0.3);

  ComTurnUnit(Brighton, Megan);

  Say(Brighton, 'DBrighton-1a');
  Say(Megan, 'DMegan-2a');
  Say(Brighton, 'DBrighton-2a');
  Say(Megan, 'DMegan-3a');
  Say(Brighton, 'DBrighton-3a');
  Say(Megan, 'DMegan-4a');

  ComMoveXY(Megan, 40, 37);
  AddComTurnUnit(Megan, Brighton);

  ComMoveXY(Palmer, 50, 42);

  repeat
   wait(0$1);
  until GetDistUnits(Brighton, Palmer) < 6;

  ComTurnUnit(Brighton, Palmer);
  ComTurnUnit(Palmer, Brighton);

  Say(Megan, 'DMegan-5a');
  Say(Palmer, 'DPalmer-1');
  Say(Brighton, 'DBrighton-1');
  Say(Palmer, 'DPalmer-2');
  Say(Brighton, 'DBrighton-2');
  Say(Palmer, 'DPalmer-3');
  Say(Brighton, 'DBrighton-3');
  Say(Palmer, 'DPalmer-4');
  ComEnterUnit(Palmer, AmTower);

  repeat
   wait(0$1);
  until IsInUnit(Palmer) = AmTower;

  SayRadio(Palmer, 'DPalmer-5');

  ComMoveXY(Brighton, 59, 36);
  ComMoveXY(Megan, 60, 35);
  ComMoveXY(Palmer, 60, 34);

  AddComTurnXY(Brighton, 99, 37);
  AddComTurnUnit(Megan, Brighton);
  AddComTurnUnit(Palmer, Brighton);

  CenterOnXY(64, 40);

  ComMeeting(BetaSquad, SpotArea);

  wait(0$30);

  Say(Brighton, 'DBrighton-4');
  Say(Brighton, 'DBrighton-5');
  AddComTurnUnit(Brighton, Megan);
  Say(Brighton, 'DBrighton-6');
  Say(Megan, 'DMegan-1');
  Say(Brighton, 'DBrighton-7');
  AddComTurnXY(Brighton, 99, 37);
  Say(Brighton, 'DBrighton-8');

  ChangeMissionObjectives('C1');
  InGameOff;

  SaveForQuickRestart;

  MissionStart := true;

  PrepareRUBase_v2;
  mc_bases := Replace(mc_bases, 1, FilterAllUnits([f_side, 3]));
  mc_bases := Replace(mc_bases, 2, RU_SecondPersonnel);
  MC_Start();

  music_nat := 1;
end;

// Return units from "block" area
every 1 trigger southAreaBlocker do
var unit;
begin

  enable;

  if FilterUnitsInArea(SouthArea, [[f_side, 1], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) = 0 then
    exit;

  for unit in FilterUnitsInArea(SouthArea, [[f_side, 1], [f_or, [f_type, unit_human], [f_type, unit_vehicle]]]) do
      ComMoveXY(unit, GetX(unit), GetY(unit) - 2);
end;


export ArmadaArrived, FirstAssaultRepelled;
export RuSpeaker;

every 0$1 trigger MissionStart do
var i, tmp;
begin
  wait(ruArrival);

  PrepareRUArmada;

  wait(0$3);

  tmp := AreaToList(RUArrivalParkingArea, 0);

  for i = 1 to RUArmada do
      ComMoveXY(RUArmada[i], tmp[1][i], tmp[2][i]);

  ArmadaArrived := true;

  Say(Palmer, 'DPalmer-A1');
  Say(Brighton, 'DBrighton-A1');

  wait(0$20);
  PrepareRUBase;

  wait(1$20);
  PrepareRUBulldozers;
  CutTreeInArea(Bulldozers[1], BulldozerArea);
  CutTreeInArea(Bulldozers[2], BulldozerArea2);
  DoNotAttack(1, Bulldozers[1]);
  DoNotAttack(1, Bulldozers[2]);
  wait(0$50);

  ShowArea(1, SouthArea);

  InGameOn;
  PlaceSeeing(95, 93, 1, -30);
  ComMoveXY(FilterUnitsInArea(US_BackOffArea, [f_side, 1]), 63, 39);
  CenterOnXY(95, 93);

  repeat
   wait(0$1);

   if not HasTask(Bulldozers[1]) then
      CutTreeInArea(Bulldozers[1], BulldozerArea);

   if not HasTask(Bulldozers[2]) then
      CutTreeInArea(Bulldozers[2], BulldozerArea2);

  until ListEnvironmentArea(BulldozerArea) < 2 or ListEnvironmentArea(BulldozerArea2) < 2
        or IsDead(Bulldozers[1]);

  if UnitFilter(Bulldozers, [f_ok]) then
     begin
     wait(0$10);
     ComMoveXY(Bulldozers, 120, 131);

     for i in Bulldozers do
         NormalAttack(1, i);
     end;

  wait(0$10);

  RemoveSeeing(95, 93, 1);

  southAreaBlocker = false;

  wait(0$3);
  InGameOff;
  
  SayEffect('DRuSol-A1');

  AssaultStarted := true;

  wait(0$2);

  Say(Yashin, 'DDeath-Yas-1'); 
  Say(Gaydar, 'DDeath-Gay-2');

  wait(0$2);

  Say(Popov, 'DDeath-Pop-1');
  Say(Gaydar, 'DDeath-Gay-1');

  wait(0$5);
  Say(Sevi, 'DDeath-Vse-1'); 
  Say(Popov, 'DDeath-Pop-2');
end;


every 0$1 trigger AssaultStarted do
var i, tmp;
begin

  PriorityAttack(6, BetaA);
  PriorityAttack(6, BetaB);

  repeat
    for i in RuArmada do
        ComAttackUnit(i, NearestUnitToUnit(FilterAllUnits([f_side, 1]), i));

    wait(0$1)
    until FilterAllUnits([f_and, [f_side, 6], [f_type, unit_human] ]) < 10
    and IsDead(Yashin)
    and IsDead(Popov)
    and IsDead(Gaydar)
    and IsDead(Sevi);

    AssaultStarted := false;

  ComMoveToArea(FilterAllUnits([f_side, 6]), RUArrivalArea);
  RuSpeaker := FilterAllUnits([f_and, [f_side, 6], [f_sex, sex_male], [f_type, unit_human], [f_lives, 500] ]);

  if RuSpeaker then
     Say(RuSpeaker[1], 'DRuSol-A2');

  wait(0$30);
  DialogueOn;
  Say(Brighton, 'DBrighton-B1');
  Say(Palmer, 'DPalmer-B1');
  Say(Brighton, 'DBrighton-B2');
  SayRadio(Burlak, 'DBurlak-B1');
  Say(Brighton, 'DBrighton-B3');
  Say(Palmer, 'DPalmer-B2');
  Say(Brighton, 'DBrighton-B4');
  Say(Brighton, 'DBrighton-B5');
  DialogueOff;

  ChangeMissionObjectives('C2');
  FirstAssaultRepelled := true;

  RemoveUnit(Bulldozers[1]);
  RemoveUnit(Bulldozers[2]);
  AddComExitVehicle(FilterAllUnits([f_side, 6]));
end;

every 0$1 trigger FirstAssaultRepelled do
var unit, insideUnit;
begin
    enable;

    if FilterUnitsInArea(RUArrivalArea, [[f_side, 6]]) = 0 then
        exit;

    for unit in FilterUnitsInArea(RUArrivalArea, [[f_side, 6]]) do
    begin
        SetSide(unit, 3);
        if (GetType(unit) = unit_vehicle) then
            for insideUnit in UnitsInside(unit) do
                SetSide(insideUnit, 3);

        mc_bases := Replace(mc_bases, 1, FilterAllUnits([f_side, 3]));
    end;
end;

every 0$1 trigger FirstAssaultRepelled and jmmArrival > 0$00 do
begin
  enable;
  jmmArrival := jmmArrival - 0$01;
end;

export JMM_Arrived;

every 0$1 trigger jmmArrival = 0$00 do
begin
  JMMReinforcement;
  DialogueOn;
  CenterOnUnits(JMM);
  Say(JMM, 'DJMM-F1');
   if RussiansDefeated then
      begin
      AddMedal('Russians', 1);
      Say(Brighton, 'DBrighton-F1a');
      end
   else
    begin
    AddMedal('Russians', -1);
    Say(Brighton, 'DBrighton-F1');
    ChangeMissionObjectives('C3');
    end;
  DialogueOff;
  JMM_Arrived := true;
end;

every 0$1 trigger JMM_Arrived and See(1,Burlak) do
begin
Say(Burlak, 'DBurlak');
Say(JMM, 'DJMM');
end;

export LaserBuilt;
every 0$1 trigger FilterAllUnits([f_and, [f_side, 1], [f_or, [f_weapon, us_laser], [f_bweapon, us_laser] ] ]) > 1 do
LaserBuilt := true;

export AssaultStarted;
every 0$1 trigger
 AssaultStarted and
 See(6, NearestUnitToUnit( FilterAllUnits([f_and, [[f_side, 1], [f_bweapon, us_laser]] ]), IsInUnit(Sevi)))
 do
begin
  Say(Sevi, 'DDeath-Vse-2');
  Say(Yashin, 'DDeath-Yas-2');
end;

every 0$1 trigger MissionStart and GetResourceArea(CratesArea, mat_cans) < 20 do
Begin
wait(rand(0$50, 2$20));
CreateCratesArea(Rand(2, 5), CratesArea,true);
enable;
end;

Every 0$1 trigger MissionStart do
begin
enable;

display_Strings := ['#tick', tick];
End;
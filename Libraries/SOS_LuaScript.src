// SoS Stream Mode by Serpent
// 01.04.2021

Export StreamModeActive,
       sSpec, sSpeed, sRocket, sEngine,
       sLevel, sArmoury, sRadar, sBunker,
       sHack, sFire, sRefresh, sExp, sDepot,
       sFlag, sSold, sDiff, sTiger, sBomb, sFog,
       sReset, sSun, sKamikadze, sTroll, sSlow, sLack,
       sWound, sTank, sRemote, sPowell, sBetray, sContamin,
       sTeleport, sOil, sOilTower, sShovel, sSheik, sStu;

Every 0$1 do
begin
ToLua('initStreamRollete();');
InitStreamMode;
End;

Function InitStreamMode;
begin
streamModeActive := false;
sRocket := false; // 1
sSpeed := false; // 2
sEngine := false; // 3
sSpec := false; // 4
sLevel := false; // 5
sArmoury := false; // 6
sRadar := false; // 7
sBunker := false; // 8
sHack := false; // 9
sFire := false; // 10
sRefresh := false; // 11
sExp := false; // 12
sDepot := false; // 13
sFlag := false; // 14
sKamikadze := false; // 15
sTroll := false; // 16
sSlow := false;  // 17
sLack := false; // 18
sTank := false; // 19
sRemote := false; // 20
sPowell := false; // 21
sTeleport := false; // 22
sOilTower := false; // 23
sShovel := false; // 24
sSheik := false; // 25
sSold := false; // 101
sDiff := false; // 102
sFog := false; // 103
sReset := false; // 104
sSun := false; // 105
sTiger := false; // 106
sBomb := false; // 107
sWound := false; // 108
sBetray := false; // 109
sContamin := false; // 110
sOil := false; // 111
sStu := false; // 112
End;

On CustomCommand(p1, p2, p3, p4, p5, p6) do
begin

if p2 = 100 then // stream mode
   begin
   if not StreamModeActive then
      StreamModeActive := true;

   if p3 = 0 then
      InitStreamMode;

   if p3 = 1 then
      sRocket := true;

   if p3 = 2 then
      sSpeed := true;

   if p3 = 3 then
      sEngine := true;

   if p3 = 4 then
      sSpec := true;

   if p3 = 5 then
      sLevel := true;

   if p3 = 6 then
      sArmoury := true;

   if p3 = 7 then
      sRadar := true;

   if p3 = 8 then
      sBunker := true;

   if p3 = 9 then
      sHack := true;

   if p3 = 10 then
      sFire := true;

   if p3 = 11 then
      sRefresh := true;

   if p3 = 12 then
      sExp := true;

   if p3 = 13 then
      sDepot := true;

   if p3 = 14 then
      sFlag := true;

   if p3 = 15 then
      sKamikadze := true;

   if p3 = 16 then
      sTroll := true;

   if p3 = 17 then
      sSlow := true;

   if p3 = 18 then
      sLack := true;

   if p3 = 19 then
      sTank := true;

   if p3 = 20 then
      sRemote := true;

   if p3 = 21 then
      sPowell := true;

   if p3 = 22 then
      sTeleport := true;

   if p3 = 23 then
      sOilTower := true;

   if p3 = 24 then
      sShovel := true;

   if p3 = 25 then
      sSheik := true;

   if p3 = 101 then
      sSold := true;

   if p3 = 102 then
      sDiff := true;

   if p3 = 103 then
      sFog := true;

   if p3 = 104 then
      sReset := true;

   if p3 = 105 then
      sSun := true;

   if p3 = 106 then
      sTiger := true;

   if p3 = 107 then
      sBomb := true;

   if p3 = 108 then
      sWound := true;

   if p3 = 109 then
      sBetray := true;

   if p3 = 110 then
      sContamin := true;

   if p3 = 111 then
      sOil := true;

   if p3 = 112 then
      sStu := true;
   end;
End;

Every 0$2 trigger StreamModeActive and sRocket do
var i, tmp;
begin
enable;

tmp := FilterAllUnits([[f_side, your_side], [f_or,
    [f_weapon, us_rocket_launcher],
    [f_weapon, ru_rocket_launcher],
    [f_weapon, ar_rocket_launcher],
    [f_weapon, ru_rocket]]]);

if not tmp then
   exit;

for i in tmp do
    begin
    SetLives(i, 0);
    end;
End;

Every 0$2 trigger StreamModeActive and sEngine do
var i, tmp;
begin
enable;

tmp := FilterAllUnits([[f_side, your_side], [f_engine, engine_siberite]]);

if not tmp then
   exit;

for i in tmp do
    begin
    DestroyUnit(i);
    end;
End;

Every 0$1 trigger StreamModeActive and sSpec do
var i;
begin
enable;

for i in FilterAllUnits([[f_side, your_side], [f_or,
    [f_class, class_sniper],
    [f_class, class_bazooker],
    [f_class, class_mortar]]]) do
    begin
    SetClass(i, 1);
    end;
End;

Every 0$1 trigger StreamModeActive and sSpeed and game_speed < 7 do
begin
enable;  
game_speed := 7;
End;

Every 0$1 trigger StreamModeActive and sLevel do
var i, k, tmp;
begin
tmp := FilterAllUnits([[f_enemy, your_side], [f_type, unit_human]]);

if not tmp then
   exit;

if tmp > 5 then
   k := 5
else
   k := tmp;

for i := 1 to k do
    if GetSkill(tmp[i], i mod 4 + 1) < 10 then
       SetSkill(tmp[i], i mod 4 + 1, GetSkill(tmp[i], i mod 4 + 1) + 1);
End;

Every 0$1 trigger StreamModeActive and sArmoury do
SetRestrict(b_armoury, your_side, false);

Every 0$1 trigger StreamModeActive and sShovel do
SetRestrict(b_ext_noncombat, your_side, false);

Every 0$1 trigger StreamModeActive and sRadar do
var i, tmp;
begin
enable;

tmp := FilterAllUnits([[f_side, your_side], [f_or, [f_weapon, us_radar], [f_weapon, ar_radar]]]);

if not tmp then
   exit;

for i in tmp do
    begin
    DestroyUnit(i);
    end;
End;

Every 0$1 trigger StreamModeActive and sBunker do
SetRestrict(b_bunker, your_side, false);

Every 0$1 trigger StreamModeActive and sHack do
var i, tmp, side;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_control, control_computer]]);

if not tmp then
   exit;

side := 0;

for i := 1 to 8 do
    if your_side <> i and GetAttitude(your_side, i) = att_enemy then
       begin
       side := i;
       break;
       end;

if not side then
   exit;

for i := 1 to tmp do
    if Prob(30) then
       SetSide(i, side);
End;

Every 0$1 trigger StreamModeActive and sRefresh do
var un;
begin
for un in FilterAllUnits([[f_side, your_side], [f_type, unit_human], [f_not, [f_nation, 0]]]) do
    if GetClass(un) in [1, 2, 3, 4] then
       SetClass(un, rand(1, 4));
End;

Every 0$1 trigger StreamModeActive and sFire do
var tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_building]]);

if not tmp then
   exit;

SetLives(tmp[rand(1, tmp)], 100);
End;

Every 0$1 trigger StreamModeActive and sExp do
var tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_human]]);

if not tmp then
   exit;

AddExperience(tmp[rand(1, tmp)], rand(1, 4), rand(3000, 9000));
End;

Every 0$1 trigger StreamModeActive and sDepot do
SetRestrict(b_warehouse, your_side, false);

Every 0$1 trigger StreamModeActive and sFlag do
var i, tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_building]]);

if not tmp then
   exit;

for i in tmp do
    SetBLevel(i, 10);
End;

Every 0$1 trigger StreamModeActive and sSold do
var i, un, tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_class, 1]]);

if not tmp then
   exit;

un := tmp[rand(1, tmp)];

if Crawls(un) then
   ComWalk(un);

SetClass(un, class_sniper);
End;

Every 0$1 trigger StreamModeActive and sDiff and Difficulty < 3 do
Difficulty := Difficulty + 1;

Every 0$1 trigger StreamModeActive and sTiger do
var i;
begin
for i := 1 to 5 do
    begin
    uc_nation := nation_nature;
    uc_side := 0;

    hc_attr := [12, 12];
    hc_agressivity := 20;
    hc_class := class_tiger;
    hc_gallery := '';
    hc_name := '';

    PlaceUnitAnyWhere(CreateHuman, false);
    end;
End;
          
Every 0$1 trigger StreamModeActive and sBomb do
var i, x, y, result;
begin
result := false;

for i := 1 to 8 do
    begin
    x := [10, 50, 90, 140][rand(1,4)];
    y := [10, 50, 90, 140][rand(1,4)];

    if ValidHex(x, y) then
       begin
       result := [x, y];
       break;
       end;
    end;

if result then
   SendSiberiteRocket(result[1], result[2]);
End;

Every 0$1 trigger StreamModeActive and sReset do
YouLost('');

Every 0$1 trigger StreamModeActive and sFog do
FogOff(your_side);

Every 0$1 trigger StreamModeActive and sSun do
begin
solar_recharge_percent := 0;
wait(5$00);
solar_recharge_percent := 100;
End;

Every 0$1 trigger StreamModeActive and sKamikadze do
var i, un, tmp;
begin
tmp := [];

for i := 1 to 6 do
    begin
    uc_nation := nation_nature;
    uc_side := 0;

    hc_attr := [12, 12];
    hc_agressivity := 20;
    hc_class := class_apeman_kamikaze;
    hc_gallery := '';
    hc_name := '';

    un := CreateHuman;
    PlaceUnitAnyWhere(un, true);

    tmp := tmp * un;
    end;

repeat
 wait(0$1);

 for un in tmp do
     begin
     if IsDead(un) then
        begin
        tmp := tmp diff un;
        continue;
        end;

     ComAttackUnit(un, NearestUnitToUnit(FilterAllUnits([f_not, [f_side, 0]]), un));
     end;

until not tmp;
End;

Every 0$1 trigger StreamModeActive and sTroll do
begin
ToLua('displayTroll();');
wait(3$00);
ToLua('hideTroll();');
End;

Every 0$1 trigger StreamModeActive and sSlow do
var p;
begin
p := 0;

repeat
 game_speed := 1;
 wait(0$1);
 p := p + 1;
until p >= 60;

game_speed := 4;
End;

Every 0$1 trigger StreamModeActive and sLack do
var depot, base;
begin
depot := FilterAllUnits([[f_side, your_side], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]);

if not depot then
   exit;

base := GetBase(depot[rand(1, depot)]);

SetResourceType(base, mat_cans, 0);
SetResourceType(base, mat_oil, 0);
SetResourceType(base, mat_siberit, 0);
End;

Every 0$1 trigger StreamModeActive and sWound do
var tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_human], [f_not, [f_nation, 0]]]);

if not tmp then
   exit;

SetLives(tmp[rand(1, tmp)], 200);
End;

Every 0$1 trigger StreamModeActive and sTank do
var tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_vehicle]]);

if not tmp then
   exit;

SetLives(tmp[rand(1, tmp)], 60);
End;

Every 0$1 trigger StreamModeActive and sRemote do
var tmp, i;
begin
enable;

tmp := FilterAllUnits([[f_side, your_side], [f_linked], [f_control, control_remote]]);

if not tmp then
   exit;

for i in tmp do
    if IsControledBy(i) then
       ComUnlink(i);
End;

Every 0$1 trigger StreamModeActive and sPowell do
begin
ToLua('displayPowell();');
wait(0$20);
ToLua('displayPowell();');
wait(0$40);
ToLua('displayPowell();');
End;

Every 0$1 trigger StreamModeActive and sStu do
begin
ToLua('displayStucuk();');
End;

Every 0$1 trigger StreamModeActive and sBetray do
var un, tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_human]]);

if not tmp then
   exit;

un := tmp[rand(1, tmp)];

SetSide(un, 0);

ComAttackUnit(un, NearestUnitToUnit(all_units diff un, un));

wait(0$20);

SetSide(un, your_side);
End;

Every 0$1 trigger StreamModeActive and sContamin do
var depot;
begin
depot := FilterAllUnits([[f_side, your_side], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]);

if not depot then
   exit;

ArtContamination(GetX(depot[1]), GetY(depot[1]), 70);
End;

Every 0$1 trigger StreamModeActive and sTeleport do
var i, x, y, d, tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_type, unit_human]]);

if not tmp then
   exit;

for i in tmp do
    begin
    d := rand(0, 5);
    x := ShiftX(GetX(i), d, rand(3, 12));
    y := ShiftY(GetY(i), d, rand(3, 12));

    if ValidHex(x, y) then
       TeleportUnit(i, x, y, rand(3, 6), true);
    end;
End;

Every 0$1 trigger StreamModeActive and sOil do
var i, tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_engine, engine_combustion], [f_type, unit_vehicle]]);

if not tmp then
   exit;

for i in tmp do
    SetFuel(i, 0);
End;

Every 0$1 trigger StreamModeActive and sOilTower do
var tmp;
begin
tmp := FilterAllUnits([[f_side, your_side], [f_btype, b_oil_mine]]);

if not tmp then
   exit;

DestroyUnit(tmp[1]);
End;

Every 0$1 trigger StreamModeActive and sSheik do
var un;
begin
uc_side := 0;
uc_nation := nation_arabian;

PrepareHuman(sex_male, 10, class_desert_warior);
un := CreateHuman;
PlaceUnitAnywhere(un, true);

repeat
 wait(0$1);
 ComAttackUnit(un, NearestUnitToUnit(FilterAllUnits([f_side, your_side]), un));
until IsDead(un);
End;
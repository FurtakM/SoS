Export function Multiplayer_UnitDestroyed(un);
var i, ape, tag, class, area, nation, side, tmp;
begin
if GetType(un) = unit_vehicle then
   exit;

side := GetSide(un);

if mpGameType = GameTypeSurvival then
   if GetBType(un) in [b_depot, b_warehouse] and GetTag(un) = side then
      begin
      for i in FilterAllUnits([[f_side, side], [f_type, unit_human]]) do
          KillUnit(i);
      end;

if GetClass(un) in [class_apeman, class_apeman_kamikaze, class_apeman_soldier, class_apeman_engineer] or GetControl(un) = control_apeman then
   begin
   tag := GetTag(un);
   area := mpApemansArea[mpSidePositions[tag]];

   if not area then
      exit;

   wait(4$00);

   repeat
    wait(10);
   until not mpBlockRespThread;

   mpBlockRespThread := true;

   PrepareApeman(-5);
   ape := CreateHuman;
   SetTag(ape, tag);
   PlaceUnitArea(ape, area, false);

   mpBlockRespThread := false;
   end;

if GetType(un) = unit_human and GetNation(un) > nation_nature then
   begin
   if not mpRespawn then
      exit;

   if not MultiplayerSideAlive(side) and not HasPowerByGameType(mpGameType, side) then
      exit;

   side := GetSide(un);
   nation := GetNation(un);
   class := GetClass(un);

   if class > 4 then
      class := 1;   
   
   repeat
    wait(10);
   until not mpBlockRespThread;

   mpBlockRespThread := true;

   uc_side := side;
   uc_nation := nation;

   PrepareHuman(false, class, 2);

   un := CreateHuman;       

   mpRespawnPull := Replace(mpRespawnPull, side, Replace(mpRespawnPull[side], mpRespawnPull[side] + 1, un));

   mpBlockRespThread := false;
   end;
End;

Export function Multiplayer_BuildingComplete(building);
begin
if GetBType(building) in [b_depot, b_warehouse] then
   SetBName(building, '@' & GetSide(building));
End;

Export function Multiplayer_BuildingCaptured(building, oldSide, unit);
var i;
begin
if mpGameType = GameTypeSurvival and GetBType(building) in [b_depot, b_warehouse] then
   begin
   KillUnit(building);

   for i in FilterAllUnits([[f_side, oldSide], [f_type, unit_human]]) do
       KillUnit(i);

   exit;
   end;

if GetBType(building) in [b_depot, b_warehouse] then
   SetBName(building, '@' & GetSide(building));
End;

Export function Multiplayer_UnitDestroyed(un);
var ape, tag, area, side, tmp;
begin
if GetClass(un) in [class_apeman, class_apeman_kamikaze, class_apeman_soldier, class_apeman_engineer] or GetControl(un) = control_apeman then
   begin
   tag := GetTag(un);
   area := mpApemansArea[tag];

   wait(4$00);

   PrepareApeman(-5);
   ape := CreateHuman;
   SetTag(ape, tag);
   PlaceUnitArea(ape, area, false);
   end;

if GetType(un) = unit_human and GetNation(un) > nation_nature then
   begin
   if not mpRespawn then
      exit;

   side := GetSide(un);

   wait(mpRespawn);

   if not MultiplayerSideAlive(side) then
      exit;

   uc_side := side;
   uc_nation := GetNation(un);

   PrepareHuman(GetSex(un), GetClass(un), 2);

   tmp := FilterAllUnits([[f_side, side], [f_or, [f_btype, b_depot], [f_btype, b_warehouse]]]);

   if not tmp then
      PlaceUnitAnywhere(CreateHuman, true)
   else
      PlaceUnitXYR(CreateHuman, GetX(tmp[1]), GetY(tmp[1]), 20, true);
   end;
End;
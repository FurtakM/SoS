// Global module for EcoWar mode
Export ecoSide, ecoNation, ecoPeopleAmount, ecoStartPoint, ecoCratesDrop,
       ecoCratesDropXY;
Export Function InitEcoMode(startPoint, cratesDropXY);
var i, tmp, side, nation, peopleAmount, resourcesAmount;
begin
ecoStartPoint := startPoint;
CenterNowOnXY(ecoStartPoint[1], ecoStartPoint[2]);

ToLua('ShowEcoButton();');

ecoCratesDropXY := cratesDropXY;

if debug then
   begin
   side := 1;
   nation := 1;
   peopleAmount := 15;
   resourcesAmount := 2;
   ecoCratesDrop := 3;
   end
else
   begin
   side := GetMultiplayerSetting(1) + 1;
   nation := GetMultiplayerSetting(2) + 1;
   peopleAmount := [6, 9, 12, 15, 18, 21, 24][GetMultiplayerSetting(3) + 1];
   resourcesAmount := GetMultiplayerSetting(4) + 1;
   ecoCratesDrop := GetMultiplayerSetting(5) + 1;
   end;

ecoSide := side;
your_side := side;
ChangeSideFog(1, side);

PlaceSeeing(1, 1, side, -32793);
RemoveSeeing(1, 1, side);

ecoNation := nation;
ecoPeopleAmount := peopleAmount;

for i = 1 to resourcesAmount * 5 do
    CreateResourcesXYR(mat_cans, 5, ecoStartPoint[1], ecoStartPoint[2], 6, false);

uc_side := side;
uc_nation := nation;

hc_importance := 100;
PrepareHuman(false, 1, 4);
PlaceUnitXYR(CreateHuman, ecoStartPoint[1], ecoStartPoint[2], 12, false);

InitHc_All();

for i = 1 to peopleAmount - 1 do
    begin
    PrepareHuman(false, i mod 4 + 1, 3);
    tmp := Replace(tmp, tmp + 1, CreateHuman);
    PlaceUnitXYR(tmp[tmp], ecoStartPoint[1], ecoStartPoint[2], 12, false);
    end;
End;

Every 0$1 trigger ecoCratesDrop and ecoCratesDropXY do
var time, xy;
begin
time := [0$30, 0$20, 0$10][ecoCratesDrop];

while true do
      begin
      wait(time);
      time := time + 0$3;

      if time > 1$00 then
         time := 0$30;

      xy := ecoCratesDropXY[rand(1, ecoCratesDropXY)];
      CreateCratesXYR(rand(1, 5), xy[1], xy[2], 6, true);
      end;
End;

On CustomCommand(p1, p2, p3, p4, p5, p6) do
begin
SOS_CustomCommand(p1, p2, p3, p4, p5, p6);
End;